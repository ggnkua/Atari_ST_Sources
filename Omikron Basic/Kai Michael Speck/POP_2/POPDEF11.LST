
 DEF PROC Popdef' (* obligatorische Definitionsprozedur *)
   PRINT
   PRINT "POPUP-Definitions Library V 1.1 (C) AZH '93"
   PRINT
 RETURN

 ' (* Popup-Definition beginnen *)

 DEF PROC Def_Pop
   Def_Pop(0)
 RETURN

 DEF PROC Def_Pop(State%)' (* Ob_State des Popup-Kastens *)
   Def_Pop(State%,32,64)' (* Default Gr”že *)
 RETURN

 DEF PROC Def_Pop(State%,Max_W%,Max_H%)
 ' GLOBAL:
   Pop_W_%=Max_W%:Pop_H_%=Max_H%' (* Die Maximalgr”že in Zeichen *)
   DIM Pop_Entry_$(Max_H%)' (* Feld fr die einzelnen Popup Entries *)
   DIM Pop_State_%(Max_H%)' (* Ob_State der Eintr„ge *)
   Pop_State_%(0)=State%
   Pop_Len_%=4' (* Mindestbreite *)
   Pops_%=0' (* aktuelle Anzahl der Eintr„ge *)
 RETURN

 ' (* Popup Eintr„ge *)

 DEF PROC Pop_Entry(Entry$,R Entry%L)' (* Normal *)
   Pop_Entry(Entry$,Entry%L,0)
 RETURN

 DEF PROC Pop_Entry(Entry$,R Entry%L,State%)' (* Mit beliebigem Ob_State *)
   IF LEN(Entry$) THEN
     WHILE MID$(Entry$,1,1)= CHR$(32)
       Entry$= MID$(Entry$,2)' (* Anfangsleerzeichen entfernen *)
     WEND
     WHILE RIGHT$(Entry$,1)= CHR$(32)
       Entry$= LEFT$(Entry$, LEN(Entry$)-1)' (* Endleerzeichen entfernen *)
       IF LEN(Entry$)=0 THEN EXIT
     WEND
     IF LEN(Entry$) THEN Pop_Entry_("  "+Entry$,Entry%L,State%)
   ENDIF
 RETURN

 DEF PROC Pop_Line' (* graue Trennlinie *)
   Pop_Entry_("-",Dummy%L,8)
 RETURN

 ' (* Popup-Definition beenden und Resource anlegen *)

 DEF PROC End_Pop(R Pop_Mem%L)
   LOCAL Ob%,Text_Ptr%L,Char%
   IF Pops_% THEN
     IF Pop_Mem%L=0 THEN Pop_Mem%L= MEMORY((Pops_%+1)*24+Pops_%*Pop_Len_%)
     Def_Ob_(Pop_Mem%L,-1,1,Pops_%,20,0,Pop_State_%(0),$FF1100,0,0,Pop_Len_%,Pops_%)
     Rsrc_Obfix(0,Pop_Mem%L)
     FOR Ob%=1 TO Pops_%' (* Text im Speicher ablegen *)
       IF LEN(Pop_Entry_$(Ob%))=1 THEN ' (* ------ *)
         Pop_Entry_$(Ob%)=Pop_Entry_$(Ob%)*Pop_Len_%
       ELSE ' (* normaler Eintrag *)
         IF RIGHT$(Pop_Entry_$(Ob%),1)="" THEN Pop_Entry_$(Ob%)= LEFT$(Pop_Entry_$(Ob%), LEN(Pop_Entry_$(Ob%))-1)
         IF LEN(Pop_Entry_$(Ob%))>=Pop_Len_% THEN
           Pop_Entry_$(Ob%)= LEFT$(Pop_Entry_$(Ob%),Pop_Len_%-1)
           Pop_Entry_$(Ob%)=Pop_Entry_$(Ob%)+" "
         ELSE
           Pop_Entry_$(Ob%)=Pop_Entry_$(Ob%)+ SPACE$(Pop_Len_%- LEN(Pop_Entry_$(Ob%)))
         ENDIF
       ENDIF
       Def_Ob_(Pop_Mem%L+24*Ob%,Ob%+1,-1,-1,28,0,Pop_State_%(Ob%),0,0,Ob%-1,Pop_Len_%,1)
       Ob_Text(Pop_Mem%L,Ob%,Pop_Entry_$(Ob%))
       Rsrc_Obfix(Ob%,Pop_Mem%L)
     NEXT Ob%
     WPOKE Pop_Mem%L+24*(Ob%-1),0' (* Ende des Astes *)
   ENDIF
 RETURN

 ' (* Zuvor definiertes Popup wieder aus dem Speicher entfernen *)

 DEF PROC Clear_Pop(R Pop_Mem%L)
   IF Pop_Mem%L THEN FRE (Pop_Mem%L):Pop_Mem%L=0
 RETURN

 ' (* Hilfsprozedur zum Erzeugen eines Eintrages *)

 DEF PROC Pop_Entry_(Entry$,R Entry%L,State%)
   Pops_%+=1
   Pop_Entry_$(Pops_%)=Entry$
   Pop_State_%(Pops_%)=State%
   Pop_Len_%= MAX( LEN(Entry$)+1,Pop_Len_%)
   Pop_Len_%= MIN(Pop_W_%,Pop_Len_%)
   Entry%L=Pops_%
 RETURN

