 ' Text-POPUP Demo fr den FALCON - ohne Verwendung eines RCS
 ' Version 1.0 fr ST Computer - Februar '93
 ' (C) Kai Michael Speck, Solmser Weg 4, 6332 Ehringshausen

 LIBRARY Gem ,"GEM.LIB"' (* Das Original - am Besten ab Version 3.0 *)
 LIBRARY Gem33 ,"GEM33.LIB"
 LIBRARY Popdef ,"POPDEF.LIB"
 LIBRARY Popup ,"POPUP.LIB"
 LIBRARY Object ,"OBJECT.LIB"

 CLEAR FRE(0)-(64*1024)' (* Alles bis auf 64KB zurck ans GEMDOS *)

 Appl_Init' (* ACHTUNG kein echter Aufruf: *)
 '          (* eignet sich nicht zum ermitteln der AES-Version ! *)

 COMPILER "DEF_CONST"' (* Auszug aus den GEMDefs, als echte Konstanten *)
   Wf_Workxywh%=4
   Beg_Update%=1:End_Update%=0
   Normal%=0:Checked%=4:Disabled%=8
 COMPILER "END_CONST"

 Def_Pop
   Pop_Entry("Info",P_Info%)
   Pop_Line
   Pop_Entry("Laden",P_Laden%)
   Pop_Entry("Sichern",P_Sichern%,Disabled%)
   Pop_Line
   Pop_Entry("Klicks: 0",P_Klicks%)
   Pop_Line
   Pop_Entry("Scrollen",P_Scroll%)
   Pop_Line
   Pop_Entry("Ende",P_Ende%)
 End_Pop(Pop_Ptr%L)

 Wind_Get(0,Wf_Workxywh%,Desk_X%,Desk_Y%,Desk_W%,Desk_H%)' (* Desktopgr”že *)

 Form_Center(Pop_Ptr%L,Box_X%,Box_Y%,Box_W%,Box_H%)' (* Gr”že des Popup ermitteln *)
 Struct_Menu(Menu_Ptr%L,Pop_Ptr%L,0,5,0,0)' (* MENU-Struktur anlegen *)
 Menu_Settings(6)' Scrollh”he = 6

 REPEAT
   REPEAT ' (* Warten, bis linker Mausklick auf Desktop erfolgt ist *)
     Evnt_Button(1,1,1,But%L,Pmx%,Pmy%,Pmb%L,Pks%L)' (* Linksklick *)
     Wind_Find(Pmx%,Pmy%,Win%L)' (* war es der Desktop ? *)
   UNTIL But%L=1 AND Pks%L=0 AND Win%L=0
   Pmx%= MIN(Desk_W%-Box_W%-1,Pmx%)' (* Damit es nicht "raush„ngt" *)
   Wind_Update(Beg_Update%)' (* Auf freien Bildschirm warten & sperren*)
     P_Entry%=FN Popup%(Menu_Ptr%L,Pmx%,Pmy%)
   Wind_Update(End_Update%)' (* Bildschirm wieder freigeben *)
   SELECT P_Entry%
     CASE P_Info%
       FORM_ALERT (1,"[0][|Popup-Demo|(C) AZH'93][OK]",Dummy%L)
       EXIT
     CASE P_Laden%
       Objc_Change(P_Sichern%,Normal%,Pop_Ptr%L)
       Objc_Change(P_Laden%,Disabled%,Pop_Ptr%L)
       EXIT
     CASE P_Sichern%
       Objc_Change(P_Laden%,Normal%,Pop_Ptr%L)
       Objc_Change(P_Sichern%,Disabled%,Pop_Ptr%L)
       EXIT
     CASE P_Klicks%
       Klicks%L= VAL( RIGHT$(FN Ob_Text$(Pop_Ptr%L,P_Klicks%),2))
       Klicks%L=Klicks%L+1: IF Klicks%L>9 THEN Klicks%L=0
       Ob_Text(Pop_Ptr%L,P_Klicks%,"  Klicks:"+ STR$(Klicks%L))
       EXIT
     CASE P_Scroll%
       IF (FN Ob_State%(Pop_Ptr%L,P_Scroll%) AND Checked%)=Checked% THEN
         Objc_Change(P_Scroll%,Normal%,Pop_Ptr%L)
         Mn_Scroll(Menu_Ptr%L,0)' (* Scrollen aus *)
       ELSE
         Objc_Change(P_Scroll%,Checked%,Pop_Ptr%L)
         Mn_Scroll(Menu_Ptr%L,1)' (* Scrollen an *)
       ENDIF
       EXIT
   END_SELECT
 UNTIL P_Entry%=P_Ende%

 Appl_Exit
 END
