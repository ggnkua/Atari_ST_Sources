' Format 1.0  20/4/87
' B.ROCKMANN  Postfach 610356  1000 BERLIN 61
'
Gosub Rsrc_load("format.rsc",*Fehler%) !RSC-Datei laden
If Fehler%=0
  Alert 3," |Ich kann meine RSC-DATEI|     nicht finden !",1,"Mist",Dummy%
  End
Endif
'
Titlew 1,"GfA-FORMAT"
Infow 1," "
Openw 1
Fullw 1
Clearw 1
'
Dialog%=0          ! Baum 0
B_ox1%=1           ! Objekte im Baum
Ib1%=2
Te1%=3
B_kn%=4
A_kn%=5
Ib2%=6
Te2%=7
S_kn%=8
D_kn%=9
Ib3%=10
Te3%=11
N_kn%=12
F_kn%=13
Eingabe%=14
Fo_kn%=15
Ib4%=16
Ab_kn%=17
B_ox2%=18
Te4%=19
Te5%=20
B_ox3%=21
Selected%=&H1                !Objekt-Attribute
Shadowed%=&X100000
Normal%=0
'
Gtype%=Dialog%
Gosub Rsrc_gaddr(Gtype%,Gindex%,*Baum%,*Fehler%) !Baumadresse feststellen
Gtype%=2
Gosub Rsrc_gaddr(Gtype%,Gindex%,*Tedinfo%,*Fehler%) !TEDINFO-Adresse holen
'
Lw%=0
S%=0
Let Form%=1
Gosub Form_center(Baum%,*Fo_cx%,*Fo_cy%,*Fo_cw%,*Fo_ch%) !Boxgroesse ermitteln
Gosub Objc_change(A_kn%,Selected%+Shadowed%,*Fehler%) !default setzen
Gosub Objc_change(S_kn%,Selected%+Shadowed%,*Fehler%)
Gosub Objc_change(N_kn%,Selected%+Shadowed%,*Fehler%)
Do
  Gosub Form_dial(0,0,0,0,0,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Growbox
  Gosub Form_dial(1,0,0,64,32,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Growbox
  Gosub Objc_draw(Baum%,0,5,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Box in allen Ebenen zeichnen
  Do
    Gosub Form_do(Baum%,0,*Ex_obj%) ! Wartet auf den OK-Klick
    If Ex_obj%=A_kn%
      Lw%=0
    Endif
    If Ex_obj%=B_kn%
      Lw%=1
    Endif
    If Ex_obj%=S_kn%
      S%=0
    Endif
    If Ex_obj%=D_kn%
      S%=1
    Endif
    If Ex_obj%=N_kn%
      Let Form%=1
    Endif
    If Ex_obj%=F_kn%
      Let Form%=2
    Endif
    Exit If (Ex_obj%=Fo_kn% Or Ex_obj%=Ab_kn%)
  Loop
  Gosub Form_dial(2,0,0,64,32,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler) !Shrinkbox
  Gosub Form_dial(3,0,0,0,0,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler) !Shrinkbox
  If Ex_obj%=Fo_kn%
    Alert 3," |Formatieren loescht alle|Daten auf Diskette: "+Chr$(Lw%+65),1,"NA UND|RETURN",Dummy%
    If Dummy%=1
      Defmouse 2
      Gosub Objc_change(Ex_obj%,Normal%,*Fehler%)
      Gosub Get_diskname
      Anz%=Form%*3-3+79
      Spt%=Form%+8
      A$=Space$(8000)
      Adr%=Varptr(A$)
      Deffill 1,2,3
      Pcircle 320,80*Xbios(4),80
      Deffill 1,4
      For Trak%=0 To Anz%
        For Side%=0 To S%
          Fehler%=Xbios(10,L:Adr%,L:1,Lw%,Spt%,Trak%,Side%,1,L:&H87654321,&H0)
          Gosub Fehler
          Infow 1,"Seite: "+Str$(Side%)+"   Spur: "+Str$(Trak%)
        Next Side%
        Pcircle 320,80*Xbios(4),80,0,3600/Anz%*(Trak%+1)
      Next Trak%
      Pcircle 320,80*Xbios(4),80
      Infow 1," "
      A$=String$(512,Chr$(&HE5))
      Adr%=Varptr(A$)
      Void Xbios(18,L:Adr%,L:&H1000000,2+S%,0)
      If Form%=2
        Mid$(A$,20,1)=Chr$(&H34)   !Erhoehte Kapazitaet in den Bootsektor schreiben
        Mid$(A$,21,1)=Chr$(3)
        Mid$(A$,25,1)=Chr$(&HA)
        If S%=1
          Mid$(A$,20,1)=Chr$(&H68)
          Mid$(A$,21,1)=Chr$(&H6)
        Endif
      Endif
      Fehler%=Xbios(9,L:Adr%,L:1,Lw%,1,0,0,1)                !Bootspur schreiben
      Gosub Fehler
      A$=String$(512,Chr$(0))
      Mid$(A$,1,1)=Chr$(&HF7)
      Mid$(A$,2,1)=Chr$(255)
      Mid$(A$,3,1)=Chr$(255)
      Adr%=Varptr(A$)
      Fehler%=Xbios(9,L:Adr%,L:1,Lw%,2,0,0,1)                !FAT kennzeichnen
      Gosub Fehler
      Fehler%=Xbios(9,L:Adr%,L:1,Lw%,7,0,0,1)                !    "
      Gosub Fehler
      Dname$=Chr$(Lw%+65)+":\"+Dname$
      Void Gemdos(&H3C,L:Varptr(Dname$),8)                !Namen schreiben
      Defmouse 0
      Alert 0,"Die Diskette: "+Mid$(Dname$,4,Instr(Dname$,Chr$(0))-4)+"|hat: "+Str$(Dfree(Lw%+1))+" Bytes|freien Speicherplatz!",1," OK ",Dummy%
      Clearw 1
    Endif
  Endif
  Exit If Ex_obj%=Ab_kn%
Loop
Closew 1
Closew 0
Cls
Gosub Rsrc_free(*Fehler%)
End
'
' DIALOGBOX-UNTERROUTINEN
'
Procedure Rsrc_load(Rscname$,Aes_return%)
  Lpoke Addrin,Varptr(Rscname$)
  Gemsys 110
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Rsrc_gaddr(Type%,Index%,Aes_adr%,Aes_return%)
  Dpoke Gintin,Type%
  Dpoke Gintin+2,Index%
  Gemsys 112
  *Aes_return%=Dpeek(Gintout)
  *Aes_adr%=Lpeek(Addrout)
Return
'
Procedure Form_center(Baum%,F_cx%,F_cy%,F_cw%,F_ch%)
  Lpoke Addrin,Baum%
  Gemsys 54
  *F_cx%=Dpeek(Gintout+2)
  *F_cy%=Dpeek(Gintout+4)
  *F_cw%=Dpeek(Gintout+6)
  *F_ch%=Dpeek(Gintout+8)
Return
'
Procedure Objc_change(Ob_cobject%,Ob_cnewstate%,Aes_return%)
  Dpoke Gintin,Ob_cobject%
  Dpoke Gintin+2,0
  Dpoke Gintin+4,Fo_cx%
  Dpoke Gintin+6,Fo_cy%
  Dpoke Gintin+8,Fo_cw%
  Dpoke Gintin+10,Fo_ch%
  Dpoke Gintin+12,Ob_cnewstate%
  Dpoke Gintin+14,0
  Lpoke Addrin,Baum%
  Gemsys 47
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Form_dial(Flag%,Littlx%,Littly%,Littlw%,Littlh%,Bigx%,Bigy%,Bigw%,Bigh%,Aes_return%)
  Dpoke Gintin,Flag%
  Dpoke Gintin+2,Littlx%
  Dpoke Gintin+4,Littly%
  Dpoke Gintin+6,Littlw%
  Dpoke Gintin+8,Littlh%
  Dpoke Gintin+10,Bigx%
  Dpoke Gintin+12,Bigy%
  Dpoke Gintin+14,Bigw%
  Dpoke Gintin+16,Bigh%
  Gemsys 51
  *Aes_return%=Dpeek(Gintout)
  If Flag%=3
    Put Bigx%,Bigy%,Rette$
  Endif
Return
'
Procedure Objc_draw(Tree%,Startob%,Depth%,Xclip%,Yclip%,Wclip%,Hclip%,Aes_return%)
  Get Xclip%,Yclip%,Xclip%+Wclip%+1,Yclip%+Hclip%+1,Rette$
  Dpoke Gintin,Startob%
  Dpoke Gintin+2,Depth%
  Dpoke Gintin+4,Xclip%
  Dpoke Gintin+6,Yclip%
  Dpoke Gintin+8,Wclip%
  Dpoke Gintin+10,Hclip%
  Lpoke Addrin,Tree%
  Gemsys 42
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Form_do(Tree,Startob%,Aes_return%)
  Dpoke Gintin,Startob%
  Lpoke Addrin,Tree
  Gemsys 50
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Get_diskname
  Txt$=""
  Txt_adr%=Lpeek(Tedinfo%)
  Te_txtlen%=Dpeek(Tedinfo%+24)-1
  For I=0 To Te_txtlen%
    Zeichen=Peek(Txt_adr%+I)
    Txt$=Txt$+Chr$(Zeichen)
  Next I
  If Len(Txt$)>9
    Txt$=Left$(Txt$,8)+"."+Right$(Txt$,Len(Txt$)-8)
  Endif
  Dname$=""
  For N%=1 To Len(Txt$)
    If Asc(Mid$(Txt$,N%,1))<>32
      Dname$=Dname$+Mid$(Txt$,N%,1)
    Endif
  Next N%
Return
'
Procedure Rsrc_free(Aes_return%)
  Gemsys 111
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Fehler
  If Fehler%<>0
    Alert 3,"Es ist XBIOS-FEHLER|Nr.: "+Str$(Fehler%)+"|aufgetreten!",1,"ABBRUCH",Dummy%
    Closew 1
    Closew 0
    Cls
    Gosub Rsrc_free(*Fehler%)
    End
  Endif
Return
'
