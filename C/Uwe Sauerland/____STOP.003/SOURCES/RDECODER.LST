' Widerstand-Decoder (R_DECODER)  Vers.1.0  19/5/87 B.Rockmann 1000 Berlin 61
'
'
If Xbios(4)=0
  Alert 3,"Dieses Programm l„uft|NUR in mittlerer und hoher|Aufl”sung!!!!",1,"sorry",Dummy%
  End
Endif
'
Gosub Rsrc_load("rdecoder.rsc",*Fehler%) !RSC-Datei laden
If Fehler%=0
  Alert 3," |Ich kann meine RSC-DATEI|     nicht finden !",1,"Mist",Dummy%
  End
Endif
'
Dim Taste%(5)
'
Tree00%=0
Schwarz1%=1
Rot1%=2
Orange1%=3
Gelb1%=4
Blau1%=5
Violett1%=6
Grau1%=7
Weiss1%=8
Braun1%=10
Gruen1%=11
Schwarz2%=12
Braun2%=13
Rot2%=14
Orange2%=15
Gelb2%=16
Gruen2%=17
Blau2%=18
Violett2%=19
Grau2%=20
Weiss2%=21
Schwarz3%=24
Braun3%=25
Rot3%=26
Orange3%=27
Gelb3%=28
Gruen3%=29
Blau3%=30
Gold3%=31
Silber3%=32
Braun4%=35
Rot4%=36
Gruen4%=37
Violett4%=38
Gold4%=39
Silber4%=40
Ok%=42
E_nde%=43
Shadowed%=&X100000
Normal%=0
'
Gtype%=Tree00%
Gosub Rsrc_gaddr(Gtype%,Gindex%,*Baum%,*Fehler%) !Baumadresse feststellen
Gosub Form_center(Baum%,*Fo_cx%,*Fo_cy%,*Fo_cw%,*Fo_ch%) !Boxgroesse ermitteln
Gosub Form_dial(0,0,0,0,0,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Growbox
Gosub Form_dial(1,0,0,64,32,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Growbox
Gosub Objc_draw(Baum%,0,4,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Box in allen Ebenen zeichnen
'
Alert 0,"R-DECODER 1.0  in GfA-BASIC  |        B.ROCKMANN|    Postfach  61 03 56|      1000 BERLIN 61",1,"weiter",Dummy%
Alert 0,"Bitte w„hlen Sie die einzelnen|Farben der vier Ringe auf dem|Widerstand aus und klicken Sie|'OK' an.   Beenden mit 'ENDE'",1,"gelesen",Dummy%
'
Do
  Do
    Gosub Form_do(Baum%,0,*Ex_obj%) ! Wartet auf den OK-Klick
    If Ex_obj%<12
      Taste%(1)=Ex_obj%
    Endif
    If Ex_obj%>=12 And Ex_obj%<=22
      Gosub Change(12,21)
      Taste%(2)=Ex_obj%
    Endif
    If Ex_obj%>=24 And Ex_obj%<=32
      Gosub Change(24,32)
      Taste%(3)=Ex_obj%
    Endif
    If Ex_obj%>=35 And Ex_obj%<=40
      Gosub Change(35,40)
      Taste%(4)=Ex_obj%
    Endif
    If Ex_obj%>40
      Taste%(5)=Ex_obj%
      Gosub Change(42,43)
    Endif
    Exit If Ex_obj%=E_nde%
    Exit If Ex_obj%=Ok% And Taste%(1)<>0 And Taste%(2)<>0 And Taste%(3)<>0
    If Ex_obj%=42
      Gosub Objc_change(Ex_obj%,Normal%+Shadowed%,*Return%)
      Taste%(5)=0
    Endif
  Loop
  If Ex_obj%=E_nde%
    Alert 2," |  Wollen Sie mich|wirklich verlassen ?",2," JA | NEIN ",Dummy%
    If Dummy%=1
      Gosub Form_dial(2,0,0,64,32,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler) !Shrinkbox
      Gosub Form_dial(3,0,0,0,0,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler) !Shrinkbox
      Gosub Rsrc_free(*Return)
      End
    Endif
  Endif
  If Ex_obj%<>E_nde%
    W1%=Taste%(1)*10
    W2%=Taste%(2)-12
    If Taste%(1)=Schwarz1%
      W1%=0
    Endif
    If Taste%(1)>=Blau1% And Taste%<=Weiss1%
      W1%=(Taste%(1)+1)*10
    Endif
    If Taste%(1)=Braun1%
      W1%=Taste%(1)
    Endif
    If Taste%(1)=Gruen1%
      W1%=50
    Endif
    W3=10^(Taste%(3)-24)
    If Taste%(3)=Gold3%
      W3=0.01
    Endif
    If Taste%(3)=Silber3%
      W3=0.1
    Endif
    W1=(W1%+W2%)*W3
    If W1<1000
      Wid$=Str$(W1)+" Ohm"
    Endif
    If W1>=1000 And W1<1000000
      Wid$=Str$(W1/1000)+" KOhm"
    Endif
    If W1>=1000000
      Wid$=Str$(W1/1000000)+" MOhm"
    Endif
    If Taste%(4)=0
      Tol$="20 %"
    Endif
    If Taste%(4)=Silber4%
      Tol$="10 %"
    Endif
    If Taste%(4)=Gold4%
      Tol$="5 %"
    Endif
    If Taste%(4)=Rot4%
      Tol$="2 %"
    Endif
    If Taste%(4)=Braun4%
      Tol$="2 %"
    Endif
    If Taste%(4)=Gruen4%
      Tol$="0.5 %"
    Endif
    If Taste%(4)=Violett4%
      Tol$="0.1 %"
    Endif
    Alert 0,"Der Widerstand hat den Wert:|============================ | |"+Wid$+"     Toleranz: "+Tol$+"|",1,"   OK   ",Dummy%
  Endif
  For N%=1 To 5
    If Taste%(N%)<>0
      Gosub Objc_change(Taste%(N%),Normal%+Shadowed%,*Return)
    Endif
  Next N%
  Tol$=""
  Arrayfill Taste%(),0
Loop
'
Procedure Change(Anf%,End%)
  Do
    If Ex_obj%=Anf%
      Inc Anf%
    Endif
    Exit If Anf%>End%
    Gosub Objc_change(Anf%,Normal%+Shadowed%,*Return%)
    Inc Anf%
  Loop
Return
' DIALOGBOX-UNTERROUTINEN
'
Procedure Rsrc_load(Rscname$,Aes_return%)
  Lpoke Addrin,Varptr(Rscname$)
  Gemsys 110
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Rsrc_gaddr(Type%,Index%,Aes_adr%,Aes_return%)
  Dpoke Gintin,Type%
  Dpoke Gintin+2,Index%
  Gemsys 112
  *Aes_return%=Dpeek(Gintout)
  *Aes_adr%=Lpeek(Addrout)
Return
'
Procedure Form_center(Baum%,F_cx%,F_cy%,F_cw%,F_ch%)
  Lpoke Addrin,Baum%
  Gemsys 54
  *F_cx%=Dpeek(Gintout+2)
  *F_cy%=Dpeek(Gintout+4)
  *F_cw%=Dpeek(Gintout+6)
  *F_ch%=Dpeek(Gintout+8)
Return
'
Procedure Objc_change(Ob_cobject%,Ob_cnewstate%,Aes_return%)
  Dpoke Gintin,Ob_cobject%
  Dpoke Gintin+2,0
  Dpoke Gintin+4,Fo_cx%
  Dpoke Gintin+6,Fo_cy%
  Dpoke Gintin+8,Fo_cw%
  Dpoke Gintin+10,Fo_ch%
  Dpoke Gintin+12,Ob_cnewstate%
  Dpoke Gintin+14,1
  Lpoke Addrin,Baum%
  Gemsys 47
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Form_dial(Flag%,Littlx%,Littly%,Littlw%,Littlh%,Bigx%,Bigy%,Bigw%,Bigh%,Aes_return%)
  Dpoke Gintin,Flag%
  Dpoke Gintin+2,Littlx%
  Dpoke Gintin+4,Littly%
  Dpoke Gintin+6,Littlw%
  Dpoke Gintin+8,Littlh%
  Dpoke Gintin+10,Bigx%
  Dpoke Gintin+12,Bigy%
  Dpoke Gintin+14,Bigw%
  Dpoke Gintin+16,Bigh%
  Gemsys 51
  *Aes_return%=Dpeek(Gintout)
  If Flag%=3
    Put Bigx%,Bigy%,Rette$
    Cls
  Endif
Return
'
Procedure Objc_draw(Tree%,Startob%,Depth%,Xclip%,Yclip%,Wclip%,Hclip%,Aes_return%)
  Get Xclip%,Yclip%,Xclip%+Wclip%+1,Yclip%+Hclip%+0,Rette$
  Dpoke Gintin,Startob%
  Dpoke Gintin+2,Depth%
  Dpoke Gintin+4,Xclip%
  Dpoke Gintin+6,Yclip%
  Dpoke Gintin+8,Wclip%
  Dpoke Gintin+10,Hclip%
  Lpoke Addrin,Tree%
  Gemsys 42
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Form_do(Tree,Startob%,Aes_return%)
  Dpoke Gintin,Startob%
  Lpoke Addrin,Tree
  Gemsys 50
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Rsrc_free(Aes_return%)
  Gemsys 111
  *Aes_return%=Dpeek(Gintout)
Return
'
