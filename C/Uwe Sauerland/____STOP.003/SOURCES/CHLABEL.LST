' CHANGE LABEL Vers. 0.0  14.4.87
'
' Diskettennamen aendern by B.ROCKMANN POSTFACH 610356 1000 BERLIN 61
'
Gosub Rsrc_load("chlabel.rsc",*Fehler%) !RSC-Datei laden
If Fehler%=0
  Alert 3," |Ich kann meine RSC-DATEI|     nicht finden !",1,"Mist",Dummy%
  End
Endif
Gtype%=0
Gosub Rsrc_gaddr(Gtype%,Gindex%,*Baum%,*Fehler%) !Baumadresse feststellen
Gtype%=2
Gosub Rsrc_gaddr(Gtype%,Gindex%,*Tedinfo%,*Fehler%) !TEDINFO-Adresse holen
'
Ch_drive%=Gemdos(&H19) !aktuelles Laufwerk
Do
  Alert 2,"Auf welchen Drive|soll der Name|geÑndert werden ?",0," A | B |RETURN",Dummy%
  If Dummy%=3
    Void Gemdos(&HE,Ch_drive%) !Urspruengliches Laufwerk setzen
    Gosub Rsrc_free(*Fehler)   !Speicher freigeben. Beim Compiler, denkste !!??
    End
  Endif
  Void Gemdos(&HE,Dummy%-1) !gewaehltes Laufwerk setzen
  Sdt$=""
  For N%=0 To 256   !Muss man so fuer den Compiler machen
    Sdt$=Sdt$+" "
  Next N%
  Void Gemdos(&H1A,L:Varptr(Sdt$)) !Disk-Transfer-Speicher einrichten
  N$="*.*"+Chr$(0)
  A%=Gemdos(&H4E,L:Varptr(N$),8) ! schauen ob ein Diskname vorhanden
  If A%=0
    Gosub Get_dialogtext !Edit-Text-Adresse holen
    Dta%=Gemdos(&H2F)    !Diskname aus dem Buffer lesen
    A$=""
    For I%=0 To 43
      A$=A$+Chr$(Peek(Dta%+I%)) !Alten Namen in die Dialogbox schreiben
    Next I%
    B$=Right$(A$,14)
    B$=Left$(B$,12)+Chr$(0)
    Zaehler%=0
    For N%=1 To 12
      If Mid$(B$,N%,1)="."
        While Zaehler%<8
          Poke Txt_adr%+Zaehler%,32
          Inc Zaehler%
        Wend
      Endif
      If Mid$(B$,N%,1)<>"."
        Poke Txt_adr%+Zaehler%,Asc(Mid$(B$,N%,1))
        Inc Zaehler%
      Endif
      Exit If Zaehler%=12
    Next N%
    Void Gemdos(&H3C,L:Varptr(B$),0) !Namen zu einem Normalfile-Namen erklÑren
    L%=Gemdos(&H41,L:Varptr(B$))     !Namen (File) lîschen
  Endif
  '
  Gosub Form_center(Baum%,*Fo_cx%,*Fo_cy%,*Fo_cw%,*Fo_ch%) !Boxgrîûe ermitteln
  Gosub Form_dial(0,0,0,0,0,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Growbox
  Gosub Form_dial(1,0,0,64,32,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Growbox
  Gosub Objc_draw(Baum%,0,5,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler%) !Box in allen Ebenen zeichnen
  Gosub Form_do(Baum%,0,*Ex_obj%) ! Wartet auf den OK-Klick
  Gosub Form_dial(2,0,0,64,32,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler) !Shrinkbox
  Gosub Form_dial(3,0,0,0,0,Fo_cx%,Fo_cy%,Fo_cw%,Fo_ch%,*Fehler) !Shrinkbox
  Gosub Get_dialogtext !Text aus der Dialogbox holen
  If Len(Txt$)>8
    Txt$=Left$(Txt$,8)+"."+Right$(Txt$,Len(Txt$)-8)
  Endif
  C$=""
  For N%=1 To Len(Txt$)
    If Asc(Mid$(Txt$,N%,1))<>32
      C$=C$+Mid$(Txt$,N%,1)
    Endif
  Next N%
  Showm   ! FÅr den Compiler
  Void Gemdos(&H3C,L:Varptr(C$),8) ! neuen Namen auf Disk eintragen
Loop ! und zurÅck
'
' DIALOGBOX-UNTERROUTINEN
'
Procedure Rsrc_load(Rscname$,Aes_return%)
  Lpoke Addrin,Varptr(Rscname$)
  Gemsys 110
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Rsrc_gaddr(Type%,Index%,Aes_adr%,Aes_return%)
  Dpoke Gintin,Type%
  Dpoke Gintin+2,Index%
  Gemsys 112
  *Aes_return%=Dpeek(Gintout)
  *Aes_adr%=Lpeek(Addrout)
Return
'
Procedure Form_center(Baum%,F_cx%,F_cy%,F_cw%,F_ch%)
  Lpoke Addrin,Baum%
  Gemsys 54
  *F_cx%=Dpeek(Gintout+2)
  *F_cy%=Dpeek(Gintout+4)
  *F_cw%=Dpeek(Gintout+6)
  *F_ch%=Dpeek(Gintout+8)
Return
'
Procedure Form_dial(Flag%,Littlx%,Littly%,Littlw%,Littlh%,Bigx%,Bigy%,Bigw%,Bigh%,Aes_return%)
  Dpoke Gintin,Flag%
  Dpoke Gintin+2,Littlx%
  Dpoke Gintin+4,Littly%
  Dpoke Gintin+6,Littlw%
  Dpoke Gintin+8,Littlh%
  Dpoke Gintin+10,Bigx%
  Dpoke Gintin+12,Bigy%
  Dpoke Gintin+14,Bigw%
  Dpoke Gintin+16,Bigh%
  Gemsys 51
  *Aes_return%=Dpeek(Gintout)
  If Flag%=3
    Put Bigx%,Bigy%,Rette$
  Endif
Return
'
Procedure Objc_draw(Tree%,Startob%,Depth%,Xclip%,Yclip%,Wclip%,Hclip%,Aes_return%)
  Get Xclip%,Yclip%,Xclip%+Wclip%+1,Yclip%+Hclip%+1,Rette$
  Dpoke Gintin,Startob%
  Dpoke Gintin+2,Depth%
  Dpoke Gintin+4,Xclip%
  Dpoke Gintin+6,Yclip%
  Dpoke Gintin+8,Wclip%
  Dpoke Gintin+10,Hclip%
  Lpoke Addrin,Tree%
  Gemsys 42
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Form_do(Tree,Startob%,Aes_return%)
  Dpoke Gintin,Startob%
  Lpoke Addrin,Tree
  Gemsys 50
  *Aes_return%=Dpeek(Gintout)
Return
'
Procedure Get_dialogtext
  Txt$=""
  Txt_adr%=Lpeek(Tedinfo%)
  Te_txtlen%=Dpeek(Tedinfo%+24)-1
  For I=0 To Te_txtlen%
    Zeichen=Peek(Txt_adr%+I)
    If Zeichen=0
      I=Te_txtlen%
    Endif
    '  If Chr$(Zeichen)<>"_"
    Txt$=Txt$+Chr$(Zeichen)
    '  Endif
  Next I
Return
'
Procedure Rsrc_free(Aes_return%)
  Gemsys 111
  *Aes_return%=Dpeek(Gintout)
Return
'
