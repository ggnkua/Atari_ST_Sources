'
'    --- Prozent-Anzeige ---
'       von Manuel Hermann
'      fr das TOS-Magazin
'      (c) 1993 ICP-Verlag
'
init_perc_bar("šberschrift")                !max. 30 Zeichen
'
~FORM_DIAL(0,OB_X(perc_adr%,0)-4,OB_Y(perc_adr%,0)-4,OB_W(perc_adr%,0)+8,OB_H(perc_adr%,0)+8,OB_X(perc_adr%,0)-4,OB_Y(perc_adr%,0)-4,OB_W(perc_adr%,0)+8,OB_H(perc_adr%,0)+8)
~WIND_UPDATE(1)
'
REPEAT
  '
  ~EVNT_TIMER(50)                           !warten
  draw_perc_bar(percent&)                   !Prozent-Balken anzeigen
  ADD percent&,5
  '
UNTIL percent&>100                          !bis 100 Prozent
'
~WIND_UPDATE(0)
~FORM_DIAL(3,OB_X(perc_adr%,0)-4,OB_Y(perc_adr%,0)-4,OB_W(perc_adr%,0)+8,OB_H(perc_adr%,0)+8,OB_X(perc_adr%,0)-4,OB_Y(perc_adr%,0)-4,OB_W(perc_adr%,0)+8,OB_H(perc_adr%,0)+8)
'
EDIT
'
'
> PROCEDURE draw_perc_bar(p&)               !Prozent-Balken anzeigen
  '
  LOCAL x&,y&,p$
  '
  IF p&>=0 AND p&<=100
    '
    OB_W(perc_adr%,2)=p&*2
    '
    p$=STR$(p&)
    CHAR{OB_SPEC(perc_adr%,3)}=SPACE$(3-LEN(p$))+p$+"%"
    '
    IF p&=0
      ~OBJC_OFFSET(perc_adr%,0,x&,y&)
      ~OBJC_DRAW(perc_adr%,0,3,x&-4,y&-4,OB_W(perc_adr%,0)+8,OB_H(perc_adr%,0)+8)
    ENDIF
    '
    ~OBJC_OFFSET(perc_adr%,2,x&,y&)
    ~OBJC_DRAW(perc_adr%,2,3,x&-1,y&-1,OB_W(perc_adr%,2)+1,OB_H(perc_adr%,2)+1)
    '
    ~OBJC_OFFSET(perc_adr%,3,x&,y&)
    ~OBJC_DRAW(perc_adr%,0,3,x&,y&,OB_W(perc_adr%,3),OB_H(perc_adr%,3))
    '
  ENDIF
RETURN
> PROCEDURE init_perc_bar(titel$)           !Objektbaum erstellen
  '
  DIM perc_dial&(5*12),perc_str1|(5),perc_str2|(31)
  LOCAL i&
  '
  ' Objektbaum-Daten:
  '
  DATA -1,1,4,20,0,16,$0002,$1100,0,0,$25,5
  DATA 3,2,2,20,0,0,$00ff,$1101,8,3,$19,1
  DATA 1,-1,-1,20,0,0,$00ff,$1141,0,0,6,1
  DATA 4,-1,-1,28,0,0,0,0,3,3,3,1
  DATA 0,-1,-1,28,32,0,0,0,3,1,$1d,1
  '
  FOR i&=0 TO 5*12-1
    READ perc_dial&(i&)                     !Objektbaum-Daten einlesen
  NEXT i&
  '
  perc_adr%=V:perc_dial&(0)
  OB_SPEC(perc_adr%,3)=V:perc_str1|(0)      !Stringadressen festlegen
  OB_SPEC(perc_adr%,4)=V:perc_str2|(0)
  CHAR{OB_SPEC(perc_adr%,3)}="0%"
  CHAR{OB_SPEC(perc_adr%,4)}=LEFT$(titel$,30)
  '
  ' Zeichen-Koordinaten in Pixel umrechnen:
  '
  FOR i&=0 TO 4
    ~RSRC_OBFIX(perc_adr%,i&)
  NEXT i&
  '
  ~FORM_CENTER(perc_adr%,i&,i&,i&,i&)
RETURN
