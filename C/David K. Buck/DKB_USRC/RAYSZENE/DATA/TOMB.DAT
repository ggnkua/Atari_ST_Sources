{ Title-"Tomb" }
{ -+ Compuserve Hall Of Fame award winner +- }
{ A haunted tomb on a hill, gravestones, rusty fence, pumpkin patch. }
{ - Drew Wells CIS 73767,1244 }
{ 11/29/90 }

{ This file is for use with DKBTrace by David Buck
  and is released into the public domain. }

INCLUDE "shapes.dat"
INCLUDE "colors.dat"
INCLUDE "textures.dat"


{ Camera }
VIEW_POINT
   UP  <0.0  1.0  0.0>
   RIGHT <-1.333 0.0 0.0>
   TRANSLATE < #view_x# #view_y# #view_z# > {-15.0  5.0  120.0}
   LOOK_AT <10.0 12.0 55.0>
END_VIEW_POINT

DECLARE Yellow_Clouds = TEXTURE
      0.05
       BOZO
       TURBULENCE 0.6
       COLOUR_MAP
          [0.0 0.5   COLOUR RED 0.9 GREEN 0.5  BLUE 0.3
                     COLOUR RED 0.4 GREEN 0.4  BLUE 0.0]  
          [0.5 0.6   COLOUR RED 1.0 GREEN 1.0  BLUE 1.0 ALPHA 1.0
                     COLOUR RED 1.0 GREEN 1.0  BLUE 1.0 ALPHA 1.0 ]
          [0.6 1.001 COLOUR RED 1.0 GREEN 1.0  BLUE 1.0 ALPHA 1.0
                     COLOUR RED 1.0 GREEN 1.0  BLUE 1.0 ALPHA 1.0]
       END_COLOUR_MAP
END_TEXTURE

DECLARE Sunset_Sky = TEXTURE
      0.05
       GRADIENT <0.0 1.0 0.0>
       
       COLOUR_MAP
          [0.0 0.4  {blue}
                    COLOUR RED 0.8 GREEN 0.0 BLUE 0.0
                    COLOUR RED 0.4 GREEN 0.0 BLUE 0.4]
          
          [0.4 0.6  COLOUR RED 0.4 GREEN 0.0 BLUE 0.4
                    {white}
                    COLOUR RED 0.0 GREEN 0.0 BLUE 0.2]
          [0.6 1.001 COLOUR RED 0.0 GREEN 0.0 BLUE 0.2
                    {grey}
                    COLOUR RED 0.0 GREEN 0.0 BLUE 0.0]
       END_COLOUR_MAP
       SCALE <700.0 700.0 700.0>
END_TEXTURE

DECLARE Moss = TEXTURE
    MARBLE
    TURBULENCE 0.62
    COLOUR_MAP
   [0.0 0.5  COLOUR RED 0.7 GREEN 0.7 BLUE 0.45
    COLOUR RED 0.7 GREEN 0.65 BLUE 0.35]
   [0.5 0.55 COLOUR RED 0.55 GREEN 0.7 BLUE 0.45
    COLOUR RED 0.45 GREEN 0.60 BLUE 0.35]
   [0.55 0.6 COLOUR RED 0.0 GREEN 0.50 BLUE 0.20
    COLOUR RED 0.5 GREEN 0.6 BLUE 0.35]
   [0.6 0.7  COLOUR RED 0.5 GREEN 0.6 BLUE 0.35
    COLOUR RED 0.05 GREEN 0.35 BLUE 0.05]
   [0.7 0.8  COLOUR RED 0.05 GREEN 0.35 BLUE 0.05
    COLOUR RED 0.20 GREEN 0.30 BLUE 0.0]
   [0.8 0.9  COLOUR RED 0.20 GREEN 0.30 BLUE 0.0
    COLOUR RED 0.20 GREEN 0.50 BLUE 0.0]
   [0.9 1.001  COLOUR RED 0.20 GREEN 0.50 BLUE 0.00
    COLOUR RED 0.20 GREEN 0.30 BLUE 0.0]
    END_COLOUR_MAP
    SCALE <1.7 1.7 1.7>
END_TEXTURE


{***********************************************}
{ Define objects for use in scene               }
{***********************************************}

DECLARE Cross =
  OBJECT
    UNION
    { Tried to make celtic cross, but vga resolution is too coarse }
    { INTERSECTION
     QUADRIC Cylinder_Z
        SCALE <0.4 0.4 0.4>
        TRANSLATE <0.0 0.45 0.0>
     END_QUADRIC
     PLANE <0.0 0.0 -1.0>  0.02 END_PLANE
     PLANE <0.0  0.0 1.0> 0.02 END_PLANE
     END_INTERSECTION }

     {vertical part}
     INTERSECTION
      {top & bottom}
      PLANE <0.0 1.0 0.0> 1.0 END_PLANE
      PLANE <0.0 -1.0 0.0> 2.0 END_PLANE
      {front & back}
      PLANE <0.0 0.0 1.0> 0.12 END_PLANE
      PLANE <0.0 0.0 -1.0> 0.12 END_PLANE
      {left & right}
      PLANE <1.0 0.0 0.0> 0.15 END_PLANE
      PLANE <-1.0 0.0 0.0> 0.15 END_PLANE
     END_INTERSECTION
 
     {horizontal part}
     INTERSECTION
      {top & bottom}
      PLANE <0.0 1.0 0.0> 0.6 END_PLANE
      PLANE <0.0 -1.0 0.0> -0.3 END_PLANE
      {front & back}
      PLANE <0.0 0.0 1.0> 0.12 END_PLANE
      PLANE <0.0 0.0 -1.0> 0.12 END_PLANE
      {left & right walls}
      PLANE <1.0 0.0 0.0> 0.7 END_PLANE
      PLANE <-1.0 0.0 0.0> 0.7 END_PLANE
     END_INTERSECTION
    END_UNION
    BOUNDED_BY
     INTERSECTION
      PLANE <0.0 1.0 0.0> 1.1 END_PLANE
      PLANE <0.0 -1.0 0.0> 2.1 END_PLANE
      PLANE <0.0 0.0 1.0> 0.2 END_PLANE
      PLANE <0.0 0.0 -1.0> 0.2 END_PLANE
      PLANE <1.0 0.0 0.0> 0.8 END_PLANE
      PLANE <-1.0 0.0 0.0> 0.8 END_PLANE
     END_INTERSECTION
    END_BOUND
    TEXTURE
      GRANITE
      SCALE <0.1 0.1 0.1>
      AMBIENT 0.1
      DIFFUSE 0.9   
    END_TEXTURE
    COLOUR Blue
END_OBJECT

DECLARE Headstone =
  OBJECT
    UNION
     INTERSECTION      
       QUADRIC Cylinder_Z END_QUADRIC
       PLANE <0.0 0.0 -1.0>  0.1 END_PLANE
       PLANE <0.0  0.0 1.0> 0.1 END_PLANE
     END_INTERSECTION      
     INTERSECTION
       {top & bottom} 
       PLANE <0.0 1.0 0.0> 0.0 END_PLANE
       PLANE <0.0 -1.0 0.0> 2.0 END_PLANE
       {front & back}
       PLANE <0.0 0.0 1.0> 0.1 END_PLANE
       PLANE <0.0 0.0 -1.0> 0.1 END_PLANE
       {left & right }
       PLANE <1.0 0.0 0.0> 1.0 END_PLANE
       PLANE <-1.0 0.0 0.0> 1.0 END_PLANE
     END_INTERSECTION
    END_UNION
    TEXTURE 
      GRANITE 
      SCALE<0.1 0.2 0.1>
      AMBIENT 0.1
      DIFFUSE 0.9   
    END_TEXTURE
   COLOUR Blue
END_OBJECT

DECLARE Beam = QUADRIC Cylinder_Y
    SCALE <1.0 1.0 1.0>
END_QUADRIC

DECLARE Beam2 = QUADRIC Cylinder_Y
     INVERSE
     SCALE <0.5 1.0 0.5>
    TRANSLATE <1.4 0.0 0.0>
END_QUADRIC

DECLARE Beam3 =
  OBJECT
   INTERSECTION
     QUADRIC Beam  SCALE <1.2 1.0 1.2> END_QUADRIC
     QUADRIC Beam2 END_QUADRIC
     QUADRIC Beam2 ROTATE <0.0  -45.0 0.0> END_QUADRIC
     QUADRIC Beam2 ROTATE <0.0  -90.0 0.0> END_QUADRIC
     QUADRIC Beam2 ROTATE <0.0 -135.0 0.0> END_QUADRIC
     QUADRIC Beam2 ROTATE <0.0  180.0 0.0> END_QUADRIC
     QUADRIC Beam2 ROTATE <0.0   45.0 0.0> END_QUADRIC
     QUADRIC Beam2 ROTATE <0.0   90.0 0.0> END_QUADRIC
     QUADRIC Beam2 ROTATE <0.0  135.0 0.0> END_QUADRIC
     PLANE <0.0 1.0 0.0> 8.0 END_PLANE
     PLANE <0.0 -1.0 0.0> 0.0 END_PLANE
   END_INTERSECTION
   { Blue marble was modified to look like green moss/mold }
   TEXTURE
     Moss
     AMBIENT 0.1
     DIFFUSE 0.99
   END_TEXTURE
   COLOUR RED 0.8 GREEN 0.0 BLUE 0.0
END_OBJECT

DECLARE Pole = QUADRIC Cylinder_Y
   SCALE <0.1 1.0 0.1>
END_QUADRIC

     
DECLARE Xpole = QUADRIC Cylinder_X
   SCALE <0.1 0.1 0.1>
END_QUADRIC

{ Rusty iron gate & fence - object should have been composite to avoid the }
{                           "carved from one piece" look. }
DECLARE Gate = OBJECT
  UNION
   INTERSECTION
     QUADRIC Xpole  END_QUADRIC
     PLANE <1.0 0.0 0.0> 8.0 END_PLANE
     PLANE <-1.0 0.0 0.0> 2.0 END_PLANE
     TRANSLATE <0.0 6.5 0.0>
   END_INTERSECTION
   INTERSECTION
     QUADRIC Xpole  END_QUADRIC
     PLANE <1.0 0.0 0.0> 8.0 END_PLANE
     PLANE <-1.0 0.0 0.0> 2.0 END_PLANE
     TRANSLATE <0.0 1.9 0.0>
   END_INTERSECTION
   INTERSECTION
     QUADRIC Pole  END_QUADRIC
     PLANE <0.0 1.0 0.0> 7.5 END_PLANE
     PLANE <0.0 -1.0 0.0> 0.0 END_PLANE
     TRANSLATE <1.0 0.0 0.2>
   END_INTERSECTION
   SPHERE <1.0 7.5 0.0> 0.3 END_SPHERE
   INTERSECTION
     QUADRIC Pole END_QUADRIC
     PLANE <0.0 1.0 0.0> 7.75 END_PLANE
     PLANE <0.0 -1.0 0.0> 0.2 END_PLANE
     TRANSLATE <2.5 0.0 0.0>
   END_INTERSECTION
   SPHERE <2.5 7.75 0.0> 0.3 END_SPHERE
   INTERSECTION
     QUADRIC Pole END_QUADRIC
     PLANE <0.0 1.0 0.0> 8.0 END_PLANE
     PLANE <0.0 -1.0 0.0> 0.2 END_PLANE
     TRANSLATE <4.0 0.0 0.0>
   END_INTERSECTION
   SPHERE <4.0 8.0 0.0> 0.3 END_SPHERE
   INTERSECTION
     QUADRIC Pole  END_QUADRIC
     PLANE <0.0 1.0 0.0> 7.75 END_PLANE
     PLANE <0.0 -1.0 0.0> 0.2 END_PLANE
     TRANSLATE<5.5 0.0 0.0>
   END_INTERSECTION
   SPHERE <5.5 7.75 0.0> 0.3 END_SPHERE
   INTERSECTION
     QUADRIC Pole END_QUADRIC
     PLANE <0.0 1.0 0.0> 7.5 END_PLANE
     PLANE <0.0 -1.0 0.0> 0.2 END_PLANE
     TRANSLATE <7.0 0.0 0.0>
   END_INTERSECTION
   SPHERE <7.0 7.5 0.0> 0.3 END_SPHERE
  END_UNION
  BOUNDED_BY
   INTERSECTION
      PLANE <0.0 1.0 0.0> 9.0 END_PLANE
      PLANE <0.0 -1.0 0.0> 0.0 END_PLANE
      PLANE <0.0 0.0 1.0> 1.0 END_PLANE
      PLANE <0.0 0.0 -1.0> 1.0 END_PLANE
      PLANE <1.0 0.0 0.0> 8.0 END_PLANE
      PLANE <-1.0 0.0 0.0> 0.0 END_PLANE
   END_INTERSECTION
  END_BOUND
  TEXTURE 
    0.05
    Iron 
    AMBIENT 0.4
    DIFFUSE 0.99   
  END_TEXTURE 
  COLOUR Blue
END_OBJECT

{ A rectangular pad to serve as a footing for the column }
DECLARE BasePad =
   OBJECT
   INTERSECTION
      PLANE <0.0 1.0 0.0> 0.25 END_PLANE
      PLANE <0.0 -1.0 0.0> 0.25 END_PLANE
      PLANE <0.0 0.0 1.0> 1.2 END_PLANE
      PLANE <0.0 0.0 -1.0> 1.2 END_PLANE
      PLANE <1.0 0.0 0.0> 1.2 END_PLANE
      PLANE <-1.0 0.0 0.0> 1.2 END_PLANE
   END_INTERSECTION

   TEXTURE
     Moss
     SCALE <0.8 0.3 1.0>
     AMBIENT 0.1
     DIFFUSE 0.9
   END_TEXTURE
   COLOUR RED 0.6 GREEN 0.6 BLUE 0.4
END_OBJECT

DECLARE Column = COMPOSITE
   OBJECT Beam3   TRANSLATE <0.0 0.0 0.0> END_OBJECT
   OBJECT BasePad TRANSLATE <0.0 1.0 0.0> END_OBJECT
   OBJECT BasePad TRANSLATE <0.0 8.0 0.0> END_OBJECT
END_COMPOSITE

DECLARE Tomb = OBJECT
  UNION
   { Main structure }
   INTERSECTION
      {ceiling and floor}
      PLANE <0.0 1.0 0.0> 10.0 END_PLANE
      PLANE <0.0 -1.0 0.0> 6.0 END_PLANE
      {front and back walls}
      PLANE <0.0 0.0 1.0> 7.5 END_PLANE
      PLANE <0.0 0.0 -1.0> 7.5 END_PLANE
      {left & right walls}
      PLANE <1.0 0.0 0.0> 10.0 END_PLANE
      PLANE <-1.0 0.0 0.0> 10.0 END_PLANE
      {doorway}
      INTERSECTION
        {ceiling and floor} 
        PLANE <0.0 1.0 0.0> 7.0 END_PLANE
        PLANE <0.0 -1.0 0.0> 6.0 END_PLANE
        {front and back walls}
        PLANE <0.0 0.0 1.0> 17.5 END_PLANE
        PLANE <0.0 0.0 -1.0> 7.0 END_PLANE
        {left & right walls}
        PLANE <1.0 0.0 0.0> 3.0 END_PLANE
        PLANE <-1.0 0.0 0.0> 3.0 END_PLANE
        INVERSE 
      END_INTERSECTION
   END_INTERSECTION
   { Foundation }
   INTERSECTION
      {top & bottom}
      PLANE <0.0 1.0 0.0> -1.0 END_PLANE
      PLANE <0.0 -1.0 0.0> 5.0 END_PLANE
      {front & back}
      PLANE <0.0 0.0 1.0> 8.5 END_PLANE
      PLANE <0.0 0.0 -1.0> 8.5 END_PLANE
      {left & right}
      PLANE <1.0 0.0 0.0> 11.0 END_PLANE
      PLANE <-1.0 0.0 0.0> 11.0 END_PLANE
   END_INTERSECTION
  END_UNION
  BOUNDED_BY
   SPHERE <0.0 0.0 0.0> 18.0 END_SPHERE
  END_BOUND
  TEXTURE
       GRANITE
       BUMPS 0.8
       SCALE < 0.5 0.1 1.0>
       AMBIENT 0.1
       DIFFUSE 0.9
  END_TEXTURE
  COLOUR Blue
END_OBJECT

DECLARE InnerBeams = OBJECT
   UNION
    INTERSECTION
     QUADRIC Beam END_QUADRIC
     PLANE <0.0 1.0 0.0> 8.0 END_PLANE
     PLANE <0.0 -1.0 0.0> 2.0 END_PLANE
     TRANSLATE < 5.0 0.0 7.5>
    END_INTERSECTION
    INTERSECTION
     QUADRIC Beam END_QUADRIC
     PLANE <0.0 1.0 0.0> 8.0 END_PLANE
     PLANE <0.0 -1.0 0.0> 2.0 END_PLANE
     TRANSLATE < -5.0 0.0 7.5>
    END_INTERSECTION
   END_UNION
   TEXTURE
      GRANITE
      BUMPS 0.5
      SCALE < 0.5 0.6 1.0>
      AMBIENT 0.10
      DIFFUSE 0.9
   END_TEXTURE
   COLOUR Blue
END_OBJECT

DECLARE Pointy = OBJECT
   UNION
    {pointy part}
    INTERSECTION
      PLANE <0.0 -1.0 0.0> 1.0 END_PLANE
      PLANE <0.0 0.0 1.0> 0.5 END_PLANE
      PLANE <0.0 0.0 -1.0> 0.5 END_PLANE
      PLANE <1.0 0.0 0.0> 0.0 ROTATE <0.0 0.0 70.0> END_PLANE
      PLANE <-1.0 0.0 0.0> 0.0 ROTATE <0.0 0.0 -70.0> END_PLANE
      SCALE <3.0 4.0 1.0>
      TRANSLATE < 0.0 12.0 7.5>
    END_INTERSECTION
   END_UNION
   TEXTURE
      GRANITE
      BUMPS 0.5
      SCALE < 0.7 0.5 0.33>
      AMBIENT 0.10
      DIFFUSE 0.9
   END_TEXTURE
   COLOUR Blue
END_OBJECT

DECLARE CornerBeams = OBJECT
  UNION
    INTERSECTION
      QUADRIC Beam SCALE <1.5 1.0 1.5>END_QUADRIC
      PLANE <0.0 1.0 0.0> 10.0 END_PLANE
      PLANE <0.0 -1.0 0.0> 2.0 END_PLANE
      TRANSLATE < 10.0 0.0 7.5>
    END_INTERSECTION
    INTERSECTION      
      QUADRIC Beam SCALE <1.5 1.0 1.5> END_QUADRIC
      PLANE <0.0 1.0 0.0> 10.0 END_PLANE
      PLANE <0.0 -1.0 0.0> 2.0 END_PLANE
      TRANSLATE < -10.0 0.0 7.5>
    END_INTERSECTION
    SPHERE < -10.0 10.0 7.5> 1.5 END_SPHERE
    SPHERE <  10.0 10.0 7.5> 1.5 END_SPHERE
  END_UNION
  TEXTURE
      GRANITE
      BUMPS 0.5
      SCALE < 0.3 0.1 1.0>
      AMBIENT 0.10
      DIFFUSE 0.9
  END_TEXTURE
  COLOUR Blue
END_OBJECT

{ Ghost in tomb doorway }
DECLARE Figure = OBJECT
  INTERSECTION
     QUADRIC Beam SCALE <2.3 1.0 2.3> END_QUADRIC
     PLANE <0.0 1.0 0.0> 8.0 END_PLANE
     PLANE <0.0 -1.0 0.0> 2.0 END_PLANE
     TRANSLATE < 0.0 0.0 4.3>
  END_INTERSECTION
  TEXTURE
     GRANITE
     { Use any ghost image you like for the tomb doorway or comment }
     { out the Figure in TombAll }
     {IMAGEMAP <-1.0 1.0 0.0> GIF "ghost.gif" ONCE}
     SCALE < 6.5 8.0 6.5 >
     TRANSLATE < -1.0 7.5 4.3 >
     AMBIENT 0.10
     DIFFUSE 0.3
   END_TEXTURE
   COLOUR Blue
END_OBJECT

{ TombAll is the completed tomb }
DECLARE TombAll = COMPOSITE
   OBJECT Tomb END_OBJECT
   OBJECT InnerBeams END_OBJECT
   OBJECT Pointy END_OBJECT
   OBJECT CornerBeams END_OBJECT
   OBJECT Figure END_OBJECT
END_COMPOSITE

{ Pumpkin parts }
DECLARE Stem = OBJECT
  INTERSECTION
    QUADRIC Pole  END_QUADRIC
    PLANE <0.0 1.0 0.0> 0.04 ROTATE <0.0 -10.0 -10.0> END_PLANE
    PLANE <0.0 -1.0 0.0> 1.0 END_PLANE
    TRANSLATE<0.0 0.3 0.0>
  END_INTERSECTION
  TEXTURE
    BUMPS 0.5
    COLOR RED 0.04 GREEN 0.33 BLUE 0.05
    SCALE < 0.2 0.2 0.2>
    AMBIENT 0.1
    DIFFUSE 0.9   
  END_TEXTURE
  COLOR RED 0.04 GREEN 0.33 BLUE 0.05
END_OBJECT

DECLARE Slice = QUADRIC
  Sphere
  TRANSLATE <0.0 0.0 0.5>
  SCALE <0.28 0.30 0.40>
END_QUADRIC

DECLARE Pumpkin = OBJECT
  UNION
    QUADRIC Slice END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -20.0  0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -40.0  0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -60.0  0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -80.0  0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -100.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -120.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -140.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -160.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -180.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -200.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -220.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -240.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -260.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -280.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -300.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -320.0 0.0> END_QUADRIC
    QUADRIC Slice ROTATE <0.0 -340.0 0.0> END_QUADRIC
  END_UNION
  BOUNDED_BY
      QUADRIC Sphere SCALE <1.5 1.5 1.5> END_QUADRIC
  END_BOUND
  TEXTURE
    BUMPS 0.3
    COLOR RED 0.5 GREEN 0.22 BLUE 0.1
    SCALE < 0.1 0.1 0.1>
    AMBIENT 0.1
    DIFFUSE 0.9
    PHONG 0.75
    PHONGSIZE 30.0
  END_TEXTURE
  COLOR RED 0.5 GREEN 0.22 BLUE 0.1
END_OBJECT


{*****************************************}
{ Scene description     }
{*****************************************}

OBJECT
   Cross
   SCALE <1.7 2.0 1.0>
   ROTATE <-15.0 0.0 0.0>
   TRANSLATE <-16.0 7.7 61.5>    
END_OBJECT

OBJECT
   Headstone
   SCALE <1.0 1.0 1.0>
   ROTATE <15.0 0.0 0.0>
   TRANSLATE <-14.0 4.0 76.0>
END_OBJECT

OBJECT
   Headstone
   SCALE <1.0 1.0 1.0>
   TRANSLATE <12.0 4.0 74.0>
END_OBJECT

OBJECT
   Headstone
   SCALE <1.0 1.0 1.0>
   ROTATE <-10.0 0.0 0.0>
   TRANSLATE <18.0 8.0 70.0>
END_OBJECT

OBJECT
   Cross
   SCALE <1.5 2.0 1.0>
   ROTATE <-15.0 0.0 0.0>
   TRANSLATE <17.0 10.0 60.0>
END_OBJECT

OBJECT
   Cross
   SCALE <1.5 2.0 1.0>
   ROTATE <-15.0 0.0 20.0>
   TRANSLATE <26.0 10.0 70.0>
END_OBJECT

OBJECT
   Cross
   SCALE <1.5 2.0 1.0>
   ROTATE <-15.0 0.0 -10.0>
   TRANSLATE <31.0 10.0 78.0>
END_OBJECT


{ Little Pumpkin Patch }
OBJECT
   Pumpkin
   SCALE <1.5 2.0 1.5>
   TRANSLATE < 5.0 1.55 95.0>
END_OBJECT
OBJECT
   Stem
   SCALE <1.5 2.5 1.5>
   TRANSLATE < 5.0 1.55 95.0>
END_OBJECT

OBJECT
   Pumpkin
   SCALE <1.5 2.5 1.5>
   TRANSLATE < 11.0 1.6 90.0>
END_OBJECT
OBJECT
   Stem
   SCALE <1.5 2.5 1.5>
   TRANSLATE < 11.0 1.6 90.0>
END_OBJECT


{ Ghastly Tomb!}
COMPOSITE
   TombAll
   ROTATE <15.0 15.0 0.0>
   TRANSLATE <0.0 10.0 58.0>
END_COMPOSITE

{left entry gate}
OBJECT
   Gate
   TRANSLATE <-9.0 0.0 88.0>
END_OBJECT

{right entry gate}
OBJECT
   Gate
   ROTATE <-10.0 -160.0 0.0>
   TRANSLATE <7.0 0.0 87.5>
END_OBJECT

{Use gate object to make left & right fence}
OBJECT
   Gate
   TRANSLATE <-19.0 0.0 88.0>
END_OBJECT
OBJECT
   Gate
   TRANSLATE <-31.0 0.0 88.0>
END_OBJECT
OBJECT
   Gate
   TRANSLATE <9.0 0.0 88.0>
END_OBJECT
OBJECT
   Gate
   TRANSLATE <19.0 0.0 88.0>
END_OBJECT

{ Columns to hold the fence and gates up }
COMPOSITE Column TRANSLATE <-20.0 0.0 88.0> END_COMPOSITE
COMPOSITE Column TRANSLATE <-10.0 0.0 88.0> END_COMPOSITE
COMPOSITE Column TRANSLATE < 8.0 0.0 88.0> END_COMPOSITE
COMPOSITE Column TRANSLATE < 18.0 0.0 88.0> END_COMPOSITE

{hill under tomb}
OBJECT
    QUADRIC Paraboloid_Y
      SCALE<40.0 10.0 77.0>
    END_QUADRIC
    ROTATE <0.0 0.0 180.0>
    TRANSLATE <0.0 21.0 -28.0>
    TEXTURE
      BUMPS 0.8
      COLOR RED 0.5 GREEN 0.6 BLUE 0.2
      SCALE < 5.0 5.0 5.0>
      AMBIENT 0.1
      DIFFUSE 0.7    
    END_TEXTURE                                      
    COLOR RED 0.5 GREEN 0.6 BLUE 0.2
END_OBJECT

{hill to right of tomb}
OBJECT
    QUADRIC Paraboloid_Y
      SCALE<30.0 10.0 40.0>
    END_QUADRIC
    ROTATE <0.0 0.0 180.0>
    TRANSLATE <40.0 14.0 50.0>
    TEXTURE
      BUMPS 0.8
      COLOR RED 0.6 GREEN 0.6 BLUE 0.1
      SCALE < 7.0 5.0 5.0>
      AMBIENT 0.1
      DIFFUSE 0.7
    END_TEXTURE
    COLOR RED 0.6 GREEN 0.6 BLUE 0.1
END_OBJECT

{ Ground }
OBJECT
    PLANE <0.0 1.0 0.0> 1.0 END_PLANE
    TEXTURE
      BUMPS 0.7
      COLOR RED 0.6 GREEN 0.6 BLUE 0.1
      SCALE < 1.0 1.0 1.0>
      AMBIENT 0.1
      DIFFUSE 0.7
    END_TEXTURE
    COLOR RED 0.6 GREEN 0.6 BLUE 0.1
END_OBJECT

{The Sun}
OBJECT
  SPHERE <0.0 0.0 0.0> 150.0 END_SPHERE
  TRANSLATE <150.0 30.0 1200.0>
  TEXTURE
    COLOR White
    AMBIENT 1.0
    DIFFUSE 0.0
  END_TEXTURE
  LIGHT_SOURCE
  COLOR White
END_OBJECT

{ Sky - gradient sunset}
OBJECT
  SPHERE <0.0 0.0 0.0> 2000.0 INVERSE END_SPHERE
   TEXTURE
     Sunset_Sky
     TRANSLATE <0.0 200.0 0.0>
     SCALE <1.2 1.2 1.2>
     AMBIENT 0.6
     DIFFUSE 0.0
   END_TEXTURE
   COLOR Green
END_OBJECT

{ Clouds - uses a sky texture with the sky portion defined as transparent }
{          so the gradient behind it is visible  }
OBJECT
   SPHERE <0.0 0.0 0.0> 1997.0 INVERSE END_SPHERE
   TEXTURE
    Yellow_Clouds
    SCALE <1000.0 30.0 100.0>
    AMBIENT 0.6
    DIFFUSE 0.0
   END_TEXTURE
   COLOR Green
END_OBJECT
