
{
*   SPIN.DAT 
*
*   Written by: Sandy Bostic		    
*		sbostic@baks.bell-atl.com
*
*		Jim Parker
*		jparker@baks.bell-atl.com
*
*   DKBTrace scene description for our first animation.  A vertical brass  
*   pole with a cube at the bottom and a mirrored sphere at the top.
*   Between the cube and pole are two rectangular cubes that rotate in
*   different directions.  The cloud texture on the cube rotates and the
*   rippled water on the floor...well...ripples.
*
*   Note: If you don't have rayscene or the .arr file is not included, or you
*   simply don't want to take the time to trace every frame, replace the
*   variables (#phase# for example) with the values in comments to trace the
*   first frame only.
*
*   This file is released into the public domain and may be used or altered
*   by anyone as desired.  We would appreciate it if any cool changes could
*   be sent to us.
*
*
}



INCLUDE "colors.dat"
INCLUDE "shapes.dat"
INCLUDE "textures.dat"

    {we are looking straight out the positive z axis}

VIEW_POINT
   LOCATION  <0 0 0>
   DIRECTION <0 0 1>
   UP        <0 1 0>
   RIGHT     <1.33333 0 0>
END_VIEW_POINT

    {this is a mirrored sphere}

OBJECT
   SPHERE <0 3.5 10> 1.25 END_SPHERE
   TEXTURE
      Mirror
   END_TEXTURE
   ROTATE <0 0 0>
   COLOR LightBlue
END_OBJECT

    {this is a clouded textured cube}

OBJECT
   INTERSECTION
      Cube
   END_INTERSECTION
   TEXTURE
      Cloud_Sky
      ROTATE <0 #rtext# 0>    {start at 0}
   END_TEXTURE
   COLOR LightBlue
   ROTATE <0 45 0>
   TRANSLATE <0 -3.5 10>
END_OBJECT

    {this is the central brass cylinder}

OBJECT
   INTERSECTION
      QUADRIC Cylinder_Y END_QUADRIC
      PLANE <0 1 0> 1 END_PLANE
      PLANE <0 -1 0> 1 END_PLANE
   END_INTERSECTION
   TEXTURE
      Brass
   END_TEXTURE
   COLOR OldGold
   SCALE <0.25 3.5 0.25>
   TRANSLATE <0 0 10>
END_OBJECT

    {inner red marble slab}

OBJECT
   INTERSECTION
      Cube
      SCALE <2 1 0.5>
   END_INTERSECTION
   TEXTURE
      Red_Marble
      REFLECTION 0.3
   END_TEXTURE
   COLOR Red
   ROTATE <0 #rislab# 0>      {start at 0}
   TRANSLATE <0 0 10>
END_OBJECT

    {outer picture frame shaped blue agate slab}

OBJECT
   DIFFERENCE
      INTERSECTION
         Cube
         SCALE <3.5 2 0.5>
      END_INTERSECTION
      INTERSECTION
         Cube
         SCALE <2.5 1.5 1>
      END_INTERSECTION
   END_DIFFERENCE
   TEXTURE
      Blue_Agate
      REFLECTION 0.3
   END_TEXTURE
   COLOR Blue
   ROTATE <0 #roslab# 0>       {start at 352.8}
   TRANSLATE <0 0 10>
END_OBJECT

    {water with ripples}

OBJECT
   PLANE <0 1 0> -10 END_PLANE
   TEXTURE
      COLOR DarkTurquoise
      WAVES 0.05
      FREQUENCY 100.0
      PHASE #phase#             {start at 0}
      SCALE <60 60 60>
      REFLECTION 1.0
   END_TEXTURE
   COLOR DarkTurquoise
END_OBJECT

    {put a floor underneath to catch any errant rays from the ripples}

OBJECT
   PLANE <0 1 0> -11 END_PLANE
   TEXTURE
      COLOR DarkTurquoise
      AMBIENT 1.0
      DIFFUSE 0.0
   END_TEXTURE
   COLOR DarkTurquoise
END_OBJECT

    {traditional checkerboard background}

OBJECT
   PLANE <0 0 1> 40 END_PLANE
   TEXTURE
      CHECKER
         COLOR RED 1.0
         COLOR BLUE 1.0
      ROTATE <-90 0 0>
      SCALE <5 5 5>
   END_TEXTURE
   COLOR Orange
END_OBJECT

    {let there be light!}

OBJECT
   SPHERE <0 0 0> 1 END_SPHERE
   TEXTURE
      COLOR White
   END_TEXTURE
   TRANSLATE <5 3 5>
   LIGHT_SOURCE
   COLOR White
END_OBJECT
