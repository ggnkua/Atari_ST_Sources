'
'   INTERFAC.GFA
'
'   GFA-Basic Version 3.5E D
'
'
'
'
'   Interface-Proceduren und -Funktionen zum Einbinden in eigene Programme
'
'
ON BREAK GOSUB last
'
ALERT 1,"RESET-Taste|    des| Interface| bet„tigen !",1," OK ",but%
'
PROCEDURE first               !  Dieses Unterprogramm muž immer als erstes
  '                              aufgerufen werden. Hier wird die Assembler-
  '                              routine in den Speicher geladen und alle
  '                              Variablen gesetzt.
  '
  '                              Aufruf durch 'GOSUB first' oder '@first'
  '                              oder nur durch 'first'
  '
  DIM bin_code%(500),regs%(16)
  '
  adr%=VARPTR(bin_code%(0))
  '
  CHDRIVE "A:\"
  BLOAD "INTERFAC.MOD",adr%
  '
  index%=0
  '
  WHILE (index%<500) AND (bin_code%(index%)<>&H4AFCDB9F)
    index%=index%+1
  WEND
  '
  IF index%=500 THEN
    ALERT 3,"Startadresse nicht gefunden",1,"Dann nicht",index%
    END
  ENDIF
  '
  init%=VARPTR(bin_code%(SUCC(index%)))
  deinit%=init%+4
  read%=deinit%+4
  write%=read%+4
  readreg%=write%+4
  wrreg%=readreg%+4
  '
RETURN
'
PROCEDURE init(dir%)          !  Datenrichtungsregister setzen
  '                              z.B. ddir%=&X1010101010101010
  '                              Bit=0 ---> Eingang
  '                              Bit=1 ---> Ausgang
  '
  '                              Aufruf durch 'GOSUB init(ddir%)' oder
  '                              '@init(ddir%)' oder nur durch 'init(ddir%)'
  '
  regs%(0)=dir%
  RCALL init%,regs%()
  '
RETURN
'
PROCEDURE outword(word%)      !  Ausgabe der Datenbits fr jeden Ausgang
  '                              z.B. dword%=&X1000
  '                              Bit=0 ---> Ausgang low
  '                              Bit=1 ---> Ausgang high
  '
  '                              Aufruf durch 'GOSUB outword(dword%)' oder
  '                              '@outword(dword%)' oder nur durch
  '                              'outword(dword%)'
  '
  regs%(0)=word%
  RCALL write%,regs%()
  '
RETURN
'
FUNCTION inword                 !  Einlesen der Datenbits an jedem Eingang
  '                                Eingang low  ---> Bit=0
  '                                Eingang high ---> Bit=1
  '
  '                                Aufruf der Funktion durch
  '                                'dword% = FN inword' oder
  '                                'dword% = @inword'
  '
  '                                Das Ergebnis liefert die Variable 'dword%'
  '                                oder besser 'BIN$(dword%)'
  '
  RCALL read%,regs%()
  RETURN regs%(0)
  '
ENDFUNC
'
PROCEDURE last                !  Dieses Unterprogramm muž immer als letztes
  '                              aufgerufen werden. Hier wird die parallele
  '                              Schnittstelle wieder auf den Druckerbetrieb
  '                              umgeschalten.
  '
  '                              Aufruf durch 'GOSUB last' oder '@last'
  '                              oder nur durch 'last'
  RCALL deinit%,regs%()
  END
  '
RETURN
'
END
