#if 0
#define _TRACE INT R_TRACE
#else
#define _TRACE
#endif

ABSOLUTE R_OK = 0x12345678
ABSOLUTE R_NEEDMORE = 0x12345679

;ABSOLUTE R_ERROR = -1
;ABSOLUTE R_NOSTATUS = -2
;ABSOLUTE R_NOMSG = -3;
ABSOLUTE R_NOSELECT = -4;
;ABSOLUTE R_NOCMD = -5;
ABSOLUTE R_UNKNPHASE = -6;
ABSOLUTE R_UNKNMESSAGE = -7;
ABSOLUTE R_SENDREJECT = -8;
ABSOLUTE R_MSGREJECT = -9;
ABSOLUTE R_DISCONNECT = -10;
ABSOLUTE R_GOT_SDTR = -11;
ABSOLUTE R_GOT_WDTR = -12;
ABSOLUTE R_GOT_IGNRESIDUE = -13;

ABSOLUTE R_TRACE = 0x55555555;

ABSOLUTE R_TEST = 0x55aa5aa5
ABSOLUTE R_TEST2 = 0x12345678

EXTERNAL scsi_status, msgin_buf, msgout_buf, scratch_buf

; Command-Struct:
ABSOLUTE d_select	= 0x00
ABSOLUTE d_unused	= 0x04	; 4 bytes unused
ABSOLUTE d_unused1	= 0x08	; 8 bytes unused (was msgin)
ABSOLUTE d_msgout	= 0x10
ABSOLUTE d_command	= 0x18
ABSOLUTE d_data0	= 0x20
ABSOLUTE d_data1	= 0x28
ABSOLUTE d_data2	= 0x30
ABSOLUTE d_data3	= 0x38
ABSOLUTE d_data4	= 0x40
ABSOLUTE d_data5	= 0x48
ABSOLUTE d_data6	= 0x50
ABSOLUTE d_data7	= 0x58

; Bits in DWT-Register:
ABSOLUTE STATE_GOT_MSGIN =  0x01

ENTRY Test1
ENTRY Test2
ENTRY Command
ENTRY cmd2
ENTRY Dispatch
ENTRY data_in
ENTRY data_in_end
ENTRY data_out
ENTRY data_out_end
ENTRY cleanup_data
ENTRY wait_reselect

Test1:
	NOP
	INT  R_TEST2

; Try a move memory from/to video ram (hardcoded addresses)
Test2:	MOVE MEMORY 0x10000, 0x20000000, 0x20010010
	MOVE MEMORY 1, 0x20000000, scsi_status
	INT  R_TEST

; The following phase sequences are possible:
; 0  BUS FREE
; v
; 1  ARBITRATION -------+
; v                     v
; 2 SELECTION           3 RESELECTION
; v                     v
; 4 Message Out         5 Message In
; v                     v
;    (nearly all combinations of Message In/Out, Command, Data, Status)
;

sel_error:
	INT	R_NOSELECT

Command:
	CLEAR	TARGET
	MOVE	0 TO DWT
	SELECT	ATN FROM d_select, sel_error
cmd2:	JUMP	data_in,	WHEN DATA_IN
	JUMP	data_out,	IF DATA_OUT
	CALL	dispatch
	JUMP	cmd2	

dispatch:
	_TRACE
	RETURN,			WHEN DATA_IN
	RETURN,			IF DATA_OUT
	JUMP	status,		IF STATUS
	JUMP	msg_in,		IF MSG_IN
	JUMP	msg_out,	IF MSG_OUT
	JUMP	command,	IF CMD
	INT	R_UNKNPHASE
	JUMP	dispatch

status:
	_TRACE
	MOVE	1,scsi_status,	WHEN STATUS
	JUMP	dispatch
	
msg_out:
	_TRACE
	MOVE	FROM d_msgout,	WHEN MSG_OUT
	CLEAR	ATN
	JUMP	dispatch
	
command:
	_TRACE
	MOVE	FROM d_command,	WHEN CMD
	JUMP	dispatch

data_in:
	MOVE	FROM d_data0+0x00,	WHEN DATA_IN

	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x08,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x10,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x18,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x20,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x28,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x30,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x38,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x40,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x48,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x50,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x58,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x60,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x68,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x70,	WHEN DATA_IN
	JUMP	cmd2,			WHEN NOT DATA_IN
	MOVE	FROM d_data0+0x78,	WHEN DATA_IN

	JUMP	cmd2,			WHEN NOT DATA_IN
data_in_end:
	INT	R_NEEDMORE

data_out:
	MOVE	FROM d_data0+0x00,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x08,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x10,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x18,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x20,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x28,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x30,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x38,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x40,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x48,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x50,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x58,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x60,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x68,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x70,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
	MOVE	FROM d_data0+0x78,	WHEN DATA_OUT
	JUMP	cmd2,			WHEN NOT DATA_OUT
data_out_end:
	INT	R_NEEDMORE

; cleanup data phase when target wants to transfer more data than 
; necessary
cleanup_data:
	_TRACE
	JUMP	dummy_data_in,	WHEN DATA_IN
	JUMP	dummy_data_out,	IF DATA_OUT
	CALL	dispatch
	JUMP	cleanup_data

dummy_data_out:
	MOVE	1,scratch_buf, WHEN DATA_OUT
	JUMP	cleanup_data

dummy_data_in:
	MOVE	1,scratch_buf, WHEN DATA_IN
	JUMP	cleanup_data

msg_in:
	_TRACE
	JUMP	dispatch,	WHEN NOT MSG_IN
	MOVE	DWT | STATE_GOT_MSGIN TO DWT
	MOVE	1,msgin_buf,	WHEN MSG_IN
        JUMP	msg_complete, 	IF 0x00
        JUMP	msg_disconnect, IF 0x04
        JUMP	msg_saveptr, 	IF 0x02
        JUMP	msg_restptr,	IF 0x03
        JUMP	msg_extended,	IF 0x01	; extended message
        JUMP	clrack,		IF 0x08	; NOP
        JUMP	msg_reject,	IF 0x07	; Reject
        JUMP	msg_ign_residue,IF 0x23	; ignore Wide residue
        JUMP	clrack,	IF 0x80, AND MASK 0x7f	; Identify
	INT	R_UNKNMESSAGE
	JUMP	clrack

msg_bad:
;	MOVE	0x07 TO SCRATCHA0		; REJECT Message
;	MOVE	MEMORY 1,SCRATCHA0, msgout_buf
	SET	ATN
	INT	R_SENDREJECT
	JUMP	clrack				; will restart here

clrack:
	CLEAR	ACK
	JUMP	dispatch
	
msg_saveptr:
	JUMP	clrack
msg_restptr:
	JUMP	clrack
	

; MSG 00 = command complete
msg_complete:
	_TRACE
	MOVE	SCNTL2 & 0x7F TO SCNTL2		; expect disconnect
;	CLEAR	ACK ATN
	CLEAR	ACK
	WAIT	DISCONNECT
	INT	R_OK

; MSG 04 = Disconnect
msg_disconnect:
	_TRACE
	MOVE SCNTL2 & 0x7F TO SCNTL2		; expect disconnect
	CLEAR	ACK
	WAIT	DISCONNECT
	MOVE	CTEST3 | 0x04 to CTEST3		; Clear the DMA fifo
	MOVE	STEST3 | 0x02 to STEST3		; Clear the SCSI fifo
	INT	R_DISCONNECT
; restart here
wait_reselect:
	_TRACE
	WAIT	RESELECT wait_resel2
wait_resel2:
	_TRACE
	SELECT	FROM d_select, wait_resel3
wait_resel3:
	_TRACE
	JUMP	dispatch
	
msg_reject:
	_TRACE
	CLEAR	ACK
	INT	R_MSGREJECT
	JUMP	dispatch

msg_ign_residue:
	_TRACE
	CLEAR	ACK
	JUMP	dispatch,	WHEN NOT MSG_IN
	MOVE	1,msgin_buf+1,	WHEN MSG_IN
	JUMP	clrack,		IF 0x00
	INT	R_GOT_IGNRESIDUE
	JUMP	dispatch

msg_extended:
	_TRACE
	CLEAR	ACK
	JUMP	dispatch,	WHEN NOT MSG_IN
	MOVE	1,msgin_buf+1,	WHEN MSG_IN	; get length
	JUMP	msg_ext3,	IF 0x03
	JUMP	msg_bad,	IF NOT 0x02
msg_ext_2:
	_TRACE
	CLEAR	ACK
	JUMP	dispatch,	WHEN NOT MSG_IN
	MOVE	1,msgin_buf+2,	WHEN MSG_IN	; get code
	JUMP	msg_bad,	IF NOT 0x03	; not WDTR -> unknown
msg_wdtr:
	_TRACE
	CLEAR	ACK
	JUMP	dispatch,	WHEN NOT MSG_IN
	MOVE	1,msgin_buf+3,	WHEN MSG_IN	; get parameter
	INT	R_GOT_WDTR
	JUMP	dispatch
	
msg_ext3:
	_TRACE
	CLEAR	ACK
	JUMP	dispatch,	WHEN NOT MSG_IN
	MOVE	1,msgin_buf+2,	WHEN MSG_IN	; get code
	JUMP	msg_bad,	IF NOT 0x02	; not SDTR -> unknown
msg_sdtr:
	_TRACE
	CLEAR	ACK
	JUMP	dispatch,	WHEN NOT MSG_IN
	MOVE	2,msgin_buf+3,	WHEN MSG_IN
	INT	R_GOT_SDTR
	JUMP	dispatch
