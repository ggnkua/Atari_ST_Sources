'
FUNCTION init_xrsrc ! Initialisierung der Routinen, am Anfang 1* aufrufen
  $F%
  $C+
  '
  LOCAL base%,fbuf%,empty%
  '
  ' Copyright (c)2000 Richard Gordon Faika, RGF Software
  '
  '  http://www.rgfsoft.com
  '  http://www.rgfsoft.org
  '   http://www.myluna.de
  '    http://rgf.atari.org
  '     http://luna.atari.org
  '
  ' neue XRSRC-Routinen fr das Interface XRSRC.OVL inclusive
  ' xpatch wegen Multiplikations-Bug im Overlay, der dazu fhrte, daž
  ' man zwar lange RSCs laden kann, aber diese zerst”rt sind ber der
  ' 64k-Grenze fr Objekte (nicht verwechseln mit der RSC-Dateigr”že).
  '
  '
  INLINE empty%,128
  fbuf%=empty%+32
  '
  ' NICHT VERGESSEN EINZULADEN:
  ' xpatch.inl
  INLINE xpatch%,128
  ADD xpatch%,28
  '
  CHAR{fbuf%}="xrsrc.ovl"
  base%=GEMDOS(75,3,L:fbuf%,L:empty%,L:empty%)
  '
  IF base%>=0
    xrsrc_base%=base%                           ! merken zum Freigeben
    init%={base%+8}                             ! Adresse Textsegment (Basepage+256)
    '
    ' XPATCH
    '
    INT{init%+1446+8}=&H4EB9                    ! jsr eintragen in xrsrc-overlay
    {init%+1446+8+2}=xpatch%                    ! Adresse der neuen Routine
    INT{init%+1446+8+6}=&H4E75                  ! rts eintragen
    '
    {init%-4}=CVL("BgNA")                       ! clr.w   -(sp) + trap #1
    {base%+8}={base%+8}-4
    {base%+12}={base%+12}+4
    tpa_len%={base%+12}+{base%+20}+{base%+28}+256
    {base%+4}=base%+tpa_len%                    ! p_hitpa anpassen
    '
    ~GEMDOS(74,0,L:base%,L:tpa_len%)            ! mshrink
    ~GEMDOS(75,4,L:0,L:base%,L:0)               ! dummy-Aufruf, Caches flushen!!!
    '
    {init%+4}=empty%                            !
    IF C:init%(L:base%)=0                       ! Initialisierung
      '
      ' Variablen umsetzen auf Overlay-Variablen (ABSOLUTE ist ABSOLUTE unkritisch)
      '
      ABSOLUTE xrsrc_main%,init%+42             ! Adresse der GFA-Verteiler-Routine auf Overlay-Variable setzen
      ABSOLUTE xrsrc_fnum&,init%+40             ! Funktionsnummer auf Overlay-Variable setzen
      ABSOLUTE xrsrc_wrd1&,init%+4              ! Word-Parameter 1
      ABSOLUTE xrsrc_wrd2&,init%+6              ! Word-Parameter 2
      ABSOLUTE xrsrc_lng1%,init%+20             ! Long-Parameter 1
      '
      CLR xrsrc_wrd1&                           ! Parameter l”schen
      CLR xrsrc_wrd2&
      CLR xrsrc_lng1%
      '
      xrsrc_fnum&=128                           ! Nummer von funktion xrsrc_init()
      IF C:xrsrc_main%()=1                      ! xrsrc_init() aufrufen
        RETURN FALSE
      ENDIF
    ENDIF
  ENDIF
  RETURN base%
ENDFUNC
PROCEDURE exit_xrsrc  ! Deinitialisierung der Routinen, am Ende 1* aufrufen
  $C+
  '
  xrsrc_fnum&=129                   ! Funktionsnummer
  ~C:xrsrc_main%()                  ! xrsrc_exit() aufrufen
  ~MFREE({xrsrc_base%+44})         ! Environment freigeben
  ~MFREE(xrsrc_base%)              ! Programmspeicher freigeben
RETURN
FUNCTION xrsrc_free
  $F%
  $C+
  '
  xrsrc_fnum&=2                 ! Nummer der Function
  RETURN C:xrsrc_main%()        ! xrsrc_free()
  '
ENDFUNC
FUNCTION xrsrc_gaddr(re_gtype&,re_gindex&)
  $F%
  $C+
  '
  LOCAL re_gaddr%
  '
  xrsrc_fnum&=3                 ! Nummer der Function
  xrsrc_wrd1&=re_gtype&         ! Parameter 1
  xrsrc_wrd2&=re_gindex&        ! Parameter 2
  xrsrc_lng1%=V:re_gaddr%       ! Parameter 3
  ~C:xrsrc_main%()              ! Funktion aufrufen
  RETURN re_gaddr%              ! xrsrc_gaddr()
  '
ENDFUNC
FUNCTION xrsrc_saddr(type&,object&,zeiger%)
  $F%
  $C+
  '
  xrsrc_fnum&=4                 ! Nummer der Function
  xrsrc_wrd1&=type&             ! Parameter 1
  xrsrc_wrd2&=object&           ! Parameter 2
  xrsrc_lng1%=zeiger%           ! Parameter 3
  RETURN C:xrsrc_main%()        ! Funktion aufrufen
  '
ENDFUNC
FUNCTION xrsrc_obfix(object&,adresse%)
  $F%
  $C+
  '
  xrsrc_fnum&=5                 ! Nummer der Function
  xrsrc_wrd1&=object&           ! Parameter 1
  xrsrc_lng1%=adresse%          ! Parameter 3
  RETURN C:xrsrc_main%()        ! Funktion aufrufen
  '
ENDFUNC
FUNCTION xrsrc_load(file%)
  $F%
  $C+
  '
  xrsrc_fnum&=1                 ! Nummer der Function
  xrsrc_lng1%=file%             ! Parameter
  RETURN C:xrsrc_main%()        ! xrsrc_load()
  '
ENDFUNC
'
