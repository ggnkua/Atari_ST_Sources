' *----------------------------------------------*
'              ð LANDSCAPE GENERATOR ð
'    version 1.00 pour ATARI ** FALCON 030 **
'                      RVB/VGA
'              ½ FANTOMAS D‚cembre 1998
'    D'aprŠs un programme QUICK Basic de Shawn
' *----------------------------------------------*
' http://perso.wanadoo.fr/michel.goux/
'
ON ERROR GOSUB fin
ON BREAK GOSUB fin
'
RESERVE 50000
INLINE palette%,1024
DIM z(320),x(320),clr(320)
super%=GEMDOS(32,L:0)                             ! Mode Superviseur.
resol&=XBIOS(88,W:-1)                             ! Sauve la r‚solution.
sauve_ecr%=XBIOS(2)                               ! Sauve l'‚cran.
buffer%=MALLOC(77824)                             ! 320x240 + palette 1024
moniteur%=XBIOS(89)                               ! Type de moniteur.
image%=buffer%+1024
IF moniteur%=2                                    ! Moniteur VGA
  ~XBIOS(5,L:image%,L:image%,W:3,W:&X100110011)   ! VGA: 320x240 256c.
ELSE
  ~XBIOS(5,L:image%,L:image%,W:3,W:&X11)          ! TV:  320x200 256c.
ENDIF
BMOVE &HFFFF9800,palette%,1024
OUT 4,18
@def_color(0,0,0,0)
@def_color(1,63,63,63)
DEFFILL 0,2,8
PBOX 0,200,319,239
CLIP 0,0,319,199
DO
  CLS
  @choix
  CLS
  ' *--- D‚finition des couleurs ---*
  pl=0
  FOR i=100 TO 160
    pll=CINT(pl*0.67)
    IF pll>60
      pll=60
    ENDIF
    IF t=1
      @def_color(i,0,0,pl*4)         ! Lunaire
    ENDIF
    IF t=2
      @def_color(i,0,pl*4,0)         ! Verdure
    ENDIF
    IF t=3
      @def_color(i,pl*4,pll*4,0)     ! D‚sert.
    ENDIF
    IF t=4
      @def_color(i,pl*4,pl*4,pl*4)   ! Glace.
    ENDIF
    INC pl
  NEXT i
  ' *--- Calcul d‚grad‚ du ciel ---*
  pl=0
  ee=RND*100                         ! Random pour la couleur du fond
  FOR i=170 TO 230
    pll=pl*2
    IF pll>60
      pll=60
    ENDIF
    IF ee<30
      @def_color(i,pll*4,pl*4,pl*4)  ! D‚grad‚ de rouge
    ELSE
      @def_color(i,pl*4,pl*4,pll*4)  ! D‚grad‚ de bleu
    ENDIF
    INC pl
  NEXT i
  ' *--- Trac‚ du ciel (sphŠre ou d‚grad‚) ---*
  z=10+300*RND
  zz=100-95*RND
  mv=20*RND
  eee=100*RND                        ! Random pour le fond
  IF eee<30
    FOR i=0 TO 60
      DEFFILL 230-i,2,8
      PBOX 0,99-i,319,99-i
    NEXT i
    DEFFILL 230,2,8
    PBOX 0,100,319,199
  ELSE
    FOR i=0 TO 60
      DEFFILL 170+i,2,8
      PCIRCLE z,zz,80-i
    NEXT i
  ENDIF
  ' *--- Trac‚ des ‚toiles ---*
  FOR i=0 TO 500
    REPEAT
      x=319*RND
      xx=190*RND
      xclr=PTST(x,xx)
    UNTIL xclr<221
    IF xclr>180
      vclr=18+((220-xclr)/4)*RND
    ELSE
      vclr=17+13*RND
    ENDIF
    PSET x,xx,vclr
  NEXT i
  ' *--- Trac‚ du sol ---*
  z(0)=0
  x(0)=100
  sz=1
  clr(0)=130
  FOR i=1 TO 320
    z(i)=z(i-1)+sz
    x(i)=x(i-1)-sz+(sz*2)*RND
    clr(i)=clr(i-1)-5+(10*RND)
    IF clr(i)<100
      clr(i)=100
    ENDIF
    IF clr(i)>160
      clr(i)=160
    ENDIF
    DEFFILL clr(i),2,8
    PBOX z(i),x(i),z(i)+sz,x(i)+sz
  NEXT i
  REPEAT
    chk=0
    sz=sz*1.012
    z(0)=0-(sz-1)*160
    x(0)=x(0)+sz*RND
    FOR i=1 TO 320
      z(i)=z(i-1)+sz
      x(i)=x(i)-sz/4+sz*RND
      IF x(i)<x(i-1)-sz
        x(i)=x(i-1)-sz
      ENDIF
      IF x(i)>x(i-1)+sz
        x(i)=x(i-1)+sz
      ENDIF
      IF z(i)>=-1 OR z(i)<=320
        clr(i)=clr(i)-4+(8*RND)
        IF clr(i)<clr(i-1)-3
          clr(i)=clr(i-1)-3
        ENDIF
        IF clr(i)>clr(i-1)+3
          clr(i)=clr(i-1)+3
        ENDIF
        IF clr(i)<100
          clr(i)=100
        ENDIF
        IF clr(i)>160
          clr(i)=160
        ENDIF
        DEFFILL clr(i),2,8
        PBOX z(i),x(i),z(i)+sz+1,x(i)+sz+1
      ENDIF
      IF x(i)>199
        INC chk
        EXIT IF chk>319
      ENDIF
    NEXT i
    ky$=INKEY$
  UNTIL ky$<>"" OR chk>319
  '
  REPEAT
    ky$=INKEY$
  UNTIL UPPER$(ky$)="S" OR ky$=" "
  IF UPPER$(ky$)="S"
    BMOVE &HFFFF9800,buffer%,1024
    BSAVE "\VOXEL\VOXEL.PI9",buffer%,77824 ! Sauvegarde au format PI9
  ENDIF
LOOP
'
PROCEDURE choix
  DEFTEXT 254,0,0,6
  TEXT 32,10,"Exemple de g‚n‚rateur de terrain"
  TEXT 32,20,"       en GFA Basic 3.6"
  TEXT 32,30,"Choisissez le type de terrain:"
  TEXT 32,50,"         1 - DESERT"
  TEXT 32,60,"         2 - GLACE"
  TEXT 32,70,"         3 - LUNAIRE"
  TEXT 32,80,"         4 - VERDURE"
  TEXT 32,100,"         5 - QUITTER"
  REPEAT
    ky$=INKEY$
  UNTIL ky$>"0" AND ky$<"6"
  IF ky$="1"
    t=3
    GOTO fini
  ELSE
    IF ky$="2"
      t=4
      GOTO fini
    ELSE
      IF ky$="3"
        t=1
      ELSE
        IF ky$="4"
          t=2
        ELSE
          @fin
        ENDIF
      ENDIF
    ENDIF
  ENDIF
fini:
RETURN
' *--- D‚finition des couleurs ---*
PROCEDURE def_color(t&,r&,v&,b&)
  r$=STRING$(2-LEN(HEX$(r&)),"0")+HEX$(r&)            ! R.
  v$=STRING$(2-LEN(HEX$(v&)),"0")+HEX$(v&)            ! V.
  b$=STRING$(2-LEN(HEX$(b&)),"0")+HEX$(b&)            ! B.
  {ADD(&HFFFF9800,MUL(t&,4))}=VAL("&h"+r$+v$+"00"+b$) ! Modifie la couleur.
RETURN
' *--- Fin du programme ---*
PROCEDURE fin
  BMOVE palette%,&HFFFF9800,1024
  ~GEMDOS(32,L:super%)                                ! Mode utilisateur.
  ~XBIOS(5,L:sauve_ecr%,L:sauve_ecr%,3,resol&)        ! Anciens r‚sol. / ‚crans.
  ~MFREE(buffer%)                                     ! LibŠre la m‚moire.
  OUT 4,8
  END
RETURN
