' *-------------------------------------------------*
'                    ð EFFET DE FEU ð
'       version 1.00 pour ATARI ** FALCON 030 **
'                        RVB/VGA
'               ½ FANTOMAS Septembre 97
'   d'aprŠs un programme Quick Basic de Martin Lindhe
' *-------------------------------------------------*
'         http://perso.wanadoo.fr/michel.goux/
'
RESERVE 50000                                     ! Reserve memoire.
super%=GEMDOS(32,L:0)                             ! Mode superviseur.
resol&=XBIOS(88,W:-1)                             ! R‚solution actuelle
sauve_ecr%=XBIOS(2)                               ! Sauve adresse ‚cran.
moniteur%=XBIOS(89)                               ! Type de moniteur.
buffer%=MALLOC(76800)                             ! Buffer 320x240 / 256c
IF moniteur%=2                                    ! Moniteur VGA
  ~XBIOS(5,L:buffer%,L:buffer%,W:3,W:&X100110011) ! VGA: 320x240 256c.
ELSE
  ~XBIOS(5,L:buffer%,L:buffer%,W:3,W:&X11)        ! TV:  320x200 256c.
ENDIF
OUT 4,18                                          ! Bloque la souris.
FOR n%=0 TO 63                                    ! Installe la palette ...
  READ c$                                         ! Couleurs 0 a 63.
  {&HFFFF9800+n%*4}=VAL("&h"+c$)
NEXT n%
DEFTEXT 63,0,0                                    ! Couleur & style du texte
TEXT 136,120,"FALCON"                             ! Affiche le texte
REPEAT
  FOR y&=105 TO 113                               ! 8 points en hauteur.
    y1&=y&+1
    FOR x&=136 TO 184                             ! 48 points en largeur.
      col&=(PTST(x&-1,y&)+PTST(x&,y1&))/2-RND*5  ! Calcul couleur du point.
      IF col&<0
        col&=0
      ENDIF
      PSET x&,y&,col&                             ! Affiche le point
    NEXT x&
  NEXT y&
UNTIL INP(-2)                                     ! Attente appui sur une touche
OUT 4,8                                           ! Remet la souris
~XBIOS(5,L:sauve_ecr%,L:sauve_ecr%,W:3,W:resol&)  ! Anciens ‚crans & r‚solution
~MFREE(buffer%)                                   ! LibŠre la m‚moire
~GEMDOS(32,L:super%)                              ! Mode utilisateur
'
DATA 00000000,0C000000,18000000,24000000          ! La palette de couleurs.
DATA 30000000,3C000000,48000000,54000000
DATA 60000000,6C000000,78000000,84000000
DATA 90000000,9C000000,A8000000,B4000000
DATA C0000000,CC000000,D8000000,E4000000
DATA F0000000,FC000000,FC0C0000,FC180000
DATA FC240000,FC300000,FC3C0000,FC480000
DATA FC540000,FC600000,FC6C0000,FC780000
DATA FC840000,FC900000,FC9C0000,FCA80000
DATA FCB40000,FCC00000,FCCC0000,FCD80000
DATA FCE40000,FCF00000,FCFC0000,FCFC000C
DATA FCFC0018,FCFC0024,FCFC0030,FCFC003C
DATA FCFC0048,FCFC0058,FCFC0064,FCFC0070
DATA FCFC007C,FCFC0088,FCFC0094,FCFC00A0
DATA FCFC00B0,FCFC00BC,FCFC00C8,FCFC00D4
DATA FCFC00E0,FCFC00EC,FCFC00FC,FCFC00FC
