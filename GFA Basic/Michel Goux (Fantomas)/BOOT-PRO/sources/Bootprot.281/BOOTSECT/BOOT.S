; ***************************************************************
;
;                     - BOOT PROTECTOR II -
;                         ½ M.GOUX 1993
; 
; ***************************************************************
;
; Taille: 454 octets 

	Opt 	O+

test	equ	1     ; Condition: 1 = non effectu‚

; -------------------------------
; Simplement pour essayer le boot
; -------------------------------

	ifeq test

; Superviseur mode

 	clr.l 	-(sp)
 	move.w 	#$20,-(sp)
 	trap 	#1
 	lea 	6(sp),sp
 	lea 	save_sp,a0
 	move.l 	d0,(a0)

; R‚solution

 	clr.w 	-(sp)
 	move.l 	#-1,-(sp)
 	move.l 	#-1,-(sp)
 	move.w 	#5,-(sp)
 	trap 	#14
 	lea 	12(sp),sp

	endc

; ***********************************************************************
; 
; BOOT SECTEUR 
;
; ***********************************************************************

code:

; -----------------------------------------------------------------------
; Souris OFF
; -----------------------------------------------------------------------

	move.b	#18,$fffffc02.w

; -----------------------------------------------------------------------
; Clic clavier
; -----------------------------------------------------------------------

	sf	$484.w

; -----------------------------------------------------------------------
; Au cas ou Reset d‚tourn‚
; -----------------------------------------------------------------------

	clr.l 	$426.w 		; Magic number
	clr.l 	$42a.w		; Reset vector

; -----------------------------------------------------------------------
; Couleurs
; -----------------------------------------------------------------------

 	lea 	$ffff8240.w,a0
 	move.l 	#$0000555,(a0)  
 	move.l 	#$7770700,4(a0)
  	move.w 	#$777,$1e(a0) 
 
; -----------------------------------------------------------------------
; Efface l'ecran
; -----------------------------------------------------------------------

	move.l	$44e.w,a6		; Adresse ecran
	lea	(a6),a5
	move.w	#7999,d0
boucle 	clr.l	(a5)+
	dbra	d0,boucle

; -----------------------------------------------------------------------
; Affiche le texte
; -----------------------------------------------------------------------
 
 	pea 	texte(pc)
 	move.w 	#9,-(sp)
 	trap 	#1
 	lea 	6(sp),sp

; -----------------------------------------------------------------------
; Teste la r‚solution
; -----------------------------------------------------------------------

	move.w	#4,-(sp)
	trap	#14
	addq.l	#2,sp
	tst.b	d0
	bne.s	wait	

; -----------------------------------------------------------------------
; Affiche la Seringue
; -----------------------------------------------------------------------

 	lea 	no_virus(pc),a1
 	lea 	72+84*160(a6),a6
 
 	moveq 	#31,d0
loop 	move.l 	(a1)+,(a6)
 	move.l 	(a1)+,8(a6)
 	lea 	160(a6),a6
 	dbra 	d0,loop

; -----------------------------------------------------------------------
; Test si [SPACE]
; -----------------------------------------------------------------------

wait 	cmp.b 	#$39,$fffffc02.w
           bne.s 	wait

; -------------
; Restaure clic 
; -------------

	move.b	#15,$484.w

; ---------
; Souris ON
; ---------

	move.b	#8,$fffffc02.w

; ------------------------------
; Simplement pour tester le boot
; ------------------------------

	ifeq test

; R‚solution

 	move.w 	#1,-(sp)
 	move.l 	#-1,-(sp)
 	move.l 	#-1,-(sp)
 	move.w 	#5,-(sp)
 	trap 	#14
 	lea 	12(sp),sp

; User mode

 	lea 	save_sp,a0
 	move.l 	(a0),-(sp)
 	move.w 	#$20,-(sp)
 	trap 	#1
 	lea 	6(sp),sp

; Pterm 

 	clr.w 	-(sp)
 	trap 	#1

	endc

; ----------
; Fin du PRG
; ----------

	 rts

; ----------------
; Texte a afficher
; ----------------
  
texte:

 	dc.b 	27,"p",27,"Y",38,43," BOOT PROTECTOR II "
 	dc.b 	27,"q",27,"Y",39,45,"By M. Goux 1993"
 	dc.b 	27,"Y",48,48,"NO VIRUS",0
 	even

no_virus:

 	incbin 	"E:\GFA_BASI\BOOT_PR2\SOURCES\VIRUS.DAT"

zeros:

	dc.b 	0,0,0,0,0,0,0,0

; -------------------------------
; Simplement pour essayer le boot
; -------------------------------

	ifeq test

save_sp:

	ds.l 	1

	endc

; Ces RTS ne sert a rien, c'est juste un repŠre pour sauver le
; boot ...

	rts  
	rts

