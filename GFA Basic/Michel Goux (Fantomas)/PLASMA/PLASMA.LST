' *-------------------------------------------------*
'             ð Effet de plasma sinusoidal ð
'        version 1.00 pour ATARI ** FALCON 030 **
'                         RVB/VGA
'                 ½ FANTOMAS / Octobre 97
' D'aprŠs un programme Quick Basic de Andrew L. Ayers
' *-------------------------------------------------*
'        http://perso.wanadoo.fr/michel.goux/
'
RESERVE 50000                                     !
super%=GEMDOS(32,L:0)                             ! Mode Superviseur.
resol&=XBIOS(88,W:-1)                             ! Sauve la r‚solution.
sauve_ecr%=XBIOS(2)                               ! Sauve l'‚cran.
buffer%=MALLOC(77824)                             ! 320x240 + palette 1024
moniteur%=XBIOS(89)                               ! Type de moniteur.
image%=buffer%+1024
IF moniteur%=2                                    ! Moniteur VGA
  ~XBIOS(5,L:image%,L:image%,W:3,W:&X100110011)   ! VGA: 320x240 256c.
ELSE
  ~XBIOS(5,L:image%,L:image%,W:3,W:&X11)          ! TV:  320x200 256c.
ENDIF
pal%=&HFFFF9800                                   ! Adresse des couleurs.
OUT 4,18                                          ! Bloque la souris.
IF EXIST("\PLASMA\PLASMA.PI9")
  BLOAD "\PLASMA\PLASMA.PI9",buffer%
  BMOVE buffer%,pal%,1024
ELSE
  ' *--------------- Trac‚ du plasma ---------------*
  tpi=6.28318
  scale=(tpi*4)/320
  FOR y%=0 TO 199
    rad=0
    INC colr%
    IF colr%>254
      colr%=1
    ENDIF
    FOR x%=0 TO 319
      ypos%=y%+SIN(rad)*15
      PSET x%,ypos%,colr%
      rad=rad+scale
    NEXT x%
  NEXT y%
  rad=0
  scale=(tpi*8)/200
  FOR y%=0 TO 199
    xpos%=INT(SIN(rad)*15)
    GET 0,y%,319-xpos%,y%,a$
    IF xpos%>=0
      GET 319-xpos%,y%,319,y%,c$
      PUT xpos%,y%,a$
      PUT 0,y%,c$
    ELSE
      GET ABS(xpos%),y%,319,y%,a$
      GET 0,y%,ABS(xpos%),y%,c$
      PUT 0,y%,a$
      PUT 319+xpos%,y%,c$
    ENDIF
    rad=rad+scale
  NEXT y%
  ' *-----------------------------------------------*
  DEFFILL 0
  PBOX 0,0,319,17
  PBOX 0,174,319,239
  PBOX 0,0,35,239
  PBOX 289,0,319,239
  PBOX 0,185,319,239
  COLOR 1
  BOX 33,15,291,176
  DEFTEXT 1,0,0,6
  TEXT 60,12,"Effet de plasma Sinusoidal!"
  TEXT 96,184,"Par FANTOMAS 10/97"
  ' *---------- Modification des couleurs ----------*
  {&HFFFF9800}=&H0                                  ! Couleur 0: noir
  {&HFFFF9804}=&HFCFC00FC                           ! Couleur 1: blanc.
  {&HFFFF98FC}=&HFC000000                           ! Couleur 63: rouge.
  {&HFFFF99FC}=&HFC0000                             ! Couleur 127: vert.
  {&HFFFF9AFC}=&HFC                                 ! Couleur 191: bleu.
  {&HFFFF9BFC}=&HFCFC00FC                           ! Couleur 254: blanc.
  {&HFFFF9BFC}=&HFCFC00FC                           ! Couleur 255: blanc.
  @setpal(1,63)                                     ! D‚grad‚
  @setpal(63,127)                                   !
  @setpal(127,191)                                  !
  @setpal(191,254)                                  !
  BMOVE &HFFFF9800,buffer%,1024
  BSAVE "\PLASMA\PLASMA.PI9",buffer%,77824
ENDIF
' *----------------- Animation -------------------*
DO                                                !
  VSYNC                                           ! Synchronisation ‚cran.
  rvb_1%={&HFFFF9808}                             ! Sauve couleur 2.
  BMOVE &HFFFF9808,&HFFFF9804,1016                ! D‚calage couleurs 3 … 254.
  {ADD(pal%,1016)}=rvb_1%                         ! Couleur 2 -> 254.
LOOP UNTIL INKEY$<>""                             !
OUT 4,8                                           ! R‚active la souris.
~GEMDOS(32,L:super%)                              ! Mode utilisateur.
~XBIOS(5,L:sauve_ecr%,L:sauve_ecr%,3,resol&)      ! Anciens r‚sol. / ‚crans.
~MFREE(buffer%)                                   ! LibŠre la m‚moire.
' *----- Calcul de d‚grad‚ entre 2 couleurs ------*
PROCEDURE setpal(deb%,fin%)                       !
  coul%=fin%-deb%                                 ! Nbre de couleurs.
  r1&=BYTE{ADD(pal%,MUL(deb%,4))}                 ! Valeur du rouge couleur 1.
  v1&=BYTE{ADD(pal%,MUL(deb%,4))+1}               ! Valeur du vert couleur 1.
  b1&=BYTE{ADD(pal%,MUL(deb%,4))+3}               ! Valeur du bleu couleur 1.
  r2&=BYTE{ADD(pal%,MUL(fin%,4))}                 ! Valeur du rouge couleur 2.
  v2&=BYTE{ADD(pal%,MUL(fin%,4))+1}               ! Valeur du vert couleur 2.
  b2&=BYTE{ADD(pal%,MUL(fin%,4))+3}               ! Valeur du bleu couleur 2.
  stepr=(r2&-r1&)/coul%                           ! Incr‚ment du rouge.
  stepv=(v2&-v1&)/coul%                           ! Incr‚ment du vert.
  stepb=(b2&-b1&)/coul%                           ! Incr‚ment du bleu.
  ro=r1&                                          !
  ve=v1&                                          !
  bl=b1&                                          !
  wr&=ro                                          !
  wv&=ve                                          !
  wb&=bl                                          !
  FOR t%=deb% TO fin%                             !
    r$=STRING$(2-LEN(HEX$(wr&)),"0")+HEX$(wr&)    !
    v$=STRING$(2-LEN(HEX$(wv&)),"0")+HEX$(wv&)    !
    b$=STRING$(2-LEN(HEX$(wb&)),"0")+HEX$(wb&)    !
    {ADD(pal%,MUL(t%,4))}=VAL("&h"+r$+v$+"00"+b$) ! Modifie la couleur.
    ro=ro+stepr                                   !
    wr&=ro                                        !
    ve=ve+stepv                                   !
    wv&=ve                                        !
    bl=bl+stepb                                   !
    wb&=bl                                        !
  NEXT t%                                         !
RETURN                                            !
