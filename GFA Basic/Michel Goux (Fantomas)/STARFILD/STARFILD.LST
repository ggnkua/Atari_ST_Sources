' *----------------------------------------------*
'                 ð STARFIELD 3D ð
'    version 1.00 pour ATARI ** FALCON 030 **
'                     RVB/VGA
'     ½ FANTOMAS D‚cembre 1997 / Janvier 98
'  D'aprŠs un programme QUICK Basic de Churl Beck
' *----------------------------------------------*
'      http://perso.wanadoo.fr/michel.goux/
'
RESERVE 50000                                    ! R‚serve m‚moire.
DIM starx%(100),stary%(100),starz%(100)          ! Tables des x, y et z.
DIM staroldx%(100),staroldy%(100)                ! Anciennes valeurs x et y.
super%=GEMDOS(32,L:0)                            ! Mode Superviseur.
resol&=XBIOS(88,W:-1)                            ! Sauve la resolution.
sauve_ecr%=XBIOS(2)                              ! Sauve l'adresse de l'ecran.
ecran%=MALLOC(76800)                             ! Taille de l'‚cran:76800 octets.
OUT 4,18                                         ! Bloque la souris.
moniteur&=XBIOS(89)                              ! Test du moniteur.
IF moniteur&=2                                   ! Si moniteur VGA,
  y_cent%=120                                    ! Centrage
  y_max%=239                                     ! Sortie d'‚cran.
  ~XBIOS(5,L:ecran%,L:ecran%,3,&X100110011)      ! mode VGA.
ELSE                                             ! sinon,
  y_cent%=100
  y_max%=199
  ~XBIOS(5,L:ecran%,L:ecran%,3,&X11)             ! mode RVB.
ENDIF
CLS                                              ! Efface l'‚cran.
' *--- Palette ---*
FOR n&=1 TO 63                                   ! Palette de 63 niveaux ...
  BYTE{&HFFFF9800+n&*4}=(63-n&)*4                ! ... de gris:
  BYTE{&HFFFF9800+n&*4+1}=(63-n&)*4              ! couleur  1: blanc.
  BYTE{&HFFFF9800+n&*4+2}=0                      ! Couleur 63: noir.
  BYTE{&HFFFF9800+n&*4+3}=(63-n&)*4
NEXT n&
{&HFFFF9800}=0                                   ! Couleur  0: noir.
' *--- Boucle principale ---*
REPEAT
  FOR n&=1 TO 100                                ! 100 ‚toiles: C'est mieux !
    ' *--- Nouvelle ‚toile ---*
    IF starz%(n&)=0                              ! Si z = 0,
      stary%(n&)=((RND*400)-200)*2048            ! Nouveau y.
      starx%(n&)=((RND*400)-200)*2048            ! Nouveau x.
      starz%(n&)=4032                            ! Nouveau z.
    ELSE
      PSET staroldx%(n&),staroldy%(n&),0         ! Efface l'‚toile.
      y%=stary%(n&)\starz%(n&)+y_cent%           !
      IF y%<0 OR y%>y_max%                       !
        starz%(n&)=0                             ! z=0.
        GOTO continue
      ENDIF
      staroldy%(n&)=y%                           ! Sauve y.
      x%=starx%(n&)\starz%(n&)+160               ! Calcul de x.
      IF x%<0 OR x%>319                          ! Si on sort de l'‚cran.
        starz%(n&)=0                             ! z=0.
        GOTO continue
      ENDIF
      staroldx%(n&)=x%                           ! Sauve x.
      colr%=starz%(n&)\64                        ! Couleur du point: z/64.
      PSET x%,y%,colr%                           ! Affiche le point.
      SUB starz%(n&),20                          ! z=z-20.
    ENDIF
  continue:
  NEXT n&                                        ! Point suivant.
UNTIL BYTE{&HFFFFFC02}=57                        ! Test appui sur [ESPACE].
' *--- Fin ---*
OUT 4,8                                          ! Restaure la souris.
~XBIOS(5,L:sauve_ecr%,L:sauve_ecr%,3,resol&)     ! Anciens r‚s. et ‚crans.
~MFREE(ecran%)                                   ! LibŠre la m‚moire.
~GEMDOS(32,L:super%)                             ! Mode utilisateur.
