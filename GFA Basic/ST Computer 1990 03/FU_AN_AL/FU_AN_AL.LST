Init
Input
Trim
Check
Const
Vars
Main
Output
'
Procedure Init                                 ! Initialisierung
  Dim Res(99),Var!(9),Con!(9)                  ! Res: fr (Zwischen-)Ergebnisse
  Op1$="-+\*/^"                                ! Operationen mit zwei Operanden
  Op2$="SINCOSTANLOGLN"                        ! Operationen mit einem Operand
Return
'
Procedure Input                                ! Eingabe (hier als Beispiel)
  Box 120,100,520,150                          ! k.v.
  Text 128,120,"Bitte geben Sie die Funktion ein:"
  Print At(17,9);"F=";                         ! k.v.
  Form input 45 As Func$                       ! Func$ bleibt erhalten
Return
'
Procedure Trim                                 ! Formatieren
  Fn$="("+Upper$(Func$)+")"                    ! Nur Grožbuchstaben
  For I|=1 To Len(Fn$)                         ! Fn$ durchsuchen
    J!=Mid$(Fn$,I|,1)=" "                      ! hier nach Leerzeichen
    Fn$=Left$(Fn$,Pred(I|))+Mid$(Fn$,I|-J!)    ! weg damit, falls gefunden
    Add I|,J!                                  ! Z„hlerkorrektur
    J|=Asc(Mid$(Fn$,I|))                       ! hier nach "C" und "V"
    If J|>47 And J|<58                         ! "C" wird "1", "V" wird "0"
      Mid$(Fn$,Pred(I|))=Chr$(48-Mid$(Fn$,Pred(I|),1)="C")
    Endif
  Next I|
  Ln|=Len(Fn$)                                 ! Endgltige L„nge
Return
'
Procedure Check                                ! Eingabekontrolle (als Bsp.)
  If Fn$<>"()"                                 ! Falls Eingabe erfolgt ist
    Clr A|
    For I|=1 To Ln|                            ! Klammern z„hlen
      Sub A|,Mid$(Fn$,I|,1)="("-Mid$(Fn$,I|,1)=")"
    Next I|                                    ! A|=0, wenn ok
    If A|<>0
      Alert 0," Klammerebenen|falsch gesetzt.|",1,"  OK  ",Z|
    Endif
  Else                                         ! Falls keine Eingabe
    Alert 0,"Keine Funktion |  eingegeben.|",1,"  OK  ",Z|
  Endif
Return
'
Procedure Const                                ! Konstanteneingabe
  Locate 1,2                                   ! k.v.
  Arrayfill Con!(),False                       ! Indikator zurcksetzen
  For I|=1 To Ln|                              ! Fn$ durchsuchen
    J|=Asc(Mid$(Fn$,I|))                       ! Aktuelles Zeichen
    K|=Asc(Mid$(Fn$,Pred(I|)))                 ! Zeichen links davon
    If J|>47 And J|<58 And K|=49               ! Falls J|=Zahl und K|="1"
      If Not Con!(J|-48)                       ! und noch nicht eingegeben
        Print " C";Chr$(J|);"=";
        Input Res(J|-38)                       ! Eingabe (in Res(10)-Res(19))
        Con!(J|-48)=True                       ! Indikator setzen
      Endif
    Endif
  Next I|
Return
'
Procedure Vars                                 ! Variableneingabe
  Print                                        ! k.v.
  Arrayfill Var!(),False                       ! Indikator zurcksetzen
  For I|=1 To Ln|                              ! Fn$ durchsuchen
    J|=Asc(Mid$(Fn$,I|))                       ! Aktuelles Zeichen
    K|=Asc(Mid$(Fn$,Pred(I|)))                 ! Zeichen links davon
    If J|>47 And J|<58 And K|=48               ! Falls J|=Zahl und K|="0"
      If Not Var!(J|-48)                       ! und noch nicht eingegeben
        Print " V";Chr$(J|);"=";
        Input Res(J|-48)                       ! Eingabe (in Res(0)-Res(9))
        Var!(J|-48)=True                       ! Indikator setzen
      Endif
    Endif
  Next I|
Return
'
Procedure Main                                 ! Hauptroutine
  F$=Fn$                                       ! Fn$ bleibt erhalten, F$ nicht
  F|=20                                        ! Index fr n„chstes Zw.ergebnis
  Repeat
    Print At(17,11);F$'''''                    ! k.v., nur zur Show
    J|=Pred(Instr(F$,")"))                     ! Erste geschlossene Klammer
    K|=Succ(Rinstr(F$,"(",J|))                 ! Dazugeh”rige offene Klammer
    L|=0
    For I|=K| To J|                            ! Suche nach h”chster Operation
      L|=Max(L|,Instr(Op1$,Mid$(F$,I|,1)))     ! L|=Position in Op1$
    Next I|
    If L|                                      ! Falls Operation vorhanden
      M|=K|+Pred(Instr(Mid$(F$,K|,J|-Pred(K|)),Mid$(Op1$,L|,1))) ! hier ist sie
      O1=Res(Val(Mid$(F$,M|-2,2)))             ! Operand links von ihr
      O2=Res(Val(Mid$(F$,Succ(M|),2)))         ! Operand rechts von ihr
      Select L|                                ! Welche Operation denn nu?
      Case 1
        R=O1-O2
      Case 2
        R=O1+O2
      Case 3                                   ! Die Zahl nach "Case" gibt die
        R=O1\O2                                ! Position des Rechensymbols
      Case 4                                   ! (Bsp. "+") in Op1$ wieder
        R=O1*O2
      Case 5
        R=O1/O2
      Case 6
        R=O1^O2
      Endselect
      Res(F|)=R                                ! Ergebnis merken
      F$=Left$(F$,M|-3)+Str$(F|)+Mid$(F$,M|+3) ! String krzen
      Inc F|                                   ! Index erh”hen
    Else                                       ! Falls keine Operation gefunden
      F$=Left$(F$,K|-2)+Mid$(F$,K|,2)+Mid$(F$,J|+2) ! Klammern eliminieren
      M|=Pred(K|)                              ! Nach evtl. vorhandener Funk-
      Repeat                                   ! tion davor (Bsp. "SIN") suchen
        Dec M|
        N|=Asc(Mid$(F$,M|))
      Until N|<65 Or N|>90                     ! bis Zeichen<>Buchstabe
      If M|<K|-2                               ! Falls Funktion gefunden
        O1=Res(Val(Mid$(F$,Pred(K|),2)))       ! Operand der Funktion
        Select Instr(Op2$,Mid$(F$,Succ(M|),K|-M|-2)) ! Funktion herauspicken
        Case 1
          R=Sin(O1)
        Case 4                                 ! Die Zahl nach "Case" gibt
          R=Cos(O1)                            ! die Position in Op2$ wieder,
        Case 7                                 ! ab der der Funktionsname steht
          R=Tan(O1)
        Case 10
          R=Log10(O1)
        Case 13
          R=Log(O1)
        Endselect
        Res(F|)=R                              ! Ergebnis merken
        F$=Left$(F$,M|)+Str$(F|)+Mid$(F$,Succ(K|)) ! Funktion rausschmeižen
        Inc F|                                 ! Index erh”hen
      Endif
    Endif
  Until Len(F$)=2                              ! Bis nur noch ein Wert brig
  Res=Res(Val(F$))                             ! šbertrag in "Res"
Return
'
Procedure Output                               ! Ausgabe (als Bsp.)
  Box 120,186,520,216                          ! k.v.
  Text 128,206,"Das Ergebnis lautet: F="+Str$(Res)
  ~Inp(2)
Return
