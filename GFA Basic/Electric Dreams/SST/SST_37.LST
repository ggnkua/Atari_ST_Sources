version$="0.37"
compiled!=BYTE{BASEPAGE+256}<>96
blitter_activation
'
initialisation
'
process.menu!=TRUE
REPEAT
  IF process.menu!
    process.menu
    '
  ELSE IF process.intro!
    process.intro
    '
  ELSE IF process.game!
    BMOVE V:bg|(0),e2%,32000
    '
    IF klingon.fire!=TRUE AND mes|=0
      IF k3|>0
        mode$="klingon"
        klingon.move
        klingon.ship|=0
        klingon.attack
      ENDIF
      klingon.fire!=FALSE
    ENDIF
    '
    IF mode$="klingon"
      klingon_keyboard
    ELSE
      IF mode$="text"
        text_keyboard
      ELSE
        game_keyboard
      ENDIF
    ENDIF
    '
    IF mode$<>"klingon" AND next.action$="move"
      next.action$=""
      do.next.action!=FALSE
      moving.starship
    ENDIF
    '
    fill.sector(35,45)
    '
    explosion.managment
    phaser.managment
    '
    fill.galaxy(405,45)
    '
    update.ship.log(10,195)
    '
    update.damage.report(10,245)
    '
    update.command.area(208,245)
    IF portrait|(mes|)>0
      RC_COPY V:tiles|(0),(portrait|(mes|)-1)*91,0,90,100 TO e2%,208+4,245+14
    ENDIF
    '
    '    b$="FALSE"
    '    IF klingon.fire!
    '    b$="TRUE"
    '  ENDIF
    '    TEXT 0,7,"mode$:"+mode$+"    process$:"+process$+"    mes|:"+STR$(mes|)+"     "+b$+"    k.s:"+STR$(klingon.ship|)+"   Na:"+next.action$+"  Mem:"+STR$(FRE())
    refresh_screen
    cursor.timer
  ENDIF
UNTIL exit.game!
'
fin
'
' -----------------------------------------------------------------------------
PROCEDURE show.progression
  cpt|=0
  xb&=235
  REPEAT
    PBOX xb&,190,xb&+20,210
    ADD xb&,50
    INC cpt|
  UNTIL cpt|>cpt.max|
  INC cpt.max|
  refresh_screen
RETURN
'
' -----------------------------------------------------------------------------
PROCEDURE initialisation
  '
  IF compiled!
    app_dir$="DATA\"
  ELSE
    app_dir$=CHR$(GEMDOS(&H19)+65)+":"+"\SST\DATA\"
  ENDIF
  '
  rez&=XBIOS(4)     ! resolution initiale
  '
  DIM palette|(32)                ! On reserve 32 octets pour stocker la palette
  super%=GEMDOS(32,L:0)           ! Mode SuperViseur
  BYTE{&H484}=BCLR(BYTE{&H484},0) ! On coupe le bip du clavier
  BMOVE &HFF8240,V:palette|(0),32 ! Copie de la palette actuelle
  ~GEMDOS(32,L:super%)            ! Mode Utilisateur
  '
  ' sauvegarde des parametres initiaux
  adr_phy%=XBIOS(2) ! adresse ecran physique
  adr_log%=XBIOS(3) ! adresse ecran logique
  '
  DIM screen_buffers|(64000+255+33280)
  e1%=(V:screen_buffers|(0)+31999) AND &HFFFF8200
  e2%=e1%+32000
  '
  ~XBIOS(5,L:e2%,L:e1%,W:-1)
  '
  OUT 4,18      ! desactive la souris
  '
  DIM bg|(32000)
  DIM physical_copy|(32000)
  DIM tiles|(32000),sprites|(2720)
  DIM instructions|(64000)
  '
  get_graphs
  '
  xb&=50
  '
  init.datas
  '
  cpt.max|=0
  show.progression
  '
  get.instructions
  '
  show.progression
  '
  construct.instructions(0)
  '
  show.progression
  '
  construct.instructions(1)
  '
  show.progression
  '
  ERASE instruction$()
  '
RETURN
'
' -----------------------------------------------------------------------------
PROCEDURE fin
  ' restauration des paramtres initiaux
  ~XBIOS(5,L:adr_log%,L:adr_phy%,W:rez&)
  super%=GEMDOS(32,L:0)
  BMOVE V:palette|(0),&HFFFF8240,32
  BYTE{&H484}=BSET(BYTE{&H484},0)
  ~GEMDOS(32,L:super%)
  '
  OUT 4,8       !reactive la souris
  '
  ERASE screen_buffers|()
  ERASE bg|(),physical_copy|(),tiles|(),instructions|()
  ERASE explosion!(),x.explosion|(),y.explosion|(),frame.explosion|()
  ERASE phaser!(),x.phaser|(),y.phaser|(),frame.phaser|()
  CLEAR
  '
  EDIT
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE init.datas
  DIM g&(8,8),c&(9,2),k&(3,3),n(3),z&(8,8),damage(8),s$(64,64),damage.lib$(8)
  DIM explosion!(3),x.explosion|(3),y.explosion|(3),frame.explosion|(3)
  DIM phaser!(3),x.phaser|(3),y.phaser|(3),frame.phaser|(3)
  DIM dam|(8),message.box|(10)
  '
  damage.lib$(1)="WARP ENGINES  "
  damage.lib$(2)="SR SENSORS    "
  damage.lib$(3)="LR SENSORS    "
  damage.lib$(4)="PHASER CONTROL"
  damage.lib$(5)="PHOTON TUBES  "
  damage.lib$(6)="DAMAGE CONTROL"
  damage.lib$(7)="SHIELD CONTROL"
  damage.lib$(8)="LIB-COMPUTER  "
  '
  FOR i|=1 TO 9
    c&(i|,1)=0
    c&(i|,2)=0
  NEXT i|
  '
  c&(3,1)=-1
  c&(2,1)=-1
  c&(4,1)=-1
  c&(4,2)=-1
  c&(5,2)=-1
  c&(6,2)=-1
  c&(1,2)=1
  c&(2,2)=1
  c&(6,1)=1
  c&(7,1)=1
  c&(8,1)=1
  c&(8,2)=1
  c&(9,2)=1
  '
  init.messages
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE set.new.game
  k3|=0
  x$=""
  x0$=" IS"
  ex|=RAND(64)
  ey|=RAND(64)
  xqe|=DIV(ex|,8)+1
  yqe|=DIV(ey|,8)+1
  xse|=MOD(ex|,8)+1
  yse|=MOD(ey|,8)+1
  s$(ex|+1,ey|+1)="<*>"
  torpedoes|=10
  energy.max&=3000
  energy&=energy.max&
  shield&=0
  '
  init_stardate
  populate
  '
  init.sector
  '
RETURN
'
' ------------------------------------------------------------------------------
FUNCTION fnd(i|)
  val=SQR((k&(i|,1)-(ex|+1))^2+(k&(i|,2)-(ey|+1))^2)
  RETURN val
ENDFUNC
'
' ------------------------------------------------------------------------------
PROCEDURE populate
  '
  k9|=0
  b9|=0
  s9=200
  '
  REPEAT
    FOR i|=1 TO 8
      FOR j|=1 TO 8
        k3|=0
        z&(i|,j|)=0
        pct|=RAND(100)+1
        '
        IF pct|>98
          k3|=3
        ELSE IF pct|>95
          k3|=2
        ELSE IF pct|>80
          k3|=1
        ENDIF
        ADD k9|,k3|
        '
        b3|=0
        IF RAND(100)+1>96
          b3|=1
          INC b9|
        ENDIF
        '
        g&(i|,j|)=k3|*100+b3|*10+RAND(8)+1
      NEXT j|
    NEXT i|
  UNTIL k9|>0 AND b9|>0
  '
  IF t9|<k9|
    t9|=k9|+1
  ENDIF
  '
  k7|=k9|
  IF b9|<>1
    x$="S"
    x0$=" ARE"
  ENDIF
  '
  z&(xqe|,yqe|)=g&(xqe|,yqe|)
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE init_stardate
  t=(RAND(21)+20)*100
  t0=t
  t9|=RAND(11)+25
  t=0
  '
RETURN
'
' ------------------------------------------------------------------------------
FUNCTION quadrant.name$(detail!)
  g2$=""
  IF yqe|<=4
    SELECT xqe|
    CASE 1
      g2$="ANTARES"
    CASE 2
      g2$="RIGEL"
    CASE 3
      g2$="PROCYON"
    CASE 4
      g2$="VEGA"
    CASE 5
      g2$="CANOPUS"
    CASE 6
      g2$="ALTAIR"
    CASE 7
      g2$="SAGITARUS"
    CASE 8
      g2$="POLLUX"
    ENDSELECT
    '
  ELSE
    SELECT xqe|
    CASE 1
      g2$="SIRIUS"
    CASE 2
      g2$="DENEB"
    CASE 3
      g2$="CAPELLA"
    CASE 4
      g2$="BETELGEUSE"
    CASE 5
      g2$="ALDEBARAN"
    CASE 6
      g2$="REGULUS"
    CASE 7
      g2$="ARCTURUS"
    CASE 8
      g2$="SPICA"
    ENDSELECT
    '
  ENDIF
  '
  IF detail!
    SELECT yqe|
    CASE 1
      g2$=g2$+" I"
    CASE 2
      g2$=g2$+" II"
    CASE 3
      g2$=g2$+" III"
    CASE 4
      g2$=g2$+" IV"
    CASE 5
      g2$=g2$+" I"
    CASE 6
      g2$=g2$+" II"
    CASE 7
      g2$=g2$+" III"
    CASE 8
      g2$=g2$+" IV"
    ENDSELECT
    '
  ENDIF
  '
  RETURN g2$
  '
ENDFUNC
'
' ------------------------------------------------------------------------------
PROCEDURE display.enterprise(x&,y&)
  TEXT x&,y&,"___________________          _-_"
  ADD y&,16
  TEXT x&,y&,"\==============_=_/ ____.---'---'---.____"
  ADD y&,16
  TEXT x&,y&,"           \_ \     \----._________.----/"
  ADD y&,16
  TEXT x&,y&,"             \ \    /  /    '-_-'"
  ADD y&,16
  TEXT x&,y&,"         __.--'.'--'..'-_"
  ADD y&,16
  TEXT x&,y&,"        /____           ||"
  ADD y&,16
  TEXT x&,y&,"             '--._____.-'"
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.trame(x&,y&)
  '
  DEFTEXT 1,0,0,13
  FOR i|=0 TO 8
    DEFLINE &X11111111111111010101010101010110,1,0,0
    IF i|=0 OR i|=8
      DEFLINE 1,1,0,0
    ENDIF
    LINE x&,y&-4+18*i|,x&+8*28,y&-4+18*(i|)
    LINE x&+28*i|,y&-4,x&+28*(i|),y&-4+18*8
  NEXT i|
  '
  FOR i|=0 TO 7
    TEXT x&+3+i|*28,y&+11-18," "+STR$(i|+1)
    TEXT x&+3-28,y&+11+i|*18,"  "+STR$(i|+1)
  NEXT i|
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.mission(x&,y&)
  TEXT x&,y&,"YOUR ORDERS ARE AS FOLLOWS:"
  ADD y&,16
  TEXT x&,y&," DESTROY THE "+STR$(k9|)+" KLINGON WARSHIPS WHICH HAVE INVADED"
  ADD y&,16
  TEXT x&,y&," THE GALAXY BEFORE THEY CAN ATTACK FEDERATION HEADQUARTERS"
  ADD y&,16
  TEXT x&,y&," ON STARDATE "+STR$(t0+t9|)+" THIS GIVES YOU "+STR$(t9|)+" DAYS."
  ADD y&,16
  TEXT x&,y&," THERE"+x0$+" "+STR$(b9|)+" STARBASE"+x$+" IN THE GALAXY FOR RESUPPLYING YOUR SHIP."
  ADD y&,16
  ADD y&,16
  '
  TEXT x&,y&,"YOUR MISSION BEGINS WITH YOUR STARSHIP LOCATED"
  ADD y&,16
  TEXT x&,y&," IN THE GALACTIC QUADRANT, '"+@quadrant.name$(TRUE)+"'."
  ADD y&,16
  ADD y&,16
  TEXT x&,y&,"HIT ANY KEY WHEN READY TO ACCEPT COMMAND."
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.intro
  CLS
  refresh_screen
  CLS
  GRAPHMODE 3
  PBOX 0,10,639,66
  DEFTEXT 1,0,0,32
  TEXT 100,50,CHR$(14)+CHR$(15)+"    SUPER STAR TREK    "+CHR$(14)+CHR$(15)
  DEFTEXT 1,0,0,13
  display.enterprise(150,100)
  display.mission(50,240)
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.rose(x&,y&)
  t.height|=8
  DEFTEXT 1,0,0,6
  TEXT x&,y&,"       3"
  ADD y&,t.height|
  TEXT x&,y&,"    4     2"
  ADD y&,t.height|
  TEXT x&,y&,"     \ | /"
  ADD y&,t.height|
  TEXT x&,y&,"      \|/"
  ADD y&,t.height|
  TEXT x&,y&,"   5 --*-- 1"
  ADD y&,t.height|
  TEXT x&,y&,"      /|\"
  ADD y&,t.height|
  TEXT x&,y&,"     / | \"
  ADD y&,t.height|
  TEXT x&,y&,"    6     8"
  ADD y&,t.height|
  TEXT x&,y&,"       7"
  ADD y&,t.height|
  ADD y&,t.height|
  ADD y&,t.height|
  ADD y&,t.height|
  TEXT x&,y&,"      ###"
  ADD y&,t.height|
  TEXT x&,y&,"     . . ."
  ADD y&,t.height|
  TEXT x&,y&,"    .  .  ."
  ADD y&,t.height|
  TEXT x&,y&,"   . BASES ."
  ADD y&,t.height|
  TEXT x&,y&,"  .         ."
  ADD y&,t.height|
  TEXT x&,y&,"KLINGONS STARS"
  DEFTEXT 1,0,0,13
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE update.ship.log(x&,y&)
  t.height|=10
  '
  DEFTEXT 1,0,0,6
  ADD x&,2
  ADD y&,9
  ADD x&,1
  ADD y&,t.height|+3
  y0&=y&
  TEXT x&+96,y&,STR$(energy&)
  ADD y&,t.height|
  TEXT x&+96,y&,STR$(shield&)
  ADD y&,t.height|
  TEXT x&+96,y&,STR$(torpedoes|)
  ADD y&,t.height|
  '
  ADD x&,180
  y&=y0&
  TEXT x&+168,y&,condition$
  ADD y&,t.height|
  TEXT x&+168,y&,STR$(t9|-t)
  ADD y&,t.height|
  TEXT x&+168,y&,STR$(k9|)
  ADD y&,t.height|
  '
  ADD x&,250
  y&=y0&
  TEXT x&+88,y&,STR$(t0+t)
  ADD y&,t.height|
  TEXT x&+88,y&,STR$(xqe|)+","+STR$(yqe|)
  ADD y&,t.height|
  TEXT x&+88,y&,STR$(xse|)+","+STR$(yse|)
  ADD y&,t.height|
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE update.damage.report(x&,y&)
  t.height|=10
  GRAPHMODE 3
  '
  ADD x&,3
  ADD y&,t.height|+12
  FOR b|=1 TO 8
    IF damage(b|)<0
      TEXT x&+122,y&,"Inactive"
    ELSE
      TEXT x&+122,y&,"Active"
    ENDIF
    ADD y&,t.height|
  NEXT b|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.command.area(x&,y&)
  t.height|=10
  GRAPHMODE 3
  BOUNDARY 0
  BOX x&,y&+12,x&+8*53,y&+t.height|*11+6
  BOUNDARY 0
  PBOX x&,y&,x&+8*53,y&+11
  '
  ADD x&,2
  ADD y&,9
  lib$="COMMAND AREA"
  DEFTEXT 1,0,0,6
  TEXT x&,y&,lib$
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.command(x&,y&)
  t.height|=10
  GRAPHMODE 3
  BOUNDARY 0
  BOX x&,y&+12,x&+640-2*x&,y&+t.height|*2+6
  BOUNDARY 0
  PBOX x&,y&,x&+640-2*x&,y&+11
  x0&=x&
  '
  SUB y&,9
  DEFTEXT 1,16,0,6
  TEXT x&,y&,"H"
  DEFTEXT 1,0,0,6
  ADD x&,8-2
  TEXT x&,y&," or"
  DEFTEXT 1,16,0,6
  ADD x&,8*4-2
  TEXT x&,y&,"HELP"
  DEFTEXT 1,0,0,6
  ADD x&,8*4
  TEXT x&,y&," : INSTRUCTIONS"
  '
  x&=x0&
  ADD x&,2
  ADD y&,18
  DEFTEXT 1,0,0,6
  lib$="COMMANDS"
  DEFTEXT 1,0,0,6
  TEXT x&,y&,lib$
  '
  ADD y&,t.height|+3
  y0&=y&
  ADD x&,2
  DEFTEXT 1,16,0,6
  TEXT x&,y&,"W"
  DEFTEXT 1,1,0,6
  ADD x&,8
  TEXT x&,y&,"arp"
  DEFTEXT 1,16,0,6
  ADD x&,8*5-1
  TEXT x&,y&,"P"
  DEFTEXT 1,1,0,6
  ADD x&,8
  TEXT x&,y&,"hasers"
  DEFTEXT 1,16,0,6
  ADD x&,8*8-1
  TEXT x&,y&,"T"
  DEFTEXT 1,1,0,6
  ADD x&,8
  TEXT x&,y&,"orpedoes"
  DEFTEXT 1,16,0,6
  ADD x&,8*10-1
  TEXT x&,y&,"S"
  DEFTEXT 1,1,0,6
  ADD x&,8
  TEXT x&,y&,"hields"
  DEFTEXT 1,16,0,6
  ADD x&,8*8-1
  TEXT x&,y&,"L"
  DEFTEXT 1,1,0,6
  ADD x&,8
  TEXT x&,y&,"ong.Range.Scan"
  DEFTEXT 1,16,0,6
  ADD x&,8*16-1
  TEXT x&,y&,"C"
  DEFTEXT 1,1,0,6
  ADD x&,8
  TEXT x&,y&,"omputer"
  DEFTEXT 1,16,0,6
  ADD x&,8*9-1
  TEXT x&,y&,"D"
  DEFTEXT 1,1,0,6
  ADD x&,8
  TEXT x&,y&,"amage"
  DEFTEXT 1,16,0,6
  ADD x&,8*7-1
  TEXT x&,y&,"A"
  DEFTEXT 1,1,0,6
  ADD x&,8
  TEXT x&,y&,"bandon"
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE fill.noise(x&,y&)
  '
  DEFTEXT 1,0,0,13
  FOR b|=0 TO 20
    xn|=RAND(200)
    yn|=RAND(130)
    TEXT x&+xn|+10,y&+yn|+10,"-"
  NEXT b|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE fill.galaxy(x&,y&)
  '
  IF damage(3)>=0
    DEFTEXT 1,0,0,13
    BOUNDARY 0
    FOR i|=0 TO 7
      FOR j|=0 TO 7
        IF z&(i|+1,j|+1)>0
          IF xqe|=i|+1 AND yqe|=j|+1
            PBOX x&+28*j|+1,y&-4+18*i|+1,x&+28*j|+1+27,y&-4+18*i|+1+17
          ENDIF
          TEXT x&+3+j|*28,y&+11+i|*18,RIGHT$("000"+STR$(g&(j|+1,i|+1)),3)
        ENDIF
      NEXT j|
    NEXT i|
  ELSE
    fill.noise(x&,y&)
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE fill.sector(x&,y&)
  '
  IF damage(2)>=0
    DEFTEXT 1,0,0,13
    lib$="'"+@quadrant.name$(TRUE)+"'"
    TEXT x&+(((28*8)-LEN(lib$)*8)/2),y&+11-18-18,lib$
    '
    FOR i|=0 TO 7
      FOR j|=0 TO 7
        IF LEN(s$(i|+1+(xqe|-1)*8,j|+1+(yqe|-1)*8))>0
          TEXT x&+3+j|*28,y&+11+i|*18,s$(i|+1+(xqe|-1)*8,j|+1+(yqe|-1)*8)
          IF s$(i|+1+(xqe|-1)*8,j|+1+(yqe|-1)*8)="<*>"
            RC_COPY V:sprites|(0),0,0,27,17 TO e2%,x&+1+j|*28,y&-3+i|*18
          ELSE IF s$(i|+1+(xqe|-1)*8,j|+1+(yqe|-1)*8)=" * "
            RC_COPY V:sprites|(0),54,0,27,17 TO e2%,x&+1+j|*28,y&-3+i|*18
          ELSE IF s$(i|+1+(xqe|-1)*8,j|+1+(yqe|-1)*8)="-K-"
            RC_COPY V:sprites|(0),27,0,27,17 TO e2%,x&+1+j|*28,y&-3+i|*18
          ELSE IF s$(i|+1+(xqe|-1)*8,j|+1+(yqe|-1)*8)=CHR$(255)+"Y"+CHR$(255)
            RC_COPY V:sprites|(0),81,0,27,17 TO e2%,x&+1+j|*28,y&-3+i|*18
          ENDIF
        ENDIF
      NEXT j|
    NEXT i|
  ELSE
    fill.noise(x&,y&)
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE clean.sector
  FOR i|=1 TO 8
    FOR j|=1 TO 8
      s$(i|+(xqe|-1)*8,j|+(yqe|-1)*8)=""
    NEXT j|
  NEXT i|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE init.sector
  condition$="GREEN"
  k3|=INT(g&(yqe|,xqe|)*0.01)
  b3|=INT(g&(yqe|,xqe|)*0.1)-10*k3|
  s3|=INT(g&(yqe|,xqe|)-100*k3|-10*b3|)
  damage.malus=0.5*RND(1)
  '
  FOR b|=1 TO s3|
    done!=FALSE
    REPEAT
      j|=RAND(8)+1+(xqe|-1)*8
      i|=RAND(8)+1+(yqe|-1)*8
      IF LEN(s$(j|,i|))=0
        s$(j|,i|)=" * "
        done!=TRUE
      ENDIF
    UNTIL done!
  NEXT b|
  '
  FOR b|=1 TO b3|
    done!=FALSE
    b4|=0
    b5|=0
    REPEAT
      j|=RAND(8)+1+(xqe|-1)*8
      i|=RAND(8)+1+(yqe|-1)*8
      IF LEN(s$(j|,i|))=0
        s$(j|,i|)=CHR$(255)+"Y"+CHR$(255)
        done!=TRUE
        b4|=j|
        b5|=i|
      ENDIF
    UNTIL done!
  NEXT b|
  '
  ARRAYFILL k&(),0
  '
  FOR b|=1 TO k3|
    done!=FALSE
    REPEAT
      j|=RAND(8)+1+(xqe|-1)*8
      i|=RAND(8)+1+(yqe|-1)*8
      IF LEN(s$(j|,i|))=0
        s$(j|,i|)="-K-"
        done!=TRUE
        k&(b|,1)=j|
        k&(b|,2)=i|
        k&(b|,3)=RAND(200)+100
      ENDIF
    UNTIL done!
  NEXT b|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE set.status
  '
  IF k3|>0
    condition$="RED"
    mes|=23
  ELSE IF docked!
    condition$="DOCKED"
    mes|=26
  ELSE IF energy&<0.1*energy.max&
    condition$="YELLOW"
  ELSE
    condition$="GREEN"
  ENDIF
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE fill.lsr(x&,y&)
  IF damage(3)<0
    new.mess(42)
  ELSE
    FOR ii|=xqe|-1 TO xqe|+1
      FOR jj|=yqe|-1 TO yqe|+1
        IF ii|>0 AND ii|<9 AND jj|>0 AND jj|<9
          z&(ii|,jj|)=g&(jj|,ii|)
        ENDIF
      NEXT jj|
    NEXT ii|
    '
    new.mess(2)
    '    mes|=2
  ENDIF
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.quadrant(x&,y&)
  display.trame(x&,y&)
  '
  DEFTEXT 1,0,900,13
  lib$="QUADRANT"
  TEXT x&-16,y&+(((18*7)-LEN(lib$)*8)/2)+LEN(lib$)*8,lib$
  DEFTEXT 1,0,0,13
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.galaxy(x&,y&)
  DEFTEXT 1,0,0,13
  display.trame(x&,y&)
  lib$="GALAXY"
  TEXT x&+(((28*8)-LEN(lib$)*8)/2),y&+11-18-18,lib$
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE game_keyboard
  t$=INKEY$
  '
  IF update.command.area!=FALSE
    ' On teste l'appui de n'importe quelle touche
    IF LEN(t$)=1
      bip
      IF mode$="klingon" AND (klingon.ship|>=3 OR docked!)
        mode$="game"
        klingon.ship|=0
        mes|=0
      ELSE IF mode$="klingon"
        klingon.attack
      ENDIF
      '
      IF mes|=23
        condition$="RED"
      ENDIF
      '
      IF mes|=35
        process.game!=FALSE
        process.menu!=TRUE
      ENDIF
      '
      update.command.area!=TRUE
    ENDIF
  ELSE
    IF LEN(t$)=1 AND (ASC(t$)=99 OR ASC(t$)=67)      ! Touche c ou C
      computer.process
      '
    ELSE IF LEN(t$)=1 AND (ASC(t$)=100 OR ASC(t$)=68)     ! Touche d ou D
      damage.process
      '
    ELSE IF LEN(t$)=1 AND (ASC(t$)=104 OR ASC(t$)=72)     ! Touche h ou H
      display.instructions
      '
    ELSE IF LEN(t$)=1 AND (ASC(t$)=108 OR ASC(t$)=76)     ! Touche l ou L
      fill.lsr(0,0)
      '
    ELSE IF LEN(t$)=1 AND (ASC(t$)=109 OR ASC(t$)=77)     ! Touche m ou M
      show.all.galaxy
      '
    ELSE IF LEN(t$)=1 AND (ASC(t$)=112 OR ASC(t$)=80)     ! Touche p ou P
      phaser.process
      '
    ELSE IF LEN(t$)=1 AND (ASC(t$)=115 OR ASC(t$)=83)     ! Touche s ou S
      shields.process
      '
    ELSE IF LEN(t$)=1 AND (ASC(t$)=116 OR ASC(t$)=84)     ! Touche t ou T
      torpedo.process
      '
    ELSE IF LEN(t$)=1 AND (ASC(t$)=119 OR ASC(t$)=87)     ! Touche w ou W
      nav.process
      '
    ELSE IF LEN(t$)=1 AND ASC(t$)=27                      ! Touche ESCAPE
      exit.game!=TRUE
      '
    ENDIF
  ENDIF
  ' On vide le buffer clavier
  REPEAT
  UNTIL INKEY$=""
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE klingon_keyboard
  t$=INKEY$
  '
  '  IF mes|=0 AND klingon.ship|>=3
  IF klingon.ship|>=3
    mode$="game"
  ENDIF
  ' On teste l'appui de n'importe quelle touche
  IF LEN(t$)=1 AND klingon.ship|<3
    klingon.attack
    update.command.area!=TRUE
  ENDIF
  '
  ' On vide le buffer clavier
  REPEAT
  UNTIL INKEY$=""
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE text_keyboard
  t$=INKEY$
  '
  ' on n'autorise qu'une saisie numerique poisitive decimale
  IF mode_text$="text"
    IF LEN(t$)=1 AND ((ASC(t$)>=65 AND ASC(t$)<=122)) ! Touche 0 … 9
      a$=a$+CHR$(ASC(t$))
    ENDIF
    '
  ELSE
    IF LEN(t$)=1 AND ((ASC(t$)>=48 AND ASC(t$)<=57) OR ASC(t$)=46) ! Touche 0 … 9
      a$=a$+CHR$(ASC(t$))
    ENDIF
  ENDIF
  '
  IF LEN(t$)=1 AND ASC(t$)=13                     ! Touche Return
    post_input
    '
  ELSE IF LEN(t$)=1 AND ASC(t$)=8                      ! Touche Backspace
    IF LEN(a$)>0
      a$=LEFT$(a$,LEN(a$)-1)
    ENDIF
    '
  ELSE IF LEN(t$)=1 AND ASC(t$)=27                     ! Touche ESCAPE
    mode$="game"
    '
  ENDIF
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE refresh_screen
  SWAP e1%,e2%
  ~XBIOS(5,L:e2%,L:e1%,W:-1)
  VSYNC
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE construct.bg
  CLS
  refresh_screen
  CLS
  display.quadrant(35,45)
  display.rose(270,45)
  display.galaxy(405,45)
  display.ship.log(10,195)
  display.damage.report(10,245)
  display.command.area(208,245)
  display.command(10,370)
  BMOVE e2%,V:bg|(0),32000
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.ship.log(x&,y&)
  t.height|=10
  GRAPHMODE 3
  BOUNDARY 0
  BOX x&,y&+12,x&+640-2*x&,y&+t.height|*4+6
  BOUNDARY 0
  PBOX x&,y&,x&+640-2*x&,y&+11
  '
  ADD x&,2
  ADD y&,9
  lib$="SHIP'S LOG"
  DEFTEXT 1,0,0,6
  TEXT x&,y&,lib$
  '
  ADD x&,1
  ADD y&,t.height|+3
  y0&=y&
  TEXT x&,y&,"ENERGY    : "
  ADD y&,t.height|
  TEXT x&,y&,"SHIELDS   : "
  ADD y&,t.height|
  TEXT x&,y&,"TORPEDOES : "
  ADD y&,t.height|
  '
  ADD x&,180
  y&=y0&
  TEXT x&,y&,"CONDITION          : "
  ADD y&,t.height|
  TEXT x&,y&,"TIME REMAINING     : "
  ADD y&,t.height|
  TEXT x&,y&,"KLINGONS REMAINING : "
  ADD y&,t.height|
  '
  ADD x&,250
  y&=y0&
  TEXT x&,y&,"STARDATE : "
  ADD y&,t.height|
  TEXT x&,y&,"QUADRANT : "
  ADD y&,t.height|
  TEXT x&,y&,"SECTOR   : "
  ADD y&,t.height|
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.damage.report(x&,y&)
  t.height|=10
  GRAPHMODE 3
  BOUNDARY 0
  BOX x&,y&+12,x&+8*24,y&+t.height|*9+6
  BOUNDARY 0
  PBOX x&,y&,x&+8*24,y&+11
  '
  ADD x&,2
  ADD y&,9
  lib$="DAMAGE REPORT"
  DEFTEXT 1,0,0,6
  TEXT x&,y&,lib$
  '
  ADD x&,1
  ADD y&,t.height|+3
  y0&=y&
  FOR b|=1 TO 8
    TEXT x&,y&,damage.lib$(b|)+":"
    ADD y&,t.height|
  NEXT b|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE shields.process
  IF docked!
    process$=""
    new.mess(27)
    '
  ELSE IF damage(7)<0
    new.mess(46)
  ELSE
    mes|=1
    process$="shields"
    mode$="text"
    mode_text$="num"
  ENDIF
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE nav.process
  mes|=10
  process$="nav"
  mode$="text"
  mode_text$="num"
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE init.messages
  DIM lib$(50,10),portrait|(50)
  '
  lib$(4,0)="DEFLECTOR SHIELDS DOWN, CAPTAIN."
  portrait|(4)=5
  '
  lib$(6,0)="SHIELD CONTROL REPORTS :"
  lib$(6,1)="'THIS IS NOT THE FEDERATION TREASURY.'"
  portrait|(6)=7
  '
  lib$(7,0)="SHIELD CONTROL REPORTS :"
  lib$(7,1)="SIELDS UNCHANGED."
  portrait|(7)=5
  '
  lib$(8,0)="SHIELD CONTROL REPORTS :"
  lib$(8,1)="DEFLECTOR SHIELDS DOWN, CAPTAIN."
  portrait|(8)=5
  '
  lib$(11,0)="INCORRECT COURSE DATA, SIR!"
  portrait|(11)=4
  '
  lib$(14,0)="WARP ENGINES ARE DAMAGED"
  lib$(14,1)="MAXIUM SPEED = WARP 0.2"
  portrait|(14)=7
  '
  lib$(18,0)="LT. UHURA REPORTS MESSAGE FROM STARFLEET"
  lib$(18,1)="COMMAND:"
  lib$(18,2)="  'PERMISSION TO ATTEMPT CROSSING OF"
  lib$(18,3)="  GALACTIC PERIMETER IS HEREBY *DENIED*."
  lib$(18,4)="  SHUT DOWN YOUR ENGINES.'"
  portrait|(18)=6
  '
  lib$(23,0)=" "
  lib$(23,1)="             COMBAT AREA"
  lib$(23,2)=" "
  lib$(23,3)="            CONDITION RED"
  portrait|(23)=1
  '
  lib$(24,0)="SENSORS SHOW NO ENEMY SHIPS IN"
  lib$(24,1)="THIS QUADRANT"
  portrait|(24)=2
  '
  lib$(26,0)="SHIELDS DROPPED FOR DOCKING PURPOSES"
  portrait|(26)=6
  '
  lib$(27,0)="ENTERPRISE IS DOCKED TO A STARBASE"
  lib$(27,1)="AND CAN NOT USE HIS SHIELD"
  lib$(27,2)="STARBASE SHIELDS PROTECT THE ENTERPRISE"
  portrait|(27)=6
  '
  lib$(29,0)="ALL PHOTON TORPEDOES EXPENDED"
  portrait|(29)=6
  '
  lib$(31,0)="INCORRECT COURSE DATA, SIR!"
  portrait|(31)=5
  '
  lib$(33,0)="STARBASE SHIELDS PROTECT THE ENTERPRISE"
  portrait|(33)=6
  '
  lib$(41,0)="SHORT RANGE SENSORS ARE OUT"
  portrait|(41)=2
  '
  lib$(42,0)="LONG RANGE SENSORS ARE INOPERABLE"
  portrait|(42)=2
  '
  lib$(43,0)="PHASERS INOPERATIVE"
  portrait|(43)=2
  '
  lib$(44,0)="PHOTON TUBES ARE NOT OPERATIONAL"
  portrait|(44)=2
  '
  lib$(45,0)="DAMAGE CONTROL REPORT NOT AVAILABLE"
  portrait|(45)=2
  '
  lib$(46,0)="SHIELD CONTROL INOPERABLE"
  portrait|(46)=2
  '
  lib$(47,0)="COMPUTER DISABLED"
  portrait|(47)=2
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE update.messages
  SELECT mes|
  CASE 1
    lib$(1,0)="ENERGY AVAILABLE = "+STR$(energy&+shield&)
    lib$(1,1)="NUMBER OF UNITS TO SHIELDS : "+a$
    portrait|(1)=5
    '
  CASE 2
    lib$(2,0)="LONG RANGE SCAN FOR QUADRANT "+STR$(xqe|)+","+STR$(yqe|)
    lib$(2,1)="DATA UPDATED ON SCREEN, CAPTAIN."
    portrait|(2)=2
    '
  CASE 3
    lib$(3,0)="NOW ENTERING "+@quadrant.name$(TRUE)+" QUADRANT."
    portrait|(3)=2
    '
  CASE 5
    lib$(5,0)="CAPTAIN, SENSORS ARE PICKING UP"
    x$=""
    IF k3|>1
      x$="S"
    ENDIF
    lib$(5,1)=STR$(k3|)+" KLINGON BATTLE CRUISER"+x$
    portrait|(5)=2
    '
  CASE 9
    lib$(9,0)="SHIELD CONTROL REPORTS :"
    lib$(9,1)="SHIELDS NOW AT "+STR$(shield&)+" UNITS"
    lib$(9,2)="PER YOUR COMMAND, CAPTAIN."
    portrait|(9)=5
    process$=""
    mode$="game"
    '
  CASE 10
    lib$(10,0)="COURSE (0-9) : "+a$
    portrait|(10)=4
    '
  CASE 12
    lib$(12,0)="COURSE (0-9) : "+STR$(c1)
    IF damage(1)<0
      wfmax=0.2
    ELSE
      wfmax=8
    ENDIF
    lib$(12,1)="WARP FACTOR (0-"+STR$(wfmax)+") : "+a$
    portrait|(12)=4
    '
  CASE 13
    lib$(13,0)="THE ENGINES WON'T TAKE WARP "+STR$(w1)
    portrait|(13)=7
    '
  CASE 15
    lib$(15,0)="INSUFFICIENT ENERGY AVAILABLE"
    lib$(15,1)="FOR MANEUVERING AT WARP "+STR$(w1)
    portrait|(15)=7
    '
  CASE 16
    lib$(16,0)="ENERGY TO WARP : "+STR$(energy.used|)
    portrait|(16)=4
    '
  CASE 17
    lib$(17,0)="WARP ENGINES SHUT DOWN AT"
    lib$(17,1)="SECTOR "+STR$(xse|)+","+STR$(yse|)+" DUE TO BAD NAVAGITION"
    portrait|(17)=4
    '
  CASE 19
    lib$(19,0)="CHIEF ENGINEER SCOTT REPORTS 'WARP ENGINES SHUT DOWN"
    lib$(19,1)=" AT SECTOR "+STR$(xse|)+","+STR$(yse|)+" OF QUADRANT "+STR$(xqe|)+","+STR$(yqe|)+".'"
    portrait|(19)=7
    '
  CASE 20
    lib$(20,0)="COMPUTER ACTIVE AND AWAITING COMMAND"
    lib$(20,1)="FUNCTIONS AVAILABLE FROM COMPUTER:"
    lib$(20,2)=" 0 = STATUS REPORT"
    lib$(20,3)=" 1 = PHOTON TORPEDO DATA"
    lib$(20,4)=" 2 = STARBASE NAV DATA"
    lib$(20,5)=" 3 = DIRECTION/DISTANCE CALCULATOR"
    lib$(20,6)=" 4 = EXIT LIBRARY-COMPUTER"
    lib$(20,7)="SELECT A COMMAND : "+a$
    portrait|(20)=2
    '
  CASE 21
    lib$(21,0)="       STATUS REPORT"
    x$=""
    IF k9|>1
      x$="S"
    ENDIF
    lib$(21,1)=". KLINGON"+x$+" LEFT: "+STR$(k9|)
    lib$(21,2)=". MISSION MUST BE COMPLETED IN "+STR$(t9|-t)
    lib$(21,3)="  STARDATES"
    x$=""
    IF b9|>1
      x$="S"
    ENDIF
    lib$(21,4)=". THE FEDERATION IS MAINTAINING "+STR$(b9|)+" BASE"+x$
    lib$(21,5)="  IN THE GALAXY"
    portrait|(21)=2
    '
  CASE 22
    clean.lib(22)
    x$=""
    IF k3|>1
      x$="S"
    ENDIF
    IF k3|>0
      lib$(22,0)="FROM ENTERPRISE TO KLINGON BATTLE CRUISER"+x$
      ind|=1
      FOR b|=1 TO 3
        IF k&(b|,3)>0
          d1=ex|+1-k&(b|,1)
          d2=ey|+1-k&(b|,2)
          '
          get.direction.and.distance
          '
          lib$(22,ind|)=" DIRECTION : "+STR$(r)
          INC ind|
          lib$(22,ind|)=" DISTANCE : "+STR$(d)
          INC ind|
        ENDIF
        '
      NEXT b|
    ELSE
      lib$(22,0)="SENSORS SHOW NO ENEMY SHIPS IN"
      lib$(22,1)="THIS QUADRANT"
    ENDIF
    portrait|(22)=2
    '
  CASE 25
    lib$(25,0)="PHASERS LOCKED ON TARGET."
    lib$(25,1)="ENERGY AVAILABLE = "+STR$(energy&)
    lib$(25,2)="NUMBER OF UNITS TO FIRE : "+a$
    portrait|(25)=4
    '
  CASE 26
    IF update!
      clean.lib(26)
      '
      ind|=0
      phaser.energy.init=phaser.energy.total/k3|
      FOR b|=1 TO 3
        IF k&(b|,3)>0
          add.phaser(b|,k&(b|,2)-1,k&(b|,1)-1)
          phaser.energy=phaser.energy.init
          DIV phaser.energy,@fnd(b|)
          MUL phaser.energy,(RND(1)+2)
          IF phaser.energy>0.15*k&(b|,3)
            IF phaser.energy>k&(b|,3)
              k&(b|,3)=0
            ELSE
              SUB k&(b|,3),phaser.energy
            ENDIF
            lib$(26,ind|)=STR$(INT(phaser.energy))+" UNIT HIT ON KLINGON AT SECTOR "+STR$(k&(b|,1)-(xqe|-1)*8)+","+STR$(k&(b|,2)-(yqe|-1)*8)
            INC ind|
            IF k&(b|,3)=0
              lib$(26,ind|)="   *** KLINGON DESTROYED ***"
              INC ind|
              s$(k&(b|,1),k&(b|,2))=""
              SUB g&(yqe|,xqe|),100
              z&(xqe|,yqe|)=g&(yqe|,xqe|)
              add.explosion(b|,k&(b|,2)-1,k&(b|,1)-1)
              DEC k3|
              IF k3|=0
                condition$="GREEN"
              ENDIF
              DEC k9|
              IF k9|=0
                game.over
              ENDIF
            ELSE
              lib$(26,ind|)=" (SENSORS SHOW "+STR$(k&(b|,3))+" UNITS REMAINING)"
              INC ind|
            ENDIF
          ELSE
            lib$(26,ind|)="SENSORS SHOW NO DAMAGES TO ENEMY"
            INC ind|
            lib$(26,ind|)="AT "+STR$(k&(b|,1)-(xqe|-1)*8)+","+STR$(k&(b|,2)-(yqe|-1)*8)
            INC ind|
          ENDIF
        ENDIF
      NEXT b|
      portrait|(26)=4
      update!=FALSE
      klingon.fire!=TRUE
    ENDIF
    '
  CASE 28
    IF b3|>0
      lib$(28,0)="FROM ENTERPRISE TO STARBASE"
      d1=ex|+1-b4|
      d2=ey|+1-b5|
      '
      get.direction.and.distance
      '
      lib$(28,1)=" DIRECTION : "+STR$(r)
      lib$(28,2)=" DISTANCE : "+STR$(d)
      '
    ELSE
      lib$(28,0)="SENSORS SHOW NO STARBASES IN"
      lib$(28,1)="THIS QUADRANT"
      lib$(28,2)=" "
    ENDIF
    portrait|(28)=2
    '
  CASE 30
    lib$(30,0)="PHOTON TORPEDO COURSE (0-9) : "+a$
    portrait|(30)=5
    '
  CASE 32
    IF update!
      clean.lib(32)
      '
      lib$(32,0)="TORPEDO TRACK:"
      moving.torpedo
      portrait|(32)=5
      update!=FALSE
    ENDIF
    '
  CASE 36
    clean.lib(36)
    IF time.to.repair=0
      lib$(36,0)="ENGINEERING, NO DAMAGE,"
      lib$(36,1)="ALL POWER LEVELS NORMAL."
    ELSE
      lib$(36,0)="TECHNICIANS STANDING BY TO EFFECT"
      lib$(36,1)="  REPAIRS TO YOUR SHIP."
      lib$(36,2)="ESTIMATED TIME TO REPAIR: "+STR$(time.to.repair)+" STARDATES."
      lib$(36,3)="WILL YOU AUTHORIZE THE REPAIR ORDER"
      lib$(36,4)="  (Y/N)? "+a$
    ENDIF
    portrait|(36)=7
    '
  CASE 37
    lib$(37,0)="DAMAGE CONTROL REPORT: "
    portrait|(37)=7
    FOR b|=1 TO 8
      IF dam|(b|)>0
        lib$(37,b|)=TRIM$(damage.lib$(dam|(b|)))+" REPAIR COMPLETED."
      ENDIF
      '
    NEXT b|
    '
  CASE 38
    lib$(38,0)="DAMAGE CONTROL REPORT: "
    portrait|(38)=7
    lib$(38,1)=TRIM$(damage.lib$(ind|))+" STATE OF REPAIR IMPROVED"
    '
  CASE 39
    lib$(39,0)="DAMAGE CONTROL REPORT: "
    portrait|(39)=7
    lib$(39,1)=TRIM$(damage.lib$(ind|))+" DAMAGED"
    '
  CASE 40
    lib$(40,0)="DAMAGE CONTROL REPORT: "
    portrait|(40)=7
    ind|=1
    FOR b|=1 TO 8
      IF damage(b|)<0
        damage(b|)=0
        lib$(40,ind|)=TRIM$(damage.lib$(b|))+" REPAIR COMPLETED."
        INC ind|
      ENDIF
      '
    NEXT b|
    '
  ENDSELECT
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE clean.lib(l|)
  FOR b|=0 TO 10
    lib$(l|,b|)=""
  NEXT b|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE post_input
  IF process$="shields"
    v&=VAL(a$)
    IF v&>energy&+shield&
      ' On demande trop d'energie
      process$=""
      new.mess(6)
      '
    ELSE IF v&=shield&
      ' Meme niveau d'energie
      process$=""
      new.mess(7)
      '
    ELSE IF v&=0
      ' Shield a ZERO
      ADD energy&,shield&
      SUB energy&,v&
      shield&=v&
      process$=""
      new.mess(8)
      '
    ELSE
      ' Nouvelle valeur acceptee
      ADD energy&,shield&
      SUB energy&,v&
      shield&=v&
      process$=""
      new.mess(9)
      '
    ENDIF
    '
  ELSE IF process$="nav2"
    v=VAL(a$)
    w1=v
    IF v>wfmax AND wfmax=8
      process$=""
      new.mess(13)
    ELSE IF v>wfmax AND wfmax=0.2
      process$=""
      new.mess(14)
    ELSE IF v>0 AND v<=wfmax
      energy.to.warp|=INT(w1)*8+(w1-INT(w1))*10
      IF energy.to.warp|>energy&
        process$=""
        new.mess(15)
      ELSE
        IF k3|>0
          klingon.move
          mode$="klingon"
          next.action$="move"
          klingon.ship|=0
          klingon.attack
        ELSE
          moving.starship
        ENDIF
        process$=""
        '
      ENDIF
    ENDIF
    '
  ELSE IF process$="nav"
    v=VAL(a$)
    IF v<1 OR v>=9
      ' Mauvais vecteur
      mes|=11
      process$=""
    ELSE
      c1=v
      mes|=12
      a$=""
      process$="nav2"
    ENDIF
    '
  ELSE IF process$="damage"
    IF a$="y" OR a$="Y"
      ' Lancement des r‚parations
      t=t+time.to.repair
      process$=""
      new.mess(40)
    ELSE IF a$="n" OR a$="N"
      a$=""
      process$=""
    ELSE
      '
    ENDIF
    '
  ELSE IF process$="com"
    v|=VAL(a$)
    IF v|>=4
      ' Exit computer
      mes|=0
      process$=""
    ELSE IF v|=0
      new.mess(21)
      process$=""
      '
    ELSE IF v|=1
      new.mess(22)
      process$=""
      '
    ELSE IF v|=2
      new.mess(28)
      process$=""
      '
    ENDIF
    '
  ELSE IF process$="torpedo"
    v=VAL(a$)
    IF v<1 OR v>=9
      ' Mauvais vecteur
      mes|=31
      process$=""
    ELSE
      c1=v
      new.mess(32)
      a$=""
      DEC torpedoes|
      update!=TRUE
      process$=""
    ENDIF
    '
  ELSE IF process$="phaser"
    v&=VAL(a$)
    IF v&>energy&
      ' On demande trop d'energie
      new.mess(25)
      process$=""
    ELSE IF v&=0
      '
      process$=""
    ELSE
      SUB energy&,v&
      phaser.energy.total=v&
      new.mess(26)
      update!=TRUE
      process$=""
      '
    ENDIF
  ENDIF
  a$=""
  IF process$="" AND mode$<>"klingon"
    mode$="game"
  ENDIF
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE cursor.timer
  IF TIMER-cursor.timer%>100
    cursor!=NOT cursor!
    cursor.timer%=TIMER
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE moving.starship
  '
  reset.message.box
  '
  dx=c&(c1,1)+(c&(c1+1,1)-c&(c1,1))*(c1-INT(c1))
  dy=c&(c1,2)+(c&(c1+1,2)-c&(c1,2))*(c1-INT(c1))
  '
  s$(ex|+1,ey|+1)=""
  '
  b|=1
  sortie|=0
  energy.used|=0
  DO
    nex=ex|+dx
    ney=ey|+dy
    '
    IF nex<0 OR nex>63 OR ney<0 OR ney>63
      sortie|=18
      '
    ELSE IF LEN(s$(nex+1,ney+1))>0
      sortie|=17
      '
    ELSE
      ex|=nex
      ey|=ney
    ENDIF
    '
    INC b|
    INC energy.used|
    EXIT IF b|>energy.to.warp| OR sortie|>0
  LOOP
  '
  SUB energy&,energy.used|
  '
  repair.when.moving
  '
  IF sortie|>0
    new.mess(sortie|)
    mes|=sortie|
  ENDIF
  '
  ' Degats ou amelioration d'un composant au hazard (20% de chance)
  pct|=RAND(100)
  IF pct|>80
    ind|=RAND(8)+1
    pct|=RAND(100)
    IF pct|>60
      ' Amelioration
      damage(ind|)=damage(ind|)+(RND(1)*3+1)
      new.mess(38)
    ELSE
      ' Degats
      damage(ind|)=damage(ind|)-(RND(1)*5+1)
      new.mess(39)
    ENDIF
    '
  ENDIF
  '
  ex|=ROUND(ex|)
  ey|=ROUND(ey|)
  '
  nxqe|=DIV(ex|,8)+1
  nyqe|=DIV(ey|,8)+1
  '
  IF nxqe|<>xqe| OR nyqe|<>yqe|
    xqe|=nxqe|
    yqe|=nyqe|
    clean.sector
    init.sector
    new.mess(3)
    IF k3|>0
      new.mess(5)
      IF condition$="GREEN" OR condition$="YELLOW"
        new.mess(23)
      ENDIF
      '
      IF shield&<=0
        new.mess(4)
      ENDIF
      '
    ENDIF
  ENDIF
  '
  check.docking.starbase
  '
  xse|=MOD(ex|,8)+1
  yse|=MOD(ey|,8)+1
  z&(xqe|,yqe|)=g&(yqe|,xqe|)
  s$(ex|+1,ey|+1)="<*>"
  '
  IF w1<1
    ADD t,w1
  ELSE
    INC t
  ENDIF
  '  EDIT
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE get_graphs
  DIM temp|(32066)
  BLOAD app_dir$+"STARTREK.PI3",V:temp|(0)
  BMOVE V:temp|(34),V:tiles|(0),32000
  '
  BLOAD app_dir$+"SPRITES.PI3",V:temp|(0)
  BMOVE V:temp|(34),V:sprites|(0),2720
  '
  ERASE temp|()
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE computer.process
  '
  IF damage(8)<0
    new.mess(47)
  ELSE
    mes|=20
    process$="com"
    mode$="text"
    mode_text$="num"
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE phaser.process
  '
  IF k3|=0
    new.mess(24)
  ELSE IF damage(4)<0
    new.mess(43)
  ELSE
    mes|=25
    process$="phaser"
    mode$="text"
    mode_text$="num"
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE show.all.galaxy
  FOR ii|=1 TO 8
    FOR jj|=1 TO 8
      z&(ii|,jj|)=g&(jj|,ii|)
    NEXT jj|
  NEXT ii|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE check.docking.starbase
  docked!=FALSE
  FOR ii=ex| TO ex|+2
    FOR jj=ey| TO ey|+2
      IF ii>=0 AND ii<64 AND jj>=0 AND jj<64
        IF s$(ii,jj)=CHR$(255)+"Y"+CHR$(255)
          docked!=TRUE
          condition$="DOCKED"
          shield&=0
          torpedoes|=10
          energy&=energy.max&
          FOR b|=1 TO 8
            damage(b|)=0
          NEXT b|
        ENDIF
      ENDIF
    NEXT jj
  NEXT ii
  '
  IF docked!=FALSE
    condition$="GREEN"
  ENDIF
  ' RETURN docked!
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE get.direction.and.distance
  IF d1=0
    IF d2>0
      r=5
    ELSE
      r=1
    ENDIF
  ELSE IF d2=0
    IF d1>0
      r=3
    ELSE
      r=7
    ENDIF
  ELSE IF d1>0 AND d2<0
    r=1
    IF ABS(d2)>ABS(d1)
      ADD r,(ABS(d1)/ABS(d2))
    ELSE
      ADD r,((2*ABS(d1)-ABS(d2))/ABS(d1))
    ENDIF
    '
  ELSE IF d1>0 AND d2>0
    r=3
    IF ABS(d2)>ABS(d1)
      ADD r,((2*ABS(d2)-ABS(d1))/ABS(d2))
    ELSE
      ADD r,(ABS(d2)/ABS(d1))
    ENDIF
    '
  ELSE IF d1<0 AND d2>0
    r=5
    IF ABS(d2)>ABS(d1)
      ADD r,(ABS(d1)/ABS(d2))
    ELSE
      ADD r,((2*ABS(d1)-ABS(d2))/ABS(d1))
    ENDIF
    '
  ELSE IF d1<0 AND d2<0
    r=7
    IF ABS(d2)>ABS(d1)
      ADD r,((2*ABS(d2)-ABS(d1))/ABS(d2))
    ELSE
      ADD r,(ABS(d2)/ABS(d1))
    ENDIF
  ENDIF
  '
  d=SQR(d1^2+d2^2)
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE torpedo.process
  '
  IF k3|=0
    new.mess(24)
  ELSE IF torpedoes|=0
    new.mess(29)
  ELSE IF damage(5)<0
    new.mess(44)
  ELSE
    mes|=30
    process$="torpedo"
    mode$="text"
    mode_text$="num"
  ENDIF
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE moving.torpedo
  '
  dx=c&(c1,1)+(c&(c1+1,1)-c&(c1,1))*(c1-INT(c1))
  dy=c&(c1,2)+(c&(c1+1,2)-c&(c1,2))*(c1-INT(c1))
  '
  b|=0
  sortie|=0
  '
  tx=ex|
  ty=ey|
  '
  DO
    ntx=tx+dx
    nty=ty+dy
    '
    IF ROUND(ntx+1)-(xqe|-1)*8<=0 OR ROUND(ntx+1)-(xqe|-1)*8>8 OR ROUND(nty+1)-(yqe|-1)*8<=0 OR ROUND(nty+1)-(yqe|-1)*8>8
      sortie|=18
      '
    ELSE IF LEN(s$(ntx+1,nty+1))>0
      IF s$(ntx+1,nty+1)=" * "
        sortie|=19
      ELSE IF s$(ntx+1,nty+1)=CHR$(255)+"Y"+CHR$(255)
        sortie|=20
      ELSE IF s$(ntx+1,nty+1)="-K-"
        sortie|=21
      ENDIF
      '
    ELSE
      tx=ntx
      ty=nty
    ENDIF
    '
    INC b|
    IF sortie|=0
      lib$(32,b|)="   "+STR$(ROUND(tx+1-(xqe|-1)*8))+" - "+STR$(ROUND(ty+1-(yqe|-1)*8))
    ELSE IF sortie|=18
      lib$(32,b|)="   TORPEDO MISSED"
    ELSE IF sortie|=19
      lib$(32,b|)="   STAR AT "+STR$(ntx+1-(xqe|-1)*8)+","+STR$(nty+1-(yqe|-1)*8)+" ABSORBED TORPEDO ENERGY."
    ELSE IF sortie|=20
      lib$(32,b|)="   *** STARBASE DESTROYED ***"
      s$(ntx+1,nty+1)=""
      SUB g&(yqe|,xqe|),10
      z&(xqe|,yqe|)=g&(yqe|,xqe|)
      DEC b3|
      DEC b9|
    ELSE IF sortie|=21
      lib$(32,b|)="   *** KLINGON DESTROYED ***"
      ' recherche des infos du Klingon detruit
      FOR k|=1 TO 3
        IF k&(k|,1)=ntx+1 AND k&(k|,2)=nty+1 AND k&(k|,3)>0
          k&(k|,3)=0
          add.explosion(k|,k&(k|,2)-1,k&(k|,1)-1)
        ENDIF
      NEXT k|
      s$(ntx+1,nty+1)=""
      SUB g&(yqe|,xqe|),100
      z&(xqe|,yqe|)=g&(yqe|,xqe|)
      DEC k3|
      IF k3|=0
        condition$="GREEN"
      ENDIF
      DEC k9|
      IF k9|=0
        game.over
      ENDIF
    ENDIF
    EXIT IF sortie|>0
  LOOP
  klingon.fire!=TRUE
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE klingon.move
  '
  FOR b|=1 TO 3
    IF k&(b|,3)>0
      done!=FALSE
      s$(k&(b|,1),k&(b|,2))=""
      REPEAT
        j|=RAND(8)+1+(xqe|-1)*8
        i|=RAND(8)+1+(yqe|-1)*8
        IF LEN(s$(j|,i|))=0
          s$(j|,i|)="-K-"
          done!=TRUE
          k&(b|,1)=j|
          k&(b|,2)=i|
        ENDIF
      UNTIL done!
    ENDIF
  NEXT b|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE klingon.attack
  '
  REPEAT
    INC klingon.ship|
  UNTIL k&(klingon.ship|,3)>0 OR klingon.ship|>=3
  '
  IF docked!
    mode$="game"
    mes|=33
    '
  ELSE IF klingon.ship|<=3
    ind|=0
    IF k&(klingon.ship|,3)>0
      phaser.energy=k&(klingon.ship|,3)
      DIV phaser.energy,@fnd(klingon.ship|)
      MUL phaser.energy,(RND(1)+2)
      phaser.energy=ROUND(phaser.energy)
      SUB shield&,phaser.energy
      lib$(34,ind|)=STR$(phaser.energy)+" UNITS HIT ON ENTERPRISE FROM"
      INC ind|
      lib$(34,ind|)="   SECTOR "+STR$(k&(klingon.ship|,1)-(xqe|-1)*8)+","+STR$(k&(klingon.ship|,2)-(yqe|-1)*8)
      INC ind|
      lib$(34,ind|)="   SHIELDS DOWN TO "+STR$(shield&)+" UNITS"
      INC ind|
      '
      IF phaser.energy>20
        pct|=RAND(100)
        IF pct|>40 AND phaser.energy/shield&>0.02
          pct|=RAND(8)+1
          lib$(34,ind|)="   "+TRIM$(damage.lib$(pct|))+" DAMAGED BY THE HIT"
          SUB damage(pct|),(phaser.energy/shield&)
          SUB damage(pct|),(0.5*RND(1))
          INC ind|
        ENDIF
      ENDIF
      portrait|(34)=5
      new.mess(34)
      '
      IF shield&<=0
        game.over
      ENDIF
    ENDIF
  ENDIF
  '
  IF klingon.ship|>3
    mode$="game"
  ENDIF
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE game.over
  IF shield&<0
    lib$(35,0)="THE ENTERPRISE HAS BEEN DESTROYED."
    lib$(35,1)="THEN FEDERATION WILL BE CONQUERED"
    lib$(35,2)="IT IS STARDATE "+STR$(t0+t)
    lib$(35,3)="THERE WERE "+STR$(k9|)+" KLINGON BATTLE CRUISERS"
    lib$(35,4)="LEFT AT THE END OF YOUR MISSION."
    '
  ELSE IF k9|=0
    lib$(35,0)="CONGRATULATION, CAPTAIN!"
    lib$(35,1)="THE LAST KLINGON BATTLE CRUISER"
    lib$(35,2)="MENACING THE FEDERATION HAS BEEN"
    lib$(35,3)="DESTROYED."
    lib$(35,4)=" "
    lib$(35,5)="YOUR EFFICIENCY RATING IS "+STR$(ROUND(1000*(k7|/t)^2))
  ENDIF
  new.mess(35)
  portrait|(35)=6
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE damage.process
  '
  IF damage(6)<0
    new.mess(45)
  ELSE
    time.to.repair=0
    FOR b|=1 TO 8
      IF damage(b|)<0
        time.to.repair=time.to.repair+0.1
      ENDIF
    NEXT b|
    '
    IF time.to.repair>0
      time.to.repair=time.to.repair+damage.malus
    ENDIF
    '
    IF time.to.repair>1
      time.to.repair=0.9
    ENDIF
    '
    time.to.repair=ROUND(time.to.repair,1)
    '
    IF time.to.repair>0
      mes|=36
      process$="damage"
      mode_text$="text"
      mode$="text"
    ELSE
      new.mess(36)
      process$=""
      mode$="game"
    ENDIF
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE new.mess(id|)
  '
  new.id|=0
  FOR b|=0 TO 10
    IF message.box|(10-b|)=0
      new.id|=10-b|
    ENDIF
  NEXT b|
  '
  message.box|(new.id|)=id|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE reset.message.box
  FOR b|=0 TO 10
    message.box|(b|)=0
  NEXT b|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE repair.when.moving
  ' R‚paration sur l'ensemble des composants
  ind|=1
  FOR b|=1 TO 8
    dam|(b|)=0
  NEXT b|
  '
  repair!=FALSE
  FOR b|=1 TO 8
    IF damage(b|)<0
      damage(b|)=damage(b|)+(energy.used|/10)
      IF damage(b|)>0
        repair!=TRUE
        dam|(ind|)=b|
        INC ind|
      ENDIF
    ENDIF
  NEXT b|
  '
  IF repair!
    new.mess(37)
    mes|=37
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE update.command.area(x&,y&)
  t.height|=10
  '
  DEFTEXT 1,0,0,6
  ADD x&,3
  ADD y&,t.height|+12
  y0&=y&
  '
  b|=0
  done!=FALSE
  '
  IF mode$<>"text"
    IF update.command.area!
      mes|=0
      REPEAT
        IF message.box|(b|)>0
          mes|=message.box|(b|)
          message.box|(b|)=0
          update.command.area!=FALSE
          done!=TRUE
        ENDIF
        INC b|
      UNTIL done! OR b|>10
    ENDIF
  ENDIF
  '
  IF mes|>0
    update.messages
    max|=0
    FOR t|=0 TO 8
      IF LEN(lib$(mes|,t|))>0
        max|=t|
      ENDIF
    NEXT t|
    '
    FOR t|=0 TO 8
      IF LEN(lib$(mes|,t|))>0
        IF mode$="text" AND t|=max| AND cursor!
          TEXT x&+96,y&,lib$(mes|,t|)+CHR$(95)
        ELSE
          TEXT x&+96,y&,lib$(mes|,t|)
        ENDIF
        ADD y&,t.height|
      ENDIF
    NEXT t|
    IF mode$<>"text"
      DEFTEXT 1,0,0,4
      lib$="PRESS ANY KEY"
      TEXT 640-(LEN(lib$)+2)*6,y0&+9*t.height|,lib$
    ENDIF
  ELSE
    IF mode$<>"klingon" AND NOT klingon.fire!
      TEXT x&+96,y&,"COMMAND ?"
    ENDIF
    '
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE get.instructions
  DIM d|(10000)
  '
  BLOAD app_dir$+"INSTRUCT.dat",V:d|(0)
  c&=0
  nb_items|=d|(c&)
  INC c&
  DIM instruction$(nb_items|)
  '
  FOR l|=0 TO nb_items|
    t|=d|(c&)
    INC c&
    FOR b|=1 TO t|
      instruction$(l|)=instruction$(l|)+CHR$(d|(c&))
      INC c&
    NEXT b|
  NEXT l|
  ERASE d|()
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.instructions
  '
  ' on sauvegarde l'etat de l'ecran physique
  BMOVE XBIOS(2),V:physical_copy|(0),32000
  '
  BMOVE V:instructions|(0),XBIOS(2),32000
  '
  REPEAT
  UNTIL INKEY$<>""
  '
  BMOVE V:instructions|(32000),XBIOS(2),32000
  '
  REPEAT
  UNTIL INKEY$<>""
  '
  ' on restitue l'ecran de jeu
  BMOVE V:physical_copy|(0),XBIOS(2),32000
  BMOVE V:physical_copy|(0),XBIOS(3),32000
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE construct.instructions(id|)
  CLS
  deb|=2
  fin|=47
  IF id|=1
    deb|=48
    fin|=90
  ENDIF
  '
  x0|=60
  x&=x0|
  y&=16
  '
  DEFTEXT 1,16,0,4
  style|=0
  dx.car|=8
  DEFTEXT 1,style|,0,6
  '
  x&=(640-LEN(instruction$(0))*8)/2
  FOR car|=1 TO LEN(instruction$(0))
    a$=MID$(instruction$(0),car|,1)
    IF a$="["
      style|=1
      DEFTEXT 1,style|,0,6
    ELSE IF a$="]" OR a$="}"
      style|=0
      DEFTEXT 1,style|,0,6
    ELSE IF a$="{"
      style|=16
      DEFTEXT 1,style|,0,6
    ELSE
      TEXT x&,y&,a$
      ADD x&,dx.car|
    ENDIF
  NEXT car|
  x&=x0|
  ADD y&,16
  dx.car|=6
  DEFTEXT 1,style|,0,4
  '
  FOR b|=deb| TO fin|
    FOR car|=1 TO LEN(instruction$(b|))
      a$=MID$(instruction$(b|),car|,1)
      IF a$="["
        style|=1
        dx.car|=8
        DEFTEXT 1,style|,0,4
      ELSE IF a$="]" OR a$="}"
        style|=0
        dx.car|=6
        DEFTEXT 1,style|,0,4
      ELSE IF a$="{"
        style|=16
        dx.car|=8
        DEFTEXT 1,style|,0,4
      ELSE
        TEXT x&,y&,a$
        ADD x&,dx.car|
      ENDIF
    NEXT car|
    x&=x0|
    ADD y&,8
  NEXT b|
  '
  DEFTEXT 1,0,0,4
  '
  BMOVE XBIOS(3),V:instructions|(id|*32000),32000
  CLS
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE construct.menu
  '
  GRAPHMODE 2
  PBOX 0,0,640,400
  DEFTEXT 0,0,0,32
  TEXT 100,50,CHR$(14)+CHR$(15)+"    SUPER STAR TREK    "+CHR$(14)+CHR$(15)
  DEFTEXT 0,0,0,13
  lib$="PRESS ANY KEY TO START A CAMPAIGN"
  xt&=(640-LEN(lib$)*8)/2
  TEXT xt&,370,lib$
  DEFTEXT 0,0,0,4
  TEXT 10,10,version$
  TEXT 590,10,"ED 2024"
  RC_COPY V:tiles|(0),0,100,640,300 TO e2%,0,50
  BMOVE e2%,V:bg|(0),32000
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.menu
  '
  BMOVE V:bg|(0),e2%,32000
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE add.explosion(b|,x.exp|,y.exp|)
  '
  explosion!(b|)=TRUE
  x.explosion|(b|)=MOD(x.exp|,8)
  y.explosion|(b|)=MOD(y.exp|,8)
  frame.explosion|(b|)=5
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE explosion.managment
  '
  FOR b|=1 TO 3
    IF explosion!(b|)
      '
      display.explosion(b|)
      DEC frame.explosion|(b|)
      '
      IF frame.explosion|(b|)=0
        explosion!(b|)=FALSE
      ENDIF
    ENDIF
  NEXT b|
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.explosion(p|)
  '
  DEFTEXT 1,0,0,13
  FOR f|=0 TO 5
    xn|=RAND(20)
    yn|=RAND(10)
    TEXT 38-2+x.explosion|(p|)*28+xn|,56-3+y.explosion|(p|)*18+yn|,"-"
  NEXT f|
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE add.phaser(b|,x.exp|,y.exp|)
  '
  phaser!(b|)=TRUE
  x.phaser|(b|)=MOD(x.exp|,8)
  y.phaser|(b|)=MOD(y.exp|,8)
  frame.phaser|(b|)=5
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE phaser.managment
  '
  FOR b|=1 TO 3
    IF phaser!(b|)
      '
      display.phaser(b|)
      DEC frame.phaser|(b|)
      '
      IF frame.phaser|(b|)=0
        phaser!(b|)=FALSE
      ENDIF
    ENDIF
  NEXT b|
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE display.phaser(p|)
  '
  LINE 48+MOD(ey|,8)*28,50+MOD(ex|,8)*18,48+x.phaser|(p|)*28,56-6+y.phaser|(p|)*18
  '
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE bip
  SOUND 1,15,10,4
  PAUSE 5
  SOUND 1,0,0,0
  PAUSE 5
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE process.string(lib$)
  REPEAT
    flag.d|=INSTR(lib$,"{",flag.d|)
    IF flag.d|>0
      flag.f|=INSTR(lib$,"}",flag.d|)
      newlib$=newlib$+MID$(lib$,1,flag.d|-1)
      char.qtt|=flag.f|-flag.d|+1
      flag$=MID$(lib$,flag.d|,char.qtt|)
      newlib$=newlib$+@convert$(flag$)
      char.qtt|=LEN(lib$)-flag.f|
      newlib$=newlib$+MID$(lib$,flag.f|+1,char.qtt|)
      lib$=newlib$
      newlib$=""
    ENDIF
  UNTIL flag.d|=0
  '
  IF LEN(newlib$)=0
    newlib$=lib$
  ENDIF
  '
RETURN
'
' ------------------------------------------------------------------------------
FUNCTION convert$(flag$)
  converted.flag$=""
  IF flag$="{Sx}"
    converted.flag$="1"
  ELSE IF flag$="{Sy}"
    converted.flag$="2"
  ELSE IF flag$="{Qx}"
    converted.flag$="3"
  ELSE IF flag$="{Qy}"
    converted.flag$="4"
  ENDIF
  RETURN converted.flag$
ENDFUNC
'
' ------------------------------------------------------------------------------
PROCEDURE process.menu
  construct.menu
  display.menu
  refresh_screen
  ~INP(2)
  '
  set.new.game
  process.menu!=FALSE
  process.intro!=TRUE
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE process.intro
  display.intro
  refresh_screen
  '
  ~INP(2)
  '
  construct.bg
  '
  new.mess(3)
  IF k3|>0
    new.mess(5)
    IF condition$="GREEN" OR condition$="YELLOW"
      new.mess(23)
    ENDIF
    '
    IF shield&<=0
      new.mess(4)
    ENDIF
    '
  ENDIF
  '
  z&(xqe|,yqe|)=g&(yqe|,xqe|)
  '
  update.command.area!=TRUE
  '
  a$=""
  process.intro!=FALSE
  process.game!=TRUE
RETURN
'
' ------------------------------------------------------------------------------
PROCEDURE blitter_activation
  blit%=XBIOS(64,-1)
  ' Si le Blitter est present mais non actif
  IF BTST(blit%,1) AND NOT BTST(blit%,0)
    blit%=BSET(blit%,0)
    ~XBIOS(64,blit%)
  ENDIF
RETURN
'
' ------------------------------------------------------------------------------
