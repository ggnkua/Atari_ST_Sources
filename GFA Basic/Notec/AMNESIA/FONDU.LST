.
.### CODE : fondu d'une palette 1 vers une palette 2
.
CLEAR
CLS
INLINE dos%,32
super%=GEMDOS(32,L:super%)
xb%=XBIOS(2)
pal1$=SPACE$(32)
pal1%=V:pal1$
BMOVE dos%,&HFFFF8240,32
OPEN "i",#1,"a:\grp\notec1.pi1"
SEEK #1,2
BGET #1,pal1%,32
VOID XBIOS(6,L:pal1%)
DIM r%(15),v%(15),b%(15)
DIM c%(15)
RESTORE col
FOR n|=0 TO 15
  READ c%
  c%(n|)=c%*2
  couleur%=CARD{&HFFFF8240+c%(n|)}
  r%(n|)=(couleur% AND &HF00)/256
  v%(n|)=(couleur% AND &HF0)/16
  b%(n|)=couleur% AND &HF
NEXT n|
FOR n|=0 TO 15
  VSETCOLOR n|,0,0,0
NEXT n|
BGET #1,xb%,32000
CLOSE #1
.
DO
  FOR n|=0 TO 15
    couleur%=CARD{&HFFFF8240+c%(n|)}
    r%=(couleur% AND &HF00)/256
    v%=(couleur% AND &HF0)/16
    b%=couleur% AND &HF
    IF r%<r%(n|)
      ADD couleur%,256
    ELSE IF r%>r%(n|)
      SUB couleur%,256
    ENDIF
    IF v%<v%(n|)
      ADD couleur%,16
    ELSE IF v%>v%(n|)
      SUB couleur%,16
    ENDIF
    IF b%<b%(n|)
      INC couleur%
    ELSE IF b%>b%(n|)
      DEC couleur%
    ENDIF
    CARD{&HFFFF8240+c%(n|)}=couleur%
  NEXT n|
  VSYNC
LOOP UNTIL PEEK(&HFFFFFC02)=57
.
BMOVE dos%,&HFFFF8240,32
VOID GEMDOS(32,L:super%)
EDIT
.
col:
.DATA 0,8,1,9,2,10,3,11,4,12,5,13,6,14,7,15
DATA 0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15
