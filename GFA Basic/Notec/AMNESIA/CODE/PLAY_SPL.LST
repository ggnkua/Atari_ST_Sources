' Programme 5: Reproduction sonore d'un ‚chantillon
' provenant de ST REPLAY/PRO SOUND/MASTER SOUND/MAESTRO
' (C) Octobre 1989 Atari Magazine / GFA Basic 3.07
RESERVE 50000
SHOWM
DO
  CLS
  FILESELECT #"Choisissez un Echantillon","A:\CYBER.MAN\*.SPL","",f$
  freq%=0
  OPEN "i",#1,f$
  lon%=LOF(#1)
  PRINT lon%
  adr%=MALLOC(lon%)
  IF adr%<1 THEN
    PRINT "Pas assez de M‚moire"
    END
  ENDIF
  BGET #1,adr%,lon%
  CLOSE #1
  ' Les fichiers de ST Replay ne sont pas sign‚s
  ' Ils faut r‚tablir les signes pour obtenir un
  ' ‚chantillon au format PCM
  . FOR i%=0 TO lon%
  . BYTE{adr%+i%}=BYTE{adr%+i%}-128
  .NEXT i%
  ' Initialisation adresse de DEPART
  convdma(adr%,a%,b%,c%)
  SPOKE &HFF8903,a%
  SPOKE &HFF8905,b%
  SPOKE &HFF8907,c%
  ' Initialisation Adresse de FIN
  convdma(adr%+lon%,a%,b%,c%)
  SPOKE &HFF890F,a%
  SPOKE &HFF8911,b%
  SPOKE &HFF8913,c%
  ' D‚finition Fr‚quence de Reproduction
toto:
  TEXT 0,6,freq%
  ' Reproduction Monophonique
  SDPOKE &HFF8920,&HA0 OR freq%
  ' Reproduction R‚p‚titive
  SDPOKE &HFF8900,3
  ' Initialisation du MicroWire
  SDPOKE &HFFFF8924,&H7FF
  ' Puissance Maximale
  SDPOKE &HFFFF8922,&H4C0 OR 40
  DO
    a$=INKEY$
  LOOP UNTIL a$<>""
  IF a$="+"
    INC freq%
    GOTO toto
  ELSE IF a$="-"
    DEC freq%
    GOTO toto
  ENDIF
  ' On fait d‚croitre le son
  . FOR i%=40 DOWNTO 20
  . SDPOKE &HFFFF8922,&H4C0 OR i%
  . PAUSE 5
  .NEXT i%
  ' Stoppe le son
  SDPOKE &HFF8900,0
  VOID MFREE(adr%)
LOOP
PROCEDURE convdma(adrt%,VAR a%,b%,c%)
  ' D‚coupage d'une adr sur 24 bits en 3 octets
  ' En commentaires Solution pour GFA 2.x
  '  a%=adrt%/65536           ! Partie Haute
  '  b%=(adrt%-a%*65536)/256  ! Octet du Milieu
  '  c%=adrt%-a%*65536-b%*256 ! Octet Bas
  a%=SWAP(adrt%) AND &HFF
  b%=SHR(adrt%,8) AND &HFF
  c%=adrt% AND &HFF
RETURN
