.#######################
.### GAIA Systäme 3D ###
.#######################
.
CLS
e2$=SPACE$(32000)
e3$=SPACE$(32000)
e2%=V:e2$
e3%=V:e3$
xb%=XBIOS(2)
BMOVE xb%,e2%,32000
BMOVE xb%,e3%,32000
OUT 4,18
SPOKE &H484,2
HIDEM
VSETCOLOR 0,7,7,7
VSETCOLOR 1,15,14,15
nop=4
nol=7
DIM xe(nop),ye(nop),ze(nop)
DIM i1(nol),i2(nol)
DATA 10,10,0,-10,10,0,-10,-10,0,10,-10,0,0,0,9
FOR i=0 TO nop
  READ xe(i),ye(i),ze(i)
NEXT i
DATA 0,1,1,2,2,3,3,0,3,4,2,4,1,4,0,4
FOR i=0 TO nol
  READ i1(i),i2(i)
NEXT i
cx=160
cy=100
d=500
x=100
y=10
z=200
incr=5
VOID XBIOS(5,L:e3%,L:e2%,-1)
TEXT 0,6,"PAS:"+STR$(incr)+" "
DO
  .
  t$=UPPER$(INKEY$)
  IF t$="4"
    ADD x,incr
  ELSE IF t$="6"
    SUB x,incr
  ELSE IF t$="2"
    ADD y,incr
  ELSE IF t$="8"
    SUB y,incr
  ELSE IF t$="7"
    ADD x,incr
    SUB y,incr
  ELSE IF t$="9"
    SUB x,incr
    SUB y,incr
  ELSE IF t$="3"
    SUB x,incr
    ADD y,incr
  ELSE IF t$="1"
    ADD x,incr
    ADD y,incr
  ELSE IF t$="-"
    z=z+incr
  ELSE IF t$="+"
    z=z-incr
  ELSE IF t$="F"
    INC incr
  ELSE IF t$="S"
    DEC incr
  ENDIF
  .
  IF incr<1
    incr=1
  ENDIF
  theta=x
  phi=y
  rho=z
  .
  IF t$<>""
    CLS
    TEXT 0,6,"PAS:"+STR$(incr)+" "
    @rot
    @aff
    BMOVE xb%,e3%,32000
    VOID XBIOS(5,L:e3%,L:e2%,-1)
    VSYNC
    SWAP e2%,e3%
  ENDIF
  .
LOOP UNTIL t$=CHR$(27)
OUT 4,8
SPOKE &H484,3
VSETCOLOR 0,15,15,15
VSETCOLOR 1,0
EDIT
.
> PROCEDURE rot
  st=SIN(theta*PI/180)
  sp=SIN(phi*PI/180)
  tc=COS(theta*PI/180)
  cp=COS(phi*PI/180)
  cs=tc*sp
  ss=st*sp
  zz=tc*cp
  sc=st*cp
RETURN
> PROCEDURE aff
  FOR j=0 TO nol
    xc=xe(i1(j))
    yc=ye(i1(j))
    zc=ze(i1(j))
    @conv3d2d
    x1=xp
    y1=yp
    xc=xe(i2(j))
    yc=ye(i2(j))
    zc=ze(i2(j))
    @conv3d2d
    x2=xp
    y2=yp
    LINE x1,y1,x2,y2
  NEXT j
RETURN
> PROCEDURE conv3d2d
  xo=-xc*st+yc*tc
  yo=-xc*cs-yc*ss+zc*cp
  zo=-xc*zz-yc*sc-zc*sp+rho
  xp=cx+(d*xo)/zo
  yp=cx-(d*yo)/zo
RETURN
