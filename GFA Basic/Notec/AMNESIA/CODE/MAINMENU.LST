.#######################################
.### AMNESIA Main-Menu By GAIA/NOTEC ###
.#######################################
.
CLEAR
RESERVE FRE(0)-(49*160+32*160+32000+26*160+124*160+32+32000+10000)
INLINE dos%,32
super%=GEMDOS(32,L:super%)
DIM fx%(255)
DIM menu$(20)
xb%=XBIOS(2)
BMOVE &HFFFF8240,dos%,32
VSYNC
OUT 4,18
HIDEM
SPOKE &H484,2
VSYNC
.
@init_fnt
@load
@aff_menu
@menu0
@gest_menu0
@fin
.
> PROCEDURE load
  .
  log%=MALLOC(49*160)
  fnt%=MALLOC(32*160)
  sky%=MALLOC(32000)
  star%=MALLOC(26*160)
  log2%=MALLOC(124*160)
  pal%=MALLOC(32)
  ecr%=MALLOC(32000)
  .
  @degas("A:\GRAPHX\M_AMNESI.PI1",log%,49*160)
  @degas("A:\GRAPHX\M_FONTES.PI1",fnt%,32*160)
  @degas("A:\GRAPHX\M_SKY.PI1",sky%,32000)
  @degas("A:\GRAPHX\M_STARS.PI1",star%,26*160)
  @degas("A:\GRAPHX\I_AMNDI2.PI1",log2%,124*160)
  OPEN "I",#1,"A:\GRAPHX\M_AMNESI.PI1"
  SEEK #1,2
  BGET #1,pal%,32
  CLOSE #1
  BMOVE pal%,&HFFFF8240,32
  VSYNC
  .
RETURN
> PROCEDURE degas(nom$,adr%,len%)
  .
  OPEN "I",#1,nom$
  SEEK #1,34
  BGET #1,adr%,len%
  CLOSE #1
  .
RETURN
> PROCEDURE aff_menu
  .
  .# Ciel #
  BMOVE sky%,xb%,32000
  VSYNC
  .
  .# Logo #
  RC_COPY log%,0,49,142,49 TO xb%,(320-142)/2,10,4
  RC_COPY log%,0,0,142,49 TO xb%,(320-142)/2,10,7
  .RC_COPY log2%,0,62,160,62 TO xb%,80,10,4
  .RC_COPY log2%,0,0,160,62 TO xb%,80,10,7
  .
  BMOVE xb%,ecr%,32000
  .
RETURN
> PROCEDURE menu0
  .
  RESTORE menu0
  dy%=100
  nb%=0
  DO
    READ ch$
    EXIT IF ch$=""
    INC nb%
    menu$(nb%)=ch$
    @fnt(-1,dy%,ch$)
    ADD dy%,16
  LOOP
  .
RETURN
> PROCEDURE gest_menu0
  .
  nbm%=nb%
  nb%=0
  px%=132-16
  py%=100
  DO
    .
    RC_COPY ecr%,px%,py%,14,13 TO xb%,px%,py%
    @aff_star(px%,py%,nb%)
    VSYNC
    INC nb%
    IF nb%=11
      nb%=0
    ENDIF
    key%=PEEK(&HFFFFFC02)
    SELECT key%
    CASE 72
      IF py%>100
        opy%=py%
        FOR py%=opy% TO opy%-15 STEP -2
          @aff_star(px%,py%,nb%)
          VSYNC
          RC_COPY ecr%,px%,py%,14,13 TO xb%,px%,py%
          INC nb%
          IF nb%=11
            nb%=0
          ENDIF
        NEXT py%
      ENDIF
    CASE 80
      IF py%<100+3*16
        opy%=py%
        FOR py%=opy% TO opy%+15 STEP 2
          @aff_star(px%,py%,nb%)
          VSYNC
          RC_COPY ecr%,px%,py%,14,13 TO xb%,px%,py%
          INC nb%
          IF nb%=11
            nb%=0
          ENDIF
        NEXT py%
      ENDIF
    ENDSELECT
    .
  LOOP UNTIL key%=57
  .
RETURN
> PROCEDURE aff_star(px%,py%,nb%)
  .
  dx%=nb%*14
  RC_COPY star%,dx%,13,14,13 TO xb%,px%,py%,4
  RC_COPY star%,dx%,0,14,13 TO xb%,px%,py%,7
  VSYNC
  .
RETURN
> PROCEDURE init_fnt
  .
  a$="ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789;,.-"
  dx%=0
  FOR x%=1 TO LEN(a$)
    fx%(ASC(MID$(a$,x%,1)))=dx%
    ADD dx%,8
  NEXT x%
  .
RETURN
> PROCEDURE fnt(px%,py%,ch$)
  .
  IF px%=-1
    px%=(320-(LEN(ch$)*8))/2
  ENDIF
  FOR x%=1 TO LEN(ch$)
    fx%=fx%(ASC(MID$(ch$,x%,1)))
    RC_COPY fnt%,fx%,16,8,16 TO xb%,px%,py%,4
    RC_COPY fnt%,fx%,0,8,16 TO xb%,px%,py%,7
    ADD px%,8
  NEXT x%
  .
RETURN
> PROCEDURE fin
  .
  BMOVE dos%,&HFFFF8240,32
  VSYNC
  OUT 4,8
  SHOWM
  SPOKE &H484,3
  VOID MFREE(log%)
  VOID MFREE(fnt%)
  VOID MFREE(sky%)
  VOID MFREE(star%)
  VOID MFREE(log2%)
  VOID MFREE(pal%)
  VOID MFREE(ecr%)
  VOID GEMDOS(32,L:super%)
  EDIT
  .
RETURN
.
menu0:
DATA "PLAY","OPTIONS","QUIT",""
