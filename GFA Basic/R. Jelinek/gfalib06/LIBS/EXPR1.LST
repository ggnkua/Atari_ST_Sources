FUNCTION expr1_main(x$,VAR exp_err!)          ! Ausdruck in x$ auswerten
  ' 28-10-94
  LOCAL erg
  '
  exp_err!=NOT @expr1_comp(x$,c_&(),mat_w_())
  erg=@expr1_eval(c_&(),mat_w_())
  ERASE c_&()
  ERASE mat_w_()
  '
  RETURN erg
ENDFUNC
FUNCTION expr1_comp(x$,VAR mat_code&(),wert())
  ' 28-10-94
  LOCAL exp_err!
  LOCAL i&
  LOCAL aktuelles_zeichen&
  LOCAL pc&
  LOCAL maxcodes&,werte&,maxwerte&,maxcodes&
  '
  maxwerte&=99
  maxcodes&=99
  ERASE mat_code&(),wert()
  DIM mat_code&(maxwerte&)
  DIM wert(maxcodes&)
  '
  x$=UPPER$(x$)
  aktuelles_zeichen&=1
  ~@expr1_ausdruck
  '
  IF pc&>0
    DIM mat_code_&(pc&-1)
    FOR i&=0 TO pc&-1
      mat_code_&(i&)=mat_code&(i&)
    NEXT i&
    SWAP mat_code_&(),mat_code&()
    ERASE mat_code_&()
    DIM mat_wert_(MAX(0,werte&-1))
    FOR i&=0 TO werte&-1
      mat_wert_(i&)=wert(i&)
    NEXT i&
    SWAP mat_wert_(),wert()
    ERASE mat_wert_()
  ENDIF
  '
  RETURN NOT exp_err!
ENDFUNC
FUNCTION expr1_eval(VAR mat_code&(),wert())  ! kodierten Ausdruck auswerten.
  ' 28-10-94
  LOCAL i&,stapel&,erg
  LOCAL code&
  LOCAL ende&
  LOCAL pc&
  '
  DIM stapel(DIM?(wert()))
  '
  ende&=DIM?(mat_code&())
  WHILE pc&<ende&
    code&=mat_code&(pc&)
    SELECT code&
    CASE 128 TO 255
      stapel(stapel&)=variablen_wert(BYTE(NOT code&))
      INC stapel&
    CASE 0
      stapel(stapel&)=wert(i&)
      INC stapel&
      INC i&
    DEFAULT
      SELECT code&
      CASE 1                                            ! ### ADD
        erg=stapel(stapel&-2)+stapel(stapel&-1)         !
        SUB stapel&,2                                   !
      CASE 2                                            ! ### SUB
        erg=stapel(stapel&-2)-stapel(stapel&-1)
        SUB stapel&,2
      CASE 3                                            ! ### MUL
        erg=stapel(stapel&-2)*stapel(stapel&-1)
        SUB stapel&,2
      CASE 4                                            ! ### DIV
        erg=stapel(stapel&-2)/stapel(stapel&-1)
        SUB stapel&,2
      CASE 5                                            ! ### POW
        erg=stapel(stapel&-2)^stapel(stapel&-1)
        SUB stapel&,2
      CASE 6                                            ! ### ABS
        DEC stapel&
        erg=ABS(stapel(stapel&))
      CASE 7                                            ! ### SIN
        DEC stapel&
        erg=SIN(stapel(stapel&))
      CASE 8                                            ! ### COS
        DEC stapel&
        erg=COS(stapel(stapel&))
      CASE 9                                            ! ### EXP
        DEC stapel&
        erg=EXP(stapel(stapel&))
      CASE 10                                           ! ### LOG
        DEC stapel&
        erg=LOG(stapel(stapel&))
      CASE 11                                           ! ### SQR
        DEC stapel&
        erg=SQR(stapel(stapel&))
      CASE 12                                           ! ### TAN
        DEC stapel&
        erg=TAN(stapel(stapel&))
      CASE 13                                           ! ### ATN
        DEC stapel&
        erg=ATN(stapel(stapel&))
      CASE 14                                           ! ### ASIN
        DEC stapel&
        erg=ASIN(stapel(stapel&))
      CASE 15                                           ! ### ACOS
        DEC stapel&
        erg=ACOS(stapel(stapel&))
      ENDSELECT
      stapel(stapel&)=erg
      INC stapel&
    ENDSELECT
    INC pc&
  WEND
  '
  erg=stapel(stapel&-1)
  ERASE stapel()
  '
  RETURN erg
ENDFUNC
'
' ### Bearbeitung der Variablen
'
FUNCTION expr1_init(anzvariablen&)      ! Arrays vorbereiten...
  ' 27-10-94
  maxvariablen&=anzvariablen&-1
  DIM variablen_name$(maxvariablen&)
  DIM variablen_wert(maxvariablen&)
  CLR nvariablen&
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_setvar(nam$,wert)        ! Variable neu einfhren oder aendern
  ' 27-10-94
  LOCAL i&
  '
  nam$=UPPER$(nam$)
  i&=-1
  IF nvariablen&>0
    i&=@expr1_varnum(nam$)
    IF i&<>-1
      variablen_wert(i&)=wert
    ENDIF
  ENDIF
  IF nvariablen&<=maxvariablen& AND i&=-1
    variablen_wert(nvariablen&)=wert
    variablen_name$(nvariablen&)=nam$
    INC nvariablen&
  ENDIF
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_varnum(nam$)             !
  ' 27-10-94
  LOCAL gefunden!
  LOCAL i&
  '
  FOR i&=0 TO nvariablen&-1
    IF variablen_name$(i&)=nam$
      gefunden!=TRUE
      EXIT IF TRUE
    ENDIF
  NEXT i&
  IF gefunden!
    RETURN i&
  ENDIF
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_end                      ! Arrays wieder l”schen...
  ' 27-10-94
  ERASE variablen_name$()
  ERASE variablen_wert()
  '
  RETURN TRUE
ENDFUNC
'
' ### Syntaxanalyse und Kodierung
'
FUNCTION expr1_ausdruck                 ! Ausdruck besteht aus einer Summe von Termen.
  LOCAL zeichen$,operator$
  '
  DO
    ~@expr1_term
    '
    EXIT IF exp_err!
    '
    IF operator$<>""
      ~@expr1_befehl(operator$)
    ENDIF
    '
    ~@expr1_nextchar
    EXIT IF zeichen$<>"+" AND zeichen$<>"-"
    operator$=zeichen$
  LOOP
  ~@expr1_charback(zeichen$)
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_brackets                 !
  ~@expr1_ausdruck
  ~@expr1_nextchar
  IF zeichen$<>")"
    exp_err!=TRUE
  ENDIF
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_term                     ! Term besteht aus einem Produkt von Faktoren.
  ' 28-10-94
  LOCAL zeichen$,operator$
  '
  DO
    ~@expr1_faktor
    '
    EXIT IF exp_err!
    '
    IF operator$<>""
      ~@expr1_befehl(operator$)
    ENDIF
    '
    ~@expr1_nextchar
    EXIT IF zeichen$<>"*" AND zeichen$<>"/"
    operator$=zeichen$
  LOOP
  ~@expr1_charback(zeichen$)
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_faktor                   !
  ' 28-10-94
  ' ein Faktor enth„lt entweder eine Zahl, eine Potenz, oder eine Funktion einer
  ' Zahl oder eines Ausdrucks (geklammert).
  LOCAL zeichen$,operator$,funktion$
  '
  DO
    ~@expr1_nextchar
    SELECT zeichen$
    CASE ""
      exp_err!=TRUE
    CASE "("
      ~@expr1_brackets
    CASE "-","+","0" TO "9"
      ~@expr1_charback(zeichen$)
      ~@expr1_zahl
    CASE "A" TO "Z"
      ' Funktionsaufruf oder Variable
      funktion$=""
      REPEAT
        funktion$=funktion$+zeichen$
        ~@expr1_nextchar
      UNTIL zeichen$<"A" OR zeichen$>"Z"
      ~@expr1_charback(zeichen$)
      '
      i&=@expr1_varnum(funktion$)
      IF i&<>-1                         ! ist es eine Variable?
        funktion$=""
        mat_code&(pc&)=BYTE(-i&-1)
        INC pc&
      ENDIF
      '
      IF funktion$<>""                   !ist es eine Funktion?
        IF funktion$="PI" ! <- einzige vordefinierte Zahl.
          ~@expr1_wert(PI)
        ELSE
          ' Argument einer Funktion ist entweder ein Ausdruck in Klammern oder
          ' eine Zahl
          ~@expr1_nextchar
          SELECT zeichen$
          CASE "("
            ~@expr1_brackets
          CASE "+","-","0" TO "9"
            ~@expr1_charback(zeichen$)
            ~@expr1_zahl
          DEFAULT
            exp_err!=TRUE
          ENDSELECT
          '
          EXIT IF exp_err!
          '
          ~@expr1_befehl(funktion$)
        ENDIF
      ENDIF
    DEFAULT
      exp_err!=TRUE
    ENDSELECT
    '
    EXIT IF exp_err!
    '
    IF operator$<>""
      ~@expr1_befehl(operator$)
    ENDIF
    '
    ~@expr1_nextchar
    EXIT IF zeichen$<>"^"
    operator$=zeichen$
  LOOP
  ~@expr1_charback(zeichen$)
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_zahl                     !
  ' eine Zahl hat ein Vorzeichen oder nicht (dann ist sie positiv) und besteht
  ' aus den Ziffern 0-9 getrennt durch ein Komma. Am Ende kann gefolgt von einem
  ' E ein 10er-Exponent-Wert stehen.
  LOCAL z$
  '
  z$=MID$(x$,aktuelles_zeichen&)
  ADD aktuelles_zeichen&,VAL?(z$)
  ~@expr1_wert(VAL(z$))
  '
  RETURN TRUE
ENDFUNC
'
' ### Zeichen aus Ausdruck auslesen
'
FUNCTION expr1_nextchar                 !
  ' 28-10-94
  IF aktuelles_zeichen&<=LEN(x$)
    REPEAT
      zeichen$=MID$(x$,aktuelles_zeichen&,1)
      INC aktuelles_zeichen&
    UNTIL zeichen$<>" "
  ELSE
    zeichen$=""
  ENDIF
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_charback(zeichen$)       !
  IF zeichen$<>""
    DEC aktuelles_zeichen&
    MID$(x$,aktuelles_zeichen&,1)=zeichen$
  ENDIF
  '
  RETURN TRUE
ENDFUNC
'
' ### Befehle & Zahlen kodieren
'
FUNCTION expr1_befehl(befehl$)          !
  ' 28-10-94
  LOCAL code&
  '
  code&=-1
  SELECT befehl$
  CASE "#"
    code&=0
  CASE "+"
    code&=1
  CASE "-"
    code&=2
  CASE "*"
    code&=3
  CASE "/"
    code&=4
  CASE "^"
    code&=5
  CASE "ABS"
    code&=6
  CASE "SIN"
    code&=7
  CASE "COS"
    code&=8
  CASE "EXP"
    code&=9
  CASE "LOG"
    code&=10
  CASE "SQR"
    code&=11
  CASE "TAN"
    code&=12
  CASE "ATN"
    code&=13
  CASE "ASIN"
    code&=14
  CASE "ACOS"
    code&=15
  ENDSELECT
  IF code&>=0
    mat_code&(pc&)=code&
    INC pc&
  ELSE
    exp_err!=TRUE
  ENDIF
  '
  RETURN TRUE
ENDFUNC
FUNCTION expr1_wert(x)                  !
  ' 28-10-94
  wert(werte&)=x
  INC werte&
  mat_code&(pc&)=0
  INC pc&
  '
  RETURN TRUE
ENDFUNC
