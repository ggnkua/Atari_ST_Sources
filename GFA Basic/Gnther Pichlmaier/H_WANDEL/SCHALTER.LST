.he RSC-DEMO fr Verwendung von Array's in Objektbamen
.n5
RESERVE -1000  !reicht fr das kleine Resourcefile
'
' Hier wird die erzeugte HEADER Datei eingemergt
'
' Dimensionierung von Feldern fr Resource-Datei:
DIM s&(8)
'
'   Resource-Set indicies fuer SCHALTER
'
LET baum&=0             !  Formula Dialog
LET s&(1)=3             !  BOXCHAR in Baum Baum
LET s&(2)=4             !  BOXCHAR in Baum Baum
LET s&(3)=5             !  BOXCHAR in Baum Baum
LET s&(4)=6             !  BOXCHAR in Baum Baum
LET s&(5)=7             !  BOXCHAR in Baum Baum
LET s&(6)=8             !  BOXCHAR in Baum Baum
LET s&(7)=9             !  BOXCHAR in Baum Baum
LET s&(8)=10            !  BOXCHAR in Baum Baum
LET e_eingab&=12        !  FTEXT in Baum Baum
LET b_ok&=16            !  BUTTON in Baum Baum
LET f_update&=17        !  IBOX in Baum Baum
LET s_ausgab&=18        !  STRING in Baum Baum
LET b_ende&=19          !  BUTTON in Baum Baum
'
'
IF RSRC_LOAD("\SCHALTER.RSC")=0
  ~FORM_ALERT(1,"[1][Resourcefile SCHALTER.RSC|nicht gefunden][Abbruch]")
  END
ENDIF
~RSRC_GADDR(0,baum&,baum%)
~FORM_CENTER(baum%,x&,y&,w&,h&)
~OBJC_DRAW(baum%,0,7,x&,y&,w&,h&)
ende!=FALSE
DO
  a%=FORM_DO(baum%,0)
  SELECT BCLR(a%,15)   !ohne doppelclick
  CASE b_ok&
    n$=CHAR{{OB_SPEC(baum%,e_eingab&)}}
    i&=VAL(n$)
    IF i&>0 AND i&<9
      OB_STATE(baum%,s&(i&))=BSET(OB_STATE(baum%,s&(i&)),3)
      ~OBJC_OFFSET(baum%,s&(i&),x_abs%,y_abs%)
      ~OBJC_DRAW(baum%,s&(i&),1,x_abs%,y_abs%,OB_W(baum%,s&(i&)),OB_H(baum%,s&(i&)))
    ENDIF
  CASE b_ende&
    ende!=TRUE
  DEFAULT
    FOR i&=1 TO 8
      IF a%=s&(i&)
        CHAR{OB_SPEC(baum%,s_ausgab&)}=STR$(i&)
        ~OBJC_OFFSET(baum%,f_update&,x_abs%,y_abs%)
        ~OBJC_DRAW(baum%,f_update&,1,x_abs%,y_abs%,OB_W(baum%,f_update&),OB_H(baum%,f_update&))
        PRINT CHR$(7);
      ENDIF
    NEXT i&
    OB_STATE(baum%,b_ok&)=BCLR(OB_STATE(baum%,b_ok&),0)
  ENDSELECT
LOOP UNTIL ende!
~RSRC_FREE()
RESERVE
