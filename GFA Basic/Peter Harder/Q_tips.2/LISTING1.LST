'(c)1995 MAXON Computer
'Autor: Peter Harder

FUNCTION get_cookie(code$,VAR eintrag%)
  $F%
  LOCAL code%,cookie_ptr%,i%
  cookie_ptr%=LPEEK(1440)
  ' Cookie Jar berhaupt installiert?
  IF cookie_ptr%>0 AND LEN(code$)=4
    code%=CVL(code$)
    FOR i%=cookie_ptr% TO cookie_ptr%+640 STEP 8
      EXIT IF code%={i%} OR {i%}=0
      ' Cookie Jar durchsuchen
    NEXT i%
    IF code%={i%}
    ' Cookie gefunden?
      eintrag%={i%+4}
    ' ja, 4 Byte langen Eintrag auslesen
      RETURN TRUE
    ELSE
      RETURN FALSE
    ENDIF
  ELSE
    RETURN FALSE
  ENDIF
ENDFUNC
'
FUNCTION set_cookie(code$,eintrag%)
  $F%
  LOCAL code%,cookie_ptr%,i%
  cookie_ptr%=LPEEK(1440)
  IF cookie_ptr%>0 AND LEN(code$)=4
    code%=CVL(code$)
    FOR i%=cookie_ptr% TO cookie_ptr%+640 STEP 8
      EXIT IF code%={i%} OR {i%}=0
    NEXT i%
    IF code%={i%}
      {i%+4}=eintrag%
      RETURN TRUE
    ELSE
      RETURN FALSE
    ENDIF
  ELSE
    RETURN FALSE
  ENDIF
ENDFUNC
'
FUNCTION exist_cookiejar
  $F%
  IF LPEEK(1440)=0
    RETURN FALSE
  ELSE
    RETURN TRUE
  ENDIF
ENDFUNC

