DIM r%(3)
'
form1%=0    ! Dialog
ikon1%=1    ! ICON in Baum FORM1
vor%=2      ! FTEXT in Baum FORM1
nac%=3      ! FTEXT in Baum FORM1
str%=4      ! FTEXT in Baum FORM1
ort%=5      ! FTEXT in Baum FORM1
abbruch%=6  ! BUTTON in Baum FORM1
ok%=7       ! BUTTON in Baum FORM1
r%(1)=8     ! BUTTON in Baum FORM1
r%(2)=9     ! BUTTON in Baum FORM1
r%(3)=10    ! BUTTON in Baum FORM1
ausgabe%=11 ! STRING in Baum FORM1
'
~RSRC_FREE()
IF RSRC_LOAD("BEISPIEL\DIALOG.RSC")=FALSE               ! Resource laden
  ALERT 3," |Konnte DIALOG.RSC nicht finden.",1,"ABBRUCH",a%
  EDIT
ENDIF
~RSRC_GADDR(0,0,tree_adr%)              ! Adresse des Objektbaumes ermitteln
~FORM_CENTER(tree_adr%,x%,y%,w%,h%)     ! Objektbaumkoordinaten zentrieren
'
' Texte in den Edit-Feldern vorbelegen
CHAR{{OB_SPEC(tree_adr%,vor%)}}="Johann Sebastian"
CHAR{{OB_SPEC(tree_adr%,nac%)}}="Bach"
CHAR{{OB_SPEC(tree_adr%,str%)}}="Kantatengasse 77"
CHAR{{OB_SPEC(tree_adr%,ort%)}}="Dresden"
'
~OBJC_DRAW(tree_adr%,0,1,x%,y%,w%,h%)   ! Objektbaum zeichnen
'
REPEAT
  ex%=FORM_DO(tree_adr%,0)              ! Objekt mit Exit-Status angeklickt ?
  '
  ' Texte aus den Edit-Feldern in entsprechende Strings einlesen
  vorname$=CHAR{{OB_SPEC(tree_adr%,vor%)}}
  nachname$=CHAR{{OB_SPEC(tree_adr%,nac%)}}
  strasse$=CHAR{{OB_SPEC(tree_adr%,str%)}}
  ort$=CHAR{{OB_SPEC(tree_adr%,ort%)}}
  '
  FOR i%=1 TO 3
    IF BTST(OB_STATE(tree_adr%,r%(i%)),0)       ! welcher radio-button
      radio%=r%(i%)                             ! wurde angeklickt ?
    ENDIF
  NEXT i%
UNTIL ex%=ok% OR ex%=abbruch%
'
~RSRC_FREE()    ! reservierten Speicherplatz wieder freigeben
'
CLS
PRINT "Ende mit : ";ex%
PRINT "Vorname  : ";vorname$
PRINT "Nachname : ";nachname$
PRINT "Strasse  : ";strasse$
PRINT "Ort      : ";ort$
PRINT "Radio    : ";radio%
