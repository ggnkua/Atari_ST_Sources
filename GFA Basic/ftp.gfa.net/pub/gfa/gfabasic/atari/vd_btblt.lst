DIM smfdb%(8),dmfdb%(8),p%(8)
'
FOR i%=0 TO 639 STEP 8
  LINE i%,0,639,399
NEXT i%
'
GET 0,0,639,399,a$
mirrorput(0,0,a$)
'
PROCEDURE mirrorput(x%,y%,VAR x$)
  IF LEN(x$)>6      !nur wenn auch etwas da ist
    a%=V:x$
    b%=INT{a%}
    h%=INT{a%+2}
    '
    smfdb%(0)=a%+6
    smfdb%(1)=(b%+16) AND &HFFF0
    smfdb%(2)=h%+1
    smfdb%(3)=smfdb%(1)/16
    smfdb%(5)=DPEEK(a%+4)
    dmfdb%(0)=XBIOS(3)
    '
    dmfdb%(1)=640
    dmfdb%(2)=400
    dmfdb%(3)=40
    dmfdb%(5)=1
    '
    p%(1)=0
    p%(3)=h%
    p%(5)=y%
    p%(7)=y%+h%
    p%(8)=3
    p%(4)=x%+b%
    p%(6)=x%+b%
    '
    FOR i%=0 TO b%
      p%(0)=i%
      p%(2)=i%
      BITBLT smfdb%(),dmfdb%(),p%()
      DEC p%(4)
      DEC p%(6)
    NEXT i%
    '
  ENDIF
RETURN
