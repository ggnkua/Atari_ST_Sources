' ** MenÅleisten-Programmierung
'
' Speicherplatz fÅr Resource reservieren, Resource laden
' und MenÅleiste darstellen.
RESERVE FRE(0)-33000
IF RSRC_LOAD("\RCS\RCS2.RSC")=FALSE
  ALERT 3," |Konnte RCS2.RSC nicht finden.|Bitte Pfad Ñndern.",1,"ABBRUCH",a%
  EDIT
ENDIF
~RSRC_GADDR(0,0,menu_adr%)
~MENU_BAR(menu_adr%,1)
'
' Message-Buffer einrichten und nÅtzliche Variablen vorbereiten
DIM message_buffer%(3)
mes_adr%=V:message_buffer%(0)
ABSOLUTE mes_type&,mes_adr%
ABSOLUTE m_titel&,mes_adr%+6
ABSOLUTE m_eintrag&,mes_adr%+8
'
REPEAT
  ~EVNT_MULTI(&X110000,0,0,0,0,0,0,0,0,0,0,0,0,0,mes_adr%,500)
  ' falls ein MenÅpunkt gewÑhlt wurde
  IF mes_type&=10
    ' falls ein anderer Eintrag als beim letzten Mal gewÑhlt wurde
    IF obj_nr%<>m_eintrag&
      obj_nr%=m_eintrag&
      titel$=CHAR{OB_SPEC(menu_adr%,m_titel&)}
      eintrag$=CHAR{OB_SPEC(menu_adr%,m_eintrag&)}
      PRINT AT(3,20);"MenÅtitel:    ";titel$;SPC(10)
      PRINT AT(3,22);"MenÅeintrag:   ";eintrag$;SPC(10)
      ~MENU_TNORMAL(menu_adr%,m_titel&,1)
    ENDIF
  ENDIF
UNTIL MOUSEK=2
'
' MenÅleiste entfernen, Resource aus Speicher entfernen und
' reservierten Speicher wieder zurÅckholen.
~MENU_BAR(menu_adr%,0)
~RSRC_FREE()
RESERVE FRE(0)+33000
END
