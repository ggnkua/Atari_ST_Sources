' ************************************************************************
' NUMTEXT.LST
' A small set of routines that will write out a number
' for you in full. It uses a globally defined array
' (36 strings) called 'numtxt$()'. For speediest
' results, you should call upon procedure numtext_init()
' somewhere at the beginning of your program.
' like the DATESTUF-listing on ICTARI #36 it uses a
' global word 'language&' to decide which of the five
' supported languages to use.
' The string returned is always in lower-cast.
'
' Theo Ros  6 march 1997
' ************************************************************************
'
DEFWRD "a-z"            ! always use these three lines..
DEFLIST 4
OPTION BASE 0
'
PRINT CHR$(27);"v";     ! text wraps on screen for demo
language=1              ! 1=english,2=dutch,3=french,4=german,5=spanish
@numtext_init
'
' my GfA 3.5 allows numbers up to 99999999999990..
' anything bigger results in strange results. (?!)
'
PRINT @numtext$(1230987654321)
~INP(2)
ERASE numtxt$()
EDIT
'
> PROCEDURE numtext_init
  LOCAL unit
  ERASE numtxt$()
  DIM numtxt$(35)
  SELECT language
  CASE 2
    RESTORE ned_numbers
  CASE 3
    RESTORE fra_numbers
  CASE 4
    RESTORE ger_numbers
  CASE 5
    RESTORE spa_numbers
  DEFAULT
    RESTORE eng_numbers
  ENDSELECT
  FOR unit=0 TO 35
    READ numtxt$(unit)
  NEXT unit
  '
  ' the last string ('and') is used in some languages to link numbers
  '
eng_numbers:
  DATA zero,one,two,three,four,five,six,seven,eight,nine
  DATA ten,eleven,twelve,thirteen,fourteen,fifteen,sixteen,seventeen,eighteen,nineteen
  DATA twenty,thirty,fourty,fifty,sixty,seventy,eighty,ninety,hundred
  DATA thousand,million,milliard,billion,trillion,quadrillion,and
ned_numbers:
  DATA nul,‚‚n,twee,drie,vier,vijf,zes,zeven,acht,negen
  DATA tien,elf,twaalf,dertien,veertien,vijftien,zestien,zeventien,achttien,negentien
  DATA twintig,dertig,veertig,vijftig,zestig,zeventig,tachtig,negentig,honderd
  DATA duizend,miljoen,miljard,biljoen,triljoen,quadriljoen,en
fra_numbers:
  DATA zero,un,deux,trois,quatre,cinq,six,sept,huit,neuf
  DATA dix,onze,douze,treize,quatorze,quinze,seize,dix-sept,dix-huit,dix-neuf
  DATA vingt,trente,quarante,cinquante,soixante,soixante-dix,quatre-vingt,quatre-vingt-dix,cent
  DATA mille,million,milliard,billion,trillion,quadrillion,et
ger_numbers:
  DATA null,ein,zwei,drei,vier,fnf,sechs,sieben,acht,neun
  DATA zehn,elf,zw”lf,dreizehn,vierzehn,fnfzehn,sechtzehn,siebzehn,achtzehn,neunzehn
  DATA zwanzig,dreizig,vierzig,fnfzig,sechzig,siebzig,achtzig,neunzig,hundert
  DATA tausend,million,milliard,billion,triljoen,quadrillion,und
spa_numbers:
  DATA cero,uno,dos,tres,cuatro,cinco,seis,siete,ocho,nueve
  DATA diez,once,doce,trece,catorce,cuince,diecis‚is,diecisiete,dieciocho,diecinueve
  DATA veinte,treinta,cuarenta,cincuenta,sesenta,setenta,ochenta,noventa,ciento
  DATA mil,mill¢n,millar,bill¢n,trill¢n,cuadrill¢n,y
RETURN
> FUNCTION numfrac(n#)
' subfunction for numtext$()
' returns decimal part as integer: 0.12345 --> 12345
$F%
LOCAL n$
n$=STR$(n#)           ! because FRAC doesn't work properly..
p=INSTR(n$,".")
IF p
  RETURN VAL(RIGHT$(n$,LEN(n$)-p))
ENDIF
RETURN 0
ENDFUNC
> FUNCTION numtext$(num#)
' returns a 'written in full' string for a number passed
' if this number has a decimal part, this mantissa is
' NOT written out, but returned as '1234/10000'
' if you don't want that, use two steps:
' 1:    num$=@numtext$(FIX(number#))                      ! read whole part
' 2:    num$=num$+" point "+@numtext$(@numfrac(number#))  ! add mantissa
'
' can handle VERY large numbers
' uses GLOBALS language& and numtxt$(0 to 35)
' (language&:  1=english,2=dutch,3=french,4=german,5=spanish)
'
LOCAL dum$,number$,comma%,unit
CLR comma%
IF num#<>FIX(num#)
comma%=@numfrac(num#)
num#=FIX(num#)
ENDIF
IF num#=0
number$=numtxt$(0)
ELSE
unit=29                     ! start off with 'thousand'
number$=@num100$(VAL(RIGHT$(STR$(num#),3)))
WHILE num#>999
  num#=num#\1000            ! DIV num#,1000 doesn't work...
  dum$=@num100$(VAL(RIGHT$(STR$(num#),3)))
  IF dum$>""                ! in numtext$(unit) 'thousand', 'million' etc.
    number$=dum$+" "+numtxt$(unit)+" "+number$
  ENDIF
  INC unit
WEND
ENDIF
IF comma%                     ! decimals as '12345/100000'
dum$=STR$(comma%)
RETURN number$+" "+dum$+"/1"+STRING$(LEN(dum$),"0")
ENDIF
RETURN number$
ENDFUNC
> FUNCTION num100$(num_in)
' subfunction for numtext$()
' can handle integers from 0 to 999
' uses GLOBALS language& and numtxt$(0 to 35)
' (language&:  1=english,2=dutch,3=french,4=german,5=spanish)
'
LOCAL num_out$,hundred
CLR num_out$
IF num_in>99
hundred=num_in\100
IF OR(hundred>1,language=1)     ! english: 'one hundred'
SELECT language               ! rest: not '‚‚n honderd' but 'honderd'
CASE 2,4                      ! dutch and german:
  num_out$=numtxt$(hundred)   ! not 'drie honderd' but 'driehonderd'
DEFAULT                       ! rest: not 'deuxcent' but 'deux cent'
  num_out$=numtxt$(hundred)+" "
ENDSELECT
ENDIF
IF AND(language=5,hundred=9)    ! spanish: not 'nueve ciento'
num_out$="novecientos "       !          but 'novecientos'
ELSE                            ! add numtxt$(28) --> 'hundred'
num_out$=num_out$+numtxt$(28)+" "
ENDIF
SUB num_in,hundred*100          ! do 0-99
ENDIF
IF num_in>0
IF num_in<21
num_out$=num_out$+numtxt$(num_in)
ELSE
SELECT language
CASE 2,4                                  ! dutch and german:
  IF (num_in MOD 10)                      ! not 'vierzig ein'
    num_out$=num_out$+numtxt$(num_in MOD 10)+" "+numtxt$(35)+" "
  ENDIF                                   ! but 'ein und vierzig'
  num_out$=num_out$+numtxt$(num_in\10+18)
CASE 3                                    ! french:
  SELECT num_in                           ! not 'soixante-dix deux'
  CASE 70 TO 76,90 TO 96                  ! but 'soixante-douze'
    num_out$=num_out$+numtxt$(num_in\10+18)
    num_out$=LEFT$(num_out$,LEN(num_out$)-3)  ! kill 'dix'
    SUB num_in,(num_in\10)*10             ! num_in becomes 7 or 9
    num_out$=num_out$+numtxt$(num_in+10)  ! to create numtxt$(17 or 19)
  DEFAULT
    num_out$=num_out$+numtxt$(num_in\10+18)
    num_in=(num_in MOD 10)                ! not 'zero-billion'
    IF num_in
      num_out$=num_out$+"-"+numtxt$(num_in)
    ENDIF
  ENDSELECT
DEFAULT                                   ! rest: 'fourty one'
  num_out$=num_out$+numtxt$(num_in\10+18)
  num_in=(num_in MOD 10)                  ! not 'zero thousand'
  IF num_in
    num_out$=num_out$+" "+numtxt$(num_in)
  ENDIF
ENDSELECT
ENDIF
ENDIF
RETURN TRIM$(num_out$)
ENDFUNC
