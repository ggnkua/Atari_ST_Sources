' ***************************************
' * DEMO-Programm fr EXTENDED FORM_DO
' * 1989  Tassilo Nitz
' *       Krablerstr.24
' *       4300 Essen 12
' *       0201 / 34 71 49
' * (c) MAXON Computer 1989
'
SHOWM          ! Anweisungen n”tig fr
DEFMOUSE 0     ! den 3.0-Compiler
main
'
'
> PROCEDURE main
  tree1&=0
  rbut1&=26    ! BUTTON in tree tree1
  rbut2&=28    ! BUTTON in tree tree1
  rbut3&=30    ! BUTTON in tree tree1
  arrright&=34    ! BOXCHAR in tree tree1
  arrleft&=36    ! BOXCHAR in tree tree1
  count&=38    ! BUTTON in tree tree1
  reset&=39    ! BUTTON in tree tree1
  dd_canc&=43    ! BUTTON in tree tree1
  dd_ok&=45    ! BUTTON in tree tree1
  tree3&=1
  info1&=2
  info3&=3
  tree2&=4
  info2&=5
  '
  '
  RESERVE -8000
  IF RSRC_LOAD("FDO_DEMO.RSC")=0
    ~RSRC_FREE()
    RESERVE
    END
  ENDIF
  '
  REPEAT
    ~RSRC_GADDR(0,info1&,tree%)
    draw_tree_at(0,0,0,0,47,4)
    ~RSRC_GADDR(0,tree1&,tree%)
    draw_tree_at(0,0,0,0,31,127)
    REPEAT
      ex&=AND(@form_do(tree%,0),&H7FFF)
      SELECT ex&
      CASE arrright&
        IF zaehl&<999
          INC zaehl&
          CHAR{OB_SPEC(tree%,count&)}=RIGHT$("00"+STR$(zaehl&),3)
          draw_obj(count&)
        ENDIF
      CASE arrleft&
        IF zaehl&>0
          DEC zaehl&
          CHAR{OB_SPEC(tree%,count&)}=RIGHT$("00"+STR$(zaehl&),3)
          draw_obj(count&)
        ENDIF
      CASE reset&
        zaehl&=0
        CHAR{OB_SPEC(tree%,count&)}="000"
        draw_obj(count&)
      ENDSELECT
    UNTIL ex&=dd_canc& OR ex&=dd_ok&
    OB_STATE(tree%,ex&)=AND(OB_STATE(tree%,ex&),NOT 1)
    erase_tree(0,0,0,0)
    CLS
    ~RSRC_GADDR(0,info2&,tree%)
    draw_tree_at(0,0,0,0,111,4)
    ~RSRC_GADDR(0,tree2&,tree%)
    draw_tree(0,0,0,0)
    ex&=@form_do(tree%,0)
    erase_tree(0,0,0,0)
    CLS
    ~RSRC_GADDR(0,info3&,tree%)
    draw_tree_at(0,0,0,0,111,4)
    ~RSRC_GADDR(0,tree3&,tree%)
    draw_tree(0,0,0,0)
    ex&=@form_do(tree%,0)
    erase_tree(0,0,0,0)
    CLS
    '
  UNTIL BIOS(11,-1) AND 3
  ~RSRC_FREE()
  RESERVE
RETURN
'
FUNCTION form_do(tree%,startobj&)
  '
  LOCAL do_dial&,ed_obj&,nxt_obj&,pos&,mpos&,p&,i&,buf|,charw&,mlen&
  LOCAL ox&,mes&,ev_mx&,ev_my&,ev_mb&,ev_ks&,ev_key&,ev_clicks&
  '
  ~WIND_UPDATE(3)    !BEG_MCTRL
  '
  IF NOT BTST(OB_FLAGS(tree%,startobj&),3)  !startobj& editable?
    nxt_obj&=@search_ob_flag(tree%,0,3,1)
  ELSE
    nxt_obj&=startobj&
  ENDIF
  ed_obj&=0      !Index des aktuellen Textobjekts
  do_dial&=1
  '
  WHILE do_dial&
    '
    IF ed_obj&<>nxt_obj& AND nxt_obj&<>0   !neues Textobjekt?
      ed_obj&=nxt_obj&
      nxt_obj&=0
      ~OBJC_EDIT(tree%,ed_obj&,0,0,1,pos&) !ja, Cursor darstellen
    ENDIF
    '
    mes&=EVNT_MULTI(&X11,2,1,1,0,0,0,0,0,0,0,0,0,0,0,0,ev_mx&,ev_my&,ev_mb&,ev_ks&,ev_key&,ev_clicks&)
    '
    IF BTST(mes&,0)            !Tastendruck
      '
      do_dial&=@process_key(tree%,ed_obj&,ev_ks&,ev_key&,nxt_obj&,ev_key&,pos&)
      '
      IF ev_key&                                       !keine Steuertaste
        ~OBJC_EDIT(tree%,ed_obj&,ev_key&,pos&,2,pos&)  !in Textobjekt einfgen
      ENDIF
      '
    ELSE                       !Maustaste
      nxt_obj&=OBJC_FIND(tree%,0,8,ev_mx&,ev_my&)  !Objekt unter Mauszeiger
      '
      IF nxt_obj&=-1   !Maus aužerhalb der Dialogbox
        OUT 2,7
        nxt_obj&=0
      ELSE
        IF BTST(OB_FLAGS(tree%,nxt_obj&),3)       !Editable?
          ~OBJC_OFFSET(tree%,nxt_obj&,ox&,dummy&)   !X-Koord. Textobjekt
          ~GRAF_HANDLE(charw&,dummy&,dummy&,dummy&) !Breite eines Zeichens
          ' L„nge der Textmaske (nullterminierter String, abschliežendes
          mlen&=CARD{OB_SPEC(tree%,nxt_obj&)+26}-1  !Nullbyte z„hlt mit)
          SELECT CARD{OB_SPEC(tree%,nxt_obj&)+16}   !Textausrichtung
          CASE 0     !linksbndig
            mpos&=ROUND((ev_mx&-ox&)/charw&)
            ' rechtsbndig: nur der Vollst„ndigkeit halber, werden vom AES
          CASE 1   !sowieso nicht korrekt verwaltet
            mpos&=ROUND((ev_mx&-ox&-OB_W(tree%,nxt_obj&))/charw&+mlen&)
          CASE 2     !zentriert
            mpos&=ROUND((ev_mx&-ox&-(OB_W(tree%,nxt_obj&)-mlen&*charw&) DIV 2)/charw&)
          ENDSELECT
          p&=0
          IF mpos&>0
            i&=0
            REPEAT    !Zeichenposition bestimmen
              IF BYTE{{OB_SPEC(tree%,nxt_obj&)+4}+i&}=95   !"_"
                INC p&
              ENDIF
              INC i&
            UNTIL i&=mpos& OR i&=mlen&
          ENDIF
          ~OBJC_EDIT(tree%,ed_obj&,0,pos&,3,pos&) !Positionierung des
          buf|=BYTE{{OB_SPEC(tree%,nxt_obj&)}+p&} !Cursors mit einem
          BYTE{{OB_SPEC(tree%,nxt_obj&)}+p&}=0    !kleinen Trick
          ~OBJC_EDIT(tree%,nxt_obj&,0,pos&,1,pos&)
          BYTE{{OB_SPEC(tree%,nxt_obj&)}+p&}=buf|
          ed_obj&=nxt_obj&                        !aktuelles Textobj neu setzen
        ENDIF
        do_dial&=FORM_BUTTON(tree%,nxt_obj&,ev_clicks&,nxt_obj&)
      ENDIF
      '
    ENDIF
    '
    IF do_dial&=0 OR (nxt_obj&<>0 AND nxt_obj&<>ed_obj&) !wenn neues Textobjekt
      ~OBJC_EDIT(tree%,ed_obj&,0,pos&,3,pos&)            !oder Ende des Dialogs
    ENDIF                                                !Cursor l”schen
    '
  WEND
  '
  ~WIND_UPDATE(2)    !END_MCTRL
  RETURN nxt_obj&
  '
ENDFUNC
'
FUNCTION process_key(tree%,obj&,kstate&,ev_key&,VAR nxt_obj&,nxtchar&,pos&)
  LOCAL key_obj&
  '
  IF BTST(kstate&,0)
    kstate&=BSET(kstate&,1)
  ENDIF
  kstate&=SHR&(kstate&,1)
  '
  nxtchar&=0
  SELECT ev_key&
  CASE &H4D36   !SHIFT/Cursor right
    ~OBJC_EDIT(tree%,obj&,0,pos&,3,pos&)
    ~OBJC_EDIT(tree%,obj&,0,pos&,1,pos&)
    '
  CASE &H4B34   !SHIFT/Cursor left
    ~OBJC_EDIT(tree%,obj&,0,pos&,3,pos&)
    buf|=BYTE{{OB_SPEC(tree%,obj&)}}
    BYTE{{OB_SPEC(tree%,obj&)}}=0
    ~OBJC_EDIT(tree%,obj&,0,pos&,1,pos&)
    BYTE{{OB_SPEC(tree%,obj&)}}=buf|
    '
  CASE &H5000    !Cursor down
    nxt_obj&=@search_ob_flag(tree%,obj&,3,1)
    '
  CASE &H4800    !Cursor up
    nxt_obj&=@search_ob_flag(tree%,obj&,3,-1)
    '
  CASE &H5032,&H4737    !SHIFT/Cursor down  SHIFT/HOME
    REPEAT
      nxt_obj&=obj&
      obj&=@search_ob_flag(tree%,obj&,3,1)
    UNTIL obj&=nxt_obj&
    '
  CASE &H4838,&H4700    !SHIFT/Cursor up / HOME
    nxt_obj&=@search_ob_flag(tree%,0,3,1)
    '
  CASE &H1C0D,&H720D    !RETURN/ENTER
    nxt_obj&=@search_ob_flag(tree%,0,1,1)  !Default-Objekt vorhanden?
    IF nxt_obj&                            !ja, dann Mausklick simulieren
      RETURN FORM_BUTTON(tree%,nxt_obj&,1,nxt_obj&)
    ELSE       !kein Default-Objekt
      nxt_obj&=@search_ob_flag(tree%,obj&,3,1)   !n„chstes Textobjekt suchen
      IF nxt_obj&=obj& AND @search_ob_flag(tree%,0,2,1)=0
        RETURN FALSE         !kein Exit-Obj und Cursor im letzten Textobjekt
      ENDIF
    ENDIF
    '
  DEFAULT   !keine Steuertaste => Objekt mit passender Tastenbelegung suchen
    key_obj&=@search_key_obj(tree%,kstate&,ev_key&)
    IF key_obj&                                     !Objekt gefunden
      RETURN FORM_BUTTON(tree%,key_obj&,1,nxt_obj&) !Mausklick simulieren
    ENDIF
    nxt_obj&=obj&
    nxtchar&=ev_key&
  ENDSELECT
  '
  RETURN TRUE
  '
ENDFUNC
FUNCTION search_key_obj(tree%,kstate&,ev_key&)
  LOCAL key|,obj&
  key|=SHR&(ev_key&,8)       !nur SCAN-Code
  '
  obj&=0
  REPEAT
    INC obj&
    IF key|=SHR&(OB_STATE(tree%,obj&),8) AND kstate&=SHR&(OB_FLAGS(tree%,obj&),13)
      RETURN obj&
    ENDIF
  UNTIL BTST(OB_FLAGS(tree%,obj&),5)
  '
  RETURN 0
ENDFUNC
FUNCTION search_ob_flag(tree%,startobj&,bitnr|,direction&)
  LOCAL obj&
  obj&=startobj&
  REPEAT
    ADD obj&,direction&
    IF BTST(OB_FLAGS(tree%,obj&),bitnr|)
      RETURN obj&
    ENDIF
  UNTIL (obj&<=0) OR BTST(OB_FLAGS(tree%,obj&),5)
  RETURN startobj&
ENDFUNC
'
> PROCEDURE draw_obj(obj&)
  LOCAL x&,y&
  ~OBJC_OFFSET(tree%,obj&,x&,y&)
  ~OBJC_DRAW(tree%,obj&,8,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
RETURN
> PROCEDURE draw_tree(x&,y&,w&,h&)
  LOCAL fo_cx&,fo_cy&,fo_cw&,fo_ch&
  ~FORM_CENTER(tree%,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  ~FORM_DIAL(0,x&,y&,w&,h&,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  ~FORM_DIAL(1,x&,y&,w&,h&,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  ~OBJC_DRAW(tree%,0,8,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
RETURN
> PROCEDURE erase_tree(x&,y&,w&,h&)
  LOCAL fo_cx&,fo_cy&,fo_cw&,fo_ch&
  ~FORM_CENTER(tree%,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  ~FORM_DIAL(2,x&,y&,w&,h&,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  ~FORM_DIAL(3,x&,y&,w&,h&,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
RETURN
> PROCEDURE draw_tree_at(x&,y&,w&,h&,posx&,posy&)
  LOCAL dw&,dh&
  OB_X(tree%,0)=posx&
  OB_Y(tree%,0)=posy&
  posx&=MAX(0,posx&-3)
  posy&=MAX(0,posy&-3)
  dw&=OB_W(tree%,0)+6
  dh&=OB_H(tree%,0)+6
  ~FORM_DIAL(0,x&,y&,w&,h&,posx&,posy&,dw&,dh&)
  ~FORM_DIAL(1,x&,y&,w&,h&,posx&,posy&,dw&,dh&)
  ~OBJC_DRAW(tree%,0,8,posx&,posy&,dw&,dh&)
RETURN
'
