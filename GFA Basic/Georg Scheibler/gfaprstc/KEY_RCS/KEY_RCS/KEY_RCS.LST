' ********************************************
' * KEY RESOURCE EDITOR fÅr EXTENDED FORM_DO *
' *                                          *
' * I: 09.06.89       Tassilo Nitz           *
' * U: 16.06.89       Krablerstr. 24         *
' *                   4300 Essen 12          *
' *                   0201 / 34 71 49        *
' * (c) MAXON Computer 1989                  *
' ********************************************
'
'
init
main
'
> PROCEDURE init     !Vorbereitungen
  '
  KEYPAD 0       !NUMLOCK deaktivieren
  '
  DEFMOUSE 0     !Nur nîtig, wenn das Programm
  SHOWM          !kompiliert werden soll
  '
  ' *** Platz fÅr eigene RSC und edierte RSC ***
  RESERVE -40000
  '
  DIM c_shf&(2),m_tr&(7),tree_type|(50),tree_modify!(50)
  DIM tree_name$(50),ob_name$(50,256),trm$(6)
  ' *** RSC-Objekte definieren ***
  k_choice&=0
  c_clear&=1
  c_obname&=2
  c_shf&(0)=3
  c_shf&(1)=4
  c_shf&(2)=5
  c_key&=6
  main&=1
  m_file&=6
  m_trbox&=7
  m_tr&(0)=8
  m_tr&(1)=9
  m_tr&(2)=10
  m_tr&(3)=11
  m_tr&(4)=12
  m_tr&(5)=13
  m_tr&(6)=14
  m_tr&(7)=15
  m_scrlup&=16
  m_pbox&=17
  m_slider&=18
  m_load&=19
  m_save&=20
  m_edit&=21
  m_reset&=22
  m_scrldn&=23
  m_quit&=24
  ' *** KÅrzel fÅr Baum-Typen ***
  trm$(0)="U "  !UNKNOWN
  trm$(2)="M "  !MENU
  trm$(3)="  "  !DIALOG/PANEL/FREE-TREE
  trm$(4)="A "  !ALERT
  trm$(5)="S "  !FREE STRING
  trm$(6)="I "  !FREE IMAGE
  '
  pfad$="\"     !Zugriffspfad
  ' *** horiz. und vert. Auflîsung bestimmen ***
  r&=XBIOS(4)
  hor_res&=320-320*(r&>0)-1
  vert_res&=200-200*(r&=2)-1
  ' *** Tastencode-Umwandlungstabellen ***
  keytbl%=XBIOS(16,L:-1,L:-1,L:-1)
  unshift_table%={keytbl%}
  shift_table%={keytbl%+4}
  ' *** Ausmaûe eines Zeichens ***
  ~GRAF_HANDLE(charw&,charh&,dummy&,dummy&)
  ' *** RSC laden ***
  IF RSRC_LOAD("KEY_RCS.RSC")=0
    ALERT 1,"READ ERROR on RSC-File!",1,"END",dummy&
    ~RSRC_FREE()
    RESERVE
    END
  ENDIF
  ' *** Baumadressen holen ***
  ~RSRC_GADDR(0,k_choice&,k_choice_adr%)
  ~RSRC_GADDR(0,main&,main_adr%)
  ' *** Anfangsadresse und LÑnge der Resource ***
  global%={GB+4}  !Adresse des GEM-internen GLOBAL-Feldes
  prg_rsc_adr%={global%+14}  !in den Elementen 7 und 8 steht RSC-Adresse
  prg_rsc_len&=CARD{global%+18}  !Element 9 enthÑlt LÑnge der Resource
  ' *** Index des ersten angezeigten Baums ***
  CLR topidx&
  ' *** BaumeintrÑge lîschen ***
  tree%=main_adr%
  FOR i&=0 TO 7
    put_string(m_tr&(i&),"")
    del_state(m_tr&(i&),4)
  NEXT i&
  OB_Y(tree%,m_slider&)=0
  OB_H(tree%,m_slider&)=OB_H(tree%,m_pbox&)
  ' *** Auswahlbox darstellen ***
  CLS
  ~FORM_CENTER(tree%,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  ~OBJC_DRAW(tree%,0,8,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  '
RETURN
> PROCEDURE main
  LOCAL ex&,i&,w&,sy&,ypos&,dclick!
  REPEAT
    ex&=FORM_DO(tree%,0)
    dclick!=BTST(ex&,15)  !Doppelklick
    ex&=AND(ex&,&H7FFF)   !Bit 15 ausmaskieren
    '
    IF (ex&>=m_tr&(0) AND ex&<=m_tr&(7)) AND dclick!
      edit_tree(topidx&+ex&-m_tr&(0))   !Baum edieren
    ELSE IF ex&=m_edit&
      CLR i&
      DO UNTIL @selected(m_tr&(i&))
        INC i&
      LOOP WHILE i&<8
      IF i&<8
        edit_tree(topidx&+i&)   !Baum edieren
      ENDIF
      deselect(m_edit&,TRUE)
    ELSE IF ex&=m_reset&  !Tastenbelegungen lîschen
      IF anz_trees&
        CLR i&    !ein Baum angewÑhlt?
        DO UNTIL @selected(m_tr&(i&))
          INC i&
        LOOP WHILE i&<8
        IF i&<8   !ja, dann RESET nur fÅr diesen Baum
          reset_tree(topidx&+i&)
          show_trees(topidx&,anz_struc&)
        ELSE      !ansonsten fÅr komplette Resource
          ALERT 2,"Reset all trees?",2,"Yes|No",w&
          IF w&=1
            FOR i&=0 TO anz_trees&-1
              reset_tree(i&)
            NEXT i&
            show_trees(topidx&,anz_struc&)
          ENDIF
        ENDIF
      ENDIF
      deselect(m_reset&,TRUE)
    ELSE IF ex&=m_scrlup&   !Pfeil oben angeklickt
      IF topidx&>0
        DEC topidx&
        show_trees(topidx&,anz_struc&)
      ENDIF
    ELSE IF ex&=m_scrldn&   !Pfeil unten angeklickt
      IF topidx&+8<anz_struc&
        INC topidx&
        show_trees(topidx&,anz_struc&)
      ENDIF
    ELSE IF ex&=m_slider&   !Slider angeklickt
      IF anz_struc&>8
        ypos&=GRAF_SLIDEBOX(tree%,m_pbox&,m_slider&,1)
        topidx&=(ypos&+500/(anz_struc&-8))/1000*(anz_struc&-8)
        show_trees(topidx&,anz_struc&)
      ENDIF
    ELSE IF ex&=m_pbox&     !Parent von Slider angeklickt
      ~GRAF_MKSTATE(dummy&,ypos&,dummy&,dummy&)
      ~OBJC_OFFSET(tree%,m_slider&,dummy&,sy&)
      IF sy&<ypos&  !unterhalb vom Slider angeklickt
        topidx&=MIN(anz_struc&-8,topidx&+8)
      ELSE          !oberhalb
        topidx&=MAX(0,topidx&-8)
      ENDIF
      show_trees(topidx&,anz_struc&)
    ELSE IF ex&=m_load&     !RSC-File laden
      rsrc_laden
      deselect(m_load&,TRUE)
    ELSE IF ex&=m_save&     !RSC-File speichern
      IF rsc_load!
        rsrc_speichern
      ENDIF
      deselect(m_save&,TRUE)
    ELSE IF ex&=m_quit&     !PRG beenden
      ALERT 1,"QUIT ? ",2,"JA|NEIN",w&
      IF w&=2
        ex&=0
      ENDIF
      deselect(m_quit&,TRUE)
    ENDIF
  UNTIL ex&=m_quit&
  '
  IF rsc_load!    !wenn RSC geladen
    ~RSRC_FREE()    !Speicher freigeben
  ENDIF
  {global%+14}=prg_rsc_adr%   !Adresse und LÑnge der eigenen
  CARD{global%+18}=prg_rsc_len&  !RSC ins GLOBAL-Feld eintragen
  ~RSRC_FREE()                   !Speicher freigeben lassen
  RESERVE                        !GFA gets it back
RETURN
'
> PROCEDURE rsrc_speichern
  LOCAL backup$
  FILESELECT pfad$+"*.RSC",filename$,file$
  IF file$<>""
    filename_split(file$,pfad$,filename$,rawfile$)
    IF EXIST(file$)
      backup$=pfad$+rawfile$+".RSB"   !Backup-File
      IF EXIST(backup$)               !heiût "xxxxxx.RSB"
        KILL backup$
      ENDIF
      NAME file$ AS backup$
    ENDIF
    BSAVE file$,keyrsc_adr%,rsc_len&
    put_text(m_file&,filename$)       !neuen Filenamen
    ~OBJC_OFFSET(tree%,m_file&,x&,y&) !eintragen
    ~OBJC_DRAW(tree%,0,8,x&,y&,OB_W(tree%,m_file&),OB_H(tree%,m_file&))
  ENDIF
RETURN
> PROCEDURE filename_split(file$,VAR pfad$,filename$,rawfile$)
  LOCAL i&
  i&=RINSTR(file$,"\")       !'splittet' von FILESELECT
  pfad$=LEFT$(file$,i&)      !zurÅckgelieferte Datei in
  filename$=MID$(file$,i&+1) !Pfad und Dateinamen mit und
  i&=INSTR(filename$,".")    !ohne Extension
  IF i&
    rawfile$=LEFT$(filename$,i&-1)
  ELSE
    rawfile$=filename$
  ENDIF
RETURN
'
> PROCEDURE edit_tree(tidx&)            !Edieren eines Baums
  LOCAL n$,edtree%,keytree%,mx&,my&,but&,ks&
  LOCAL ox&,oy&,ow&,oh&,obj&
  ~RSRC_GADDR(0,tidx&,edtree%)  !Baumadresse der dargestellten Dial.box
  CLS
  tree%=edtree%
  keytree%=@get_keytree(tidx&)   !Baumadresse der edierten Dialogbox
  IF tree_name$(tidx&)<>""       !Name des Baums
    n$=tree_name$(tidx&)
  ELSE
    n$="[NONAME]"
  ENDIF
  PRINT AT(34,1);"TREE: ";n$
  ~FORM_CENTER(tree%,fo_cx&,fo_cy&,fo_cw&,fo_ch&)  !Box zeichnen
  ~OBJC_DRAW(tree%,0,8,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  '
  DO
    ~EVNT_BUTTON(1,1,1,mx&,my&,but&,ks&) !wartet auf Mausklick
    EXIT IF (but&=&X11) OR ks&    !Ausgang bei Druck auf beide Maustasten
    '                             !oder Mausklick mit SHIFT,CTRL oder ALT
    obj&=OBJC_FIND(tree%,0,8,mx&,my&)  !Obj unter Mauszeiger suchen
    '
    IF obj&>0     !Objekt gefunden (auûer Baumobjekt 0)
      ~OBJC_OFFSET(tree%,obj&,ox&,oy&)  !Ausmaûe des Objekts
      ow&=OB_W(tree%,obj&)              !berechnen
      oh&=OB_H(tree%,obj&)
      IF @selected(obj&)        !bereits selektiert?
        del_state(obj&,1)         !dann de-selektieren
      ELSE                      !ansonsten
        set_state(obj&,1)         !selektieren
      ENDIF
      ~OBJC_DRAW(tree%,0,8,ox&,oy&,ow&,oh&)  !Obj mit neuem STATE zeichnen
      ed_obj(tidx&,obj&,mx&,my&)             !und edieren
      tree%=edtree%                          !aktuelle Baumadresse neu setzen
      IF @selected(obj&)                     !und Obj wieder in altem
        del_state(obj&,1)                    !Zustand darstellen
      ELSE
        set_state(obj&,1)
      ENDIF
      ~OBJC_DRAW(tree%,0,8,ox&,oy&,ow&,oh&)
    ENDIF
    '
  LOOP
  '
  tree%=main_adr%    !aktuelle Baumadresse
  CLS
  ~FORM_CENTER(tree%,fo_cx&,fo_cy&,fo_cw&,fo_ch&)  !Auswahlbox zeichnen
  ~OBJC_DRAW(tree%,0,8,fo_cx&,fo_cy&,fo_cw&,fo_ch&)
  check_tree_modify(tidx&)    !sind im edierten Baum Tastenbel. gespeichert?
  show_trees(topidx&,anz_struc&)  !Ausschnitt neu darstellen
RETURN
> PROCEDURE ed_obj(tidx&,obj&,mx&,my&)  !Edieren eines Objekts
  LOCAL back$,drx&,dry&,drw&,drh&,maxx&,maxy&,key|,kstate|
  LOCAL mes&,kst&,t&,fund!,c.obj&
  tree%=k_choice_adr%   !Tastenauswahlbox aktuelle Baumadresse
  '
  drw&=OB_W(tree%,0)+4   !Koordinaten und Ausmaûe der Box
  drh&=OB_H(tree%,0)+4   !berechnen
  maxx&=hor_res&-drw&
  maxy&=vert_res&-drh&
  drx&=MIN(mx&,maxx&)
  dry&=MIN(my&,maxy&)
  OB_X(tree%,0)=drx&+1
  OB_Y(tree%,0)=dry&+1
  '
  IF ob_name$(tidx&,obj&)=""   !Namen des Objekts in Box eintragen
    put_string(c_obname&,"[NONAME]")
  ELSE
    put_string(c_obname&,ob_name$(tidx&,obj&))
  ENDIF
  '
  GET drx&,dry&,drx&+drw&,dry&+drh&,back$  !Hintergrund retten
  '
  IF BTST(OB_FLAGS(keytree%,obj&),1)   !Default-Objekt?
    show_key(0,0)                      !wenn ja, dann nicht edierbar
    put_text(c_key&,"DEFAULT-OBJ")
    ~OBJC_DRAW(tree%,0,8,drx&,dry&,drw&,drh&)
    ~EVNT_BUTTON(1,1,1)
  ELSE
    '
    key|=SHR&(OB_STATE(keytree%,obj&),8)      !bisherige Tastenbelegung
    kstate|=SHR&(OB_FLAGS(keytree%,obj&),13)  !des Objekts
    '
    show_key(key|,kstate|)   !in Box eintragen
    '
    ~OBJC_DRAW(tree%,0,8,drx&,dry&,drw&,drh&)  !Box zeichnen
    '
    c.obj&=0
    '
    REPEAT
      ' auf Tastendruck oder Mausklick warten
      mes&=EVNT_MULTI(&X11,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,mx&,my&,dummy&,kst&,t&,dummy&)
      '
      IF BTST(mes&,0)     !Taste gedrÅckt
        '
        IF BTST(kst&,0)      !zwischen rechter und linker
          kst&=BSET(kst&,1)  !SHIFT-Taste wird nicht
        ENDIF                !unterschieden
        kstate|=AND(SHR&(kst&,1),&X111) !SHIFT/CONTRL/ALT
        key|=SHR&(t&,8)      !Scan-Code der gedrÅckten Taste
        '
        CLR o&
        REPEAT        !prÅfen, ob Tastenbelegung bereits
          INC o&      !vorhanden ist
          fund!=(SHR&(OB_STATE(keytree%,o&),8)=key| AND SHR&(OB_FLAGS(keytree%,o&),13)=kstate| AND o&<>obj&)
          EXIT IF fund!
        UNTIL BTST(OB_FLAGS(keytree%,o&),5)
        '
        IF fund!
          ALERT 1,"Tastenzuordnung bereits|vorhanden!",1,"RETURN",dummy&
        ELSE IF key|=28 OR key|=114
          ALERT 1,"Kein RETURN/ENTER mîglich!",1,"OK",dummy&
        ELSE    !alles ok
          ' neue Tastenbelegung in Objekt-Struktur eintragen
          OB_STATE(keytree%,obj&)=OR(AND(OB_STATE(keytree%,obj&),&HFF),SHL&(key|,8))
          OB_FLAGS(keytree%,obj&)=OR(AND(OB_FLAGS(keytree%,obj&),&H1FF),SHL&(kstate|,13))
          '
          show_key(key|,kstate|)   !Tastenbelegung anzeigen
          ~OBJC_OFFSET(tree%,0,x&,y&)
          ~OBJC_DRAW(tree%,0,1,x&+1,y&+1,OB_W(tree%,0)-2,OB_H(tree%,0)-2)
          '
        ENDIF
      ELSE   !Maustaste gedrÅckt
        c.obj&=OBJC_FIND(tree%,0,1,mx&,my&) !Objekt unter Mauszeiger
        IF c.obj&=c_clear&    !war es der CLR-Button?
          ' Tastenbelegung des Objekts lîschen
          OB_STATE(keytree%,obj&)=AND(OB_STATE(keytree%,obj&),&HFF)
          OB_FLAGS(keytree%,obj&)=AND(OB_FLAGS(keytree%,obj&),&H1FF)
          show_key(0,0)   !Eintrag in Box lîschen
          draw_obj(c_shf&(0))  !und Objekte neu zeichnen
          draw_obj(c_shf&(1))
          draw_obj(c_shf&(2))
          draw_obj(c_key&)
        ENDIF
      ENDIF
      '
    UNTIL c.obj&=-1   !bis auûerhalb der Box geklickt wurde
  ENDIF
  '
  PUT drx&,dry&,back$  !Hintergrund wiederherstellen
  '
RETURN
> PROCEDURE show_key(k|,state|)         !Taste im Klartext darstellen
  LOCAL i&,k$
  '
  FOR i&=0 TO 2
    IF BTST(state|,i&)
      select(c_shf&(i&),FALSE)
    ELSE
      deselect(c_shf&(i&),FALSE)
    ENDIF
  NEXT i&
  '
  IF k|
    k$=""
    SELECT k|
    CASE 2 TO 13,16 TO 27,30 TO 41,43,44 TO 53,120 TO 131
      ADD k|,118*(k|>53)   !fÅr ALT + 1,2,3,...
      IF BTST(state|,0)    !SHIFT
        k$=CHR$(BYTE{shift_table%+k|})   !ASCII-Code aus SHIFT-Tabelle
      ELSE
        k$=CHR$(BYTE{unshift_table%+k|}) !ASCII-Code aus UNSHIFT-Tabelle
      ENDIF
    CASE 1
      k$="ESCAPE"
    CASE 14
      k$="BACK"
    CASE 15
      k$="TAB"
    CASE 28
      k$="RETURN"
    CASE 57
      k$="SPACE"
    CASE 59 TO 68,84 TO 93    !Funktionstasten
      k$="F"+STR$(k|-58+25*(k|>68))
      ' Cursorblock
    CASE 71,119
      k$="HOME"
    CASE 72
      k$=""
    CASE 75,115
      k$=""
    CASE 77,116
      k$=""
    CASE 80
      k$=""
    CASE 82
      k$="INSERT"
    CASE 83
      k$="DELETE"
    CASE 97
      k$="UNDO"
    CASE 98
      k$="HELP"
      ' Ziffernblock
    CASE 99 TO 113,74,78
      k$=CHR$(BYTE{unshift_table%+k|})+" Zi"
    CASE 114
      k$="ENTER"
    ENDSELECT
    put_text(c_key&,"Taste: "+k$)
  ELSE
    put_text(c_key&,"")
  ENDIF
  '
RETURN
'
> PROCEDURE reset_tree(tidx&)           !Tastenbelegungen eines Baums lîschen
  LOCAL obj&,adr%
  adr%=@get_keytree(tidx&)
  obj&=-1
  REPEAT
    INC obj&
    OB_STATE(adr%,obj&)=AND(OB_STATE(adr%,obj&),&HFF)
    OB_FLAGS(adr%,obj&)=AND(OB_FLAGS(adr%,obj&),&H1FF)
  UNTIL BTST(OB_FLAGS(adr%,obj&),5)   !bis LASTOB
  tree_modify!(tidx&)=FALSE   !Flag => Tree ist 'sauber'
RETURN
> PROCEDURE show_trees(idx&,anz&)       !BÑume namentlich ab idx& anzeigen
  LOCAL z&,n$,type|,x&,y&
  CLR z&
  '
  WHILE z&<8 AND z&+idx&<anz&
    type|=tree_type|(idx&+z&)
    IF tree_name$(idx&+z&)=""   !Name vorhanden?
      n$=trm$(type|)+"[NONAME]"   !nein, dann unbekannt
    ELSE
      n$=trm$(type|)+tree_name$(idx&+z&)  !sonst Åbernehmen
    ENDIF
    put_string(m_tr&(z&),n$)
    IF tree_type|(idx&+z&)<>3     !kein Dialog
      OB_STATE(tree%,m_tr&(z&))=8   !also DISABLED darstellen
      del_flag(m_tr&(z&),65)        !und nicht SELECTABLE/TOUCHEXIT
    ELSE
      IF tree_modify!(idx&+z&)    !Tastenbelegungen gespeichert?
        OB_STATE(tree%,m_tr&(z&))=4  !ja, dann CHECKED
      ELSE
        OB_STATE(tree%,m_tr&(z&))=0
      ENDIF
      set_flag(m_tr&(z&),65)      !SELECTABLE+TOUCHEXIT
    ENDIF
    INC z&
  WEND
  WHILE z&<8      !Rest des Fensters lîschen
    OB_STATE(tree%,m_tr&(z&))=0
    del_flag(m_tr&(z&),65)
    put_string(m_tr&(z&),"")
    INC z&
  WEND
  ' Slider-Position und -Grîûe bestimmen
  IF anz&<=8
    OB_Y(tree%,m_slider&)=0
    OB_H(tree%,m_slider&)=OB_H(tree%,m_pbox&)
  ELSE
    OB_Y(tree%,m_slider&)=OB_H(tree%,m_pbox&)*idx&/anz&
    OB_H(tree%,m_slider&)=OB_H(tree%,m_pbox&)*8/anz&
  ENDIF
  ' Slider und Fensterausschnitt mit Baumnamen neuzeichnen
  ~OBJC_OFFSET(tree%,m_trbox&,x&,y&)
  ~OBJC_DRAW(tree%,m_trbox&,1,x&+2,y&,OB_W(tree%,m_trbox&)-4,OB_H(tree%,m_trbox&))
  ~OBJC_OFFSET(tree%,m_pbox&,x&,y&)
  ~OBJC_DRAW(tree%,m_pbox&,1,x&,y&,OB_W(tree%,m_pbox&),OB_H(tree%,m_pbox&)-1)
  '
RETURN
> PROCEDURE check_all_structs           !manuelle Typzuweisung
  LOCAL obj&,t&,adr%,i&
  ' zunÑchst alle ObjektbÑume ÅberprÅfen
  CLR t&
  WHILE t&<anz_trees&
    ~RSRC_GADDR(0,t&,adr%)
    obj&=-1
    REPEAT
      INC obj&
      EXIT IF OB_TYPE(adr%,obj&)=32     !G_TITLE => MENU-TREE
      EXIT IF AND(OB_STATE(adr%,obj&),&HFF00)  !Tastenbelegung
    UNTIL BTST(OB_FLAGS(adr%,obj&),5)   !bis LASTOB
    tree_modify!(t&)=AND(OB_STATE(adr%,obj&),&HFF00)
    IF OB_TYPE(adr%,obj&)=32
      tree_type|(t&)=2    !MenÅ
    ELSE
      tree_type|(t&)=3    !Dialog
    ENDIF
    INC t&
  WEND
  ' jetzt die FREE STRINGS
  CLR i&
  WHILE i&<anz_frestr&
    ~RSRC_GADDR(15,i&,adr%)
    x$=LEFT$(CHAR{{adr%}},3)
    IF x$="[0]" OR x$="[1]" OR x$="[2]" OR x$="[3]"  !auf ALERT prÅfen
      tree_type|(t&+i&)=4   !ALERT
    ELSE
      tree_type|(t&+i&)=5   !sonstiger FREE STRING
    ENDIF
    INC i&
  WEND
  ' zum Schluû die FREE IMAGES
  WHILE i&<anz_freimg&
    tree_type|(t&+i&)=6
    INC i&
  WEND
  '
RETURN
> PROCEDURE check_tree_modify(tidx&)    !prÅft auf Tastenbelegungen in einem Baum
  LOCAL obj&,adr%
  '
  adr%=@get_keytree(tidx&)
  obj&=-1
  REPEAT
    INC obj&
    EXIT IF AND(OB_STATE(adr%,obj&),&HFF00)
  UNTIL BTST(OB_FLAGS(adr%,obj&),5)   !bis LASTOB
  tree_modify!(tidx&)=AND(OB_STATE(adr%,obj&),&HFF00)
RETURN
> PROCEDURE rsrc_laden                  !Laden eines Resource-Files
  LOCAL laenge%,i&,x&,y&
  FILESELECT pfad$+"*.RSC",filename$,file$
  IF file$<>""
    IF EXIST(file$)
      '
      filename_split(file$,pfad$,filename$,rawfile$)
      '
      IF RIGHT$(filename$,4)=".RSC"
        IF rsc_load!     !war schon eine Resource geladen?
          ~RSRC_FREE()     !ja, dann Speicher freigeben
          ERASE resource|()  !Duplikat lîschen
        ENDIF
        IF RSRC_LOAD(file$)=0  !laden
          ALERT 1,"RSC READ ERROR!",1,"RETURN",dummy&
          rsc_load!=FALSE
        ELSE
          rsc_load!=TRUE    !Flag fÅr geladene RSC setzen
          rsc_adr%={global%+14}    !RSC-Adresse und LÑnge
          rsc_len&=CARD{global%+18} !stehen im GEM-internen GLOBAL-Feld
          anz_trees&=CARD{rsc_adr%+22}  !wichtige Informationen aus
          anz_frestr&=CARD{rsc_adr%+30} !RSC-Header: Anzahl der ObjektbÑume,
          anz_freimg&=CARD{rsc_adr%+32} !Anzahl FREE STRINGS/FREE IMAGES
          anz_struc&=anz_trees&+anz_frestr&+anz_freimg&  !Gesamtzahl
          '
          ERASE tree_type|(),tree_modify!(),tree_name$(),ob_name$()
          ~FRE(0)  !erzwungene Garbage-Collection
          ' neue Dimensionierung
          DIM tree_type|(anz_struc&-1),tree_modify!(anz_trees&-1)
          DIM tree_name$(anz_struc&-1),ob_name$(anz_trees&-1,255)
          '
          OPEN "I",#1,file$    !Duplikat laden => die Tastenbelegungen werden
          laenge%=LOF(#1)      !nur im Duplikat vorgenommen, das erspart das
          DIM resource|(laenge%)  !Konvertieren von Adressen und Koordinaten
          keyrsc_adr%=V:resource|(0)  !vor dem Abspeichern
          BGET #1,keyrsc_adr%,laenge%
          CLOSE #1
          keyrsc_ttab%=CARD{keyrsc_adr%+18}+keyrsc_adr%  !Baum-Adreûtabelle
          '
          IF @get_dfn_file(pfad$+rawfile$)  !Definitionsfile laden
            FOR i&=0 TO anz_trees&-1   !erfolgreich, dann alle BÑume auf
              check_tree_modify(i&)    !Tastenbelegungen prÅfen
            NEXT i&
          ELSE                      !kein Definitionsfile vorhanden
            check_all_structs         !manuelle Typzuweisung
          ENDIF
          '
          topidx&=0                 !Index des ersten dargestellten Baums
          show_trees(topidx&,anz_struc&)  !BÑume namentlich darstellen
          '
          put_text(m_file&,filename$)  !Filenamen eintragen
          ~OBJC_OFFSET(tree%,m_file&,x&,y&)  !und darstellen
          ~OBJC_DRAW(tree%,0,8,x&,y&,OB_W(tree%,m_file&),OB_H(tree%,m_file&))
        ENDIF
      ELSE   !kein RSC-File
        ALERT 1,"NO RSC-FILE!",1,"CANCEL",dummy&
      ENDIF
    ELSE   !File nicht gefunden
      ALERT 1,"FILE NOT FOUND!",1,"RETURN",dummy&
    ENDIF
  ENDIF
RETURN
' get_keytree holt die Adresse eines zu edierenden Objektbaums
DEFFN get_keytree(idx&)={keyrsc_ttab%+4*idx&}+keyrsc_adr%
FUNCTION load_def_rsd(file$,dri!) !lÑdt DEF-Datei vom Kuma- oder DRI-RCS V1.x
  ' je nach Flag dri! wird entweder *.DEF vom DRI-RCS V1.x
  ' oder *.RSD vom Kuma-RCS geladen
  LOCAL adr%,type|,laenge%,anz&,i&,p%,offs&
  '
  OPEN "I",#1,file$
  '
  laenge%=LOF(#1)
  '
  anz&=CVI(INPUT$(2,#1))   !Anzahl der DatensÑtze im MOTOROLA-Format
  SEEK #1,0
  IF dri!     !beim DRI *.DEF-File gehîren die ersten beiden
    SUB laenge%,2   !Bytes nicht zum ersten Datensatz
  ENDIF
  IF anz&<>laenge%/16    !ein Datensatz belegt 16 Bytes
    CLOSE #1
    ALERT 1,"BAD FILE FORMAT!",1,"RETURN",dummy&
    RETURN FALSE
  ELSE
    DIM defbuf|(laenge%+1)            !letztes Byte muû Nullbyte sein
    adr%=V:defbuf|(0)
    BGET #1,adr%,laenge%   !Definitionsdatei laden
    CLOSE #1
    '
    CLR i&
    REPEAT
      p%=ADD(adr%,MUL(i&,16))
      type|=BYTE{ADD(p%,7)}
      IF BYTE{ADD(p%,6)}=0
        SELECT type|
        CASE 0 TO 3    !Unknown, Free-Tree, Dialog- oder MenÅ-Baum
          IF type|=1     !Free-Tree
            type|=3        !konvertieren zu Dialog
          ENDIF
          offs&=0
        CASE 4,5       !Alert oder Free String
          offs&=anz_trees&
        DEFAULT
          offs&=ADD(anz_trees&,anz_frestr&)
        ENDSELECT
        tree_name$(ADD(BYTE{ADD(p%,5)},offs&))=CHAR{ADD(p%,8)}
        tree_type|(ADD(BYTE{ADD(p%,5)},offs&))=type|
      ELSE
        ob_name$(BYTE{ADD(p%,4)},BYTE{ADD(p%,5)})=CHAR{ADD(p%,8)}
      ENDIF
      '
      INC i&
    UNTIL i&=anz&
    '
    ERASE defbuf|()
    RETURN TRUE
  ENDIF
ENDFUNC
FUNCTION load_dfn(file$)   !lÑdt DEF-Datei (*.DFN) vom DRI-RCS V2.x
  LOCAL adr%,a%,type|,subtype|,laenge%,anz&,i&,p%,offs&
  '
  OPEN "I",#1,file$
  '
  laenge%=LOF(#1)-2
  '
  a%=CVI(INPUT$(2,#1))   !Anzahl DatensÑtze im INTEL-Format
  anz&=SHL&(a%,8)+SHR&(a%,8)  !so wird's PC-kompatibel
  IF anz&<>laenge%/14    !1 Datensatz belegt 14 Bytes
    CLOSE #1
    ALERT 1,"BAD FILE FORMAT!",1,"RETURN",dummy&
    RETURN FALSE
  ELSE
    DIM defbuf|(laenge%)
    adr%=V:defbuf|(0)
    BGET #1,adr%,laenge%
    CLOSE #1
    '
    CLR i&
    REPEAT
      p%=ADD(adr%,MUL(i&,14))
      type|=BYTE{ADD(p%,2)}
      subtype|=BYTE{ADD(p%,3)}
      IF type|<>0 OR subtype|=0
        IF subtype|                     !Free String oder Free Image
          IF type|=1     !Free String
            type|=5        !konvertieren
          ELSE           !Free Image
            type|=6        !konvertieren
          ENDIF
        ENDIF
        SELECT type|
        CASE 0 TO 3                 !Unknown/Panel-/Dialog- oder MenÅ-Baum
          IF type|=1   !Panel
            type|=3      !wird konvertiert zu Dialog (3)
          ENDIF
          offs&=0
        CASE 4,5                      !Alert
          offs&=anz_trees&
        DEFAULT
          offs&=ADD(anz_trees&,anz_frestr&)
        ENDSELECT
        tree_name$(ADD(BYTE{p%},offs&))=CHAR{ADD(p%,4)}
        tree_type|(ADD(BYTE{p%},offs&))=type|
      ELSE
        ob_name$(BYTE{SUCC(p%)},BYTE{p%})=CHAR{ADD(p%,4)}
      ENDIF
      '
      INC i&
    UNTIL i&=anz&
    '
    ERASE defbuf|()
    RETURN TRUE
  ENDIF
ENDFUNC
FUNCTION get_dfn_file(file$)  !DEF-File laden von Kuma oder DRI (Vers.1+2)
  IF EXIST(file$+".DFN")
    RETURN @load_dfn(file$+".DFN")
  ELSE IF EXIST(file$+".DEF")
    RETURN @load_def_rsd(file$+".DEF",TRUE)
  ELSE IF EXIST(file$+".RSD")
    RETURN @load_def_rsd(file$+".RSD",FALSE)
  ELSE
    RETURN FALSE
  ENDIF
ENDFUNC
' nÅtzliche Prozeduren im Umgang mit Resourcen
> PROCEDURE draw_obj(obj&)
  LOCAL objdx&,objdy&
  ~OBJC_OFFSET(tree%,obj&,objdx&,objdy&)
  ~OBJC_DRAW(tree%,obj&,8,objdx&,objdy&,OB_W(tree%,obj&),OB_H(tree%,obj&))
RETURN
> PROCEDURE set_state(obj&,val&)
  OB_STATE(tree%,obj&)=OR(OB_STATE(tree%,obj&),val&)
RETURN
> PROCEDURE del_state(obj&,val&)
  OB_STATE(tree%,obj&)=AND(OB_STATE(tree%,obj&),NOT val&)
RETURN
> PROCEDURE select(obj&,redraw!)
  set_state(obj&,1)
  IF redraw!
    draw_obj(obj&)
  ENDIF
RETURN
> PROCEDURE deselect(obj&,redraw!)
  del_state(obj&,1)
  IF redraw!
    draw_obj(obj&)
  ENDIF
RETURN
DEFFN selected(obj&)=OB_STATE(tree%,obj&) AND 1
> PROCEDURE set_flag(obj&,val&)
  OB_FLAGS(tree%,obj&)=OR(OB_FLAGS(tree%,obj&),val&)
RETURN
> PROCEDURE del_flag(obj&,val&)
  OB_FLAGS(tree%,obj&)=AND(OB_FLAGS(tree%,obj&),NOT val&)
RETURN
> PROCEDURE put_string(obj&,str_txt$)
  CHAR{OB_SPEC(tree%,obj&)}=str_txt$
RETURN
DEFFN rsc_string$(obj&)=CHAR{OB_SPEC(tree%,obj&)}
> PROCEDURE put_text(obj&,txt$)  !G_TEXT schreiben
  CHAR{{OB_SPEC(tree%,obj&)}}=txt$
RETURN
DEFFN rsc_text$(obj&)=CHAR{{OB_SPEC(tree%,obj&)}}
