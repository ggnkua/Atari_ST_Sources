' Programm zum Žndern der Dateiattribute
' Filenamen ermitteln
Fileselect "A:\*.*","",F$
If (F$<>"") And (Exist(F$)) Then
  Print "File ";F$
  ' C-Format fr Filenamen herstellen
  F$=F$+Chr$(0)
  ' bestehende Atrribute ermitteln
  A%=Gemdos(&H43,L:Varptr(F$),0,0)
  ' Attribute anzeigen
  Gosub Anzeigen
  ' Žnderungen vornehmen?
  Print
  Print "Wollen Sie Žnderungen vornehmen? (j/n)"
  Z%=Inp(2)
  If Z%=Asc("j") Then
    ' Žnderungen durchfhren
    Print "Bitte die zum Attribut geh”rende Zahl drcken oder 0 fr Ende"
    Repeat
      Z%=Inp(2)-48
      If (Z%>0) And (Z%<5) Then
        ' zugeh”riges Bit in A% umdrehen
        Bit%=Sgn(A% And 2^(Z%-1))
        Bit%=(1-Bit%)*2^(Z%-1)
        A%=(A% And Not 2^(Z%-1))+Bit%
        ' neue Belegung anzeigen
        Gosub Anzeigen
      Endif
    Until Z%=0
    ' Attribute „ndern
    A%=Gemdos(&H43,L:Varptr(F$),-1,A%)
    If A%<0 Then
      Print "GEMDOS-Fehlermeldung: ";A%
      Stop
    Endif
  Endif
Endif
Procedure Anzeigen
  Print At(1,1)
  If (A% And 1)=1 Then
    Print "[1] Read-only "
  Else
    Print "[1] Read/Write"
  Endif
  If (A% And 2)=2 Then
    Print "[2] versteckt      "
  Else
    Print "[2] nicht versteckt"
  Endif
  If (A% And 4)=4 Then
    Print "[3] Systemfile"
  Else
    Print "[3] Normalfile"
  Endif
  If (A% And 8)=8 Then
    Print "[4] Volume-Label     "
  Else
    Print "[4] kein Volume-Label"
  Endif
Return
