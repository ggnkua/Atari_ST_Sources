' Programm zur Demonstration der Random-Access-Dateien
'
' Satzl„nge festlegen
Satzlaenge=20+20+20 ! Name, Straže, Wohnort
' Datei ”ffnen
Open "R",#1,"ADRESSEN.RAC",Satzlaenge
' Filefenster definieren (besteht aus Name$, Strasse$, Ort$)
Field #1,20 As Name$,20 As Strasse$,20 As Ort$
' Letzte Datensatznummer berechnen
Dateiende=Lof(#1)/Satzlaenge+1
' Men anbieten
Do
  Cls
  Print "W„hlen Sie bitte..."
  Print "1. Neue Adresse eingeben"
  Print "2. Adresse nach Namen suchen"
  Print "3. Adresse nach Satznummer suchen"
  Print "4. Programm beenden"
  Print "Auswahl (1 - 4)?"
  Repeat
    A=Inp(2)
    A=Val(Chr$(A))
  Until (A>0) And (A<5)
  If A<4 Then
    On A Gosub Neu,Suchen,Direkt
  Else
    Close #1
    End
  Endif
Loop
Procedure Neu
  Cls
  Print "Neue Adresse hat die Nummer ";Dateiende
  Print
  ' Daten von Tastatur einlesen
  Input "Name ";N$
  Input "Straže ";S$
  Input "Ort ";O$
  ' Daten in das Fenster setzen
  ' Wichtig: Schreiben in das Fenster nur ber Lset oder Rset!
  Lset Name$=N$
  Lset Strasse$=S$
  Lset Ort$=O$
  ' Fenster in Datei (an deren Ende) bertragen
  Put #1,Dateiende
  Inc Dateiende
Return
Procedure Suchen
  Cls
  Print "Adresse nach Namen suchen"
  Print
  Input "Name ";N$
  Print
  ' auf Dateianfang positionieren und ersten Satz lesen
  Satznr=1
  Get #1,1
  While (Left$(Name$,Len(N$))<>N$) And (Dateiende>Satznr)
    ' n„chsten Satz lesen
    Inc Satznr
    If Satznr<Dateiende Then
      ' Satz nur lesen, wenn Dateiende noch nicht erreicht
      Get #1
    Endif
  Wend
  If Satznr>=Dateiende Then
    ' am Fileende angekommen, Adresse nicht gefunden
    Print "Adresse nicht in der Datei"
  Else
    ' Satz gefunden, ausgeben
    Print "Adresse Nr. ";Satznr
    Print "Name  : ";Name$
    Print "Straže: ";Strasse$
    Print "Ort   : ";Ort$
  Endif
  ' auf Tastendruck warten
  Void Inp(2)
Return
Procedure Direkt
  Cls
  Print "Adresse nach Nummer suchen"
  Print
  Input "Nummer ";Nr
  Print
  ' Datensatz einlesen und ausgeben
  If Nr<Dateiende Then
    Get #1,Nr
    Print "Name  : ";Name$
    Print "Straže: ";Strasse$
    Print "Ort   : ";Ort$
  Endif
  ' auf Tastendruck warten
  Void Inp(2)
Return
