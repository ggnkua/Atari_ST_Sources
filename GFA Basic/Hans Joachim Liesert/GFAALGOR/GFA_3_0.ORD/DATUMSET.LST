' Programm zum Žndern von Uhrzeit und Datum eines Files
' Filenamen ermitteln
fs_iinpath$="A:\*.*"
fs_ireturn=FSEL_INPUT(fs_iinpath$,fs_insel$,fs_iexbutton)
IF fs_ireturn<>0 AND fs_iexbutton=1
  PRINT "File ";fs_insel$
  pathnr|=RINSTR(fs_iinpath$,"\")
  path$=LEFT$(fs_iinpath$,pathnr|)
  filename$=path$+fs_insel$
  ' File er”ffnen
  a%=GEMDOS(&H3D,L:VARPTR(filename$),0)
  IF a%<0 THEN
    PRINT "GEMDOS-Fehlermeldung ";a%
    STOP
  ELSE
    ' A% enth„lt das Handle der ge”ffneten Datei (wichtig!!!)
    h%=a%
  ENDIF
  ' Puffervariable fr Zeit/Datum bereitstellen
  buffer%=0
  ' Datum und Zeit aus der Datei holen
  VOID GEMDOS(&H57,L:VARPTR(buffer%),h%,0)
  ' Datum und Zeit dekodieren
  zeit%=buffer% DIV 65536
  datum%=buffer% MOD 65536
  tag%=datum% MOD 32
  datum%=datum% DIV 32
  monat%=datum% MOD 16
  jahr%=1980+(datum% DIV 16)
  sekunde%=(zeit% MOD 32)*2
  zeit%=zeit% DIV 32
  minute%=zeit% MOD 64
  stunde%=zeit% DIV 64
  ' Daten zur Ausgabe aufbereiten
  minute$=LEFT$(":00",3-LEN(STR$(minute%)))+STR$(minute%)
  sekunde$=LEFT$(":00",3-LEN(STR$(sekunde%)))+STR$(sekunde%)
  PRINT tag%;".";monat%;".";jahr%
  PRINT USING "##",stunde%;
  PRINT minute$;sekunde$
  PRINT "Wollen Sie Žnderungen vornehmen?"
  KEYGET ab|
  IF ab|=ASC("j") THEN
    ' Datum und Zeit „ndern
    INPUT "Tag   ";tag%
    INPUT "Monat ";monat%
    INPUT "Jahr  ";jahr%
    INPUT "Sekunde ";sekunde%
    INPUT "Minute  ";minute%
    INPUT "Stunde  ";stunde%
    ' Daten kodieren und schreiben
    zeit%=(sekunde% DIV 2)+minute%*32+stunde%*2048
    datum%=tag%+monat%*32+(jahr%-1980)*512
    buffer%=zeit%*65536+datum%
    VOID GEMDOS(&H57,L:VARPTR(buffer%),h%,1)
  ENDIF
  ' File wieder schliežen
  a%=GEMDOS(&H3E,h%)
  IF a%<0 THEN
    PRINT "GEMDOS-Fehlermeldung ";a%
    STOP
  ENDIF
ENDIF
'
