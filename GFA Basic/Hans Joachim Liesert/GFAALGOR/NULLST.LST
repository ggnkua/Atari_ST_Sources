Dim Koeff(100,2)
Input "Geben Sie bitte den Rang der Funktion ein: ",Rang
If Rang<2 Then
  Run
Endif
Print "Geben Sie nun die Funktion anhand der Koeffizienten ein."
Print "Hierbei bedeutet 'a3' der Koeffizient der Potenz X^3"
For I=Rang To 0 Step -1
  Print "a";I;"  = ";
  Input Koeff(I,0)
Next I
Repeat
  Rem Differenzieren der Funktion
  For I=Rang-1 To 0 Step -1
    Koeff(I,1)=Koeff(I+1,0)*(I+1)
  Next I
  Input "Startwert: ",Xn
  Rem Nullstellen raten mit dem Newton Verfahren
  Repeat
    Index=Index+1
    Gosub Funktion(Xn)
    Gosub Ableitung(Xn)
    Xn=Xa-Wert1/Wert2
    Diff=Abs(Xa-Xn)
    Xa=Xn
  Until Diff<1.0E-10 Or Index=1000
  If Index=1000 Then
    Print "Verfahren Konvergiert nicht. Wahrscheinlich keine Nullstellen"
    Void Inp(2)
    Run
  Else
    Index=0
  Endif
  Nullstelle=Xn
  Rem Krzen der Funktion mit dem Hornerschema
  For I=Rang To 0 Step -1
    Koeff(I,2)=Koeff(I,0)
  Next I
  For I=Rang-1 To 0 Step -1
    Koeff(I,2)=Koeff(I+1,2)*Nullstelle+Koeff(I,2)
    Koeff(I,2)=Int(Koeff(I,2)*10000+0.5)/10000
  Next I
  If Koeff(0,2)<>0 Then
    Print "Bitte versuchen Sie einen anderen Startwert."
    For I=Rang To 0 Step -1
      Koeff(I,2)=Koeff(I,0)
    Next I
  Else
    For I=Rang To 1 Step -1
      Koeff(I-1,0)=Koeff(I,2)
    Next I
    Rang=Rang-1
    Print "Nullstelle: ";Nullstelle
  Endif
Until Rang<=3        ! zur Vermeidung von Rundungsfehlern
If Rang=2 Then
  Rem Pqformel
  A=Koeff(2,0)
  B=Koeff(1,0)
  C=Koeff(0,0)
  Wurzel=(B^2)-4*A*C
  If Wurzel<0 Then
    Print "Gleichung hat keine weiteren reellen Nullstellen"
  Else
    X1=(-B-Sqr(Wurzel))/(2*A)
    X2=(-B+Sqr(Wurzel))/(2*A)
    Print "Nullstelle: ";X1
    Print "Nullstelle: ";X2
  Endif
Else
  Rem Cardanische Formel
  A=Koeff(3,0)
  B=Koeff(2,0)
  C=Koeff(1,0)
  D=Koeff(0,0)
  B=B/A
  C=C/A
  D=D/A
  A=1
  P=(3*C-B^2)/3
  Q=(2*B^3)/27-(B*C)/3+D
  W=(P/3)^3+(Q/2)^2
  If W>0 Then
    Print "Weitere Wurzeln sind Imagin„r"
  Else
    R=Sqr(-P^3/27)
    Phi=Pi*(-Q)/(360*R)
    S=Pi/2-Atn(Phi/Sqr(1-Phi^2))
    Y1=2*(R)^(1/3)*Cos(S/3)-B/3
    Y2=2*(R)^(1/3)*Cos(S/3+2*Pi/3)-B/3
    Y3=2*(R)^(1/3)*Cos(S/3+4*Pi/3)-B/3
    Print "Nullstelle: ";Y1
    Print "Nullstelle: ";Y2
    Print "Nullstelle: ";Y3
  Endif
Endif
Procedure Funktion(X)
  Wert1=0
  For I=Rang To 0 Step -1
    Wert1=Wert1+Koeff(I,0)*X^I
  Next I
Return
Procedure Ableitung(X)
  Wert2=0
  For I=Rang-1 To 0 Step -1
    Wert2=Wert2+Koeff(I,1)*X^I
  Next I
Return


