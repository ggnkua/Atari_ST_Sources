On break cont         ! nur fr die V2.0-Version (unbedingt n”tig)
Xt%=2-Sgn(Xbios(4))
Yt%=Min(2,3-Xbios(4))
Deftext ,17,,26/Yt%
Text 100/Xt%,200/Yt%,440/Xt%,"* STAR *"
Print At(26/Xt%,16);"Alle folgenden Demonstrationen"
Print At(26/Xt%,17);"verwenden im wesentlichen ein-"
Print At(26/Xt%,18);"und denselben Grafik-Befehl!"
Pause 200
@Screen(1)
@Screen(2)
Deffill ,2,2
For I%=0 To 180
Cls
@Star(2,320/Xt%,200/Yt%,100/Xt%,50/Yt%,I%,I%,320/Xt%,200/Yt%,200/Xt%,100/Yt%,I%,I%,8,1,I%+180)
@Star(2,320/Xt%,200/Yt%,100/Xt%,50/Yt%,I%,I%,320/Xt%,200/Yt%,20/Xt%,10/Yt%,I%,I%,8,1,360-I%)
@Star(1,320/Xt%,200/Yt%,100/Xt%,50/Yt%,I%,I%,320/Xt%,200/Yt%,200/Xt%,100/Yt%,I%,I%,8,1,180-I%)
@Star(1,320/Xt%,200/Yt%,100/Xt%,50/Yt%,I%,I%,320/Xt%,200/Yt%,20/Xt%,10/Yt%,I%,I%,8,1,I%)
@Screen(2)
Next I%
For I%=200 To 0 Step -1
Cls
@Star(1,320/Xt%,200/Yt%,(300-I%)/Xt%,(300-I%)/Yt%,I%,I%,320/Xt%,200/Yt%,(20+I%)/Xt%,(20+I%)/Yt%,0,0,16,0,I%)
@Screen(2)
Next I%
For I%=1 To 500 Step 3
Cls
J%=J%-I%
@Star(1,(500-I%)/Xt%,(300-I%*0.5)/Yt%,140/Xt%,140/Yt%,I%/30,I%/30,(600-I%)/Xt%,(200+Sin(I%/180)*60)/Yt%,30+I%/20,30+I%/20,I%/30,I%/30,4,0,J%/4)
@Screen(2)
Next I%
Cls
For I%=1 To 100
Cls
@Star(1,320/Xt%,200/Yt%,100/Xt%,50/Yt%,I%/5,I%/5,326/Xt%,206/Yt%,(10+I%)/Xt%,(5+I%)/Yt%,I%/5,I%/5,4,0,I%*4)
@Screen(2)
Next I%
Cls
Deffill ,2,4
For I%=-60 To 160 Step 4
Cls
@Star(1,(200+I%)/Xt%,(200-I%)/Yt%,(300-I%)/Xt%,(200-I%*1.2)/Yt%,0,0,(200+I%)/Xt%,(220-I%)/Yt%,(300-I%)/Xt%,(200-I%*1.2)/Yt%,0,0,4,0,I%/2)
@Screen(2)
Next I%
Cls
Deffill ,2,1
Pellipse 320/Xt%,300/Yt%,240/Xt%,70/Yt%
Deffill ,2,4
Pellipse 320/Xt%,300/Yt%,30/Xt%,12/Yt%
Sget Aa$
For I%=30 To 620 Step 8
Sput Aa$
If I%>30
Circle Px%(0),Py%(0),1
Circle Px%(3),Py%(3),1
Endif
Sget Aa$
Deffill ,2,4
@Star(2,320/Xt%,300/Yt%,240/Xt%,70/Yt%,0,0,320/Xt%,300/Yt%,30/Xt%,12/Yt%,0,0,6,1,I%)
@Star(2,320/Xt%,300/Yt%,240/Xt%,70/Yt%,0,0,320/Xt%,300/Yt%,30/Xt%,12/Yt%,0,0,6,1,I%+180)
Deffill ,2,3
@Star(1,320/Xt%,300/Yt%,240/Xt%,70/Yt%,0,0,320/Xt%,(80+I%/4)/Yt%,30/Xt%,12/Yt%,0,0,6,1,I%)
@Star(1,320/Xt%,300/Yt%,240/Xt%,70/Yt%,0,0,320/Xt%,(80+I%/4)/Yt%,30/Xt%,12/Yt%,0,0,6,1,I%+180)
Pellipse 320/Xt%,80/Yt%,30/Xt%,12/Yt%
@Screen(2)
Next I%
Bmove Xbios(2),Xbios(3),32000
Print "MOMENT, BITTE !"
@Screen(2)
Deffill ,2,8
Pbox 0,0,639/Xt%,399/Yt%
Dpoke Vdibase+34,0
Deffill ,2,4
For I%=1 To 360 Step 12
Add Ii,0.55
Deffill ,2,Abs(9-Ii)
@Star(2,500/Xt%,200/Yt%,40/Xt%,40/Yt%,0,0,600/Xt%,40/Yt%,20/Xt%,20/Yt%,0,0,30,1,I%+45)
Next I%
Ii=0
For I%=1 To 360 Step 12
Add Ii,0.4
Deffill ,2,Abs(8-Ii)
@Star(2,300/Xt%,140/Yt%,90/Xt%,90/Yt%,0,0,500/Xt%,200/Yt%,40/Xt%,40/Yt%,0,0,30,1,I%+80)
Next I%
Circle 300/Xt%,140/Yt%,90/Yt%
Ii=0
For I%=0 To 356 Step 12
Add Ii,0.33
Deffill ,2,Abs(-1-Ii) Mod 8
@Star(2,150/Xt%,280/Yt%,200/Xt%,200/Yt%,0,0,300/Xt%,140/Yt%,90/Xt%,90/Yt%,0,0,30,1,I%)
Next I%
Circle 150/Xt%,280/Yt%,200/Yt%
Dpoke Vdibase+34,1
Sget Aa$
For I%=1 To 166 Step 3
Cls
Sput Aa$
@Star(2,(150+I%)/Xt%,(280-I%/2)/Yt%,(160-I%)/Xt%,(140-I%)/Yt%,-30,-30,(266+I%/3)/Xt%,(210-I%/8)/Yt%,0,0,0,0,4,1,45)
@Screen(2)
Next I%
Cls
@Screen(2)
Cls
Pbox 10/Xt%,10/Yt%,629/Xt%,389/Yt%
Deffill ,0,0
For I%=1 To 7
Deffill ,2,I%
Pcircle 320/Xt%,200/Yt%,(200-I%*25)/Yt%
Next I%
Sget Aa$
@Screen(2)
For I%=1 To 640 Step 8
Cls
Sput Aa$
Deffill ,2,8
@Star(2,I%/Xt%,(200+Sin(I%/180)*120)/Yt%,200/Xt%,200/Yt%,22,22,I%/Xt%,(200+Sin(I%/180)*120)/Yt%,20/Xt%,20/Yt%,0,0,4,1,I%)
Deffill ,0,0
@Star(2,I%/Xt%,(206+Sin(I%/180)*120)/Yt%,200/Xt%,200/Yt%,22,22,I%/Xt%,(206+Sin(I%/180)*120)/Yt%,20/Xt%,20/Yt%,0,0,4,1,I%)
Deffill ,2,8
@Star(2,I%/Xt%,(200+Sin(I%/180)*120)/Yt%,180/Xt%,180/Yt%,40,40,I%/Xt%,(200+Sin(I%/180)*120)/Yt%,80/Xt%,80/Yt%,30,30,4,1,I%)
Deffill ,2,4
@Star(2,I%/Xt%,(210+Sin(I%/180)*120)/Yt%,180/Xt%,180/Yt%,40,40,I%/Xt%,(210+Sin(I%/180)*120)/Yt%,80/Xt%,80/Yt%,30,30,4,1,I%)
@Screen(2)
Next I%
Pause 10
Cls
Deffill ,2,8
Pbox 0,0,639/Xt%,399/Yt%
Deffill ,0,0
Pcircle 320/Xt%,200/Yt%,199/Yt%
Circle 320/Xt%,200/Yt%,195/Yt%
Circle 320/Xt%,200/Yt%,146/Yt%
Deffill ,3,9
Pcircle 320/Xt%,200/Yt%,140/Yt%
Deffill ,2,4
Pcircle 320/Xt%,200/Yt%,20/Yt%
Sget Aa$
For I%=0 To 360 Step 6
Repeat
Until (Timer-T)/200=>1
Sput Aa$
@Star(2,320/Xt%,200/Yt%,190/Xt%,190/Yt%,-2,9,320/Xt%,200/Yt%,160/Xt%,160/Yt%,-2,9,30,1,I%-90)
@Star(1,320/Xt%,200/Yt%,100/Xt%,100/Yt%,0,12,320/Xt%,200/Yt%,30/Xt%,30/Yt%,0,12,30,1,I%-90)
Print At(66/Xt%,2);"00:00:";Spc(2-Len(Str$(I%/6)));I%/6
If I% Mod 30=0
Pcircle Px%(1),Py%(1),6/Yt%
Graphmode 3
@Star(2,320/Xt%,200/Yt%,190/Xt%,190/Yt%,-2,9,320/Xt%,200/Yt%,160/Xt%,160/Yt%,-2,9,30,1,I%-90)
Graphmode 1
@Star(1,320/Xt%,200/Yt%,190/Xt%,190/Yt%,-2,9,320/Xt%,200/Yt%,160/Xt%,160/Yt%,-2,9,30,1,I%-90)
Sget Aa$
Endif
T=Timer
@Screen(2)
Next I%
Pause 100
@Screen(0)
Procedure Star(Md%,Xpa%,Ypa%,Xra%,Yra%,Oa1,Oa2,Xpi%,Ypi%,Xri%,Yri%,Oi1,Oi2,Eck%,Pkt%,Sw)
Local J%,Stp,I
Eck%=Min(126,Max(1,Abs(Eck%)))
If Pkt%=0
Pkt%=Eck%
Else
Pkt%=Min(Eck%,Abs(Pkt%))
Endif
Stp=360/(Eck%+1.0E-100)
Erase Px%()
Erase Py%()
Dim Px%(4),Py%(4)
For I=90-Sw To -270-Sw Step -Stp
Px%(0)=Xpi%+Sin((I-Oi1)*Pi/180)*Xri%
Py%(0)=Ypi%+Cos((I-Oi1)*Pi/180)*Yri%
Px%(1)=Xpa%+Sin((I-Oa1)*Pi/180)*Xra%
Py%(1)=Ypa%+Cos((I-Oa1)*Pi/180)*Yra%
Px%(2)=Xpa%+Sin((I-Stp+Oa2)*Pi/180)*Xra%
Py%(2)=Ypa%+Cos((I-Stp+Oa2)*Pi/180)*Yra%
Px%(3)=Xpi%+Sin((I-Stp+Oi2)*Pi/180)*Xri%
Py%(3)=Ypi%+Cos((I-Stp+Oi2)*Pi/180)*Yri%
Px%(4)=Px%(0)
Py%(4)=Py%(0)
If Md%=1
Polyline 5,Px%(),Py%()
Endif
If Md%=2
Polyfill 4,Px%(),Py%()
Endif
If Md%=3
Polymark 4,Px%(),Py%()
Endif
Inc J%
Exit if J%=Pkt%
Next I
Return
Procedure Screen(Flag%)
If Flag%=1
Dim L_adr%(8064)
Pbase%=Xbios(2)
Lbase%=Int(Varptr(L_adr%(0))/256+1)*256
Endif
If (Flag%=2) And Dim?(L_adr%())
Swap Lbase%,Pbase%
Void Xbios(5,L:Pbase%,L:Lbase%,-1)
Endif
If (Flag%=0) And Dim?(L_adr%())
Void Xbios(5,L:Max(Pbase%,Lbase%),L:Max(Pbase%,Lbase%),-1)
Erase L_adr%()
Endif
Return
