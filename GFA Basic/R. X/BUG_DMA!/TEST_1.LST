'
' *******************************************
' * Test d'‚criture d'un fichier de 3.2 Mos *
' *     (V‚rification des circuits DMA)     *
' *******************************************
'
Deflist 3
Defint "a-z"
'
Cls
Print "TESTEUR DE DISQUE .. VERIFIE SI LES DONNES SONT BIEN TRANSMISES"
Print "Cr‚er un fichier .."
Do
  Fileselect "\*.*","SAMPLE",Nom$
Loop until Not Exist(Nom$)
'
If Len(Nom$)>0
  Cls
  Print "Cr‚ation du fichier test .. "+Nom$
  '
  Open "O",#1,Nom$
  '
  Clr B$                        ! Bloc de test
  For A&=1 To 1000
    B$=B$+Chr$(Random(255))     ! Trame al‚atoire (rus‚ le STS!)
  Next A&
  A$=String$(32,B$)
  '
  Print "Ecriture sur le fichier test .."
  '
  For A&=1 To 100         ! Ecriture
    Print #1,A$;
    Print "Bloc #"+Str$(A&)+" / 100",Chr$(13);
  Next A&
  '
  Print "Fermeture du fichier"
  '
  Close #1
  '
  Print "2 Passes de lecture. Si il y a les mˆmes erreurs aux mˆmes blocs cela signifie"
  Print "qu'il y a eu une erreur d'‚criture"
  Print
  For T&=0 To 1
    Print "Lecture du fichier test .. PASSE "+Str$(T&+1)
    '
    Open "I",#1,Nom$
    '
    E&=0
    Clr C$
    For A&=1 To 100
      C$=Input$(32000,#1)
      If C$<>A$                 ! Gasp! C‚pƒlamˆmechose!!!!!!!!!!!!
        Print "Erreur!!!! DONNEEES ALTEREES au bloc #"+Str$(A&)
        Inc E&
      Endif
    Next A&
    '
    Close #1
    '
    Print "Fin du test. "+Str$(E&)+" erreurs de lecture sur des blocs de 32Kos"
  Next T&
  '
  Kill Nom$             ! On efface fichier test
  Print
  Print "Pressez ue touche."
  '
  While Inp?(2)
    ~Inp(2)
  Wend
  ~Inp(2)
  '
Endif
'
