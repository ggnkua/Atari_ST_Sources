'
' *** ANUCSOFT ***  Datenordnungsprogramm 2.00  *** (c) 12'89 W.Ante ***
'
' --------------------------------------------------------------------------
' Liest die Datei "Source$" ein, ordnet sie und schreibt sie geordnet in die
' Datei "Target$" zurck.   In der Variablen "Datenfelder%" liegt die Anzahl
' der Zeilen in der Datei "Source$",  bis sich der gleiche Datensatztyp wie-
' derholt (z.B. in der Datei COMPLETE.DOC liegen ein Songname, ein Casseten-
' name, ein Songname, ein Cassettenname, etc. - so ist:  Datenfelder% = 2 ).
' Schliežlich liegt in der Variablen "Sortierdatensatz%" die Nummer des Dat-
' ensatzes,  nach dem sortiert werden soll,   dabei beginnt die Nummerierung
' mit >1<. Das Programm verarbeitet Dateien mit max 1000 Datens„tzen, sollen
' mehr Datens„tze verwaltet werden, so muž die Variable "Md%" erh”ht werden.
' --------------------------------------------------------------------------
'
'
Source$="\GFABASIC\BASIC\PROBEDAT.DAT"
Target$="\GFABASIC\BASIC\PROBEDAT.ORD"
Datenfelder%=1
Sortierdatensatz%=1
Md%=1002
'
'
Dim Tabelle$(Md%,Datenfelder%-1)
Cls
Hidem
Print
Print "****************   Datensortierer 2.00"
Print "*** ANUCSOFT ***   (c) 12'89  Wolfgang Ante"
Print "****************   PUBLIC DOMAIN SOFTWARE"
Print
'
' --- DATEN LESEN
'
Print
Print "Daten werden aus Datei ";Source$;" gelesen."
Open "I",#1,Source$
Durchgaenge%=Lof(#1)/256
Rest%=Lof(#1) Mod 256
Datensaetze%=0
Feld%=0
For I%=1 To Durchgaenge%
  Daten$=Input$(256,#1)
  Gosub Analyse
Next I%
Daten$=Input$(Rest%,#1)
Gosub Analyse
Close #1
'
' DATEN SORTIEREN (Quicksort nach C.A.R. Hoare)
'
Print
Print "Daten werden intern nach QUICKSORT sortiert."
Print "Bitte haben Sie etwas Geduld..."
Gosub Quicksort(0,Datensaetze%-1)
'
' DATEN SCHREIBEN
'
Print
Print "Daten werden in Datei ";Target$;" geschrieben."
Open "O",#1,Target$
For I%=0 To Datensaetze%
  Print At(1,14);"Schreibe Datensatz Nummer ";I%;"."
  For J%=0 To Datenfelder%-1
    Print #1;Tabelle$(I%,J%)
  Next J%
Next I%
Close #1
'
' ENDE
'
Print
Print "Daten erfolgreich sortiert. Bitte drcken Sie ein Taste..."
Void Inp(2)
Edit
'
' PROCEDUREN
'
Procedure Analyse
  Ladestring$=Ladestring$+Daten$
  Repeat
    Position%=Instr(Ladestring$,Chr$(10))
    If Position%
      Tabelle$(Datensaetze%,Feld%)=Left$(Ladestring$,Position%-2)
      Ladestring$=Mid$(Ladestring$,Position%+1)
      Inc Feld%
      If Feld%>=Datenfelder%
        Feld%=0
        Inc Datensaetze%
      Endif
      Print At(1,8);"Lese Datensatz Nummer ";Datensaetze%;"."
    Endif
  Until Position%=0
Return
'
'
Procedure Quicksort(Anfang%,Ende%)
  Local Zl%,Zr%
  Zentrum%=Anfang%
  Zl%=Anfang%
  Zr%=Ende%
  Repeat
    If Tabelle$(Zl%,Sortierdatensatz%-1)>=Tabelle$(Zr%,Sortierdatensatz%-1)
      For I%=0 To Datenfelder%-1
        Swap Tabelle$(Zl%,I%),Tabelle$(Zr%,I%)
      Next I%
      Zentrum%=Zl%+Zr%-Zentrum%
    Endif
    If Zentrum%=Zl%
      Dec Zr%
    Else
      Inc Zl%
    Endif
  Until Zl%=Zr%
  If Anfang%<Zl%-1
    Gosub Quicksort(Anfang%,Zl%-1)
  Endif
  If Ende%>Zr%+1
    Gosub Quicksort(Zr%+1,Ende%)
  Endif
Return
'
'
