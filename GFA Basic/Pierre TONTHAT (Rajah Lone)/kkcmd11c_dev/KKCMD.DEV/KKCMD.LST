$m320000
DEFFLT "a-b"
' RESERVE
'
~FRE(0)
IF FRE()<160000
  ~FORM_ALERT(1,"[1][Insufficient or fragmented memory. ][ Quit ]")
  QUIT 0
ELSE
  order$=CHAR{ADD(BASEPAGE,128)}
  '
  init_start
  init_rsc
  @load_options
  init_end
  main
ENDIF
'
> PROCEDURE ask_leaving
  '
  IF misc_confirm_quit!
    proceed!=(@alert(1,18)=1)
  ELSE
    proceed!=TRUE
  ENDIF
  '
  IF proceed!
    validate
    leave
  ENDIF
RETURN
> PROCEDURE validate
  '
  '
RETURN
> PROCEDURE leave
  mouse_free
  '
  IF misc_save_configuration_at_leave!
    @save_options
  ENDIF
  close_all_windows
  '
  @record_close(1)
  @record_close(3)
  @record_close(4)
  @record_general_exit
  @gxalloc_main_exit(4)
  '
  mxfree(fslx_mem%)
  mxfree(raster_buf%)
  mxfree(photo_buf%)
  mxfree(m_adr%)
  mxfree(shell_buf%)
  mxfree(xattr_buf%)
  mxfree(remote_path%)
  mxfree(va_start%)
  mxfree(vtxt_buf%)
  '
  ~MENU_BAR(adtree%(0),0)
  ~RSRC_FREE()
  '
  v_clsvwk(vdi_handle&)
  ~APPL_EXIT()
  '
  QUIT 0
RETURN
'
> PROCEDURE init_start
  global%=LONG{ADD(GB,4)}
  '
  ap_id&=APPL_INIT()
  '
  vdi_handle&=@v_opnvwk
  '
  mouse_free
  '
  IF ap_id&=-1 OR vdi_handle&<1
    leave
  ENDIF
  '
  IF VAL(HEX$(INT{global%}))>=399
    naes!=TRUE
  ELSE
    naes!=FALSE
  ENDIF
  IF VAL(HEX$(INT{global%}))<340
    old_aes!=TRUE
  ELSE
    old_aes!=FALSE
  ENDIF
  '
  mx_alloc!=(GEMDOS(48)>=&H1900)
  mx_mask%=@mx_mask
  '
  magic!=@test_cookie("MagX",dummy%)
  mint!=@test_cookie("MiNT",dummy%)
  nvdi!=@test_cookie("NVDI",dummy%)
  '
  IF mint! OR (magic! AND ap_id&>0)
    multi!=TRUE
  ELSE
    multi!=FALSE
  ENDIF
  '
  IF multi!
    shl_write(9,1,0,"","")
  ENDIF
  '
  pdomain(1)
  '
  ~WIND_UPDATE(1)
  ~WIND_UPDATE(3)
  ~WIND_GET(0,4,screenx&,screeny&,screenl&,screenh&)
  '
  @declare_variables
  IF mint!=FALSE
    RESERVE 128000
  ENDIF
  @declare_allocations
  '
  IF @s_exist(kkcmd_path$+kkcmd_rsc$)=TRUE
    IF RSRC_LOAD(kkcmd_path$+kkcmd_rsc$)=0
      ~FORM_ALERT(1,"[1][ KKCMD.RSC couldn't be loaded. ][ Quit ]")
      leave
    ELSE
      FOR i&=0 TO nb_tree&
        ~RSRC_GADDR(0,i&,adtree%(i&))
      NEXT i&
      FOR i&=1 TO nb_tree&
        ~FORM_CENTER(adtree%(i&),xd&(i&),yd&(i&),dummy&,dummy&)
        ld&(i&)=OB_W(adtree%(i&),0)
        hd&(i&)=OB_H(adtree%(i&),0)
      NEXT i&
    ENDIF
  ELSE
    ~FORM_ALERT(1,"[1][ Can't find KKCMD.RSC| put it besides LITCHI.PRG |][ Quit ]")
    leave
  ENDIF
  '
  kkcmd_configuration_file$=kkcmd_path$+kkcmd_inf$
  '
  home_path$=@get_env$("HOME")
  '
  IF LEN(home_path$)>0
    IF @s_exist(home_path$+kkcmd_inf$) ! already in $HOME
      '
      kkcmd_configuration_file$=home_path$+kkcmd_inf$
      '
    ELSE IF @s_exist(kkcmd_path$+kkcmd_inf$) ! already in native directory
      '
      kkcmd_configuration_file$=kkcmd_path$+kkcmd_inf$
      '
    ELSE ! is the home directory is writable?
      '
      kkcmd_tst_file$=home_path$+"kkcmd.tst"+c0$
      dummy&=GEMDOS(60,L:V:kkcmd_tst_file$,W:0)
      IF dummy&>0
        ~GEMDOS(62,W:dummy&)
        ~GEMDOS(65,L:V:kkcmd_tst_file$)
        '
        kkcmd_configuration_file$=home_path$+kkcmd_inf$
        '
      ELSE
        '
        kkcmd_configuration_file$=kkcmd_path$+kkcmd_inf$
        '
      ENDIF
    ENDIF
  ENDIF
  '
RETURN
> PROCEDURE init_rsc
  '
  IF nb_plan&<4
    obj_deselect(5,9)
    OB_SPEC(adtree%(5),10)=SUCC(OB_SPEC(adtree%(5),10) AND &X110010000)
    OB_SPEC(adtree%(5),11)=SUCC(OB_SPEC(adtree%(5),11) AND &X110110000)
  ENDIF
  '
  IF screenh&<250
    INT{ADD(OB_SPEC(adtree%(3),2),6)}=8
  ENDIF
  obj_hide(3,4)
  '
  IF screenh&<250
    INT{ADD(OB_SPEC(adtree%(4),2),6)}=8
  ENDIF
  obj_hide(4,4)
  '
  field_bar_h&(3)=SUCC(OB_H(adtree%(3),0))
  field_bar_h&(4)=SUCC(OB_H(adtree%(4),0))
  '
  FOR i&=1 TO nb_tree&
    j&=OB_TAIL(adtree%(i&),OB_TAIL(adtree%(i&),0))
    IF j&=-1
      j&=OB_TAIL(adtree%(i&),0)
    ENDIF
    FOR k&=1 TO j&
      IF (OB_TYPE(adtree%(i&),k&)=30 OR OB_TYPE(adtree%(i&),k&)=29) AND BTST(OB_FLAGS(adtree%(i&),k&),3)
        CHAR{{OB_SPEC(adtree%(i&),k&)}}=""
      ENDIF
      IF NOT magic!
        IF OB_TYPE(adtree%(i&),k&)=26 AND BTST(OB_STATE(adtree%(i&),k&),15)=TRUE AND BTST(OB_STATE(adtree%(i&),k&),6)=TRUE AND BTST(OB_FLAGS(adtree%(i&),k&),9)=FALSE
          CHAR{OB_SPEC(adtree%(i&),k&)}=""
          OB_FLAGS(adtree%(i&),k&)=BSET(OB_FLAGS(adtree%(i&),k&),9)
        ENDIF
      ENDIF
    NEXT k&
  NEXT i&
  '
  OB_Y(adtree%(8),display_subdialog&(0))=SUCC(OB_Y(adtree%(8),display_subdialog&(0)))
  FOR i&=0 TO 2
    IF i&>0
      OB_Y(adtree%(8),display_subdialog&(i&))=OB_Y(adtree%(8),display_subdialog&(0))
    ENDIF
    OB_SPEC(adtree%(8),display_subdialog&(i&))=OB_SPEC(adtree%(8),display_subdialog&(i&)) AND &X1111111111111111
  NEXT i&
  OB_H(adtree%(8),0)=ADD(ADD(OB_H(adtree%(8),display_subdialog&(0)),OB_Y(adtree%(8),display_subdialog&(0))),8)
  hd&(8)=OB_H(adtree%(8),0)
  display_hide_subdialogs
  obj_select(8,1)
  obj_unhide(8,display_subdialog&(0))
  '
  IF screenh&<250
    obj_disable(8,19)
  ENDIF
  '
  OB_Y(adtree%(9),misc_subdialog&(0))=SUCC(OB_Y(adtree%(9),misc_subdialog&(0)))
  FOR i&=0 TO 2
    IF i&>0
      OB_Y(adtree%(9),misc_subdialog&(i&))=OB_Y(adtree%(9),misc_subdialog&(0))
    ENDIF
    OB_SPEC(adtree%(9),misc_subdialog&(i&))=OB_SPEC(adtree%(9),misc_subdialog&(i&)) AND &X1111111111111111
  NEXT i&
  OB_H(adtree%(9),0)=ADD(ADD(OB_H(adtree%(9),misc_subdialog&(0)),OB_Y(adtree%(9),misc_subdialog&(0))),8)
  hd&(9)=OB_H(adtree%(9),0)
  misc_hide_subdialogs
  obj_select(9,1)
  obj_unhide(9,misc_subdialog&(0))
  '
  FOR i&=0 TO 10
    CHAR{{OB_SPEC(adtree%(15),ADD(3,i&))}}=""
  NEXT i&
  '
  str_files$=CHAR{OB_SPEC(adtree%(alert_tree&),14)}
  str_octets$=CHAR{OB_SPEC(adtree%(alert_tree&),15)}
  str_folders$=CHAR{OB_SPEC(adtree%(alert_tree&),16)}
  str_bytes$=CHAR{OB_SPEC(adtree%(alert_tree&),9)}
  str_kilobytes$=CHAR{OB_SPEC(adtree%(alert_tree&),10)}
  str_megabytes$=CHAR{OB_SPEC(adtree%(alert_tree&),11)}
  str_gigabytes$=CHAR{OB_SPEC(adtree%(alert_tree&),12)}
  '
  FOR i&=1 TO 2
    OB_X(adtree%(nb_tree&),i&)=0
    OB_Y(adtree%(nb_tree&),i&)=0
  NEXT i&
  '
RETURN
> PROCEDURE declare_variables
  '
  crlf$=CHR$(13)+CHR$(10)
  '
  DIM boolean$(1)
  boolean$(0)="FALSE"
  boolean$(1)="TRUE"
  '
  home_path$=SPACE$(256)
  '
  c0$=CHR$(0)
  mask_all$="*.*"+c0$
  '
  kkcmd_path$=CHR$(ADD(GEMDOS(25),65))+":"+DIR$(SUCC(GEMDOS(25)))+"\"
  IF mint!
    kkcmd_prg$="kkcmd.prg"+c0$
    kkcmd_rsc$="kkcmd.rsc"+c0$
    kkcmd_inf$="kkcmd.cfg"+c0$
  ELSE
    kkcmd_prg$="KKCMD.PRG"+c0$
    kkcmd_rsc$="KKCMD.RSC"+c0$
    kkcmd_inf$="KKCMD.CFG"+c0$
  ENDIF
  kkcmd_configuration_file$=SPACE$(256)
  kkcmd_register_file$=SPACE$(256)
  '
  preferences_text_size&=100
  DIM preferences_text$(preferences_text_size&)
  FOR i&=0 TO PRED(preferences_text_size&)
    preferences_text$(i&)=SPACE$(128)
  NEXT i&
  '
  fi_path$=SPACE$(512)
  fi_name$=SPACE$(128)
  '
  nb_tree&=18
  nb_win&=9
  alert_tree&=11
  '
  DIM bubble_str%(nb_win&,64)
  '
  DIM adtree%(nb_tree&),xd&(nb_tree&),yd&(nb_tree&),ld&(nb_tree&),hd&(nb_tree&)
  DIM hand_win&(nb_tree&),wx&(nb_tree&),wy&(nb_tree&),wl&(nb_tree&),wh&(nb_tree&)
  DIM pwx&(nb_tree&),pwy&(nb_tree&),pwl&(nb_tree&),pwh&(nb_tree&),win_pos$(nb_tree&),ope!(nb_tree&)
  '
  icon!=FALSE
  DIM win!(nb_tree&),aff!(nb_tree&),ful!(nb_tree&),forced!(nb_tree&)
  FOR i&=0 TO nb_tree&
    win!(i&)=FALSE
    aff!(i&)=FALSE
    ful!(i&)=FALSE
    ope!(i&)=FALSE
  NEXT i&
  '
  DIM cp_win&(nb_tree&)
  FOR i&=1 TO nb_win&
    cp_win&(i&)=&X1011
  NEXT i&
  FOR i&=3 TO 4
    cp_win&(i&)=&X111111111101
  NEXT i&
  '
  DIM font_files_real_height&(nb_win&),display_disk_icons!(nb_win&)
  '
  local_path_left$=SPACE$(256)
  local_path_right$=SPACE$(256)
  '
  loc_drive_nb&=0
  DIM loc_drive$(26),loc_drive&(26)
  DIM local_drive_index&(nb_win&)
  '
  FOR i&=0 TO 26
    loc_drive$(i&)=SPACE$(4)
    loc_drive$(i&)=SPACE$(4)
  NEXT i&
  '
  DIM shortcuts$(10)
  FOR i&=0 TO 10
    shortcuts$(i&)=SPACE$(256)
  NEXT i&
  '
  DIM display_subdialog&(2)
  '
  display_subdialog&(0)=3
  display_subdialog&(1)=20
  '
  display_win_pos_type&=1
  display_win_pos_save!=FALSE
  display_disk_b!=TRUE
  display_sort_discard_hidden_files!=FALSE
  display_files_with_date!=TRUE
  display_files_date_format&=1
  display_files_with_length!=TRUE
  display_files_length_format&=1
  display_files_length_unity$=SPACE$(10)
  display_files_font_size&=0
  '
  display_sort_type&=1
  display_sort_distinguish_case!=TRUE
  display_sort_folder_at_top!=TRUE
  display_sort_reverse!=FALSE
  '
  DIM misc_subdialog&(2)
  '
  misc_subdialog&(0)=4
  misc_subdialog&(1)=7
  misc_subdialog&(2)=12
  '
  misc_keep_date!=TRUE
  misc_save_configuration_at_leave!=FALSE
  misc_verify_free_space!=TRUE
  '
  misc_choice_existing_file_type&=0
  '
  misc_confirm_quit!=TRUE
  misc_confirm_send!=TRUE
  misc_confirm_delete!=TRUE
  '
  base_extension$=".KDB"+c0$
  back_extension$=".BAK"+c0$
  '
  DIM pop_up_value&(22)
  pop_up_start&=0
  pop_up_height&=20
  pop_up_max&=0
  '
  DIM minor|(256)
  FOR i&=0 TO 255
    minor|(i&)=i&
  NEXT i&
  FOR i&=65 TO 90
    minor|(i&)=ADD(i&,32)
  NEXT i&
  minor|(128)=135
  minor|(142)=132
  minor|(143)=134
  minor|(144)=130
  minor|(146)=145
  minor|(153)=148
  minor|(154)=129
  minor|(165)=164
  minor|(178)=179
  minor|(181)=180
  minor|(182)=133
  minor|(183)=176
  minor|(184)=177
  minor|(193)=192
  '
  DIM major|(256)
  FOR i&=0 TO 255
    major|(i&)=i&
  NEXT i&
  FOR i&=97 TO 122
    major|(i&)=SUB(i&,32)
  NEXT i&
  major|(128)=67
  major|(129)=85
  major|(130)=69
  major|(131)=65
  major|(132)=65
  major|(133)=65
  major|(134)=65
  major|(135)=67
  major|(136)=69
  major|(137)=69
  major|(138)=69
  major|(139)=73
  major|(140)=73
  major|(141)=73
  major|(142)=65
  major|(143)=65
  major|(144)=69
  major|(145)=146
  major|(147)=79
  major|(148)=79
  major|(149)=79
  major|(150)=85
  major|(151)=85
  major|(152)=89
  major|(153)=79
  major|(154)=85
  major|(160)=65
  major|(161)=73
  major|(162)=79
  major|(163)=85
  major|(164)=78
  major|(165)=78
  major|(166)=65
  major|(167)=79
  major|(176)=65
  major|(177)=79
  major|(178)=79
  major|(179)=79
  major|(180)=181
  major|(182)=65
  major|(183)=65
  major|(184)=79
  '
RETURN
> PROCEDURE declare_allocations
  '
  FOR i&=0 TO 10
    shortcuts$(i&)=c0$
  NEXT i&
  '
  @gxalloc_main_init(4)
  @record_general_init(4)
  @record_open(1,16,324,0)
  @record_open(3,64,142,7)
  @record_open(4,64,142,7)
  '
  IF magic! OR mint!
    m_adr%=@mxalloc(32,3)
    bubble_buf%=@mxalloc_global(256,3)
    shell_buf%=@mxalloc_global(256,3)
    xattr_buf%=@mxalloc(64,0)
    vtxt_buf%=@mxalloc(256,0)
    local_path0%=@mxalloc(640,0)
    local_path1%=ADD(local_path0%,256)
    local_info0%=ADD(local_path1%,256)
    local_info1%=ADD(local_info0%,64)
  ELSE
    m_adr%=@mxalloc(32,3)
    bubble_buf%=@mxalloc(256,3)
    shell_buf%=@mxalloc(256,3)
    xattr_buf%=0
    vtxt_buf%=@mxalloc(256,0)
    local_path0%=@mxalloc(512,0)
    local_path1%=ADD(local_path0%,256)
    local_info0%=ADD(local_path1%,256)
    local_info1%=ADD(local_info0%,64)
  ENDIF
  '
  fslx_init
  '
  va_start%=@mxalloc(512,3)
  '
  IF m_adr%=<0
    leave
  ELSE
    ABSOLUTE m_type&,m_adr%
    ABSOLUTE m_ap_id&,ADD(m_adr%,2)
    ABSOLUTE m_title&,ADD(m_adr%,6)
    ABSOLUTE m_win&,ADD(m_adr%,6)
    ABSOLUTE m_item&,ADD(m_adr%,8)
    ABSOLUTE m_x&,ADD(m_adr%,8)
    ABSOLUTE m_y&,ADD(m_adr%,10)
    ABSOLUTE m_l&,ADD(m_adr%,12)
    ABSOLUTE m_h&,ADD(m_adr%,14)
    '
    s_adr%=ADD(m_adr%,16)
  ENDIF
  '
  INT{ADD(CONTRL,12)}=vdi_handle&
  INT{INTIN}=1
  VDISYS 102,1,0
  nb_plan&=INT{ADD(INTOUT,8)}
  IF nb_plan&=0
    nb_plan&=ROUND(LOG(WORK_OUT(13))/LOG(2))
  ENDIF
  '
  photo_buf%=0
  raster_buf%=@mxalloc(128,3)
  IF raster_buf%>0
    '
    photo_buf_old_length%=MUL(8000,nb_plan&)
    photo_buf%=@mxalloc(photo_buf_old_length%,3)
    IF photo_buf%<1
      leave
    ENDIF
    '
    pscrmfdb%=raster_buf%
    pdesmfdb%=ADD(pscrmfdb%,20)
    pxyarray%=ADD(pdesmfdb%,20)
    '
    make_zero_mfdb(pscrmfdb%)
    make_zero_mfdb(pdesmfdb%)
    '
    LONG{pdesmfdb%}=photo_buf%
    WORD{ADD(pdesmfdb%,4)}=320
    WORD{ADD(pdesmfdb%,6)}=200
    WORD{ADD(pdesmfdb%,8)}=20
    WORD{ADD(pdesmfdb%,12)}=nb_plan&
    '
  ENDIF
  '
RETURN
> PROCEDURE init_end
  '
  vsf_interior(1)
  vsf_color(0)
  vsf_style(0)
  vsf_perimeter(0)
  '
  vst_alignment(0,5)
  '
  ~WIND_UPDATE(2)
  ~WIND_UPDATE(0)
  '
  ~MENU_BAR(adtree%(0),1)
  IF multi!=FALSE
    ~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
  ENDIF
  '
  win(4)
  win(3)
  '
  IF display_disk_icons!(4)
    @local_get_disks_list(4)
  ELSE
    @local_get_directory(4)
  ENDIF
  IF display_disk_icons!(3)
    @local_get_disks_list(3)
  ELSE
    @local_get_directory(3)
  ENDIF
  '
RETURN
'
> PROCEDURE main
  DO
    evnt&=@ev_multi(&X110011,258,3,0,100,mo_x&,mo_y&,mo_k&,m_touche&,m_clavier&,mo_c&)
    IF BTST(evnt&,0)
      manage_keys_general
    ENDIF
    IF BTST(evnt&,1)
      manage_mouse_general
    ENDIF
    IF BTST(evnt&,4)
      SELECT m_type&
      CASE 10
        manage_menu
      CASE 20
        redraw
      CASE 21
        win_topped
      CASE 29
        win_ontop
      CASE 30
      CASE 31
      CASE 33
        win_untopped
      CASE 22
        win_closed
      CASE 23
        win_fulled
      CASE 24
        win_arrowed
      CASE 25
        IF m_win&=@get_top_window
          win_hslided
        ELSE
          FOR i&=1 TO 4
            IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
              force_top(i&)
            ENDIF
          NEXT i&
        ENDIF
      CASE 26
        IF m_win&=@get_top_window
          win_vslided
        ELSE
          FOR i&=1 TO 4
            IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
              force_top(i&)
            ENDIF
          NEXT i&
        ENDIF
      CASE 28
        win_moved
      CASE 27
        win_sized
      CASE 50
        shut_down
      CASE 22360
        FOR i&=1 TO nb_win&
          IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
            aff!(i&)=FALSE
          ENDIF
        NEXT i&
      CASE 22361
        FOR i&=1 TO nb_win&
          IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
            aff!(i&)=TRUE
          ENDIF
        NEXT i&
      ENDSELECT
      clear_m
    ENDIF
    IF BTST(evnt&,5)
      INC cpt_garbage&
      IF cpt_garbage&>300
        ~FRE()
        ~FRE(0)
        cpt_garbage&=0
      ENDIF
    ENDIF
    '
  LOOP
RETURN
> PROCEDURE clear_m
  IF m_adr%>0
    LONG{m_adr%}=0
    LONG{ADD(m_adr%,4)}=0
    LONG{ADD(m_adr%,8)}=0
    LONG{ADD(m_adr%,12)}=0
  ENDIF
RETURN
> PROCEDURE clear_keys
  m_clavier&=0
  m_touche&=0
RETURN
> PROCEDURE clear_mouse
  mo_k&=0
  mo_c&=0
RETURN
> PROCEDURE test_evnt
  evnt&=@ev_multi(&X110000,2,1,1,30,mo_x&,mo_y&,mo_k&,m_touche&,m_clavier&,mo_c&)
  IF BTST(evnt&,4)
    SELECT m_type&
    CASE 20
      redraw
    CASE 21
      win_topped
    CASE 22
      win_closed
    CASE 28
      win_moved
    ENDSELECT
  ENDIF
RETURN
'
> PROCEDURE manage_menu
  ~MENU_TNORMAL(adtree%(0),m_title&,1)
  SELECT m_item&
  CASE 8 ! info
    dialog_about
  CASE 17 ! shortcuts
    win(2)
  CASE 19 ! quit
    ask_leaving
  CASE 21 ! display
    win(8)
  CASE 22 ! misc
    win(9)
  CASE 24 ! save prefs
    @save_options
  ENDSELECT
RETURN
> PROCEDURE manage_mouse_general
  IF mo_c&=1 AND mo_k&=1
    clic_win&=WIND_FIND(mo_x&,mo_y&)
    IF mint!=FALSE
      delay
    ENDIF
    manage_mouse_on_dials
  ELSE IF mo_c&=2 AND mo_k&=1
    clic_win&=WIND_FIND(mo_x&,mo_y&)
    manage_mouse_on_wins(0)
  ELSE IF mo_c&=1 AND mo_k&=2
    clic_win&=WIND_FIND(mo_x&,mo_y&)
    '
    IF clic_win&=hand_win&(3) AND aff!(3)=TRUE
      kk_win&=3
    ELSE IF clic_win&=hand_win&(4) AND aff!(4)=TRUE
      kk_win&=4
    ENDIF
    '
    IF kk_win&=3 OR kk_win&=4
      '
      IF record_nb_selected&(kk_win&)=0
        dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
        IF dummy&>-1
          record_select(kk_win&,dummy&)
          record_selected&(kk_win&)=dummy&
          record_nb_selected&(kk_win&)=1
          '
          get_record_xywh(kk_win&,record_selected&(kk_win&),sx&,sy&,sl&,sh&)
          force_update_xywh(kk_win&,record_selected&(kk_win&),sx&,sy&,sl&,sh&)
        ENDIF
      ENDIF
      '
      IF display_disk_icons!(kk_win&)
        '
        obj_enable(17,1)
        obj_disable(17,2)
        obj_disable(17,3)
        obj_disable(17,5)
        obj_disable(17,7)
        obj_disable(17,9)
        '
        IF record_nb_selected&(kk_win&)=1 AND record_selected&(kk_win&)>-1
          obj_enable(17,2)
          IF (kk_win&=3 AND display_disk_icons!(4)=FALSE) OR (kk_win&=4 AND display_disk_icons!(3)=FALSE)
            obj_enable(17,5)
            obj_enable(17,7)
          ENDIF
        ENDIF
        '
      ELSE
        obj_disable(17,5)
        obj_disable(17,7)
        obj_disable(17,9)
        '
        IF record_nb_selected&(kk_win&)>0
          IF record_nb_selected&(kk_win&)=1 AND record_selected&(kk_win&)=0
          ELSE
            obj_enable(17,5)
            obj_enable(17,7)
            obj_enable(17,9)
          ENDIF
        ENDIF
        '
        IF record_nb_selected&(kk_win&)=1 AND record_selected&(kk_win&)>0
          obj_enable(17,2)
        ELSE
          obj_disable(17,2)
        ENDIF
        '
      ENDIF
      '
      SELECT @pop_up(kk_win&,3,17,TRUE)
      CASE 0 ! refresh
        IF display_disk_icons!(kk_win&)=FALSE
          @local_get_directory(kk_win&)
        ENDIF
      CASE 1 ! informations
        IF display_disk_icons!(kk_win&)=FALSE
          @dialog_informations(kk_win&,record_selected&(kk_win&))
        ENDIF
      CASE 2 ! new folder
        IF display_disk_icons!(kk_win&)=FALSE
          @dialog_new_folder(kk_win&)
        ENDIF
      CASE 4 ! copy
        IF (kk_win&=3 AND display_disk_icons!(4)=FALSE) OR (kk_win&=4 AND display_disk_icons!(3)=FALSE)
          @local_copy_files(kk_win&,FALSE)
        ENDIF
      CASE 6 ! move
        IF (kk_win&=3 AND display_disk_icons!(4)=FALSE) OR (kk_win&=4 AND display_disk_icons!(3)=FALSE)
          @local_copy_files(kk_win&,TRUE)
        ENDIF
      CASE 8 ! delete
        IF display_disk_icons!(kk_win&)=FALSE
          @local_delete_files(kk_win&)
        ENDIF
      ENDSELECT
    ENDIF
  ENDIF
RETURN
> PROCEDURE manage_mouse_on_dials
  IF clic_win&=hand_win&(2) AND aff!(2)=TRUE
    dialog_shortcuts
  ELSE IF clic_win&=hand_win&(3) AND aff!(3)=TRUE
    dialog_local(3)
  ELSE IF clic_win&=hand_win&(4) AND aff!(4)=TRUE
    dialog_local(4)
  ELSE IF clic_win&=hand_win&(8) AND aff!(8)=TRUE
    dialog_display
  ELSE IF clic_win&=hand_win&(9) AND aff!(9)=TRUE
    dialog_misc
  ENDIF
RETURN
> PROCEDURE manage_mouse_on_wins(kc_win&)
  LOCAL double_click_selected&,dc_win&,dc_darkened!
  '
  IF kc_win&>0
    dc_win&=kc_win&
  ELSE IF clic_win&=hand_win&(3) AND aff!(3)=TRUE
    dc_win&=3
  ELSE IF clic_win&=hand_win&(4) AND aff!(4)=TRUE
    dc_win&=4
  ELSE
    dc_win&=0
  ENDIF
  '
  IF dc_win&=3 OR dc_win&=4
    IF kc_win&>0
      dummy&=record_selected&(kc_win&)
    ELSE
      dummy&=@get_record_under_cursor(dc_win&,mo_y&,mo_x&)
    ENDIF
    IF dummy&>-1
      '
      LET double_click_selected&=dummy&
      '
      SELECT @loc_get_type(@px(dc_win&,double_click_selected&))
      CASE 0
        IF kc_win&=0
          IF @is_record_selected(dc_win&,double_click_selected&)
            record_select(dc_win&,double_click_selected&)
          ENDIF
          get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
        ENDIF
        '
        IF @loc_get_name$(@px(dc_win&,double_click_selected&))=".."
          @local_change_directory(dc_win&,0)
        ENDIF
        '
      CASE 2
        IF kc_win&=0
          IF @is_record_selected(dc_win&,double_click_selected&)
            record_select(dc_win&,double_click_selected&)
          ENDIF
          get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
        ENDIF
        '
        @local_change_directory(dc_win&,@px(dc_win&,double_click_selected&))
        '
      CASE 1
        '
        IF kc_win&=0 AND @is_record_selected(dc_win&,double_click_selected&)=FALSE
          dc_darkened!=TRUE
          record_select(dc_win&,double_click_selected&)
          get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
        ELSE
          dc_darkened!=FALSE
        ENDIF
        '
        txt$=@loc_get_name$(@px(dc_win&,double_click_selected&))
        ext$=UPPER$(RIGHT$(txt$,4))
        IF ext$=".PRG" OR ext$=".TOS" OR ext$=".TTP" OR ext$=".APP"
          IF dc_win&=3
            txt$=CHAR{local_path0%}+txt$+c0$
          ELSE IF dc_win&=4
            txt$=CHAR{local_path1%}+txt$+c0$
          ENDIF
          @launch(txt$)
        ENDIF
        '
        IF dc_darkened!
          IF kc_win&=0 AND @is_record_selected(dc_win&,double_click_selected&)=TRUE
            record_select(dc_win&,double_click_selected&)
            get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
            force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          ENDIF
        ENDIF
        '
      CASE 11,12
        '
        IF kc_win&=0
          IF @is_record_selected(dc_win&,double_click_selected&)
            record_select(dc_win&,double_click_selected&)
          ENDIF
          get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
        ENDIF
        '
        txt$=@loc_get_name$(@px(dc_win&,double_click_selected&))
        IF dc_win&=3
          CHAR{local_path0%}=txt$+"\\"
        ELSE IF dc_win&=4
          CHAR{local_path1%}=txt$+"\\"
        ENDIF
        '
        @local_change_directory(dc_win&,0)
        '
      ENDSELECT
    ENDIF
  ENDIF
RETURN
> PROCEDURE manage_keys_general
  m_clavier|=BYTE(m_clavier&)
  top_win&=@get_top_window
  '
  SELECT m_clavier|
  CASE 9 ! Tab
    IF m_touche&=0
      IF hand_win&(3)=top_win& AND win!(3)=TRUE AND win!(4)=TRUE
        win(4)
      ELSE IF hand_win&(4)=top_win& AND win!(4)=TRUE AND win!(3)=TRUE
        win(3)
      ENDIF
    ENDIF
  CASE 17 ! ^Q
    ask_leaving
  CASE 21 ! ^U
    FOR i&=2 TO nb_win&
      IF top_win&=hand_win&(i&) AND aff!(i&)=TRUE AND i&<>3 AND i&<>4
        close_win(i&)
      ENDIF
    NEXT i&
  ENDSELECT
  '
  IF m_touche&=0
    SELECT m_clavier&
    CASE 15360 ! F2 swap windows contents
      dummy$=CHAR{local_path1%}
      @local_set_win_info(4,CHAR{local_path0%})
      @local_set_win_info(3,dummy$)
    CASE 16128 ! F5 = copy
      IF top_win&=hand_win&(3) AND win!(3)=TRUE
        IF (record_nb_selected&(3)>0 AND display_disk_icons!(3)=FALSE) OR record_nb_selected&(3)=1
          IF display_disk_icons!(4)=FALSE
            @local_copy_files(3,FALSE)
          ENDIF
        ENDIF
      ELSE IF top_win&=hand_win&(4) AND win!(4)=TRUE
        IF (record_nb_selected&(4)>0 AND display_disk_icons!(4)=FALSE) OR record_nb_selected&(4)=1
          IF display_disk_icons!(3)=FALSE
            @local_copy_files(4,FALSE)
          ENDIF
        ENDIF
      ENDIF
    CASE 16384 ! F6 = move
      IF top_win&=hand_win&(3) AND win!(3)=TRUE
        IF (record_nb_selected&(3)>0 AND display_disk_icons!(3)=FALSE) OR record_nb_selected&(3)=1
          IF display_disk_icons!(4)=FALSE
            @local_copy_files(3,TRUE)
          ENDIF
        ENDIF
      ELSE IF top_win&=hand_win&(4) AND win!(4)=TRUE
        IF (record_nb_selected&(4)>0 AND display_disk_icons!(4)=FALSE) OR record_nb_selected&(4)=1
          IF display_disk_icons!(3)=FALSE
            @local_copy_files(4,TRUE)
          ENDIF
        ENDIF
      ENDIF
    CASE 16640 ! F7 = new dir
      IF top_win&=hand_win&(3) AND win!(3)=TRUE
        IF display_disk_icons!(3)=FALSE
          @dialog_new_folder(3)
        ENDIF
      ELSE IF top_win&=hand_win&(4) AND win!(4)=TRUE
        IF display_disk_icons!(4)=FALSE
          @dialog_new_folder(4)
        ENDIF
      ENDIF
    CASE 16896 ! F8 = delete
      IF top_win&=hand_win&(3) AND win!(3)=TRUE
        IF display_disk_icons!(3)=FALSE
          IF record_nb_selected&(3)>0
            IF misc_confirm_delete!
              @local_delete_files(3)
            ENDIF
          ENDIF
        ENDIF
      ELSE IF top_win&=hand_win&(4) AND win!(4)=TRUE
        IF display_disk_icons!(4)=FALSE
          IF record_nb_selected&(4)>0
            IF misc_confirm_delete!
              @local_delete_files(4)
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    CASE 17152 ! F9
      IF display_sort_type&=1
        display_set_sort_type(4)
      ELSE
        IF display_sort_type&=4
          display_set_sort_type(3)
        ELSE
          IF display_sort_type&=3
            display_set_sort_type(2)
          ELSE
            IF display_sort_type&=2
              display_set_sort_type(0)
            ELSE
              IF display_sort_type&=0
                display_set_sort_type(1)
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    CASE 17408 ! F10
    CASE 28720 ! num 0
      @launch(shortcuts$(0)+c0$)
    CASE 27953 ! num 1
      @launch(shortcuts$(1)+c0$)
    CASE 28210 ! num 2
      @launch(shortcuts$(2)+c0$)
    CASE 28467 ! num 3
      @launch(shortcuts$(3)+c0$)
    CASE 27188 ! num 4
      @launch(shortcuts$(4)+c0$)
    CASE 27445 ! num 5
      @launch(shortcuts$(5)+c0$)
    CASE 27702 ! num 6
      @launch(shortcuts$(6)+c0$)
    CASE 26423 ! num 7
      @launch(shortcuts$(7)+c0$)
    CASE 26680 ! num 8
      @launch(shortcuts$(8)+c0$)
    CASE 26937 ! num 9
      @launch(shortcuts$(9)+c0$)
    ENDSELECT
  ELSE IF m_touche&=4
    SELECT m_clavier&
    CASE 15616 ! F3
      display_set_sort_type(1)
    CASE 15872 ! F4
      display_set_sort_type(4)
    CASE 16128 ! F5
      display_set_sort_type(3)
    CASE 16384 ! F6
      display_set_sort_type(2)
    CASE 16640 ! F7
      display_set_sort_type(0)
    ENDSELECT
  ELSE IF m_touche&=6
    SELECT m_clavier&
    CASE 19742 ! ^->
      IF (record_nb_selected&(3)>0 AND display_disk_icons!(3)=FALSE) OR record_nb_selected&(3)=1
        IF display_disk_icons!(4)=FALSE
          @local_copy_files(3,TRUE)
        ENDIF
      ENDIF
    CASE 19220 ! ^<-
      IF (record_nb_selected&(4)>0 AND display_disk_icons!(4)=FALSE) OR record_nb_selected&(4)=1
        IF display_disk_icons!(3)=FALSE
          @local_copy_files(4,TRUE)
        ENDIF
      ENDIF
    ENDSELECT
  ELSE IF m_touche&=8
    SELECT m_clavier&
    CASE 15104 ! F1
      @local_set_win_info(3,"")
    CASE 15360 ! F2
      @local_set_win_info(4,"")
    ENDSELECT
  ENDIF
  '
  IF m_touche&=4
    manage_keys_control
  ELSE
    manage_keys_normal
  ENDIF
  '
RETURN
> PROCEDURE manage_keys_control
  '
  IF m_clavier&=29696 ! ^Right
    IF (record_nb_selected&(3)>0 AND display_disk_icons!(3)=FALSE) OR record_nb_selected&(3)=1
      IF display_disk_icons!(4)=FALSE
        @local_copy_files(3,FALSE)
      ENDIF
    ENDIF
  ELSE IF m_clavier&=29440 ! ^Left
    IF (record_nb_selected&(4)>0 AND display_disk_icons!(4)=FALSE) OR record_nb_selected&(4)=1
      IF display_disk_icons!(3)=FALSE
        @local_copy_files(4,FALSE)
      ENDIF
    ENDIF
  ELSE IF m_clavier&=18432 ! ^Up
    key_arrowed(0)
  ELSE IF m_clavier&=20480 ! ^Down
    key_arrowed(1)
  ELSE IF win!(3)=TRUE AND hand_win&(3)=@get_top_window
    SELECT m_clavier|
    CASE 1 ! ^A
      IF display_disk_icons!(3)=FALSE
        @local_select_all_files(3)
      ENDIF
    CASE 8 ! ^Backspace
      IF display_disk_icons!(3)=FALSE
        @local_change_directory(3,0) ! up
      ENDIF
    CASE 9 ! ^I
      IF record_nb_selected&(3)=1
        dialog_informations(3,record_selected&(3))
      ENDIF
    CASE 14 ! ^N
      IF display_disk_icons!(3)=FALSE
        @dialog_new_folder(3)
      ENDIF
    CASE 18 ! ^R
      IF display_disk_icons!(3)
        @local_get_disks_list(3)
      ELSE
        @local_get_directory(3)
      ENDIF
    CASE 31 ! ^Delete
      IF display_disk_icons!(3)=FALSE
        IF record_nb_selected&(3)>0
          IF misc_confirm_delete!
            @local_delete_files(3)
          ENDIF
        ENDIF
      ENDIF
    CASE 23 ! ^\
      dummy$=CHAR{local_path0%}
      dummy&=INSTR(dummy$,"\")
      IF dummy&>0
        @local_set_win_info(3,LEFT$(dummy$,dummy&))
      ENDIF
    ENDSELECT
  ELSE IF win!(4)=TRUE AND hand_win&(4)=@get_top_window
    SELECT m_clavier|
    CASE 1 ! ^A
      IF display_disk_icons!(4)=FALSE
        @local_select_all_files(4)
      ENDIF
    CASE 8 ! ^Backspace
      IF display_disk_icons!(4)=FALSE
        @local_change_directory(4,0) ! up
      ENDIF
    CASE 9 ! ^I
      IF record_nb_selected&(4)=1
        dialog_informations(4,record_selected&(4))
      ENDIF
    CASE 14 ! ^N
      IF display_disk_icons!(4)=FALSE
        @dialog_new_folder(4)
      ENDIF
    CASE 18 ! ^R
      IF display_disk_icons!(4)
        @local_get_disks_list(4)
      ELSE
        @local_get_directory(4)
      ENDIF
    CASE 31 ! ^Delete
      IF display_disk_icons!(4)=FALSE
        IF record_nb_selected&(4)>0
          IF misc_confirm_delete!
            @local_delete_files(4)
          ENDIF
        ENDIF
      ENDIF
    CASE 23 ! ^\
      dummy$=CHAR{local_path1%}
      dummy&=INSTR(dummy$,"\")
      IF dummy&>0
        @local_set_win_info(4,LEFT$(dummy$,dummy&))
      ENDIF
    ENDSELECT
  ENDIF
RETURN
> PROCEDURE manage_keys_normal
  LOCAL space_win&
  '
  SELECT m_clavier&
  CASE 283 ! Esc
    IF hand_win&(3)=@get_top_window AND win!(3)=TRUE
      IF display_disk_icons!(3)
        @local_get_disks_list(3)
      ELSE
        @local_get_directory(3)
      ENDIF
    ELSE IF hand_win&(4)=@get_top_window AND win!(4)=TRUE
      IF display_disk_icons!(4)
        @local_get_disks_list(4)
      ELSE
        @local_get_directory(4)
      ENDIF
    ENDIF
  CASE 7181,29197 ! Return, Enter
    IF hand_win&(3)=@get_top_window AND win!(3)=TRUE
      @manage_mouse_on_wins(3)
    ELSE IF hand_win&(4)=@get_top_window AND win!(4)=TRUE
      @manage_mouse_on_wins(4)
    ENDIF
  CASE 18176 ! ClrHome
    key_arrowed(4)
  CASE 18231 ! ClrHome
    key_arrowed(5)
  CASE 18432 ! Up
    key_arrowed(2)
  CASE 20480 ! Down
    key_arrowed(3)
  CASE 18488 ! Up
    key_arrowed(6)
  CASE 20530 ! Down
    key_arrowed(7)
  CASE 14624 ! Space
    space_win&=WIND_FIND(mo_x&,mo_y&)
    IF hand_win&(3)=space_win& AND win!(3)=TRUE
      sp_win&=3
    ELSE IF hand_win&(4)=space_win& AND win!(4)=TRUE
      sp_win&=4
    ELSE
      sp_win&=0
    ENDIF
    '
    IF sp_win&>0
      dummy&=@get_record_under_cursor(sp_win&,mo_y&,mo_x&)
      IF dummy&>-1 AND display_disk_icons!(sp_win&)=FALSE
        IF record_nb_selected&(sp_win&)=0
          '
          record_selected&(sp_win&)=dummy&
          record_select(sp_win&,record_selected&(sp_win&))
          INC record_nb_selected&(sp_win&)
          '
        ELSE IF record_nb_selected&(sp_win&)>0
          '
          record_select(sp_win&,dummy&)
          IF @is_record_selected(sp_win&,dummy&)
            record_selected&(sp_win&)=MIN(dummy&,record_selected&(sp_win&))
            INC record_nb_selected&(sp_win&)
          ELSE
            DEC record_nb_selected&(sp_win&)
          ENDIF
          '
        ENDIF
        record_nb_selected&(sp_win&)=MAX(0,MIN(record_nb_selected&(sp_win&),head_nbline&(sp_win&)))
        get_record_xywh(sp_win&,dummy&,sx&,sy&,sl&,sh&)
        force_update_xywh(sp_win&,dummy&,sx&,sy&,sl&,sh&)
      ENDIF
    ENDIF
  DEFAULT
    IF m_clavier|>64 AND m_clavier|<91
      @local_get_drives
      dummy&=@local_get_drive_index_from_letter(m_clavier|)
      IF hand_win&(3)=top_win& AND win!(3)=TRUE
        local_drive_index&(3)=dummy&
        CHAR{{OB_SPEC(adtree%(3),1)}}=" "+loc_drive$(local_drive_index&(3))+": "
        @local_set_win_info(3,loc_drive$(local_drive_index&(3))+":\")
        black_white(3,1,-1)
      ELSE IF hand_win&(4)=top_win& AND win!(4)=TRUE
        local_drive_index&(4)=dummy&
        CHAR{{OB_SPEC(adtree%(4),1)}}=" "+loc_drive$(local_drive_index&(4))+": "
        @local_set_win_info(4,loc_drive$(local_drive_index&(4))+":\")
        black_white(4,1,-1)
      ENDIF
    ENDIF
  ENDSELECT
  '
RETURN
'
> PROCEDURE key_arrowed(cur_code&)
  LOCAL redraw_line!
  '
  FOR key&=3 TO 4
    IF top_win&=hand_win&(key&) AND win!(key&)=TRUE
      SELECT cur_code&
      CASE 0
        IF record_start&(key&)>0
          SUB record_start&(key&),record_height&(key&)
          record_start&(key&)=MAX(0,record_start&(key&))
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDIF
      CASE 1
        IF record_start&(key&)<SUB(PRED(head_nbline&(key&)),record_height&(key&))
          ADD record_start&(key&),record_height&(key&)
          record_start&(key&)=MAX(0,MIN(record_start&(key&),SUB(head_nbline&(key&),record_height&(key&))))
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDIF
      CASE 2
        SELECT record_nb_selected&(key&)
        CASE 0
          record_selected&(key&)=PRED(head_nbline&(key&))
          record_select(key&,record_selected&(key&))
          '
          IF record_selected&(key&)=<ADD(record_start&(key&),record_height&(key&))
            get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
            force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          ELSE
            record_start&(key&)=MAX(0,SUB(head_nbline&(key&),record_height&(key&)))
            @set_vslide(key&)
            record_start&(key&)=@get_vslide(key&)
            ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
            control
            redraw_table(key&)
            uncontrol
          ENDIF
        CASE 1
          record_select(key&,record_selected&(key&))
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          '
          record_selected&(key&)=MAX(0,MIN(PRED(record_selected&(key&)),PRED(head_nbline&(key&))))
          record_select(key&,record_selected&(key&))
          '
          IF record_start&(key&)>0
            IF record_selected&(key&)<record_start&(key&)
              control
              DEC record_start&(key&)
              scroll_to_top(key&)
              uncontrol
            ENDIF
          ENDIF
          '
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          '
        DEFAULT
          record_deselect_all(key&)
          '
          record_selected&(key&)=PRED(head_nbline&(key&))
          record_select(key&,record_selected&(key&))
          '
          record_start&(key&)=MAX(0,SUB(head_nbline&(key&),record_height&(key&)))
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDSELECT
        '
        record_nb_selected&(key&)=1
        '
      CASE 3
        SELECT record_nb_selected&(key&)
        CASE 0
          record_selected&(key&)=0
          record_select(key&,record_selected&(key&))
          '
          IF record_start&(key&)=0
            get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
            force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          ELSE
            record_start&(key&)=0
            @set_vslide(key&)
            record_start&(key&)=@get_vslide(key&)
            ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
            control
            redraw_table(key&)
            uncontrol
          ENDIF
        CASE 1
          record_select(key&,record_selected&(key&))
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          '
          record_selected&(key&)=MAX(0,MIN(SUCC(record_selected&(key&)),PRED(head_nbline&(key&))))
          record_select(key&,record_selected&(key&))
          '
          IF record_start&(key&)<SUB(PRED(head_nbline&(key&)),record_height&(key&))
            IF record_selected&(key&)>ADD(record_start&(key&),record_height&(key&))
              control
              INC record_start&(key&)
              scroll_to_bottom(key&)
              uncontrol
            ENDIF
          ENDIF
          '
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          '
        DEFAULT
          record_deselect_all(key&)
          '
          record_selected&(key&)=0
          record_select(key&,record_selected&(key&))
          '
          record_start&(key&)=0
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDSELECT
        '
        record_nb_selected&(key&)=1
        '
      CASE 4
        IF record_start&(key&)>0
          record_start&(key&)=0
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDIF
      CASE 5
        IF record_start&(key&)<SUB(PRED(head_nbline&(key&)),record_height&(key&))
          record_start&(key&)=MAX(0,SUB(head_nbline&(key&),record_height&(key&)))
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDIF
      CASE 6
        IF record_selected&(key&)>0 AND display_disk_icons!(key&)=FALSE
          IF @is_record_selected(key&,PRED(record_selected&(key&)))
            record_select(key&,record_selected&(key&))
            get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
            force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
            DEC record_nb_selected&(key&)
            DEC record_selected&(key&)
          ELSE
            INC record_nb_selected&(key&)
            DEC record_selected&(key&)
            record_select(key&,record_selected&(key&))
          ENDIF
          '
          IF record_start&(key&)>0
            IF record_selected&(key&)<record_start&(key&)
              control
              DEC record_start&(key&)
              scroll_to_top(key&)
              uncontrol
            ENDIF
          ENDIF
          '
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
        ENDIF
      CASE 7
        IF record_selected&(key&)<head_nbline&(key&) AND display_disk_icons!(key&)=FALSE
          IF @is_record_selected(key&,SUCC(record_selected&(key&)))
            record_select(key&,record_selected&(key&))
            get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
            force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
            DEC record_nb_selected&(key&)
            INC record_selected&(key&)
          ELSE
            INC record_nb_selected&(key&)
            INC record_selected&(key&)
            record_select(key&,record_selected&(key&))
          ENDIF
          '
          IF record_start&(key&)<SUB(PRED(head_nbline&(key&)),record_height&(key&))
            IF record_selected&(key&)>ADD(record_start&(key&),record_height&(key&))
              control
              INC record_start&(key&)
              scroll_to_bottom(key&)
              uncontrol
            ENDIF
          ENDIF
          '
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
        ENDIF
      ENDSELECT
    ENDIF
  NEXT key&
RETURN
'
> PROCEDURE win_closed
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      IF i&=6
        IF scan_is_shown!
          scan_is_aborted!=TRUE
        ELSE
          receive_aborted!=TRUE
          send_aborted!=TRUE
        ENDIF
      ELSE
        close_win(i&)
      ENDIF
    ENDIF
  NEXT i&
RETURN
> PROCEDURE win_sized
  '
  m_x&=MAX(SUCC(screenx&),m_x&)
  m_y&=MAX(SUCC(screeny&),m_y&)
  m_l&=MAX(75,m_l&)
  m_h&=MAX(150,m_h&)
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),5,m_x&,m_y&,m_l&,m_h&)
      move_win(i&,m_x&,m_y&,m_l&,m_h&)
      @set_vslide(i&)
      @set_hslide(i&)
      IF record_start&(i&)<>@get_vslide(i&) OR field_start&(i&)<>@get_hslide(i&)
        force_update(i&)
      ENDIF
      record_start&(i&)=@get_vslide(i&)
      field_start&(i&)=@get_hslide(i&)
      ful!(i&)=FALSE
    ENDIF
  NEXT i&
  '
RETURN
> PROCEDURE win_moved
  m_x&=MAX(SUCC(screenx&),m_x&)
  m_y&=MAX(SUCC(screeny&),m_y&)
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),5,m_x&,m_y&,m_l&,m_h&)
      move_win(i&,m_x&,m_y&,m_l&,m_h&)
      ful!(i&)=FALSE
    ENDIF
  NEXT i&
RETURN
> PROCEDURE win_topped
  top_win&=@get_top_window
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE AND aff!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),10,0,0,0,0)
    ENDIF
  NEXT i&
RETURN
> PROCEDURE win_untopped
  ~WIND_SET(m_win&,25,0,0,0,0)
RETURN
> PROCEDURE win_ontop
RETURN
> PROCEDURE win_fulled
  '
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      IF ful!(i&)=FALSE
        wx&(i&)=SUCC(screenx&)
        wy&(i&)=SUCC(screeny&)
        wl&(i&)=SUB(screenl&,4)
        wh&(i&)=ADD(SUB(screenh&,4),MUL(20,multi!))
        ful!(i&)=TRUE
      ELSE
        ~WIND_GET(hand_win&(i&),6,wx&(i&),wy&(i&),wl&(i&),wh&(i&))
        ful!(i&)=FALSE
      ENDIF
      move_win(i&,wx&(i&),wy&(i&),wl&(i&),wh&(i&))
      ~WIND_SET(hand_win&(i&),5,wx&(i&),wy&(i&),wl&(i&),wh&(i&))
      @set_vslide(i&)
      @set_hslide(i&)
      IF record_start&(i&)<>@get_vslide(i&) OR field_start&(i&)<>@get_hslide(i&)
        force_update(i&)
      ENDIF
      record_start&(i&)=@get_vslide(i&)
      field_start&(i&)=@get_hslide(i&)
    ENDIF
  NEXT i&
  '
RETURN
> PROCEDURE win_vslided
  '
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),9,m_x&,0,0,0)
      record_start&(i&)=@get_vslide(i&)
      force_update(i&)
    ENDIF
  NEXT i&
  '
RETURN
> PROCEDURE win_hslided
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),8,m_x&,0,0,0)
      field_start&(i&)=@get_hslide(i&)
      force_update(i&)
    ENDIF
  NEXT i&
RETURN
> PROCEDURE win_arrowed
  '
  FOR i&=3 TO 5
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      SELECT m_x&
      CASE 0
        SUB record_start&(i&),record_height&(i&)
        record_start&(i&)=MAX(0,record_start&(i&))
        @set_vslide(i&)
        record_start&(i&)=@get_vslide(i&)
        control
        ~WIND_GET(hand_win&(i&),4,m_x&,m_y&,m_l&,m_h&)
        ~WIND_GET(hand_win&(i&),11,rx&,ry&,rl&,rh&)
        WHILE rl&<>0 AND rh&<>0
          IF RC_INTERSECT(m_x&,m_y&,m_l&,m_h&,rx&,ry&,rl&,rh&)
            redraw_table(i&)
          ENDIF
          ~WIND_GET(hand_win&(i&),12,rx&,ry&,rl&,rh&)
        WEND
        uncontrol
      CASE 1
        ADD record_start&(i&),record_height&(i&)
        record_start&(i&)=MAX(0,MIN(record_start&(i&),SUB(head_nbline&(i&),record_height&(i&))))
        @set_vslide(i&)
        record_start&(i&)=@get_vslide(i&)
        control
        ~WIND_GET(hand_win&(i&),4,m_x&,m_y&,m_l&,m_h&)
        ~WIND_GET(hand_win&(i&),11,rx&,ry&,rl&,rh&)
        WHILE rl&<>0 AND rh&<>0
          IF RC_INTERSECT(m_x&,m_y&,m_l&,m_h&,rx&,ry&,rl&,rh&)
            redraw_table(i&)
          ENDIF
          ~WIND_GET(hand_win&(i&),12,rx&,ry&,rl&,rh&)
        WEND
        uncontrol
      CASE 2
        IF m_win&=@get_top_window
          control
          REPEAT
            ~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
            IF record_start&(i&)>0
              DEC record_start&(i&)
              scroll_to_top(i&)
            ENDIF
          UNTIL NOT mo_c&
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 3
        IF m_win&=@get_top_window
          control
          REPEAT
            ~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
            IF record_start&(i&)<SUB(PRED(head_nbline&(i&)),record_height&(i&))
              INC record_start&(i&)
              scroll_to_bottom(i&)
            ENDIF
          UNTIL NOT mo_c&
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 4
        IF m_win&=@get_top_window
          old_dec_x&=MIN(field_start&(i&),150)
          SUB field_start&(i&),old_dec_x&
          control
          scroll_to_left(i&,old_dec_x&)
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 5
        IF m_win&=@get_top_window
          old_dec_x&=MIN(SUB(1000,field_start&(i&)),150)
          ADD field_start&(i&),old_dec_x&
          control
          scroll_to_right(i&,old_dec_x&)
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 6
        IF m_win&=@get_top_window
          control
          REPEAT
            ~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
            IF field_start&(i&)>0
              old_dec_x&=MIN(field_start&(i&),15)
              SUB field_start&(i&),old_dec_x&
              scroll_to_left(i&,old_dec_x&)
            ENDIF
          UNTIL NOT mo_c&
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 7
        IF m_win&=@get_top_window
          control
          REPEAT
            ~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
            IF field_start&(i&)<1000
              old_dec_x&=MIN(SUB(1000,field_start&(i&)),15)
              ADD field_start&(i&),old_dec_x&
              scroll_to_right(i&,old_dec_x&)
            ENDIF
          UNTIL NOT mo_c&
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      ENDSELECT
    ENDIF
  NEXT i&
  '
RETURN
> PROCEDURE win_join
  LOCAL screenll&
  '
  IF multi!=TRUE AND screenl&>640
    screenll&=SUB(screenl&,10)
  ELSE
    screenll&=screenl&
  ENDIF
  '
  pwx&(3)=SUCC(screenx&)
  pwy&(3)=SUCC(screeny&)
  pwl&(3)=MAX(157,SUB(screenll&/2,2))
  IF win!(5)
    pwh&(3)=SUB(screenh&,100)
  ELSE
    pwh&(3)=SUB(screenh&,2)
    IF multi!
      SUB pwh&(3),16
    ENDIF
  ENDIF
  '
  IF win!(3)
    ~WIND_SET(hand_win&(3),5,pwx&(3),pwy&(3),pwl&(3),pwh&(3))
    move_win(3,pwx&(3),pwy&(3),pwl&(3),pwh&(3))
    record_start&(3)=0
    set_vslide(3)
    record_start&(3)=@get_vslide(3)
    force_update(3)
  ENDIF
  '
  pwx&(4)=SUCC(ADD(pwx&(3),pwl&(3)))
  pwy&(4)=SUCC(screeny&)
  pwl&(4)=MAX(157,MIN(SUB(screenll&/2,2),SUB(PRED(screenl&),pwx&(4))))
  IF win!(5)
    pwh&(4)=SUB(screenh&,100)
  ELSE
    pwh&(4)=SUB(screenh&,2)
    IF multi!
      SUB pwh&(4),16
    ENDIF
  ENDIF
  '
  IF win!(4)
    ~WIND_SET(hand_win&(4),5,pwx&(4),pwy&(4),pwl&(4),pwh&(4))
    move_win(4,pwx&(4),pwy&(4),pwl&(4),pwh&(4))
    record_start&(4)=0
    set_vslide(4)
    record_start&(4)=@get_vslide(4)
    force_update(4)
  ENDIF
  '
RETURN
> PROCEDURE redraw
  '
  control
  '
  ~WIND_GET(m_win&,11,rx&,ry&,rl&,rh&)
  WHILE rl&<>0 AND rh&<>0
    IF RC_INTERSECT(m_x&,m_y&,m_l&,m_h&,rx&,ry&,rl&,rh&)
      FOR i&=1 TO nb_win&
        IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
          forced!(i&)=FALSE
          '
          ~OBJC_DRAW(adtree%(i&),0,4,rx&,ry&,rl&,rh&)
          IF i&=3 OR i&=4
            redraw_table(i&)
          ENDIF
        ENDIF
      NEXT i&
    ENDIF
    ~WIND_GET(m_win&,12,rx&,ry&,rl&,rh&)
  WEND
  '
  win_ontop
  '
  uncontrol
RETURN
'
> PROCEDURE redraw_table(dial&)
  redraw_grid(dial&)
  SELECT dial&
  CASE 3
    @local_redraw(3,-1)
  CASE 4
    @local_redraw(4,-1)
  ENDSELECT
RETURN
> PROCEDURE redraw_grid(dial&)
  '
  local_set_fonts(dial&)
  '
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  ADD yf&,field_bar_h&(dial&)
  SUB hf&,field_bar_h&(dial&)
  v_hide_c
  clip(1,rx&,ry&,rl&,rh&)
  vswr_mode(1)
  vsf_color(0)
  v_bar(xf&,yf&,ADD(lf&,PRED(xf&)),ADD(hf&,PRED(yf&)))
  '
  IF dial&=3 OR dial&=4
    '
    IF head_nbline&(dial&)>0
      ef&=MIN(SUCC(ADD(record_start&(dial&),record_height&(dial&))),PRED(head_nbline&(dial&)))
      '
      FOR j&=record_start&(dial&) TO ef&
        yp&=MUL(font_files_real_height&(dial&),SUB(SUCC(j&),record_start&(dial&)))
        IF dial&=3
          IF @is_record_selected(3,j&)
            vsf_color(1)
            v_bar(xf&,ADD(yf&,SUB(SUCC(yp&),font_files_real_height&(dial&))),lf&,PRED(font_files_real_height&(dial&)))
          ENDIF
        ELSE IF dial&=4
          IF @is_record_selected(4,j&)
            vsf_color(1)
            v_bar(xf&,ADD(yf&,SUB(SUCC(yp&),font_files_real_height&(dial&))),lf&,PRED(font_files_real_height&(dial&)))
          ENDIF
        ENDIF
      NEXT j&
    ENDIF
  ENDIF
  '
  clip(0,rx&,ry&,rl&,rh&)
  v_show_c
  '
RETURN
> PROCEDURE scroll_to_bottom(dial&)
  set_vslide_pos(dial&)
  '
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  IF ADD(yf&,hf&)>ADD(screeny&,screenh&)
    ~WIND_GET(hand_win&(dial&),4,rx&,ry&,rl&,rh&)
    redraw_table(dial&)
  ELSE
    ADD yf&,SUCC(field_bar_h&(dial&))
    SUB hf&,SUCC(field_bar_h&(dial&))
    '
    eh&=MUL(font_files_real_height&(dial&),record_height&(dial&))
    make_xywh(xf&,ADD(yf&,font_files_real_height&(dial&)),lf&,eh&,xf&,yf&,lf&,eh&)
    vro_cpyfm(pscrmfdb%,pscrmfdb%)
    '
    IF ADD(record_start&(dial&),record_height&(dial&))<head_nbline&(dial&)
      get_record_xywh(dial&,ADD(record_start&(dial&),record_height&(dial&)),sx&,sy&,sl&,sh&)
      force_update_xywh(dial&,ADD(record_start&(dial&),record_height&(dial&)),sx&,sy&,sl&,sh&)
      IF ADD(record_start&(dial&),SUCC(record_height&(dial&)))<head_nbline&(dial&)
        get_record_xywh(dial&,ADD(record_start&(dial&),SUCC(record_height&(dial&))),sx&,sy&,sl&,sh&)
        force_update_xywh(dial&,ADD(record_start&(dial&),SUCC(record_height&(dial&))),sx&,sy&,sl&,sh&)
      ELSE
        ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
        ADD yf&,SUCC(field_bar_h&(dial&))
        SUB hf&,SUCC(field_bar_h&(dial&))
        eh&=MUL(font_files_real_height&(dial&),SUCC(record_height&(dial&)))
        '
        rx&=xf&
        ry&=ADD(yf&,eh&)
        rl&=lf&
        rh&=SUB(hf&,eh&)
        '
        IF rh&>0
          redraw_table(dial&)
        ENDIF
      ENDIF
    ELSE
      rx&=xf&
      ry&=ADD(yf&,eh&)
      rl&=lf&
      rh&=SUB(hf&,eh&)
      '
      redraw_table(dial&)
    ENDIF
    '
  ENDIF
RETURN
> PROCEDURE scroll_to_top(dial&)
  set_vslide_pos(dial&)
  '
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  ADD yf&,SUCC(field_bar_h&(dial&))
  SUB hf&,SUCC(field_bar_h&(dial&))
  '
  eh&=MAX(0,SUB(hf&,font_files_real_height&(dial&)))
  make_xywh(xf&,yf&,lf&,eh&,xf&,ADD(yf&,font_files_real_height&(dial&)),lf&,eh&)
  vro_cpyfm(pscrmfdb%,pscrmfdb%)
  '
  rx&=xf&
  ry&=yf&
  rl&=lf&
  rh&=font_files_real_height&(dial&)
  force_update_xywh(dial&,record_start&(dial&),rx&,ry&,rl&,rh&)
  '
RETURN
> PROCEDURE scroll_to_left(dial&,offset&)
  IF field_start&(dial&)>-1 AND offset&>0
    set_hslide(dial&)
    '
    ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
    ADD yf&,field_bar_h&(dial&)
    SUB hf&,field_bar_h&(dial&)
    '
    offset&=MIN(offset&,lf&)
    dummy&=SUB(lf&,offset&)
    IF dummy&>0
      make_xywh(xf&,yf&,dummy&,hf&,ADD(xf&,offset&),yf&,dummy&,hf&)
      vro_cpyfm(pscrmfdb%,pscrmfdb%)
    ENDIF
    '
    rx&=xf&
    ry&=yf&
    rl&=offset&
    rh&=hf&
    redraw_table(dial&)
    '
    IF field_bar_obj&(dial&)>0
      black_white(dial&,field_bar_obj&(dial&),-1)
    ENDIF
  ENDIF
RETURN
> PROCEDURE scroll_to_right(dial&,offset&)
  IF offset&>0
    set_hslide(dial&)
    '
    ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
    ADD yf&,field_bar_h&(dial&)
    SUB hf&,field_bar_h&(dial&)
    '
    offset&=MIN(offset&,lf&)
    dummy&=SUB(lf&,offset&)
    IF dummy&>0
      make_xywh(ADD(xf&,offset&),yf&,dummy&,hf&,xf&,yf&,dummy&,hf&)
      vro_cpyfm(pscrmfdb%,pscrmfdb%)
    ENDIF
    '
    rx&=ADD(xf&,dummy&)
    ry&=yf&
    rl&=offset&
    rh&=hf&
    redraw_table(dial&)
    '
    black_white(dial&,field_bar_obj&(dial&),-1)
  ENDIF
RETURN
'
> FUNCTION get_record_under_cursor(dial&,co_y&,co_x&)
LOCAL cu_y&
IF dial&=3 OR dial&=4
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  ADD yf&,field_bar_h&(dial&)
  SUB hf&,field_bar_h&(dial&)
  '
  IF co_y&>yf& AND co_y&<ADD(yf&,hf&) AND co_x&>xf& AND co_x&<ADD(xf&,lf&)
    cu_y&=MAX(0,SUB(co_y&,yf&))/font_files_real_height&(dial&)
    IF cu_y&>PRED(head_nbline&(dial&))
      RETURN -1
    ENDIF
    IF head_nbline&(dial&)>0
      RETURN MAX(0,MIN(ADD(record_start&(dial&),MIN(cu_y&,SUCC(record_height&(dial&)))),PRED(head_nbline&(dial&))))
    ENDIF
  ENDIF
ENDIF
RETURN -1
ENDFUNC
> PROCEDURE get_record_xywh(dial&,record_id&,VAR sx&,sy&,sl&,sh&)
IF dial&=3 OR dial&=4
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  sx&=xf&
  sl&=lf&
  '
  ADD yf&,field_bar_h&(dial&)
  IF record_id&>=record_start&(dial&) AND record_id&=<MIN(ADD(record_start&(dial&),SUCC(record_height&(dial&))),PRED(head_nbline&(dial&)))
    sy&=ADD(yf&,MUL(SUB(record_id&,record_start&(dial&)),font_files_real_height&(dial&)))
    sh&=font_files_real_height&(dial&)
  ELSE
    sy&=0
    sh&=0
  ENDIF
ENDIF
RETURN
> PROCEDURE get_window_xywh(dial&,record_id&,VAR sx&,sy&,sl&,sh&)
IF dial&=3 OR dial&=4
  ~WIND_GET(hand_win&(dial&),4,sx&,sy&,sl&,sh&)
  ADD sy&,field_bar_h&(dial&)
  SUB sh&,field_bar_h&(dial&)
ENDIF
RETURN
'
> PROCEDURE shut_down
'
validate
'
IF misc_save_configuration_at_quit!
  @save_options
ENDIF
~APPL_EXIT()
QUIT 0
RETURN
'
> PROCEDURE win(dial&)
IF win!(dial&)
  force_top(dial&)
ELSE
  create_win(dial&)
ENDIF
RETURN
> PROCEDURE create_win(dial&)
hand_win&(dial&)=@window_create(cp_win&(dial&))
IF hand_win&(dial&)>0
  win!(dial&)=TRUE
  IF dial&=2 OR dial&>4
    ~WIND_SET(hand_win&(dial&),2,CARD(SWAP(OB_SPEC(adtree%(10),dial&))),CARD(OB_SPEC(adtree%(10),dial&)),0,0)
  ENDIF
  IF dial&=3
    @local_set_win_info(3,local_path_left$)
  ENDIF
  IF dial&=4
    @local_set_win_info(4,local_path_right$)
  ENDIF
  ~WIND_SET(hand_win&(dial&),24,&X1,0,0,0)
  '
  IF pwx&(dial&)=0 AND pwy&(dial&)=0
    IF dial&=3 OR dial&=4
      xd&(dial&)=ADD(ADD(screenx&,16),MUL(dial&,8))
      yd&(dial&)=ADD(ADD(screeny&,16),MUL(dial&,8))
      ld&(dial&)=300
      hd&(dial&)=200
    ELSE
      ~FORM_CENTER(adtree%(dial&),xd&(dial&),yd&(dial&),ld&(dial&),dummy&)
      IF dial&=6
        ld&(6)=transfert_save_w&
        hd&(6)=ADD(ADD(OB_Y(adtree%(6),2),OB_H(adtree%(6),2)),4)
      ENDIF
    ENDIF
    ~WIND_CALC(0,cp_win&(dial&),xd&(dial&),yd&(dial&),ld&(dial&),hd&(dial&),wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))
  ELSE
    wx&(dial&)=MIN(SUB(screenl&,ADD(screenx&,40)),pwx&(dial&))
    wy&(dial&)=MIN(SUB(screenh&,ADD(screeny&,40)),pwy&(dial&))
    IF dial&=6
      ld&(6)=transfert_save_w&
      hd&(6)=ADD(ADD(OB_Y(adtree%(6),2),OB_H(adtree%(6),2)),4)
    ENDIF
    IF BTST(cp_win&(dial&),5)=FALSE
      ~WIND_CALC(0,cp_win&(dial&),xd&(dial&),yd&(dial&),ld&(dial&),hd&(dial&),dummy&,dummy&,pwl&(dial&),pwh&(dial&))
    ENDIF
    wl&(dial&)=pwl&(dial&)
    wh&(dial&)=pwh&(dial&)
    ~WIND_CALC(1,cp_win&(dial&),wx&(dial&),wy&(dial&),pwl&(dial&),pwh&(dial&),xd&(dial&),yd&(dial&),ld&(dial&),hd&(dial&))
  ENDIF
  '
  wx&(dial&)=MAX(SUCC(screenx&),wx&(dial&))
  wy&(dial&)=MAX(SUCC(screeny&),wy&(dial&))
  '
  move_win(dial&,wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))
  '
  IF WIND_OPEN(hand_win&(dial&),wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))=0
    win!(dial&)=FALSE
  ENDIF
  IF dial&=3 OR dial&=4
    set_vslide(dial&)
    record_start&(dial&)=@get_vslide(dial&)
    ~WIND_SET(hand_win&(dial&),15,-1,dummy&,dummy&,dummy&)
    set_hslide(dial&)
    field_start&(dial&)=@get_hslide(dial&)
  ENDIF
  aff!(dial&)=win!(dial&)
  ful!(dial&)=FALSE
ELSE
  no_more_win(dial&)
ENDIF
RETURN
> PROCEDURE close_all_windows
FOR i&=1 TO nb_win&
  close_win(i&)
NEXT i&
RETURN
> PROCEDURE close_win(dial&)
IF win!(dial&)
  v_hide_c
  ~WIND_CLOSE(hand_win&(dial&))
  ~WIND_DELETE(hand_win&(dial&))
  v_show_c
  win!(dial&)=FALSE
  aff!(dial&)=FALSE
  ful!(dial&)=FALSE
ENDIF
RETURN
> PROCEDURE move_win(dial&,x0&,y0&,l0&,h0&)
IF win!(dial&)
  ~WIND_CALC(1,cp_win&(dial&),x0&,y0&,l0&,h0&,xd&(dial&),yd&(dial&),ld&(dial&),dummy%)
  OB_X(adtree%(dial&),0)=xd&(dial&)
  OB_Y(adtree%(dial&),0)=yd&(dial&)
  OB_W(adtree%(dial&),0)=ld&(dial&)
  '
  IF dial&=3 OR dial&=4
    IF field_bar_obj&(dial&)>0
      OB_X(adtree%(dial&),field_bar_obj&(dial&))=-MAX(0,field_start&(dial&))
      OB_W(adtree%(dial&),field_bar_obj&(dial&))=ADD(ld&(dial&),field_start&(dial&))
    ENDIF
  ELSE IF dial&=6 AND scan_is_shown!=TRUE
    OB_X(adtree%(25),0)=xd&(dial&)
    OB_Y(adtree%(25),0)=yd&(dial&)
  ENDIF
  '
  pwx&(dial&)=x0&
  pwy&(dial&)=y0&
  IF BTST(cp_win&(dial&),5)
    pwl&(dial&)=l0&
    pwh&(dial&)=h0&
  ELSE
    pwl&(dial&)=0
    pwh&(dial&)=0
  ENDIF
  '
ENDIF
IF dial&=17
  ~WIND_CALC(1,&X1001,m_x&,m_y&,m_l&,m_h&,xd&(17),yd&(17),ld&(17),hd&(17))
  OB_X(adtree%(17),0)=xd&(17)
  OB_Y(adtree%(17),0)=yd&(17)
  OB_W(adtree%(17),0)=ld&(17)
  OB_H(adtree%(17),0)=hd&(17)
  OB_X(adtree%(17),1)=SUB(OB_W(adtree%(17),0),OB_W(adtree%(17),1))/2
  OB_Y(adtree%(17),1)=SUB(OB_H(adtree%(17),0),OB_H(adtree%(17),1))/2
ENDIF
RETURN
> PROCEDURE no_more_win(dial&)
~@alert(1,2)
win!(dial&)=FALSE
aff!(dial&)=FALSE
ful!(dial&)=FALSE
RETURN
'
> PROCEDURE set_vslide(dial&)
IF win!(dial&)=TRUE AND dial&>2 AND dial&<5
  ~WIND_GET(hand_win&(dial&),4,dummy&,dummy&,dummy&,hf&)
  SUB hf&,field_bar_h&(dial&)
  record_height&(dial&)=PRED(MIN(INT(hf&/font_files_real_height&(dial&)),head_nbline&(dial&)))
  '
  IF head_nbline&(dial&)>0
    len_slide&=MAX(0,MIN(1000,1000*(SUCC(record_height&(dial&))/MAX(1,head_nbline&(dial&)))))
    ~WIND_SET(hand_win&(dial&),16,len_slide&,dummy&,dummy&,dummy&)
    pos_slide&=MAX(0,MIN(1000,(1000*record_start&(dial&))/(MAX(1,SUB(head_nbline&(dial&),SUCC(record_height&(dial&)))))))
    ~WIND_SET(hand_win&(dial&),9,pos_slide&,dummy&,dummy&,dummy&)
  ELSE
    ~WIND_SET(hand_win&(dial&),16,1000,dummy&,dummy&,dummy&)
    ~WIND_SET(hand_win&(dial&),9,0,dummy&,dummy&,dummy&)
  ENDIF
ENDIF
RETURN
> PROCEDURE set_vslide_pos(dial&)
IF win!(dial&)
  pos_slide&=MAX(0,MIN(1000,(1000*record_start&(dial&))/(MAX(1,SUB(head_nbline&(dial&),SUCC(record_height&(dial&)))))))
  ~WIND_SET(hand_win&(dial&),9,pos_slide&,dummy&,dummy&,dummy&)
ENDIF
RETURN
> FUNCTION get_vslide(dial&)
$F%
IF win!(dial&)=TRUE AND dial&>2 AND dial&<5
~WIND_GET(hand_win&(dial&),4,dummy&,dummy&,dummy&,hf&)
SUB hf&,field_bar_h&(dial&)
record_height&(dial&)=PRED(MIN(INT(hf&/font_files_real_height&(dial&)),head_nbline&(dial&)))
'
~WIND_GET(hand_win&(dial&),9,pos_slide&,dummy&,dummy&,dummy&)
pos_slide&=INT(((SUB(head_nbline&(dial&),SUCC(record_height&(dial&))))*pos_slide&)/1000)
'
RETURN MAX(0,MIN(pos_slide&,SUB(PRED(head_nbline&(dial&)),record_height&(dial&))))
ENDIF
RETURN 0
ENDFUNC
> PROCEDURE set_hslide(dial&)
IF win!(dial&)=TRUE AND dial&>2 AND dial&<5 AND BTST(cp_win&(dial&),10)=TRUE
IF dial&<5
  IF field_bar_obj&(dial&)>0
    OB_X(adtree%(dial&),field_bar_obj&(dial&))=-MAX(0,field_start&(dial&))
    OB_W(adtree%(dial&),field_bar_obj&(dial&))=ADD(ld&(dial&),MAX(0,field_start&(dial&)))
  ENDIF
ENDIF
'
~WIND_SET(hand_win&(dial&),8,MAX(0,field_start&(dial&)),dummy&,dummy&,dummy&)
ENDIF
RETURN
> FUNCTION get_hslide(dial&)
$F%
IF win!(dial&)=TRUE AND dial&>2 AND dial&<5 AND BTST(cp_win&(dial&),10)=TRUE
~WIND_GET(hand_win&(dial&),8,pos_slide&,dummy&,dummy&,dummy&)
pos_slide&=MAX(0,MIN(pos_slide&,1000))
'
IF dial&<6
IF field_bar_obj&(dial&)>0
  OB_X(adtree%(dial&),field_bar_obj&(dial&))=-MAX(0,pos_slide&)
  OB_W(adtree%(dial&),field_bar_obj&(dial&))=ADD(ld&(dial&),pos_slide&)
ENDIF
ENDIF
'
RETURN MAX(0,pos_slide&)
ENDIF
RETURN 0
ENDFUNC
'
> PROCEDURE force_update(bar&)
IF aff!(bar&) AND NOT forced!(bar&)
INT{s_adr%}=20
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,4)}=0
INT{ADD(s_adr%,6)}=hand_win&(bar&)
INT{ADD(s_adr%,8)}=screenx&
INT{ADD(s_adr%,10)}=screeny&
INT{ADD(s_adr%,12)}=screenl&
INT{ADD(s_adr%,14)}=screenh&
~APPL_WRITE(ap_id&,16,s_adr%)
'
forced!(bar&)=TRUE
ENDIF
RETURN
> PROCEDURE force_update_xywh(bar&,fline&,sx&,sy&,sl&,sh&)
IF fline&<-1
IF aff!(bar&) AND sl&>0 AND sh&>0
INT{s_adr%}=20
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,4)}=0
INT{ADD(s_adr%,6)}=hand_win&(bar&)
INT{ADD(s_adr%,8)}=sx&
INT{ADD(s_adr%,10)}=sy&
INT{ADD(s_adr%,12)}=sl&
INT{ADD(s_adr%,14)}=sh&
~APPL_WRITE(ap_id&,16,s_adr%)
ENDIF
ELSE IF fline&>-1
xfe&=sx&
yfe&=sy&
lfe&=sl&
hfe&=sh&
~WIND_GET(hand_win&(bar&),11,rx&,ry&,rl&,rh&)
'
control
WHILE rl&<>0 AND rh&<>0
IF RC_INTERSECT(xfe&,yfe&,lfe&,hfe&,rx&,ry&,rl&,rh&)
  '
  redraw_grid(bar&)
  SELECT bar&
  CASE 3
    @local_redraw(3,fline&)
  CASE 4
    @local_redraw(4,fline&)
  ENDSELECT
ENDIF
~WIND_GET(hand_win&(bar&),12,rx&,ry&,rl&,rh&)
WEND
uncontrol
'
ENDIF
RETURN
> PROCEDURE force_top(bar&)
IF hand_win&(bar&)<>@get_top_window AND win!(bar&)=TRUE
clear_s
INT{s_adr%}=21
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,6)}=hand_win&(bar&)
~APPL_WRITE(ap_id&,16,s_adr%)
ENDIF
RETURN
> PROCEDURE force_full(bar&)
IF win!(bar&)
clear_s
INT{s_adr%}=23
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,6)}=hand_win&(bar&)
~APPL_WRITE(ap_id&,16,s_adr%)
ENDIF
RETURN
> PROCEDURE clear_s
IF s_adr%>0
LONG{s_adr%}=0
LONG{ADD(s_adr%,4)}=0
LONG{ADD(s_adr%,8)}=0
LONG{ADD(s_adr%,12)}=0
ENDIF
RETURN
'
> PROCEDURE blitter(x1&,l1&,y1&,y2&,h1&)
'
dummy_blit&=MIN(ADD(x1&,PRED(l1&)),ADD(screenx&,PRED(screenl&)))
'
make_xyarray(x1&,y1&,dummy_blit&,ADD(y1&,PRED(h1&)),x1&,y2&,dummy_blit&,ADD(y2&,PRED(h1&)))
'
vro_cpyfm(pscrmfdb%,pscrmfdb%)
'
RETURN
> PROCEDURE noir_blanc(arbre&,fils&,nb_etat&)
IF BTST(OB_FLAGS(adtree%(arbre&),fils&),9)=0
black_white(arbre&,fils&,OB_STATE(adtree%(arbre&),fils&))
ENDIF
black_white(arbre&,fils&,nb_etat&)
RETURN
> PROCEDURE black_white(arbre&,fils&,etat&)
IF fils&>0 AND etat&>-1
SELECT etat&
CASE 0
obj_deselect(arbre&,fils&)
CASE 1
obj_select(arbre&,fils&)
CASE 2
obj_unhide(arbre&,fils&)
CASE 3
obj_hide(arbre&,fils&)
CASE 4
obj_enable(arbre&,fils&)
CASE 5
obj_disable(arbre&,fils&)
ENDSELECT
ENDIF
'
IF win!(arbre&)=TRUE
~WIND_GET(hand_win&(arbre&),4,xf&,yf&,lf&,hf&)
~WIND_GET(hand_win&(arbre&),11,rx&,ry&,rl&,rh&)
ENDIF
'
IF aff!(arbre&)=TRUE OR arbre&>11 OR arbre&=5
IF arbre&>11 OR arbre&=5
xf&=screenx&
yf&=screeny&
lf&=screenl&
hf&=screenh&
rx&=xf&
ry&=yf&
rl&=lf&
rh&=hf&
ENDIF
control
WHILE rl&<>0 AND rh&<>0
IF RC_INTERSECT(xf&,yf&,lf&,hf&,rx&,ry&,rl&,rh&)
  ~OBJC_DRAW(adtree%(arbre&),fils&,3,rx&,ry&,rl&,rh&)
ENDIF
IF arbre&>11 OR arbre&=5
  rl&=0
  rh&=0
ELSE
  ~WIND_GET(hand_win&(arbre&),12,rx&,ry&,rl&,rh&)
ENDIF
WEND
uncontrol
ENDIF
RETURN
> PROCEDURE copy_string(arbre&,fils&,str&)
OB_SPEC(adtree%(arbre&),fils&)=OB_SPEC(adtree%(alert_tree&),str&)
black_white(arbre&,fils&,-1)
RETURN
> PROCEDURE obj_enable(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BCLR(OB_STATE(adtree%(arbre&),fils&),3)
RETURN
> PROCEDURE obj_disable(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BSET(OB_STATE(adtree%(arbre&),fils&),3)
RETURN
> PROCEDURE obj_select(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BSET(OB_STATE(adtree%(arbre&),fils&),0)
RETURN
> PROCEDURE obj_deselect(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BCLR(OB_STATE(adtree%(arbre&),fils&),0)
RETURN
> PROCEDURE obj_check(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BSET(OB_STATE(adtree%(arbre&),fils&),2)
RETURN
> PROCEDURE obj_uncheck(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BCLR(OB_STATE(adtree%(arbre&),fils&),2)
RETURN
> PROCEDURE obj_set_editable(arbre&,fils&,editable!)
IF editable!
OB_FLAGS(adtree%(arbre&),fils&)=BSET(OB_FLAGS(adtree%(arbre&),fils&),3)
ELSE
OB_FLAGS(adtree%(arbre&),fils&)=BCLR(OB_FLAGS(adtree%(arbre&),fils&),3)
ENDIF
RETURN
> PROCEDURE obj_hide(arbre&,fils&)
OB_FLAGS(adtree%(arbre&),fils&)=BSET(OB_FLAGS(adtree%(arbre&),fils&),7)
RETURN
> PROCEDURE obj_unhide(arbre&,fils&)
OB_FLAGS(adtree%(arbre&),fils&)=BCLR(OB_FLAGS(adtree%(arbre&),fils&),7)
RETURN
> FUNCTION tst_hidden(arbre&,fils&)
$F%
RETURN BTST(OB_FLAGS(adtree%(arbre&),fils&),7)
ENDFUNC
> FUNCTION tst_selected(arbre&,fils&)
$F%
RETURN BTST(OB_STATE(adtree%(arbre&),fils&),0)
ENDFUNC
> FUNCTION tst_enabled(arbre&,fils&)
$F%
RETURN NOT BTST(OB_STATE(adtree%(arbre&),fils&),3)
ENDFUNC
> FUNCTION obj_hslide(arbre&,fils&,fiston&)
$F%
'
obj_slide_old_pos&=obj_slide_pos&
WHILE mo_c&=1
obj_slide_pos&=GRAF_SLIDEBOX(adtree%(arbre&),fils&,fiston&,0)
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WEND
obj_slide_pos&=MAX(0,MIN((OB_W(adtree%(arbre&),fils&)*obj_slide_pos&)/1000,MAX(1,SUB(OB_W(adtree%(arbre&),fils&),OB_W(adtree%(arbre&),fiston&)))))
IF obj_slide_pos&>0
ADD obj_slide_pos&,5
ENDIF
IF fiston&>SUCC(fils&)
obj_slide_pos&=MAX(10,obj_slide_pos&)
ENDIF
IF obj_slide_pos&<>obj_slide_old_pos&
OB_X(adtree%(arbre&),fiston&)=SUB(obj_slide_pos&,5)
IF aff!(arbre&)
black_white(arbre&,fils&,-1)
ENDIF
ENDIF
'
RETURN obj_slide_pos&
ENDFUNC
> PROCEDURE obj_set_template(arbre&,fils&,template$)
CHAR{{ADD(OB_SPEC(adtree%(arbre&),fils&),4)}}=template$
INT{ADD(OB_SPEC(adtree%(arbre&),fils&),26)}=SUCC(LEN(template$))
RETURN
> PROCEDURE obj_set_valid(arbre&,fils&,valid$)
CHAR{{ADD(OB_SPEC(adtree%(arbre&),fils&),8)}}=valid$
INT{ADD(OB_SPEC(adtree%(arbre&),fils&),24)}=SUCC(LEN(valid$))
RETURN
'
> PROCEDURE dialog_about
'
obj_deselect(1,6)
'
control_form(1)
DO
form_result&=FORM_DO(adtree%(1),0)
LOOP UNTIL form_result&=6
uncontrol_form(1)
'
RETURN
'
> PROCEDURE dialog_shortcuts
result&=OBJC_FIND(adtree%(2),0,3,mo_x&,mo_y&)
SELECT result&
CASE 2,4,6,8,10,12,14,16,18,20
black_white(2,result&,1)
dummy$=@fsl_get_file$(8,kkcmd_path$+mask_all$,"DUMMY.PRG"+c0$)
IF @s_exist(dummy$)=FALSE
dummy$=""
ELSE
IF RIGHT$(dummy$)=c0$
dummy$=LEFT$(dummy$,PRED(LEN(dummy$)))
ENDIF
ENDIF
shortcuts$(DIV(SUB(result&,2),2))=dummy$
CHAR{{OB_SPEC(adtree%(2),result&)}}=RIGHT$(shortcuts$(DIV(SUB(result&,2),2)),30)
black_white(2,result&,0)
DEFAULT
win(2)
ENDSELECT
RETURN
> PROCEDURE shortcuts_set_all
LOCAL short_i&
'
FOR short_i&=0 TO 9
CHAR{{OB_SPEC(adtree%(2),ADD(MUL(short_i&,2),2))}}=RIGHT$(shortcuts$(short_i&),30)
NEXT short_i&
'
RETURN
'
> PROCEDURE dialog_new_folder(kk_win&)
LOCAL command_line$,command_pos&
'
CHAR{{OB_SPEC(adtree%(6),2)}}=""
obj_set_template(6,2,"________.___")
obj_set_valid(6,2,"fffffffffff")
obj_deselect(6,3)
obj_deselect(6,4)
obj_deselect(6,5)
'
control_form(6)
form_result&=0
DO
form_result&=FORM_DO(adtree%(6),0)
LOOP UNTIL form_result&>3
uncontrol_form(6)
'
IF form_result&=4
command_line$=CHAR{{OB_SPEC(adtree%(6),2)}}
'
IF LEN(command_line$)>0
IF RIGHT$(command_line$)="\"
command_line$=LEFT$(command_line$,PRED(LEN(command_line$)))
ENDIF
IF kk_win&=3
command_line$=CHAR{local_path0%}+command_line$+c0$
ELSE IF kk_win&=4
command_line$=CHAR{local_path1%}+command_line$+c0$
ENDIF
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF GEMDOS(57,L:V:command_line$)=0
IF @tst_selected(6,3)
command_line$=LEFT$(command_line$,PRED(LEN(command_line$)))
IF kk_win&=3
@local_set_win_info(3,command_line$+"\")
ELSE IF kk_win&=4
@local_set_win_info(4,command_line$+"\")
ENDIF
ENDIF
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
ENDIF
ENDIF
ENDIF
'
RETURN
'
> PROCEDURE dialog_informations(kk_win&,kk_selected&)
LOCAL info_pathname$,info_name$,info_ext$,info_attr&,info_pos&,info_longname!
LOCAL info_new_name$,info_new_pathname$,info_date%,info_ptr%
LOCAL fil_cursor&,fil_cursor_ptr%,info_nb_files&,info_nb_folders&,info_length%,info_i&
'
info_ptr%=@px(kk_win&,kk_selected&)
'
info_name$=@loc_get_name$(info_ptr%)
IF display_disk_icons!(kk_win&)
info_pathname$=info_name$+"\"+c0$
FOR info_i&=7 TO 14
obj_hide(7,info_i&)
NEXT info_i&
ELSE
IF kk_win&=3
info_pathname$=CHAR{local_path0%}+info_name$+c0$
ELSE IF kk_win&=4
info_pathname$=CHAR{local_path1%}+info_name$+c0$
ENDIF
FOR info_i&=7 TO 14
obj_unhide(7,info_i&)
NEXT info_i&
ENDIF
'
SELECT @loc_get_type(info_ptr%)
CASE 1
obj_hide(7,3)
obj_hide(7,4)
CHAR{OB_SPEC(adtree%(7),6)}=LEFT$(STR$(@loc_get_length(info_ptr%))+str_octets$,22)
CASE 2,11,12
obj_unhide(7,3)
obj_unhide(7,4)
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
fil_set_type(record_buffer%(1),2)
fil_set_listed(record_buffer%(1),FALSE)
IF display_disk_icons!(kk_win&)
fil_set_name(record_buffer%(1),@loc_get_name$(info_ptr%))
ELSE
IF kk_win&=3
fil_set_name(record_buffer%(1),CHAR{local_path0%}+@loc_get_name$(info_ptr%))
ELSE IF kk_win&=4
fil_set_name(record_buffer%(1),CHAR{local_path1%}+@loc_get_name$(info_ptr%))
ENDIF
ENDIF
record_insert(1,head_nbline&(1))
'
@mouse_busy
fil_cursor&=0
WHILE fil_cursor&<head_nbline&(1)
fil_cursor_ptr%=@px(1,fil_cursor&)
IF @fil_get_type(fil_cursor_ptr%)=2 AND @fil_is_listed(fil_cursor_ptr%)=FALSE
@local_add_files_in_directories(ADD(fil_cursor_ptr%,6))
@fil_set_listed(@px(1,fil_cursor&),TRUE)
ENDIF
INC fil_cursor&
WEND
@mouse_free
'
IF display_disk_icons!(kk_win&)
record_delete(1,0)
ENDIF
'
FOR fil_cursor&=PRED(head_nbline&(1)) TO 0 STEP -1
fil_cursor_ptr%=@px(1,fil_cursor&)
SELECT @fil_get_type(fil_cursor_ptr%)
CASE 1
INC info_nb_files&
ADD info_length%,@fil_get_length(fil_cursor_ptr%)
CASE 2
INC info_nb_folders&
ENDSELECT
NEXT fil_cursor&
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
CHAR{OB_SPEC(adtree%(7),4)}=LEFT$(STR$(info_nb_files&)+str_files$+STR$(info_nb_folders&)+str_folders$,22)
CHAR{OB_SPEC(adtree%(7),6)}=LEFT$(STR$(info_length%)+str_octets$,22)
'
ENDSELECT
'
IF display_disk_icons!(kk_win&)=FALSE
'
info_date%=@loc_get_date(info_ptr%)
CHAR{OB_SPEC(adtree%(7),8)}=LEFT$(@get_troll_date$(info_date%,TRUE)+" "+@get_troll_time$(info_date%,TRUE),22)
'
info_pos&=RINSTR(info_name$,".")
IF info_pos&>0
info_ext$=MID$(info_name$,SUCC(info_pos&))
info_name$=LEFT$(info_name$,PRED(info_pos&))
ELSE
info_ext$=""
ENDIF
IF LEN(info_name$)>8
info_longname!=TRUE
obj_set_template(7,2,STRING$(22,"_"))
obj_set_valid(7,2,STRING$(22,"f"))
CHAR{{OB_SPEC(adtree%(7),2)}}=LEFT$(info_name$+"."+info_ext$,22)
ELSE
info_longname!=FALSE
obj_set_template(7,2,"________.___")
obj_set_valid(7,2,"fffffffffff")
IF LEN(info_name$)<8
info_name$=info_name$+STRING$(SUB(8,LEN(info_name$))," ")
ENDIF
CHAR{{OB_SPEC(adtree%(7),2)}}=info_name$+info_ext$
ENDIF
info_name$=TRIM$(info_name$)+"."+info_ext$
'
obj_deselect(7,10)
obj_deselect(7,11)
obj_deselect(7,12)
obj_deselect(7,13)
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF magic! OR mint!
IF GEMDOS(300,W:0,L:V:info_pathname$,L:xattr_buf%)=0
info_attr&=INT{ADD(xattr_buf%,40)}
ENDIF
ELSE
info_attr%=GEMDOS(67,L:V:info_pathname$,W:0,W:0)
info_attr&=CARD(info_attr%)
ENDIF
IF BTST(info_attr&,0)=TRUE
obj_select(7,10)
ENDIF
IF BTST(info_attr&,1)=TRUE
obj_select(7,11)
ENDIF
IF BTST(info_attr&,5)=TRUE
obj_select(7,12)
ENDIF
IF BTST(info_attr&,2)=TRUE
obj_select(7,13)
ENDIF
'
ELSE
'
info_longname!=FALSE
obj_set_template(7,2,"_")
obj_set_valid(7,2,"F")
CHAR{{OB_SPEC(adtree%(7),2)}}=LEFT$(info_name$,1)
'
ENDIF
'
obj_deselect(7,14)
obj_deselect(7,15)
control_form(7)
form_result&=0
DO
form_result&=FORM_DO(adtree%(7),0)
LOOP UNTIL form_result&>13
uncontrol_form(7)
'
IF form_result&=14
IF info_longname!
info_new_name$=CHAR{{OB_SPEC(adtree%(7),2)}}
ELSE
info_new_name$=CHAR{{OB_SPEC(adtree%(7),2)}}
IF LEN(info_new_name$)>8
info_new_ext$=MID$(info_new_name$,9)
info_new_name$=LEFT$(info_new_name$,8)
ELSE
info_new_ext$=""
ENDIF
info_new_name$=TRIM$(info_new_name$)
IF LEN(info_new_ext$)>0
info_new_name$=info_new_name$+"."+TRIM$(info_new_ext$)
ENDIF
ENDIF
'
IF @tst_selected(7,10)
info_attr&=BSET(info_attr&,0)
ELSE
info_attr&=BCLR(info_attr&,0)
ENDIF
IF @tst_selected(7,11)
info_attr&=BSET(info_attr&,1)
ELSE
info_attr&=BCLR(info_attr&,1)
ENDIF
IF @tst_selected(7,12)
info_attr&=BSET(info_attr&,5)
ELSE
info_attr&=BCLR(info_attr&,5)
ENDIF
IF @tst_selected(7,13)
info_attr&=BSET(info_attr&,2)
ELSE
info_attr&=BCLR(info_attr&,2)
ENDIF
~GEMDOS(67,L:V:info_pathname$,W:1,W:info_attr&)
'
IF info_name$<>info_new_name$
IF kk_win&=3
info_new_pathname$=CHAR{local_path0%}+info_new_name$+c0$
ELSE IF kk_win&=4
info_new_pathname$=CHAR{local_path1%}+info_new_name$+c0$
ENDIF
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(86,W:0,L:V:info_pathname$,L:V:info_new_pathname$)
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
ENDIF
ENDIF
'
RETURN
'
> PROCEDURE dialog_local(kk_win&)
result&=OBJC_FIND(adtree%(kk_win&),0,3,mo_x&,mo_y&)
SELECT result&
CASE 1
@local_get_drives
dummy&=@pop_up_with_slider(kk_win&,1,6,FALSE,FALSE,4)
IF dummy&>-1
local_drive_index&(kk_win&)=@local_get_drive_index(dummy&)
CHAR{{OB_SPEC(adtree%(kk_win&),1)}}=" "+loc_drive$(local_drive_index&(kk_win&))+": "
@local_set_win_info(kk_win&,loc_drive$(local_drive_index&(kk_win&))+":\")
black_white(kk_win&,1,-1)
ENDIF
CASE 2
@local_get_drives
dummy&=@pop_up_next_value(6,local_drive_index&(kk_win&),FALSE)
IF dummy&>-1
local_drive_index&(kk_win&)=dummy&
CHAR{{OB_SPEC(adtree%(kk_win&),1)}}=" "+loc_drive$(local_drive_index&(kk_win&))+": "
@local_set_win_info(kk_win&,loc_drive$(local_drive_index&(kk_win&))+":\")
black_white(kk_win&,1,-1)
ENDIF
CASE 3
'
IF display_disk_icons!(kk_win&)
'
obj_enable(17,1)
obj_disable(17,2)
obj_disable(17,3)
obj_disable(17,5)
obj_disable(17,7)
obj_disable(17,9)
'
IF record_nb_selected&(kk_win&)=1 AND record_selected&(kk_win&)>-1
obj_enable(17,2)
IF (kk_win&=3 AND display_disk_icons!(4)=FALSE) OR (kk_win&=4 AND display_disk_icons!(3)=FALSE)
obj_enable(17,5)
obj_enable(17,7)
ENDIF
ENDIF
'
ELSE
obj_disable(17,5)
obj_disable(17,7)
obj_disable(17,9)
'
IF record_nb_selected&(kk_win&)>0
IF record_nb_selected&(kk_win&)=1 AND record_selected&(kk_win&)=0
ELSE
obj_enable(17,5)
obj_enable(17,7)
obj_enable(17,9)
ENDIF
ENDIF
'
IF record_nb_selected&(kk_win&)=1 AND record_selected&(kk_win&)>0
obj_enable(17,2)
ELSE
obj_disable(17,2)
ENDIF
'
ENDIF
'
SELECT @pop_up(kk_win&,3,17,FALSE)
CASE 0 ! refresh
IF display_disk_icons!(kk_win&)
@local_get_disks_list(kk_win&)
ELSE
@local_get_directory(kk_win&)
ENDIF
CASE 1 ! informations
@dialog_informations(kk_win&,record_selected&(kk_win&))
CASE 2 ! new folder
IF display_disk_icons!(kk_win&)=FALSE
@dialog_new_folder(kk_win&)
ENDIF
CASE 4 ! copy
IF (kk_win&=3 AND display_disk_icons!(4)=FALSE) OR (kk_win&=4 AND display_disk_icons!(3)=FALSE)
@local_copy_files(kk_win&,FALSE)
ENDIF
CASE 6 ! move
IF (kk_win&=3 AND display_disk_icons!(4)=FALSE) OR (kk_win&=4 AND display_disk_icons!(3)=FALSE)
@local_copy_files(kk_win&,TRUE)
ENDIF
CASE 8 ! delete
IF display_disk_icons!(kk_win&)=FALSE
@local_delete_files(kk_win&)
ENDIF
ENDSELECT
CASE 8 TO 13
field_x&(kk_win&,SUB(result&,field_bar_obj&(kk_win&)))=@obj_hslide(kk_win&,field_bar_obj&(kk_win&),result&)
set_fields_width(kk_win&)
force_update(kk_win&)
DEFAULT
IF (m_touche&=1 OR m_touche&=2) AND display_disk_icons!(kk_win&)=FALSE
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
IF dummy&>-1
IF record_nb_selected&(kk_win&)=0
'
record_selected&(kk_win&)=dummy&
record_select(kk_win&,record_selected&(kk_win&))
INC record_nb_selected&(kk_win&)
'
ELSE IF record_nb_selected&(kk_win&)>0
'
record_select(kk_win&,dummy&)
IF @is_record_selected(kk_win&,dummy&)
record_selected&(kk_win&)=MIN(dummy&,record_selected&(kk_win&))
INC record_nb_selected&(kk_win&)
ELSE
DEC record_nb_selected&(kk_win&)
ENDIF
'
ENDIF
record_nb_selected&(kk_win&)=MAX(0,MIN(record_nb_selected&(kk_win&),head_nbline&(kk_win&)))
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
ENDIF
ELSE IF m_touche&=8 AND display_disk_icons!(kk_win&)=FALSE
IF record_nb_selected&(kk_win&)=1 AND record_selected&(kk_win&)>0
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
IF dummy&>0
IF dummy&>record_selected&(kk_win&)
DO
INC record_nb_selected&(kk_win&)
record_select(kk_win&,dummy&)
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
DEC dummy&
LOOP UNTIL dummy&=record_selected&(kk_win&)
ELSE IF dummy&<record_selected&(kk_win&)
DO
INC record_nb_selected&(kk_win&)
record_select(kk_win&,dummy&)
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
INC dummy&
LOOP UNTIL dummy&=record_selected&(kk_win&)
ENDIF
ENDIF
ENDIF
ELSE
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
IF dummy&>-1
IF @is_record_selected(kk_win&,dummy&)
delay
IF record_nb_selected&(kk_win&)=1
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
ELSE
get_window_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
ENDIF
delay
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,mo_kbd&)
IF mo_c&>0
dummy&=@local_drag_selection(kk_win&,sx&,sy&,sl&,sh&,mo_x&,mo_y&,mov_win&)
IF dummy&=1
IF mo_kbd&=0
  ~GRAF_MKSTATE(dummy&,dummy&,dummy&,mo_kbd&)
ENDIF
IF display_disk_icons!(4)=FALSE
  local_copy_files(3,BTST(mo_kbd&,2))
ENDIF
ELSE IF dummy&=-1
IF mo_kbd&=0
  ~GRAF_MKSTATE(dummy&,dummy&,dummy&,mo_kbd&)
ENDIF
IF display_disk_icons!(3)=FALSE
  local_copy_files(4,BTST(mo_kbd&,2))
ENDIF
ENDIF
ELSE
@local_mouse_select(kk_win&)
ENDIF
ELSE
delay
delay
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,mo_kbd&)
IF mo_c&>0 AND display_disk_icons!(kk_win&)=FALSE
~@local_rubber_selection(kk_win&,mo_x&,mo_y&)
ELSE
@local_mouse_select(kk_win&)
ENDIF
ENDIF
ELSE
@local_mouse_select(kk_win&)
ENDIF
ENDIF
ENDSELECT
RETURN
> PROCEDURE local_mouse_select(kk_win&)
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
IF dummy&>-1
IF record_nb_selected&(kk_win&)=0
'
record_select(kk_win&,dummy&)
record_selected&(kk_win&)=dummy&
record_nb_selected&(kk_win&)=1
'
get_record_xywh(kk_win&,record_selected&(kk_win&),sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,record_selected&(kk_win&),sx&,sy&,sl&,sh&)
'
ELSE IF record_nb_selected&(kk_win&)=1 OR display_disk_icons!(kk_win&)=TRUE
'
old_record_selected&=record_selected&(kk_win&)
record_select(kk_win&,record_selected&(kk_win&))
IF record_selected&(kk_win&)=dummy&
dummy&=-1
record_nb_selected&(kk_win&)=0
ELSE
record_nb_selected&(kk_win&)=1
ENDIF
'
record_selected&(kk_win&)=dummy&
record_select(kk_win&,record_selected&(kk_win&))
get_record_xywh(kk_win&,record_selected&(kk_win&),sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,record_selected&(kk_win&),sx&,sy&,sl&,sh&)
get_record_xywh(kk_win&,old_record_selected&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,old_record_selected&,sx&,sy&,sl&,sh&)
'
ELSE IF record_nb_selected&(kk_win&)>1
'
record_deselect_all(kk_win&)
'
record_selected&(kk_win&)=dummy&
record_select(kk_win&,record_selected&(kk_win&))
record_nb_selected&(kk_win&)=1
force_update(kk_win&)
'
ENDIF
ELSE IF OBJC_FIND(adtree%(kk_win&),0,0,mo_x&,mo_y&)<>-1
win(kk_win&)
ELSE
@local_deselect_all_files(kk_win&)
ENDIF
RETURN
> FUNCTION local_drag_selection(kk_win&,mov_x&,mov_y&,mov_l&,mov_h&,VAR mo_x&,mo_y&,mov_win&)
$F%
'
~WIND_UPDATE(1)
~WIND_UPDATE(3)
~GRAF_MOUSE(4,0)
~GRAF_DRAGBOX(mov_l&,mov_h&,mov_x&,mov_y&,screenx&,screeny&,screenl&,screenh&)
~GRAF_MKSTATE(mo_x&,mo_y&,dummy&,dummy&)
mov_win&=WIND_FIND(mo_x&,mo_y&)
~GRAF_MOUSE(0,0)
~WIND_UPDATE(0)
~WIND_UPDATE(2)
'
IF mov_win&=hand_win&(4) AND win!(4)=TRUE AND kk_win&=3
RETURN 1
ELSE IF mov_win&=hand_win&(3) AND win!(3)=TRUE AND kk_win&=4
RETURN -1
ENDIF
RETURN 0
ENDFUNC
> FUNCTION local_rubber_selection(kk_win&,VAR mo_x&,mo_y&)
$F%
LOCAL ro_x&,ro_y&,ro_start&,ro_end&
'
ro_x&=mo_x&
ro_y&=mo_y&
'
~WIND_UPDATE(1)
~WIND_UPDATE(3)
~GRAF_MOUSE(3,0)
'
~GRAF_RUBBERBOX(ro_x&,ro_y&,1,1)
~GRAF_MKSTATE(mo_x&,mo_y&,dummy&,mo_kbd&)
'
~GRAF_MOUSE(0,0)
~WIND_UPDATE(0)
~WIND_UPDATE(2)
'
IF BTST(mo_kbd&,0) OR BTST(mo_kbd&,1)
'
ro_start&=@get_record_under_cursor(kk_win&,ro_y&,ro_x&)
ro_end&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
'
IF ro_start&>-1
DO
IF @is_record_selected(kk_win&,ro_start&)=FALSE
INC record_nb_selected&(kk_win&)
record_select(kk_win&,ro_start&)
get_record_xywh(kk_win&,ro_start&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,ro_start&,sx&,sy&,sl&,sh&)
ENDIF
INC ro_start&
LOOP UNTIL ro_start&>ro_end&
ENDIF
'
ELSE
record_deselect_all(kk_win&)
record_selected&(kk_win&)=-1
record_nb_selected&(kk_win&)=0
'
ro_start&=@get_record_under_cursor(kk_win&,ro_y&,ro_x&)
ro_end&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
'
IF ro_start&>-1
DO
INC record_nb_selected&(kk_win&)
record_select(kk_win&,ro_start&)
INC ro_start&
LOOP UNTIL ro_start&>ro_end&
ENDIF
'
IF head_nbline&(kk_win&)>0
force_update(kk_win&)
ENDIF
'
ENDIF
'
RETURN 0
ENDFUNC
> PROCEDURE local_select_all_files(kk_win&)
LOCAL rec_i&
'
record_deselect_all(kk_win&)
record_selected&(kk_win&)=-1
record_nb_selected&(kk_win&)=0
'
IF head_nbline&(kk_win&)>0
'
FOR rec_i&=0 TO PRED(head_nbline&(kk_win&))
IF @loc_get_type(@px(kk_win&,rec_i&))=1 OR @loc_get_type(@px(kk_win&,rec_i&))=2
record_select(kk_win&,rec_i&)
IF record_selected&(kk_win&)=-1
record_selected&(kk_win&)=rec_i&
ENDIF
INC record_nb_selected&(kk_win&)
ENDIF
NEXT rec_i&
'
force_update(kk_win&)
ENDIF
'
RETURN
> PROCEDURE local_deselect_all_files(kk_win&)
'
record_deselect_all(kk_win&)
record_selected&(kk_win&)=-1
record_nb_selected&(kk_win&)=0
'
IF head_nbline&(kk_win&)>0
force_update(kk_win&)
ENDIF
'
RETURN
> PROCEDURE local_set_fonts(kk_win&)
'
IF display_disk_icons!(kk_win&)
'
~@vst_point(6)
IF screenh&<250
font_files_real_height&(kk_win&)=SUCC(MUL(OB_H(adtree%(nb_tree&),1),2))
ELSE
font_files_real_height&(kk_win&)=SUCC(OB_H(adtree%(nb_tree&),1))
ENDIF
'
ELSE
'
IF screenh&<250
SELECT display_files_font_size&
CASE 1
font_files_real_height&(kk_win&)=SUCC(@vst_point(6))
CASE 0,2,3
font_files_real_height&(kk_win&)=SUCC(@vst_point(9))
ENDSELECT
ELSE
SELECT display_files_font_size&
CASE 1
font_files_real_height&(kk_win&)=SUCC(@vst_point(6))
CASE 2
font_files_real_height&(kk_win&)=SUCC(@vst_point(9))
CASE 0,3
font_files_real_height&(kk_win&)=SUCC(@vst_point(13))
ENDSELECT
ENDIF
'
field_length_max_width&=@vqt_extent("100000000000MM",FALSE)
ENDIF
'
RETURN
> PROCEDURE local_set_display(kk_win&)
'
IF display_files_with_date!
obj_unhide(kk_win&,ADD(field_bar_obj&(kk_win&),2))
ELSE
obj_hide(kk_win&,ADD(field_bar_obj&(kk_win&),2))
ENDIF
IF display_files_with_length!
obj_unhide(kk_win&,ADD(field_bar_obj&(kk_win&),3))
ELSE
obj_hide(kk_win&,ADD(field_bar_obj&(kk_win&),3))
ENDIF
'
set_fields_display(kk_win&)
'
RETURN
> PROCEDURE local_set_win_info(kk_win&,str$)
IF local_path0%>0
IF kk_win&=3
CHAR{local_path0%}=LEFT$(str$,250)
IF win!(3)
~WIND_SET(hand_win&(3),2,CARD(SWAP(local_path0%)),CARD(local_path0%),0,0)
ENDIF
ELSE IF kk_win&=4
CHAR{local_path1%}=LEFT$(str$,250)
IF win!(4)
~WIND_SET(hand_win&(4),2,CARD(SWAP(local_path1%)),CARD(local_path1%),0,0)
ENDIF
ENDIF
IF LEN(str$)>0
display_disk_icons!(kk_win&)=FALSE
@local_set_fonts(kk_win&)
@local_set_drive(kk_win&)
@local_set_path(kk_win&)
@local_get_directory(kk_win&)
ELSE
display_disk_icons!(kk_win&)=TRUE
@local_set_fonts(kk_win&)
@local_get_disks_list(kk_win&)
ENDIF
ENDIF
RETURN
> PROCEDURE local_redraw(kk_win&,rline&)
LOCAL local_length%,local_date%
'
IF head_nbline&(kk_win&)>0
'
IF FRE()<4000
~FRE(0)
ENDIF
'
v_hide_c
vswr_mode(3)
vst_effect(0)
vst_color(1)
'
~WIND_GET(hand_win&(kk_win&),4,xf&,yf&,lf&,hf&)
ADD yf&,field_bar_h&(kk_win&)
SUB hf&,field_bar_h&(kk_win&)
'
IF display_disk_icons!(kk_win&)
'
ex&=ADD(xf&,1)
ed&=ADD(ex&,3)
ey&=yf&
el&=SUB(lf&,1)
eh&=hf&
IF rline&<0
ee&=ADD(yf&,2)
ef&=MIN(ADD(record_start&(kk_win&),SUCC(record_height&(kk_win&))),PRED(head_nbline&(kk_win&)))
ei&=record_start&(kk_win&)
ELSE
ee&=ADD(ADD(yf&,2),MUL(font_files_real_height&(kk_win&),SUB(rline&,record_start&(kk_win&))))
ef&=rline&
ei&=rline&
ENDIF
'
local_text_dy&=MAX(0,DIV(SUB(font_files_real_height&(kk_win&),6),2))
'
IF el&>0
IF RC_INTERSECT(rx&,ry&,rl&,rh&,ex&,ey&,el&,eh&)
clip(1,ex&,ey&,el&,eh&)
FOR j&=ei& TO ef&
yp&=MUL(font_files_real_height&(kk_win&),SUB(j&,ei&))
ptr%=@px(kk_win&,j&)
txt$=" "+@loc_get_name$(ptr%)
'
OB_X(adtree%(nb_tree&),0)=ed&
OB_Y(adtree%(nb_tree&),0)=PRED(ADD(ee&,yp&))
clip(0,ex&,ey&,el&,eh&)
SELECT @loc_get_type(ptr%)
CASE 11
~OBJC_DRAW(adtree%(nb_tree&),1,0,ex&,ey&,el&,eh&)
CASE 12
~OBJC_DRAW(adtree%(nb_tree&),2,0,ex&,ey&,el&,eh&)
ENDSELECT
clip(1,ex&,ey&,el&,eh&)
'
v_text(ADD(ed&,ADD(OB_W(adtree%(nb_tree&),1),2)),ADD(ADD(ee&,yp&),local_text_dy&),txt$,FALSE)
'
NEXT j&
clip(0,ex&,ey&,el&,eh&)
ENDIF
ENDIF
'
ELSE
'
ex&=ADD(xf&,SUB(field_x&(kk_win&,1),field_start&(kk_win&)))
ed&=ADD(ex&,3)
ey&=yf&
el&=field_w&(kk_win&,1)
eh&=hf&
IF rline&<0
ee&=ADD(yf&,2)
ef&=MIN(ADD(record_start&(kk_win&),SUCC(record_height&(kk_win&))),PRED(head_nbline&(kk_win&)))
ei&=record_start&(kk_win&)
ELSE
ee&=ADD(ADD(yf&,2),MUL(font_files_real_height&(kk_win&),SUB(rline&,record_start&(kk_win&))))
ef&=rline&
ei&=rline&
ENDIF
'
IF el&>0
IF RC_INTERSECT(rx&,ry&,rl&,rh&,ex&,ey&,el&,eh&)
clip(1,ex&,ey&,el&,eh&)
FOR j&=ei& TO ef&
yp&=MUL(font_files_real_height&(kk_win&),SUB(j&,ei&))
ptr%=@px(kk_win&,j&)
txt$=@loc_get_name$(ptr%)
ext$=UPPER$(RIGHT$(txt$,4))
IF @loc_get_type(ptr%)=2
txt$=" "+txt$
ELSE IF ext$=".PRG" OR ext$=".TOS" OR ext$=".TTP"
txt$=". "+txt$
ELSE
txt$="  "+txt$
ENDIF
'
IF @loc_get_type(ptr%)=4
vst_effect(&X10)
ENDIF
'
v_text(ed&,ADD(ee&,yp&),txt$,FALSE)
'
IF @loc_get_type(ptr%)=4
vst_effect(0)
ENDIF
'
NEXT j&
clip(0,ex&,ey&,el&,eh&)
ENDIF
ENDIF
'
ex&=ADD(xf&,SUB(field_x&(kk_win&,2),field_start&(kk_win&)))
ed&=ADD(ex&,3)
ey&=yf&
el&=MIN(field_w&(kk_win&,2),field_length_max_width&)
em&=SUB(el&,3)
eh&=hf&
'
vst_alignment(2,5)
'
IF el&>0 AND display_files_with_length!
IF RC_INTERSECT(rx&,ry&,rl&,rh&,ex&,ey&,el&,eh&)
clip(1,ex&,ey&,el&,eh&)
FOR j&=ei& TO ef&
yp&=MUL(font_files_real_height&(kk_win&),SUB(j&,ei&))
'
LET local_length%=@loc_get_length(@px(kk_win&,j&))
SELECT display_files_length_format&
CASE 0
IF local_length%>1073741824
LET local_length%=MAX(1,local_length%/1073741824)
txt$=STR$(local_length%)+str_kilobytes$
ELSE IF local_length%>1048576
LET local_length%=MAX(1,local_length%/1048576)
txt$=STR$(local_length%)+str_megabytes$
ELSE IF local_length%>1024
LET local_length%=MAX(1,local_length%/1024)
txt$=STR$(local_length%)+str_kilobytes$
ELSE IF local_length%>0
txt$=STR$(local_length%)+str_bytes$
ELSE
txt$=""
ENDIF
CASE 1
IF local_length%=0
txt$=""
ELSE
LET local_length%=MAX(1,local_length%/1024)
txt$=STR$(local_length%)+str_kilobytes$
ENDIF
CASE 2
IF local_length%>0
txt$=STR$(local_length%)+str_bytes$
ELSE
txt$=""
ENDIF
ENDSELECT
'
v_text(ADD(ed&,em&),ADD(ee&,yp&),txt$,FALSE)
NEXT j&
clip(0,ex&,ey&,el&,eh&)
ENDIF
ENDIF
'
vst_alignment(0,5)
'
ex&=ADD(xf&,SUB(field_x&(kk_win&,3),field_start&(kk_win&)))
ed&=ADD(ex&,3)
ey&=yf&
el&=field_w&(kk_win&,3)
eh&=hf&
'
IF el&>0 AND display_files_with_date!
IF RC_INTERSECT(rx&,ry&,rl&,rh&,ex&,ey&,el&,eh&)
clip(1,ex&,ey&,el&,eh&)
FOR j&=ei& TO ef&
yp&=MUL(font_files_real_height&(kk_win&),SUB(j&,ei&))
LET local_date%=@loc_get_date(@px(kk_win&,j&))
v_text(ed&,ADD(ee&,yp&),@get_troll_date$(local_date%,TRUE)+" "+@get_troll_time$(local_date%,TRUE),FALSE)
NEXT j&
clip(0,ex&,ey&,el&,eh&)
ENDIF
ENDIF
'
ENDIF
'
v_show_c
'
ENDIF
'
RETURN
> PROCEDURE local_get_drives
LOCAL dgetdrive%,dget_i&
'
dgetdrive%=BIOS(10)
loc_drive_nb&=0
'
FOR dget_i&=0 TO 25
IF BTST(dgetdrive%,dget_i&)
IF dget_i&<>1 OR display_disk_b!=TRUE
loc_drive$(loc_drive_nb&)=CHR$(ADD(65,dget_i&))
loc_drive&(loc_drive_nb&)=dget_i&
INC loc_drive_nb&
ENDIF
ENDIF
NEXT dget_i&
'
RETURN
> FUNCTION local_get_drive_index(number&)
FOR ipo&=0 TO PRED(loc_drive_nb&)
IF loc_drive&(ipo&)=number&
RETURN ipo&
ENDIF
NEXT ipo&
RETURN 0
ENDFUNC
> FUNCTION local_get_drive_index_from_letter(number|)
FOR ipo&=0 TO PRED(loc_drive_nb&)
IF loc_drive$(ipo&)=CHR$(number|)
RETURN ipo&
ENDIF
NEXT ipo&
RETURN 0
ENDFUNC
> PROCEDURE local_change_directory(kk_win&,loc_ptr%)
LOCAL d_folder$
'
IF loc_ptr%=0
IF kk_win&=3
d_folder$=CHAR{local_path0%}
ELSE IF kk_win&=4
d_folder$=CHAR{local_path1%}
ENDIF
d_folder$=LEFT$(d_folder$,PRED(LEN(d_folder$)))
IF RINSTR(d_folder$,"\")>0
d_folder$=LEFT$(d_folder$,RINSTR(d_folder$,"\"))
IF RIGHT$(d_folder$)=":"
d_folder$=d_folder$+"\"
ENDIF
local_set_win_info(kk_win&,d_folder$)
ELSE
local_set_win_info(kk_win&,"")
ENDIF
ELSE
IF kk_win&=3
local_set_win_info(3,CHAR{local_path0%}+@loc_get_name$(loc_ptr%)+"\")
ELSE IF kk_win&=4
local_set_win_info(4,CHAR{local_path1%}+@loc_get_name$(loc_ptr%)+"\")
ENDIF
ENDIF
'
RETURN
> PROCEDURE local_set_drive(kk_win&)
IF kk_win&=3
~GEMDOS(14,W:SUB(BYTE{local_path0%},65))
CHAR{{OB_SPEC(adtree%(3),1)}}=" "+CHR$(BYTE{local_path0%})+": "
black_white(3,1,-1)
ELSE IF kk_win&=4
~GEMDOS(14,W:SUB(BYTE{local_path1%},65))
CHAR{{OB_SPEC(adtree%(4),1)}}=" "+CHR$(BYTE{local_path1%})+": "
black_white(4,1,-1)
ENDIF
RETURN
> PROCEDURE local_set_path(kk_win&)
IF kk_win&=3
~GEMDOS(59;L:local_path0%)
ELSE IF kk_win&=4
~GEMDOS(59;L:local_path1%)
ENDIF
RETURN
> PROCEDURE local_get_directory(kk_win&)
$F%
LOCAL d_handle%,d_err%,d_test$,d_dta%,d_folder$,d_file$,d_type&,d_length%,d_date%
LOCAL loc_mem_i&,loc_mem_j&,loc_mem_k&,d_total_length%
'
IF shell_buf%>0
'
@mouse_busy
@record_close(kk_win&)
@record_open(kk_win&,64,record_len&(kk_win&),field_bar_obj&(kk_win&))
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF kk_win&=3
d_folder$=CHAR{local_path0%}
ELSE IF kk_win&=4
d_folder$=CHAR{local_path1%}
ENDIF
d_folder$=LEFT$(d_folder$,PRED(LEN(d_folder$)))
loc_add_ptr%=record_buffer%(kk_win&)
'
IF (magic!=TRUE OR mint!=TRUE) AND xattr_buf%>0
'
d_handle%=GEMDOS(296,L:V:d_folder$,0)
IF d_handle%>0
'
WHILE GEMDOS(297,W:250,L:d_handle%,L:shell_buf%)=0
'
record_clear_buffer(kk_win&)
'
d_type&=-1
d_file$=CHAR{ADD(shell_buf%,4)}
'
IF d_file$<>"." AND d_file$<>".."
'
@loc_set_number(loc_add_ptr%,0)
@loc_set_name(loc_add_ptr%,d_file$)
'
d_test$=d_folder$+"\"+d_file$+c0$
IF GEMDOS(300,W:0,L:V:d_test$,L:xattr_buf%)=0
SELECT (INT{xattr_buf%} AND &HF000)
CASE &H4000
d_type&=2
CASE &H8000
d_type&=1
CASE &HE000
d_type&=3
ENDSELECT
'
@loc_set_type(loc_add_ptr%,d_type&)
@loc_set_length(loc_add_ptr%,LONG{ADD(xattr_buf%,16)})
IF d_type&=1
ADD d_total_length%,LONG{ADD(xattr_buf%,16)}
ENDIF
@loc_set_date(loc_add_ptr%,@gemdos_to_troll_timestamp(INT{ADD(xattr_buf%,30)},INT{ADD(xattr_buf%,28)}))
ENDIF
ENDIF
IF display_sort_discard_hidden_files!
IF LEFT$(d_file$)="."
d_type&=-1
ENDIF
ENDIF
'
IF d_type&>0
SELECT display_sort_type&
CASE 0
record_insert(kk_win&,head_nbline&(kk_win&))
CASE 1
record_insert(kk_win&,@local_sort_by_name(kk_win&,ADD(loc_add_ptr%,8)))
CASE 2
record_insert(kk_win&,@local_sort_by_length(kk_win&,@loc_get_length(loc_add_ptr%)))
CASE 3
record_insert(kk_win&,@local_sort_by_date(kk_win&,@loc_get_date(loc_add_ptr%)))
CASE 4
record_insert(kk_win&,@local_sort_by_type(kk_win&,ADD(loc_add_ptr%,4)))
ENDSELECT
ENDIF
'
WEND
'
~GEMDOS(299,L:d_handle%)
ENDIF
'
ELSE
'
IF kk_win&=3
d_folder$=CHAR{local_path0%}+"*.*"+c0$
ELSE IF kk_win&=4
d_folder$=CHAR{local_path1%}+"*.*"+c0$
ENDIF
d_err%=GEMDOS(78,L:V:d_folder$,W:&X110111)
DO
EXIT IF d_err%<>0
d_dta%=FGETDTA()
'
record_clear_buffer(kk_win&)
'
d_type&=-1
d_file$=CHAR{ADD(d_dta%,30)}
'
IF d_file$<>"." AND d_file$<>".."
'
@loc_set_number(loc_add_ptr%,0)
@loc_set_name(loc_add_ptr%,d_file$)
'
IF BTST(BYTE{ADD(d_dta%,21)},4)
d_type&=2
ELSE IF BTST(BYTE{ADD(d_dta%,21)},1)
IF display_sort_discard_hidden_files!
d_type&=-1
ELSE
d_type&=4
ENDIF
ELSE
d_type&=1
ENDIF
'
@loc_set_type(loc_add_ptr%,d_type&)
@loc_set_length(loc_add_ptr%,LONG{ADD(d_dta%,26)})
IF d_type&=1
ADD d_total_length%,LONG{ADD(d_dta%,26)}
ENDIF
@loc_set_date(loc_add_ptr%,@gemdos_to_troll_timestamp(INT{ADD(d_dta%,24)},INT{ADD(d_dta%,22)}))
ENDIF
IF display_sort_discard_hidden_files!
IF LEFT$(d_file$)="."
d_type&=-1
ENDIF
ENDIF
'
IF d_type&>0
SELECT display_sort_type&
CASE 0
record_insert(kk_win&,head_nbline&(kk_win&))
CASE 1
record_insert(kk_win&,@local_sort_by_name(kk_win&,ADD(loc_add_ptr%,8)))
CASE 2
record_insert(kk_win&,@local_sort_by_length(kk_win&,@loc_get_length(loc_add_ptr%)))
CASE 3
record_insert(kk_win&,@local_sort_by_date(kk_win&,@loc_get_date(loc_add_ptr%)))
CASE 4
record_insert(kk_win&,@local_sort_by_type(kk_win&,ADD(loc_add_ptr%,4)))
ENDSELECT
ENDIF
'
d_err%=GEMDOS(79)
LOOP
'
IF kk_win&=3
d_folder$=CHAR{local_path0%}
ELSE IF kk_win&=4
d_folder$=CHAR{local_path1%}
ENDIF
d_folder$=LEFT$(d_folder$,PRED(LEN(d_folder$)))
ENDIF
'
IF display_sort_folder_at_top!
IF head_nbline&(kk_win&)>0
LET loc_mem_i&=0
LET loc_mem_k&=PRED(head_nbline&(kk_win&))
IF loc_mem_k&>loc_mem_i&
FOR loc_mem_j&=loc_mem_i& TO loc_mem_k&
IF @loc_get_type(@px(kk_win&,loc_mem_j&))=2
IF loc_mem_i&<loc_mem_j&
record_clear_buffer(kk_win&)
BMOVE @px(kk_win&,loc_mem_j&),record_buffer%(kk_win&),record_len&(kk_win&)
record_delete(kk_win&,loc_mem_j&)
record_insert(kk_win&,loc_mem_i&)
INC loc_mem_i&
ELSE IF loc_mem_i&=loc_mem_j&
INC loc_mem_i&
ENDIF
ENDIF
NEXT loc_mem_j&
ENDIF
ENDIF
ENDIF
'
IF kk_win&=3 AND win!(3)=TRUE
CHAR{local_info0%}=LEFT$(" "+STR$(head_nbline&(3))+str_files$+STR$(d_total_length%)+str_octets$,63)
~WIND_SET(hand_win&(3),3,CARD(SWAP(local_info0%)),CARD(local_info0%),0,0)
ELSE IF kk_win&=4 AND win!(4)=TRUE
CHAR{local_info1%}=LEFT$(" "+STR$(head_nbline&(4))+str_files$+STR$(d_total_length%)+str_octets$,63)
~WIND_SET(hand_win&(4),3,CARD(SWAP(local_info1%)),CARD(local_info1%),0,0)
ENDIF
'
' IF RIGHT$(d_folder$)<>":"
record_clear_buffer(kk_win&)
@loc_set_number(loc_add_ptr%,0)
@loc_set_type(loc_add_ptr%,0)
@loc_set_name(loc_add_ptr%,"..") ! adding CDUP
record_insert(kk_win&,0)
' ENDIF
@mouse_free
'
set_vslide(kk_win&)
record_start&(kk_win&)=@get_vslide(kk_win&)
force_update(kk_win&)
ENDIF
RETURN
> PROCEDURE local_get_disks_list(kk_win&)
LOCAL dgetdrive%,dget_i&
'
dgetdrive%=BIOS(10)
loc_drive_nb&=0
'
@mouse_busy
@record_close(kk_win&)
@record_open(kk_win&,64,record_len&(kk_win&),field_bar_obj&(kk_win&))
'
IF FRE()<4000
~FRE(0)
ENDIF
'
loc_add_ptr%=record_buffer%(kk_win&)
'
FOR dget_i&=0 TO 25
IF BTST(dgetdrive%,dget_i&)
IF dget_i&<>1 OR display_disk_b!=TRUE
'
record_clear_buffer(kk_win&)
'
@loc_set_number(loc_add_ptr%,0)
@loc_set_name(loc_add_ptr%,CHR$(ADD(65,dget_i&))+":")
IF dget_i&<2
@loc_set_type(loc_add_ptr%,11)
ELSE
@loc_set_type(loc_add_ptr%,12)
ENDIF
@loc_set_length(loc_add_ptr%,0)
'
record_insert(kk_win&,head_nbline&(kk_win&))
'
ENDIF
ENDIF
NEXT dget_i&
'
IF kk_win&=3 AND win!(3)=TRUE
CHAR{local_info0%}=""
~WIND_SET(hand_win&(3),3,CARD(SWAP(local_info0%)),CARD(local_info0%),0,0)
ELSE IF kk_win&=4 AND win!(4)=TRUE
CHAR{local_info1%}=""
~WIND_SET(hand_win&(4),3,CARD(SWAP(local_info1%)),CARD(local_info1%),0,0)
ENDIF
'
@mouse_free
'
set_vslide(kk_win&)
record_start&(kk_win&)=@get_vslide(kk_win&)
force_update(kk_win&)
'
RETURN
> FUNCTION local_sort_by_name(kk_win&,loc_name_ptr%)
$F%
LOCAL loc_ins_j&,loc_ins_exit!
'
IF head_nbline&(kk_win&)=0
RETURN 0
ENDIF
'
loc_ins_j&=0
loc_ins_exit!=FALSE
'
IF display_sort_reverse!
WHILE loc_ins_j&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @compare_name(loc_name_ptr%,ADD(@px(kk_win&,loc_ins_j&),8))=2
INC loc_ins_j&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ELSE
WHILE loc_ins_j&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @compare_name(loc_name_ptr%,ADD(@px(kk_win&,loc_ins_j&),8))=1
INC loc_ins_j&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ENDIF
'
RETURN MAX(0,MIN(loc_ins_j&,head_nbline&(kk_win&)))
ENDFUNC
> FUNCTION local_sort_by_length(kk_win&,loc_len%)
$F%
LOCAL loc_ins_i&,loc_ins_exit!
'
IF head_nbline&(kk_win&)=0
RETURN 0
ENDIF
'
loc_ins_i&=0
loc_ins_exit!=FALSE
'
IF display_sort_reverse!
WHILE loc_ins_i&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @loc_get_length(@px(kk_win&,loc_ins_i&))<loc_len%
INC loc_ins_i&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ELSE
WHILE loc_ins_i&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @loc_get_length(@px(kk_win&,loc_ins_i&))>loc_len%
INC loc_ins_i&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ENDIF
'
RETURN MAX(0,MIN(loc_ins_i&,head_nbline&(kk_win&)))
ENDFUNC
> FUNCTION local_sort_by_date(kk_win&,loc_dat%)
$F%
LOCAL loc_ins_i&,loc_ins_exit!
'
IF head_nbline&(kk_win&)=0
RETURN 0
ENDIF
'
loc_ins_i&=0
loc_ins_exit!=FALSE
'
IF display_sort_reverse!
WHILE loc_ins_i&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @loc_get_date(@px(kk_win&,loc_ins_i&))<loc_dat%
INC loc_ins_i&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ELSE
WHILE loc_ins_i&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @loc_get_date(@px(kk_win&,loc_ins_i&))>loc_dat%
INC loc_ins_i&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ENDIF
'
RETURN MAX(0,MIN(loc_ins_i&,head_nbline&(kk_win&)))
ENDFUNC
> FUNCTION local_sort_by_type(kk_win&,loc_type_ptr%)
$F%
LOCAL loc_ins_j&,loc_ins_exit!
'
IF head_nbline&(kk_win&)=0
RETURN 0
ENDIF
'
loc_ins_j&=0
loc_ins_exit!=FALSE
'
IF display_sort_reverse!
WHILE loc_ins_j&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @compare_name(loc_type_ptr%,ADD(@px(kk_win&,loc_ins_j&),4))=2
INC loc_ins_j&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ELSE
WHILE loc_ins_j&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @compare_name(loc_type_ptr%,ADD(@px(kk_win&,loc_ins_j&),4))=1
INC loc_ins_j&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ENDIF
'
RETURN MAX(0,MIN(loc_ins_j&,head_nbline&(kk_win&)))
ENDFUNC
> FUNCTION compare_name(str1_ptr%,str2_ptr%)
$F%
gscn_exit!=FALSE
gscn_ret&=0
gscn_i1%=str1_ptr%
gscn_i2%=str2_ptr%
'
IF display_sort_distinguish_case!
DO
gscn_b1|=major|(BYTE{gscn_i1%})
gscn_b2|=major|(BYTE{gscn_i2%})
IF gscn_b1|=0 OR gscn_b2|=0
gscn_exit!=TRUE
ELSE IF gscn_b1|>gscn_b2|
gscn_exit!=TRUE
gscn_ret&=1
ELSE IF gscn_b1|<gscn_b2|
gscn_exit!=TRUE
gscn_ret&=2
ELSE
INC gscn_i1%
INC gscn_i2%
ENDIF
LOOP UNTIL gscn_exit!
ELSE
DO
gscn_b1|=BYTE{gscn_i1%}
gscn_b2|=BYTE{gscn_i2%}
IF gscn_b1|=0 OR gscn_b2|=0
gscn_exit!=TRUE
ELSE IF gscn_b1|>gscn_b2|
gscn_exit!=TRUE
gscn_ret&=1
ELSE IF gscn_b1|<gscn_b2|
gscn_exit!=TRUE
gscn_ret&=2
ELSE
INC gscn_i1%
INC gscn_i2%
ENDIF
LOOP UNTIL gscn_exit!
ENDIF
'
RETURN gscn_ret&
ENDFUNC
> PROCEDURE local_copy_files(kk_win&,move_action!)
LOCAL fil_cursor&,fil_cursor_ptr%,fil_folders!,cmd_i&,cmd_del_ptr%,fil_rename!,all_disk!
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
copy_root&=0
copy_nb_files&=0
copy_nb_folders&=0
copy_total_length%=0
'
IF kk_win&=3
IF display_disk_icons!(3)
copy_root&=3
ELSE
copy_root&=SUCC(LEN(CHAR{local_path0%}))
ENDIF
receive_folder$=CHAR{local_path1%}
ELSE IF kk_win&=4
IF display_disk_icons!(4)
copy_root&=3
ELSE
copy_root&=SUCC(LEN(CHAR{local_path1%}))
ENDIF
receive_folder$=CHAR{local_path0%}
ENDIF
'
fil_folders!=FALSE
fil_rename!=FALSE
'
IF display_disk_icons!(kk_win&)
FOR ipo&=0 TO PRED(head_nbline&(kk_win&))
IF @is_record_selected(kk_win&,ipo&)
record_clear_buffer(1)
SELECT @loc_get_type(@px(kk_win&,ipo&))
CASE 11,12
fil_set_type(record_buffer%(1),2)
fil_set_listed(record_buffer%(1),FALSE)
fil_folders!=TRUE
all_disk!=TRUE
ENDSELECT
fil_set_name(record_buffer%(1),@loc_get_name$(@px(kk_win&,ipo&)))
record_insert(1,head_nbline&(1))
ENDIF
NEXT ipo&
fil_rename!=FALSE
ELSE
FOR ipo&=0 TO PRED(head_nbline&(kk_win&))
IF @is_record_selected(kk_win&,ipo&)
record_clear_buffer(1)
SELECT @loc_get_type(@px(kk_win&,ipo&))
CASE 2
fil_set_type(record_buffer%(1),2)
fil_set_listed(record_buffer%(1),FALSE)
fil_folders!=TRUE
CASE 1
fil_set_type(record_buffer%(1),1)
fil_set_listed(record_buffer%(1),TRUE)
fil_set_length(record_buffer%(1),@loc_get_length(@px(kk_win&,ipo&)))
ENDSELECT
IF kk_win&=3
fil_set_name(record_buffer%(1),CHAR{local_path0%}+@loc_get_name$(@px(kk_win&,ipo&)))
ELSE IF kk_win&=4
fil_set_name(record_buffer%(1),CHAR{local_path1%}+@loc_get_name$(@px(kk_win&,ipo&)))
ENDIF
record_insert(1,head_nbline&(1))
ENDIF
NEXT ipo&
'
IF BYTE{local_path0%}=BYTE{local_path1%} AND move_action!=TRUE AND GEMDOS(48)>=&H1500
fil_rename!=TRUE
ENDIF
ENDIF
'
IF fil_folders!
'
@mouse_busy
fil_cursor&=0
WHILE fil_cursor&<head_nbline&(1)
fil_cursor_ptr%=@px(1,fil_cursor&)
IF @fil_get_type(fil_cursor_ptr%)=2 AND @fil_is_listed(fil_cursor_ptr%)=FALSE
@local_add_files_in_directories(ADD(fil_cursor_ptr%,6))
@fil_set_listed(@px(1,fil_cursor&),TRUE)
ENDIF
INC fil_cursor&
WEND
@mouse_free
'
ENDIF
'
IF all_disk!
IF head_nbline&(1)>0
@record_delete(1,0)
ENDIF
ENDIF
'
IF head_nbline&(1)>0
IF fil_rename!
'
IF misc_confirm_delete!
proceed!=(@alert(1,19)=1)
ELSE
proceed!=TRUE
ENDIF
'
IF proceed!
@mouse_busy
@rename_files
'
FOR cmd_i&=PRED(head_nbline&(1)) TO 0 STEP -1
cmd_del_ptr%=@px(1,cmd_i&)
SELECT @fil_get_type(cmd_del_ptr%)
CASE 2
~GEMDOS(58,L:ADD(cmd_del_ptr%,6))
ENDSELECT
NEXT cmd_i&
@mouse_free
ENDIF
'
ELSE
'
FOR fil_cursor&=PRED(head_nbline&(1)) TO 0 STEP -1
fil_cursor_ptr%=@px(1,fil_cursor&)
SELECT @fil_get_type(fil_cursor_ptr%)
CASE 1
INC copy_nb_files&
ADD copy_total_length%,@fil_get_length(fil_cursor_ptr%)
CASE 2
INC copy_nb_folders&
ENDSELECT
NEXT fil_cursor&
'
obj_unhide(5,12)
obj_enable(5,12)
obj_deselect(5,12)
obj_unhide(5,13)
obj_deselect(5,13)
IF move_action!
obj_deselect(5,1)
obj_select(5,2)
ELSE
obj_select(5,1)
obj_deselect(5,2)
ENDIF
CHAR{{OB_SPEC(adtree%(5),4)}}=LEFT$(STR$(copy_nb_files&)+" / "+STR$(copy_total_length%)+str_octet$,31)
CHAR{{OB_SPEC(adtree%(5),6)}}=LEFT$(STR$(copy_nb_folders&),31)
CHAR{{OB_SPEC(adtree%(5),8)}}=""
OB_W(adtree%(5),10)=2
OB_W(adtree%(5),11)=2
'
proceed!=FALSE
IF misc_confirm_send!
control_form(5)
form_result&=0
DO
form_result&=FORM_DO(adtree%(5),0)
LOOP UNTIL form_result&=12 OR form_result&=13
IF form_result&=12
move_action!=@tst_selected(5,2)
obj_disable(5,12)
obj_deselect(5,12)
black_white(5,12,-1)
proceed!=TRUE
ELSE IF result&=13
obj_deselect(5,13)
proceed!=FALSE
ENDIF
ELSE
obj_hide(5,12)
obj_hide(5,13)
control_form(5)
proceed!=TRUE
ENDIF
'
IF proceed!=TRUE
@mouse_busy
'
@copy_files(move_action!)
'
IF move_action!
FOR cmd_i&=PRED(head_nbline&(1)) TO 0 STEP -1
cmd_del_ptr%=@px(1,cmd_i&)
SELECT @fil_get_type(cmd_del_ptr%)
CASE 2
~GEMDOS(58,L:ADD(cmd_del_ptr%,6))
ENDSELECT
NEXT cmd_i&
ENDIF
@mouse_free
ENDIF
'
uncontrol_form(5)
'
ENDIF
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
IF proceed!
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
ENDIF
ENDIF
'
RETURN
> PROCEDURE local_add_files_in_directories(enquiry_path%)
$F%
LOCAL d_handle%,d_err%,d_test$,d_folder$,d_dta%,d_file$,d_type&
'
IF shell_buf%>0
'
IF FRE()<4000
~FRE(0)
ENDIF
'
d_folder$=CHAR{enquiry_path%}+"\"
d_folder$=LEFT$(d_folder$,PRED(LEN(d_folder$)))
fil_add_ptr%=record_buffer%(1)
'
IF (magic!=TRUE OR mint!=TRUE) AND xattr_buf%>0
'
d_handle%=GEMDOS(296,L:V:d_folder$,0)
IF d_handle%>0
'
WHILE GEMDOS(297,W:250,L:d_handle%,L:shell_buf%)=0
'
record_clear_buffer(1)
'
d_type&=-1
d_file$=CHAR{ADD(shell_buf%,4)}
'
IF d_file$<>"." AND d_file$<>".."
'
@fil_set_name(fil_add_ptr%,CHAR{enquiry_path%}+"\"+d_file$)
'
d_test$=d_folder$+"\"+d_file$+c0$
IF GEMDOS(300,W:0,L:V:d_test$,L:xattr_buf%)=0
SELECT (INT{xattr_buf%} AND &HF000)
CASE &H4000
d_type&=2
@fil_set_type(fil_add_ptr%,2)
@fil_set_listed(fil_add_ptr%,FALSE)
CASE &H8000
d_type&=1
@fil_set_type(fil_add_ptr%,1)
@fil_set_listed(fil_add_ptr%,TRUE)
@fil_set_length(fil_add_ptr%,LONG{ADD(xattr_buf%,16)})
ENDSELECT
ENDIF
ENDIF
'
IF d_type&>0
record_insert(1,head_nbline&(1))
ENDIF
'
WEND
'
~GEMDOS(299,L:d_handle%)
ENDIF
'
ELSE
'
d_folder$=CHAR{enquiry_path%}+"\*.*"+c0$
d_err%=GEMDOS(78,L:V:d_folder$,W:&X110111)
DO
EXIT IF d_err%<>0
d_dta%=FGETDTA()
'
record_clear_buffer(1)
'
d_type&=-1
d_file$=CHAR{ADD(d_dta%,30)}
'
IF d_file$<>"." AND d_file$<>".."
'
@fil_set_name(fil_add_ptr%,CHAR{enquiry_path%}+"\"+d_file$)
'
IF BTST(BYTE{ADD(d_dta%,21)},1)
ELSE IF BTST(BYTE{ADD(d_dta%,21)},4)
d_type&=2
@fil_set_type(fil_add_ptr%,2)
@fil_set_listed(fil_add_ptr%,FALSE)
ELSE
d_type&=1
@fil_set_type(fil_add_ptr%,1)
@fil_set_listed(fil_add_ptr%,TRUE)
@fil_set_length(fil_add_ptr%,LONG{ADD(d_dta%,26)})
ENDIF
ENDIF
'
IF d_type&>0
record_insert(1,head_nbline&(1))
ENDIF
'
d_err%=GEMDOS(79)
LOOP
'
ENDIF
'
ENDIF
RETURN
> PROCEDURE local_delete_files(kk_win&)
LOCAL cmd_i&,cmd_del_ptr%,fil_folders!,fil_cursor&
'
IF misc_confirm_delete!
proceed!=(@alert(1,13)=1)
ELSE
proceed!=TRUE
ENDIF
'
IF proceed!
'
@mouse_busy
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
fil_folders!=FALSE
FOR ipo&=0 TO PRED(head_nbline&(kk_win&))
IF @is_record_selected(kk_win&,ipo&)
record_clear_buffer(1)
IF @loc_get_type(@px(kk_win&,ipo&))=2
fil_set_type(record_buffer%(1),2)
fil_set_listed(record_buffer%(1),FALSE)
fil_folders!=TRUE
ELSE
fil_set_type(record_buffer%(1),1)
fil_set_listed(record_buffer%(1),TRUE)
ENDIF
IF kk_win&=3
fil_set_name(record_buffer%(1),CHAR{local_path0%}+@loc_get_name$(@px(kk_win&,ipo&)))
ELSE IF kk_win&=4
fil_set_name(record_buffer%(1),CHAR{local_path1%}+@loc_get_name$(@px(kk_win&,ipo&)))
ENDIF
record_insert(1,head_nbline&(1))
ENDIF
NEXT ipo&
'
IF fil_folders!
'
@mouse_busy
fil_cursor&=0
WHILE fil_cursor&<head_nbline&(1)
fil_cursor_ptr%=@px(1,fil_cursor&)
IF @fil_get_type(fil_cursor_ptr%)=2 AND @fil_is_listed(fil_cursor_ptr%)=FALSE
@local_add_files_in_directories(ADD(fil_cursor_ptr%,6))
@fil_set_listed(@px(1,fil_cursor&),TRUE)
ENDIF
INC fil_cursor&
WEND
@mouse_free
'
ENDIF
'
IF head_nbline&(1)>0
FOR cmd_i&=PRED(head_nbline&(1)) TO 0 STEP -1
cmd_del_ptr%=@px(1,cmd_i&)
SELECT @fil_get_type(cmd_del_ptr%)
CASE 1
~GEMDOS(65,L:ADD(cmd_del_ptr%,6))
CASE 2
~GEMDOS(58,L:ADD(cmd_del_ptr%,6))
ENDSELECT
NEXT cmd_i&
ENDIF
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
@mouse_free
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
ENDIF
'
RETURN
'
> PROCEDURE rename_files
LOCAL rec_handle&,rec_i&,rec_ptr%
LOCAL receive_is_file!,receive_filename$,send_filename$
LOCAL rec_date%,rec_behavior&,rec_choice&,rec_remain&
LOCAL rec_drive&
'
receive_file_length%=0
receive_buffer_adr%=0
rec_drive&=SUB(ASC(LEFT$(receive_folder$)),64)
'
FOR rec_i&=0 TO PRED(head_nbline&(1))
'
rec_ptr%=@px(1,rec_i&)
receive_is_file!=FALSE
'
SELECT @fil_get_type(rec_ptr%)
CASE 1 ! file
IF @fil_get_length(rec_ptr%)>0
'
receive_is_file!=TRUE
'
ENDIF
CASE 2 ! folder, created emptied if not existing
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)
IF RIGHT$(receive_filename$)="\"
receive_filename$=LEFT$(receive_filename$,PRED(LEN(receive_filename$)))
ENDIF
receive_filename$=receive_filename$+c0$
IF @d_exist(receive_filename$)=FALSE
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(57,L:V:receive_filename$)
ENDIF
'
ENDSELECT
'
rec_behavior&=-1
receive_continue!=FALSE
'
IF receive_is_file!
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)+c0$
send_filename$=@fil_get_name$(rec_ptr%)+c0$
'
rec_handle&=GEMDOS(61,L:V:receive_filename$,W:0)
IF rec_handle&>0
~GEMDOS(62,W:rec_handle&)
'
IF misc_choice_existing_file_type&=0
'
CHAR{{OB_SPEC(adtree%(16),2)}}=LEFT$(@fil_get_name$(rec_ptr%),30)
rec_remain&=10
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
obj_deselect(16,7)
'
control_form(16)
form_result&=0
DO
evnt&=@ev_multi(&X100011,258,3,0,1000,mo_x&,mo_y&,mo_k&,dummy&,m_clavier&,mo_c&)
IF BTST(evnt&,0)
IF BYTE(m_clavier&)=13
black_white(16,7,1)
form_result&=8
ENDIF
ENDIF
IF BTST(evnt&,1)
IF mo_k&=1 AND mo_c&=1
delay
pop_result&=OBJC_FIND(adtree%(16),0,9,mo_x&,mo_y&)
IF pop_result&>2 AND pop_result&<8
IF @tst_enabled(16,pop_result&)
black_white(16,ADD(2,rec_choice&),0)
rec_choice&=SUB(pop_result&,2)
black_white(16,ADD(2,rec_choice&),1)
ENDIF
ELSE IF pop_result&=8
black_white(16,7,1)
form_result&=8
ENDIF
ENDIF
ENDIF
IF BTST(evnt&,5)
DEC rec_remain&
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
black_white(16,9,-1)
ENDIF
LOOP UNTIL form_result&=7 OR rec_remain&=0
uncontrol_form(16)
obj_deselect(16,7)
'
rec_behavior&=rec_choice&
ELSE
rec_behavior&=misc_choice_existing_file_type&
ENDIF
'
SELECT rec_behavior&
CASE 1 ! rewrite
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(65,L:V:send_filename$)
CASE 2 ! rename
receive_filename$=@get_other_name$(receive_filename$)
CASE 3 ! skip
receive_is_file!=FALSE
CASE 4 ! abort
receive_aborted!=TRUE
ENDSELECT
'
ENDIF
'
IF receive_is_file!=TRUE AND receive_aborted!=FALSE
receive_continue!=TRUE
ENDIF
'
IF receive_continue!=TRUE
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(86,W:0,L:V:send_filename$,L:V:receive_filename$)
ENDIF
ENDIF
EXIT IF receive_aborted!
NEXT rec_i&
'
RETURN
> PROCEDURE copy_files(move_action!)
LOCAL rec_handle&,rec_i&,rec_ptr%
LOCAL receive_is_file!,receive_filename$,send_filename$
LOCAL rec_date%,rec_behavior&,rec_choice&,rec_remain&
LOCAL rec_drive&
'
receive_file_length%=0
receive_buffer_adr%=0
rec_drive&=SUB(ASC(LEFT$(receive_folder$)),64)
'
FOR rec_i&=0 TO PRED(head_nbline&(1))
'
rec_ptr%=@px(1,rec_i&)
receive_is_file!=FALSE
'
CHAR{{OB_SPEC(adtree%(5),4)}}=LEFT$(STR$(copy_nb_files&)+" / "+STR$(copy_total_length%)+str_octets$+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),6)}}=LEFT$(STR$(copy_nb_folders&)+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),8)}}=LEFT$(RIGHT$(@fil_get_name$(rec_ptr%),31)+SPACE$(31),31)
black_white(5,4,-1)
black_white(5,6,-1)
black_white(5,8,-1)
'
SELECT @fil_get_type(rec_ptr%)
CASE 1 ! file
IF @fil_get_length(rec_ptr%)>0
'
receive_is_file!=TRUE
'
ELSE IF @fil_get_length(rec_ptr%)=0 ! just create empty if not exist
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)+c0$
IF @s_exist(receive_filename$)=FALSE
rec_handle&=GEMDOS(60,L:V:receive_filename$,W:0)
IF rec_handle&>0
IF misc_keep_date!
rec_date%=@to_gemdos_timedate(@fil_get_date(rec_ptr%))
~GEMDOS(87,L:V:rec_date%,W:rec_handle&,W:1)
ENDIF
~GEMDOS(62,W:rec_handle&)
ENDIF
ENDIF
IF move_action!
~GEMDOS(65,L:ADD(rec_ptr%,6))
ENDIF
DEC copy_nb_files&
'
ENDIF
CASE 2 ! folder, created emptied if not existing
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)
IF RIGHT$(receive_filename$)="\"
receive_filename$=LEFT$(receive_filename$,PRED(LEN(receive_filename$)))
ENDIF
receive_filename$=receive_filename$+c0$
IF @d_exist(receive_filename$)=FALSE
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(57,L:V:receive_filename$)
ENDIF
DEC copy_nb_folders&
'
ENDSELECT
'
rec_behavior&=-1
receive_continue!=FALSE
'
IF receive_is_file!
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)+c0$
send_filename$=@fil_get_name$(rec_ptr%)+c0$
'
rec_handle&=GEMDOS(61,L:V:receive_filename$,W:0)
IF rec_handle&>0
~GEMDOS(62,W:rec_handle&)
'
IF misc_choice_existing_file_type&=0
'
CHAR{{OB_SPEC(adtree%(16),2)}}=LEFT$(@fil_get_name$(rec_ptr%),30)
rec_remain&=10
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
obj_deselect(16,7)
'
uncontrol_form(5)
control_form(16)
form_result&=0
DO
evnt&=@ev_multi(&X100011,258,3,0,1000,mo_x&,mo_y&,mo_k&,dummy&,m_clavier&,mo_c&)
IF BTST(evnt&,0)
IF BYTE(m_clavier&)=13
black_white(16,7,1)
form_result&=8
ENDIF
ENDIF
IF BTST(evnt&,1)
IF mo_k&=1 AND mo_c&=1
delay
pop_result&=OBJC_FIND(adtree%(16),0,9,mo_x&,mo_y&)
IF pop_result&>2 AND pop_result&<8
IF @tst_enabled(16,pop_result&)
black_white(16,ADD(2,rec_choice&),0)
rec_choice&=SUB(pop_result&,2)
black_white(16,ADD(2,rec_choice&),1)
ENDIF
ELSE IF pop_result&=8
black_white(16,7,1)
form_result&=8
ENDIF
ENDIF
ENDIF
IF BTST(evnt&,5)
DEC rec_remain&
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
black_white(16,9,-1)
ENDIF
LOOP UNTIL form_result&=7 OR rec_remain&=0
uncontrol_form(16)
control_form(5)
obj_deselect(16,7)
'
rec_behavior&=rec_choice&
ELSE
rec_behavior&=misc_choice_existing_file_type&
ENDIF
'
SELECT rec_behavior&
CASE 1 ! rewrite
CASE 2 ! rename
receive_filename$=@get_other_name$(receive_filename$)
CASE 3 ! skip
receive_is_file!=FALSE
CASE 4 ! abort
receive_aborted!=TRUE
ENDSELECT
'
ENDIF
'
IF receive_is_file!=TRUE AND receive_aborted!=FALSE
receive_continue!=TRUE
ENDIF
'
IF receive_continue!=TRUE
'
receive_file_length%=@fil_get_length(rec_ptr%)
'
IF receive_buffer_adr%>0
@mxfree(receive_buffer_adr%)
receive_buffer_adr%=0
ENDIF
receive_buffer_adr%=@copy_mxalloc(receive_file_length%)
IF receive_buffer_adr%>0
'
IF @s_exist(receive_filename$)
~GEMDOS(65,L:V:receive_filename$)
ENDIF
rec_handle&=0
rec_handle&=GEMDOS(60,L:V:receive_filename$,W:0)
IF rec_handle&>0
~GEMDOS(62,W:rec_handle&)
ENDIF
'
IF @has_enough_space(rec_drive&,receive_file_length%)=TRUE
@copy(receive_buffer_adr%,receive_file_length%,send_filename$,receive_filename$)
'
IF FRE()<4000
~FRE(0)
ENDIF
'
rec_handle&=0
rec_handle&=GEMDOS(61,L:V:receive_filename$,W:0)
IF rec_handle&>0
~GEMDOS(66,L:0,W:rec_handle&,W:2)=receive_file_length%
IF misc_keep_date!
rec_date%=@to_gemdos_timedate(@fil_get_date(rec_ptr%))
~GEMDOS(87,L:V:rec_date%,W:rec_handle&,W:1)
ENDIF
~GEMDOS(62,W:rec_handle&)
ENDIF
IF move_action!
~GEMDOS(65,L:ADD(rec_ptr%,6))
ENDIF
DEC copy_nb_files&
'
ADD receive_saved_length%,receive_file_length%
'
OB_W(adtree%(5),10)=2
OB_W(adtree%(5),11)=MAX(2,PRED(OB_W(adtree%(5),9)*MIN(1,receive_saved_length%/MAX(1,copy_total_length%))))
black_white(5,9,-1)
ENDIF
ENDIF
ENDIF
ENDIF
EXIT IF receive_aborted!
NEXT rec_i&
'
IF receive_buffer_adr%>0
@mxfree(receive_buffer_adr%)
receive_buffer_adr%=0
ENDIF
'
RETURN
> FUNCTION get_other_name$(str$)
LOCAL ron_name$,ron_folder$,ron_ext$,ron_pos&,ron_final$
'
ron_pos&=RINSTR(str$,"\")
IF ron_pos&>0
ron_folder$=LEFT$(str$,ron_pos&)
str$=MID$(str$,SUCC(ron_pos&))
ENDIF
'
ron_pos&=RINSTR(str$,".")
IF ron_pos&
ron_name$=LEFT$(str$,PRED(ron_pos&))
ron_ext$=MID$(str$,SUCC(ron_pos&))
ENDIF
'
IF LEN(ron_name$)>8
ron_pos&=2
ron_final$=ron_folder$+ron_name$+"("+STR$(ron_pos&)+")."+ron_ext$
'
WHILE @s_exist(ron_final$)=TRUE
INC ron_pos&
IF FRE()<4000
~FRE(0)
ENDIF
ron_final$=ron_folder$+ron_name$+"("+STR$(ron_pos&)+")."+ron_ext$
WEND
ELSE
ron_pos&=2
ron_final$=ron_folder$+LEFT$(ron_name$,6)+"~"+STR$(ron_pos&)+"."+ron_ext$
'
WHILE @s_exist(ron_final$)=TRUE
INC ron_pos&
IF FRE()<4000
~FRE(0)
ENDIF
ron_final$=ron_folder$+LEFT$(ron_name$,SUB(7,LEN(STR$(ron_pos&))))+"~"+STR$(ron_pos&)+"."+ron_ext$
WEND
ENDIF
'
RETURN ron_final$
ENDFUNC
> FUNCTION has_enough_space(used_drive&,needed_space%)
LOCAL disk_free
'
IF misc_verify_free_space!
@clear_s
IF GEMDOS(54,L:s_adr%,W:used_drive&)=0
disk_free=LONG{s_adr%}*LONG{ADD(s_adr%,8)}*LONG{ADD(s_adr%,12)}
ELSE
disk_free=ADD(needed_space%,1000)
ENDIF
ELSE
disk_free=ADD(needed_space%,1000)
ENDIF
'
IF disk_free=<needed_space%
RETURN FALSE
ELSE
RETURN TRUE
ENDIF
ENDFUNC
> PROCEDURE copy(data_copy_adr%,data_copy_len%,source_filename$,target_filename$)
LOCAL data_copy_offset%,data_copy_min%
'
OB_W(adtree%(5),10)=2
black_white(5,10,-1)
'
IF copy_buffer_mode&=1
LET data_copy_offset%=0
WHILE data_copy_offset%<data_copy_len%
LET data_copy_min%=MIN(copy_buffer_length%,SUB(data_copy_len%,copy_buffer_length%))
copy_load_file_into_buffer(data_copy_adr%,source_filename$,data_copy_offset%,data_copy_min%)
copy_save_file_from_buffer(data_copy_adr%,target_filename$,data_copy_min%)
ADD data_copy_offset%,copy_buffer_length%
'
OB_W(adtree%(5),10)=MAX(2,PRED(OB_W(adtree%(5),9)*MIN(1,data_copy_offset%/MAX(1,data_copy_len%))))
black_white(5,10,-1)
WEND
ELSE
copy_load_file_into_buffer(data_copy_adr%,source_filename$,0,data_copy_len%)
copy_save_file_from_buffer(data_copy_adr%,target_filename$,data_copy_len%)
ENDIF
'
OB_W(adtree%(5),10)=PRED(OB_W(adtree%(5),9))
black_white(5,10,-1)
'
RETURN
> FUNCTION copy_mxalloc(copy_buffer_asked_length%)
$F%
'
copy_buffer_value%=384000
copy_buffer_length%=0
copy_buffer_mode&=0
copy_buffer_adr%=@mxalloc(SHL(ADD(SHR(copy_buffer_asked_length%,4),4),4),3)
'
IF copy_buffer_adr%>0
copy_buffer_length%=copy_buffer_asked_length%
ELSE
copy_buffer_mode&=1
copy_buffer_adr%=@mxalloc(ADD(copy_buffer_value%,4),3)
WHILE copy_buffer_adr%<1 AND copy_buffer_value%>0
INC copy_buffer_mode&
SUB copy_buffer_value%,19200
copy_buffer_adr%=@mxalloc(ADD(copy_buffer_value%,4),3)
WEND
IF copy_buffer_value%<38400
IF copy_buffer_adr%>0
@mxfree(copy_buffer_adr%)
ENDIF
copy_buffer_adr%=-1
ELSE
copy_buffer_length%=copy_buffer_value%
ENDIF
ENDIF
'
RETURN copy_buffer_adr%
ENDFUNC
> PROCEDURE copy_load_file_into_buffer(slb_adr%,slb_filename$,slb_offset%,slb_length%)
LOCAL slb_handle&,slb_actually_read%
'
IF FRE()<4000
~FRE(0)
ENDIF
'
slb_handle&=GEMDOS(61,L:V:slb_filename$,W:0)
IF slb_handle&>0
~GEMDOS(66,L:slb_offset%,W:slb_handle&,W:0)
slb_actually_read%=GEMDOS(63,W:slb_handle&,L:slb_length%,L:slb_adr%)
~GEMDOS(62,W:slb_handle&)
ENDIF
'
RETURN
> PROCEDURE copy_save_file_from_buffer(rlb_adr%,rlb_filename$,rlb_length%)
LOCAL rlb_handle&
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF rlb_length%>0
IF LEN(rlb_filename$)>1
rlb_handle&=GEMDOS(61,L:V:rlb_filename$,W:1)
IF rlb_handle&>0
~GEMDOS(66,L:0,W:rlb_handle&,W:2)
~GEMDOS(64,W:rlb_handle&,L:rlb_length%,L:rlb_adr%)
~GEMDOS(62,W:rlb_handle&)
ENDIF
ENDIF
ENDIF
'
RETURN
'
> FUNCTION loc_search_name(name$)
$F%
'
IF name$=""
RETURN -1
ENDIF
'
LET name$=UPPER$(name$)
dummy&=PRED(head_nbline&(4))
IF dummy&>-1
FOR ipo&=0 TO dummy&
IF UPPER$(@loc_get_name$(@px(4,ipo&)))=name$
RETURN ipo&
ENDIF
NEXT ipo&
ENDIF
RETURN -1
ENDFUNC
> PROCEDURE loc_set_number(loc_ptr%,number&)
INT{loc_ptr%}=number&
RETURN
> FUNCTION loc_get_number(loc_ptr%)
$F%
RETURN INT{loc_ptr%}
ENDFUNC
> PROCEDURE loc_set_type(loc_ptr%,value|)
BYTE{ADD(loc_ptr%,2)}=value|
RETURN
> FUNCTION loc_get_type(loc_ptr%)
$F%
IF loc_ptr%>0
RETURN BYTE{ADD(loc_ptr%,2)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE loc_set_name(loc_ptr%,name$)
LOCAL dot_pos&
'
CHAR{ADD(loc_ptr%,8)}=TRIM$(LEFT$(name$,124))
'
dot_pos&=RINSTR(name$,".")
IF dot_pos&>0
LET name$=MID$(name$,SUCC(dot_pos&))
ELSE
LET name$=""
ENDIF
LONG{ADD(loc_ptr%,4)}=CVL(UPPER$(LEFT$(name$+"    ",4)))
RETURN
> FUNCTION loc_get_name$(loc_ptr%)
IF loc_ptr%>0
RETURN CHAR{ADD(loc_ptr%,8)}
ELSE
RETURN ""
ENDIF
ENDFUNC
> FUNCTION loc_get_type$(loc_ptr%)
IF loc_ptr%>0
RETURN TRIM$(MKL$(LONG{ADD(loc_ptr%,4)}))
ELSE
RETURN ""
ENDIF
ENDFUNC
> PROCEDURE loc_set_length(loc_ptr%,value%)
LONG{ADD(loc_ptr%,134)}=value%
RETURN
> FUNCTION loc_get_length(loc_ptr%)
$F%
IF loc_ptr%>0
RETURN LONG{ADD(loc_ptr%,134)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE loc_set_date(loc_ptr%,value%)
LONG{ADD(loc_ptr%,138)}=value%
RETURN
> FUNCTION loc_get_date(loc_ptr%)
$F%
IF loc_ptr%>0
RETURN LONG{ADD(loc_ptr%,138)}
ELSE
RETURN 0
ENDIF
ENDFUNC
'
> PROCEDURE fil_set_type(fil_ptr%,value|)
BYTE{fil_ptr%}=value|
RETURN
> FUNCTION fil_get_type(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN BYTE{fil_ptr%}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_listed(fil_ptr%,value!)
IF value!
BYTE{SUCC(fil_ptr%)}=1
ELSE
BYTE{SUCC(fil_ptr%)}=0
ENDIF
RETURN
> FUNCTION fil_is_listed(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN BYTE{SUCC(fil_ptr%)}>0
ELSE
RETURN FALSE
ENDIF
ENDFUNC
> PROCEDURE fil_set_length(fil_ptr%,value%)
LONG{ADD(fil_ptr%,2)}=value%
RETURN
> FUNCTION fil_get_length(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN LONG{ADD(fil_ptr%,2)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_name(fil_ptr%,name$)
CHAR{ADD(fil_ptr%,6)}=TRIM$(LEFT$(name$,310))
RETURN
> FUNCTION fil_get_name$(fil_ptr%)
IF fil_ptr%>0
RETURN CHAR{ADD(fil_ptr%,6)}
ELSE
RETURN ""
ENDIF
ENDFUNC
> PROCEDURE fil_set_date(fil_ptr%,value%)
LONG{ADD(fil_ptr%,320)}=value%
RETURN
> FUNCTION fil_get_date(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN LONG{ADD(fil_ptr%,320)}
ELSE
RETURN 0
ENDIF
ENDFUNC
'
> FUNCTION get_troll_date$(td_timestamp%,separator!)
LOCAL td_year$,td_month$,td_day$
'
IF td_timestamp%=0
RETURN ""
ENDIF
'
td_year$=STR$(SHR(td_timestamp%,20) AND &X11111111111111)
IF LEN(td_year$)<4
td_year$=STRING$(SUB(4,LEN(td_year$)),"0")+td_year$
ENDIF
td_month$=STR$(SHR(td_timestamp%,16) AND &X1111)
IF LEN(td_month$)<2
td_month$=STRING$(SUB(2,LEN(td_month$)),"0")+td_month$
ENDIF
td_day$=STR$(SHR(td_timestamp%,11) AND &X11111)
IF LEN(td_day$)<2
td_day$=STRING$(SUB(2,LEN(td_day$)),"0")+td_day$
ENDIF
'
IF separator!
SELECT display_files_date_format&
CASE 1
RETURN td_day$+"/"+td_month$+"/"+td_year$
CASE 2
RETURN td_day$+"/"+td_month$+"/"+RIGHT$(td_year$,2)
CASE 3
RETURN td_month$+"-"+td_day$+"-"+td_year$
CASE 4
RETURN td_month$+"-"+td_day$+"-"+RIGHT$(td_year$,2)
ENDSELECT
ELSE
SELECT display_files_date_format&
CASE 1
RETURN td_day$+td_month$+td_year$
CASE 2
RETURN td_day$+td_month$+RIGHT$(td_year$,2)
CASE 3
RETURN td_month$+td_day$+td_year$
CASE 4
RETURN td_month$+td_day$+RIGHT$(td_year$,2)
ENDSELECT
ENDIF
RETURN td_year$+"/"+td_month$+"/"+td_day$
ENDFUNC
> FUNCTION get_troll_time$(td_timestamp%,separator!)
LOCAL td_hour$,td_minute$
'
IF AND(td_timestamp%,&X11111111111)=0
RETURN ""
ENDIF
'
td_hour$=STR$(SHR(td_timestamp%,6) AND &X11111)
IF LEN(td_hour$)<4
td_hour$=STRING$(SUB(2,LEN(td_hour$)),"0")+td_hour$
ENDIF
td_minute$=STR$(td_timestamp% AND &X111111)
IF LEN(td_minute$)<2
td_minute$=STRING$(SUB(2,LEN(td_minute$)),"0")+td_minute$
ENDIF
'
IF separator!
RETURN td_hour$+":"+td_minute$
ENDIF
RETURN td_hour$+td_minute$
ENDFUNC
> FUNCTION gemdos_to_troll_timestamp(td_date%,td_time%)
$F%
LOCAL td_year&,td_month&,td_day&,td_hour&,td_minute&,td_timestamp%
'
td_year&=ADD(SHR&(td_date%,9) AND &X1111111,1980)
td_month&=SHR&(td_date%,5) AND &X1111
td_day&=td_date% AND &X11111
td_hour&=SHR&(td_time%,11) AND &X11111
td_minute&=SHR&(td_time%,5) AND &X111111
'
td_timestamp%=0
'
ADD td_timestamp%,SHL(MAX(1,MIN(td_day&,31)),11)
ADD td_timestamp%,SHL(MAX(1,MIN(td_month&,12)),16)
ADD td_timestamp%,SHL(td_year&,20)
ADD td_timestamp%,SHL(td_hour&,6)
ADD td_timestamp%,td_minute&
'
RETURN td_timestamp%
ENDFUNC
> FUNCTION to_gemdos_timedate(td_timestamp%)
$F%
LOCAL td_year&,td_month&,td_day&,td_hour&,td_minute&,td_second&,td_gemdos%
'
td_gemdos%=0
'
td_hour&=(SHR(td_timestamp%,6) AND &X11111)
td_minute&=(td_timestamp% AND &X111111)
td_second&=0
'
td_hour&=SHL&(MAX(0,MIN(td_hour&,23)),11)
td_minute&=SHL&(MAX(0,MIN(td_minute&,59)),5)
td_second&=MAX(0,MIN(SHR&(td_second&,1),29))
'
ADD td_gemdos%,SHL(ADD(td_hour&,ADD(td_minute&,td_second&)),16)
'
td_year&=(SHR(td_timestamp%,20) AND &X11111111111111)
td_month&=(SHR(td_timestamp%,16) AND &X1111)
td_day&=(SHR(td_timestamp%,11) AND &X11111)
'
td_year&=SHL&(MAX(0,MIN(SUB(td_year&,1980),119)),9)
td_month&=SHL&(MAX(1,MIN(td_month&,12)),5)
td_day&=MAX(1,MIN(td_day&,31))
'
ADD td_gemdos%,ADD(td_year&,ADD(td_month&,td_day&))
'
RETURN td_gemdos%
ENDFUNC
'
> PROCEDURE launch(launch_name$)
IF @s_exist(launch_name$)=TRUE
close_multi
num_drive&=SUB(ASC(launch_name$),65)
~GEMDOS(14,W:num_drive&)
CHDIR LEFT$(launch_name$,RINSTR(launch_name$,"\"))+c0$
IF multi!
'
SELECT LEFT$(RIGHT$(launch_name$,4),3)
CASE "ACC","ACX"
shl_write(3,1,100,dummy$,launch_name$)
CASE "TOS","TTP"
shl_write(1,0,100,dummy$,launch_name$)
DEFAULT
shl_write(1,1,100,dummy$,launch_name$)
ENDSELECT
FOR i&=0 TO 7
delay
NEXT i&
'
ELSE
~EXEC(0,launch_name$,c0$,c0$+c0$)
OUT 4,8
v_show_c
ENDIF
open_multi
IF multi!=FALSE
win(3)
win(4)
ENDIF
ENDIF
RETURN
'
> PROCEDURE dialog_display
result&=OBJC_FIND(adtree%(8),0,3,mo_x&,mo_y&)
SELECT result&
CASE 1 TO 2
display_change_subdialog(result&)
CASE 5
display_win_pos_type&=0
black_white(8,5,1)
black_white(8,6,0)
CASE 6
display_win_pos_type&=1
black_white(8,5,0)
black_white(8,6,1)
CASE 7
IF display_win_pos_save!
display_win_pos_save!=FALSE
ELSE
display_win_pos_save!=TRUE
ENDIF
black_white(8,7,ABS(display_win_pos_save!))
CASE 8
IF display_disk_b!
display_disk_b!=FALSE
ELSE
display_disk_b!=TRUE
ENDIF
black_white(8,8,ABS(display_disk_b!))
CASE 9
IF display_sort_discard_hidden_files!
display_sort_discard_hidden_files!=FALSE
ELSE
display_sort_discard_hidden_files!=TRUE
ENDIF
black_white(8,9,ABS(display_sort_discard_hidden_files!))
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
CASE 10
IF display_files_with_date!
display_files_with_date!=FALSE
ELSE
display_files_with_date!=TRUE
ENDIF
black_white(8,10,ABS(display_files_with_date!))
@local_set_display(3)
@local_set_display(4)
force_update(3)
force_update(4)
CASE 12
dummy&=@pop_up(8,12,13,FALSE)
IF dummy&>-1
display_files_date_format&=MAX(0,MIN(dummy&,4))
CHAR{{OB_SPEC(adtree%(8),12)}}=CHAR{{OB_SPEC(adtree%(13),SUCC(display_files_date_format&))}}
black_white(8,12,-1)
IF display_files_with_date!
force_update(3)
force_update(4)
ENDIF
ENDIF
CASE 13
IF display_files_with_length!
display_files_with_length!=FALSE
ELSE
display_files_with_length!=TRUE
ENDIF
black_white(8,13,ABS(display_files_with_length!))
@local_set_display(3)
@local_set_display(4)
force_update(3)
force_update(4)
CASE 15
dummy&=@pop_up(8,15,14,FALSE)
IF dummy&>-1
display_files_length_format&=MAX(0,MIN(dummy&,3))
CHAR{{OB_SPEC(adtree%(8),15)}}=CHAR{{OB_SPEC(adtree%(14),SUCC(display_files_length_format&))}}
black_white(8,15,-1)
IF display_files_with_length!
force_update(3)
force_update(4)
ENDIF
ENDIF
CASE 17
IF @tst_selected(8,17)
display_files_font_size&=0
black_white(8,17,0)
ELSE
display_files_font_size&=1
black_white(8,17,1)
black_white(8,18,0)
black_white(8,19,0)
ENDIF
local_set_fonts(3)
local_set_fonts(4)
set_vslide(3)
record_start&(3)=@get_vslide(3)
set_vslide(4)
record_start&(4)=@get_vslide(4)
force_update(3)
force_update(4)
CASE 18
IF @tst_selected(8,18)
display_files_font_size&=0
black_white(8,18,0)
ELSE
display_files_font_size&=2
black_white(8,17,0)
black_white(8,18,1)
black_white(8,19,0)
ENDIF
local_set_fonts(3)
local_set_fonts(4)
set_vslide(3)
record_start&(3)=@get_vslide(3)
set_vslide(4)
record_start&(4)=@get_vslide(4)
force_update(3)
force_update(4)
CASE 19
IF screenh&>250
IF @tst_selected(8,19)
display_files_font_size&=0
black_white(8,19,0)
ELSE
display_files_font_size&=3
black_white(8,17,0)
black_white(8,18,0)
black_white(8,19,1)
ENDIF
local_set_fonts(3)
local_set_fonts(4)
set_vslide(3)
record_start&(3)=@get_vslide(3)
set_vslide(4)
record_start&(4)=@get_vslide(4)
force_update(3)
force_update(4)
ENDIF
CASE 22
display_set_sort_type(0)
CASE 23
display_set_sort_type(1)
CASE 24
display_set_sort_type(2)
CASE 25
display_set_sort_type(3)
CASE 26
display_set_sort_type(4)
CASE 27
IF display_sort_distinguish_case!
display_sort_distinguish_case!=FALSE
ELSE
display_sort_distinguish_case!=TRUE
ENDIF
black_white(8,27,ABS(display_sort_distinguish_case!))
IF display_sort_type&=4 OR display_sort_type&=1
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
ENDIF
CASE 28
IF display_sort_folder_at_top!
display_sort_folder_at_top!=FALSE
ELSE
display_sort_folder_at_top!=TRUE
ENDIF
black_white(8,28,ABS(display_sort_folder_at_top!))
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
CASE 29
IF display_sort_reverse!
display_sort_reverse!=FALSE
ELSE
display_sort_reverse!=TRUE
ENDIF
black_white(8,29,ABS(display_sort_reverse!))
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
DEFAULT
win(8)
ENDSELECT
RETURN
> PROCEDURE display_hide_subdialogs
FOR i&=0 TO 1
obj_deselect(8,SUCC(i&))
obj_hide(8,display_subdialog&(i&))
NEXT i&
RETURN
> PROCEDURE display_change_subdialog(obj&)
IF NOT @tst_selected(8,obj&)
display_hide_subdialogs
obj_select(8,obj&)
obj_unhide(8,display_subdialog&(PRED(obj&)))
force_update(8)
ENDIF
RETURN
> PROCEDURE display_set_all
'
SELECT display_win_pos_type&
CASE 1
obj_deselect(8,5)
obj_select(8,6)
DEFAULT
obj_select(8,5)
obj_deselect(8,6)
ENDSELECT
IF display_win_pos_save!
obj_select(8,7)
ELSE
obj_deselect(8,7)
ENDIF
IF display_disk_b!
obj_select(8,8)
ELSE
obj_deselect(8,8)
ENDIF
IF display_sort_discard_hidden_files!
obj_select(8,9)
ELSE
obj_deselect(8,9)
ENDIF
IF display_files_with_date!
obj_select(8,10)
ELSE
obj_deselect(8,10)
ENDIF
CHAR{{OB_SPEC(adtree%(8),12)}}=CHAR{{OB_SPEC(adtree%(13),SUCC(display_files_date_format&))}}
IF display_files_with_length!
obj_select(8,13)
ELSE
obj_deselect(8,13)
ENDIF
CHAR{{OB_SPEC(adtree%(8),15)}}=CHAR{{OB_SPEC(adtree%(14),SUCC(display_files_length_format&))}}
'
obj_deselect(8,17)
obj_deselect(8,18)
obj_deselect(8,19)
IF display_files_font_size&>0
obj_select(8,ADD(17,display_files_font_size&))
ENDIF
'
obj_deselect(8,22)
obj_deselect(8,23)
obj_deselect(8,24)
obj_deselect(8,25)
obj_deselect(8,26)
SELECT display_sort_type&
CASE 1
obj_select(8,23)
CASE 2
obj_select(8,24)
CASE 3
obj_select(8,25)
CASE 4
obj_select(8,26)
DEFAULT
obj_select(8,22)
ENDSELECT
IF display_sort_distinguish_case!
obj_select(8,27)
ELSE
obj_deselect(8,27)
ENDIF
IF display_sort_folder_at_top!
obj_select(8,28)
ELSE
obj_deselect(8,28)
ENDIF
IF display_sort_reverse!
obj_select(8,29)
ELSE
obj_deselect(8,29)
ENDIF
'
IF win!(8)
force_update(8)
ENDIF
RETURN
> PROCEDURE display_set_sort_type(value&)
black_white(8,ADD(22,display_sort_type&),0)
display_sort_type&=value&
black_white(8,ADD(22,display_sort_type&),1)
IF display_disk_icons!(3)
@local_get_disks_list(3)
ELSE
@local_get_directory(3)
ENDIF
IF display_disk_icons!(4)
@local_get_disks_list(4)
ELSE
@local_get_directory(4)
ENDIF
RETURN
'
> PROCEDURE dialog_misc
result&=OBJC_FIND(adtree%(9),0,3,mo_x&,mo_y&)
SELECT result&
CASE 1 TO 3
misc_change_subdialog(result&)
CASE 5
IF misc_keep_date!
misc_keep_date!=FALSE
ELSE
misc_keep_date!=TRUE
ENDIF
black_white(9,5,ABS(misc_keep_date!))
CASE 6
IF misc_verify_free_space!
misc_verify_free_space!=FALSE
ELSE
misc_verify_free_space!=TRUE
ENDIF
black_white(9,6,ABS(misc_verify_free_space!))
CASE 8
misc_choice_existing_file_type&=0
black_white(9,8,1)
black_white(9,9,0)
black_white(9,10,0)
black_white(9,11,0)
CASE 9
misc_choice_existing_file_type&=1
black_white(9,8,0)
black_white(9,9,1)
black_white(9,10,0)
black_white(9,11,0)
CASE 10
misc_choice_existing_file_type&=2
black_white(9,8,0)
black_white(9,9,0)
black_white(9,10,1)
black_white(9,11,0)
CASE 11
misc_choice_existing_file_type&=3
black_white(9,8,0)
black_white(9,9,0)
black_white(9,10,0)
black_white(9,11,1)
CASE 13
IF misc_confirm_quit!
misc_confirm_quit!=FALSE
ELSE
misc_confirm_quit!=TRUE
ENDIF
black_white(9,13,ABS(misc_confirm_quit!))
CASE 14
IF misc_confirm_send!
misc_confirm_send!=FALSE
ELSE
misc_confirm_send!=TRUE
ENDIF
black_white(9,14,ABS(misc_confirm_send!))
CASE 15
IF misc_confirm_delete!
misc_confirm_delete!=FALSE
ELSE
misc_confirm_delete!=TRUE
ENDIF
black_white(9,15,ABS(misc_confirm_delete!))
DEFAULT
win(9)
ENDSELECT
RETURN
> PROCEDURE misc_hide_subdialogs
FOR i&=0 TO 2
obj_deselect(9,SUCC(i&))
obj_hide(9,misc_subdialog&(i&))
NEXT i&
RETURN
> PROCEDURE misc_change_subdialog(obj&)
IF NOT @tst_selected(9,obj&)
misc_hide_subdialogs
obj_select(9,obj&)
obj_unhide(9,misc_subdialog&(PRED(obj&)))
force_update(9)
ENDIF
RETURN
> PROCEDURE misc_set_all
'
IF misc_keep_date!
obj_select(9,5)
ELSE
obj_deselect(9,5)
ENDIF
IF misc_verify_free_space!
obj_select(9,6)
ELSE
obj_deselect(9,6)
ENDIF
'
obj_deselect(9,8)
obj_deselect(9,9)
obj_deselect(9,10)
obj_deselect(9,11)
SELECT misc_choice_existing_file_type&
CASE 1
obj_select(9,9)
CASE 2
obj_select(9,10)
CASE 3
obj_select(9,11)
CASE 4
obj_select(9,12)
DEFAULT
obj_select(9,8)
ENDSELECT
'
IF misc_confirm_quit!
obj_select(9,13)
ELSE
obj_deselect(9,13)
ENDIF
IF misc_confirm_send!
obj_select(9,14)
ELSE
obj_deselect(9,14)
ENDIF
IF misc_confirm_delete!
obj_select(9,15)
ELSE
obj_deselect(9,15)
ENDIF
'
IF win!(9)
force_update(9)
ENDIF
RETURN
'
> PROCEDURE load_options
new_preferences_text
IF @s_exist(kkcmd_configuration_file$)=TRUE
'
OPEN "i",#4,kkcmd_configuration_file$
RECALL #4,preferences_text$(),-1,nb_line%
CLOSE #4
'
IF LEFT$(preferences_text$(0),9)="KKCMD_CFG"
'
display_win_pos_type&=MAX(0,MIN(VAL(@find$("DISPLAY_WIN_POS_TYPE")),1))
display_win_pos_save!=@get_flag("DISPLAY_WIN_POS_SAVE",TRUE)
'
IF display_win_pos_save!
@set_win_pos(3,@find$("WIN_POS_LEFT"))
@set_win_pos(4,@find$("WIN_POS_RIGHT"))
ELSE
FOR i&=3 TO 4
@set_win_pos(i&,"0,0,0,0")
NEXT i&
ENDIF
IF display_win_pos_type&>0
win_join
ENDIF
'
local_path_left$=@find$("WIN_PATH_LEFT")
local_path_right$=@find$("WIN_PATH_RIGHT")
'
display_disk_b!=@get_flag("DISPLAY_DISK_B",TRUE)
display_sort_discard_hidden_files!=@get_flag("DISPLAY_SORT_DISCARD_HIDDEN_FILES",FALSE)
display_files_with_date!=@get_flag("DISPLAY_FILES_WITH_DATE",FALSE)
display_files_date_format&=MAX(0,MIN(VAL(@find$("DISPLAY_FILES_DATE_FORMAT")),4))
display_files_with_length!=@get_flag("DISPLAY_FILES_WITH_LENGTH",TRUE)
display_files_length_format&=MAX(0,MIN(VAL(@find$("DISPLAY_FILES_LENGTH_FORMAT")),2))
display_files_font_size&=MAX(0,MIN(VAL(@find$("DISPLAY_FILES_FONT_SIZE")),3))
'
display_sort_type&=MAX(0,MIN(VAL(@find$("DISPLAY_SORT_TYPE")),2))
display_sort_distinguish_case!=@get_flag("DISPLAY_SORT_DISTINGUISH_CASE",TRUE)
display_sort_folder_at_top!=@get_flag("DISPLAY_SORT_FOLDER_AT_TOP",TRUE)
display_sort_reverse!=@get_flag("DISPLAY_SORT_REVERSE",FALSE)
'
display_set_all
'
misc_keep_date!=@get_flag("MISC_KEEP_DATE",TRUE)
misc_verify_free_space!=@get_flag("MISC_VERIFY_FREE_SPACE",TRUE)
'
misc_choice_existing_file_type&=MAX(0,MIN(VAL(@find$("MISC_CHOICE_EXISTING_FILE")),3))
'
misc_confirm_quit!=@get_flag("MISC_CONFIRM_QUIT",TRUE)
misc_confirm_send!=@get_flag("MISC_CONFIRM_SEND",FALSE)
misc_confirm_delete!=@get_flag("MISC_CONFIRM_DELETE",TRUE)
'
misc_set_all
'
@set_fields_pos(3,@find$("DISPLAY_LEFT_FIELDS_POS"))
@set_fields_pos(4,@find$("DISPLAY_RIGHT_FIELDS_POS"))
'
FOR i&=0 TO 9
shortcuts$(i&)=@find$("SHORTCUT_"+STR$(SUCC(i&)))
NEXT i&
'
shortcuts_set_all
ELSE
~@alert(1,4)
ENDIF
new_preferences_text
ELSE
'
FOR i&=3 TO 4
@set_win_pos(i&,"0,0,0,0")
NEXT i&
IF display_win_pos_type&>0
win_join
ENDIF
'
local_path_left$=""
local_path_right$=""
'
display_set_all
'
misc_set_all
'
@set_fields_pos(3,"")
@set_fields_pos(4,"")
'
shortcuts_set_all
ENDIF
'
IF LEN(local_path_left$)=0
local_path_left$=kkcmd_path$
ENDIF
IF LEN(local_path_right$)=0
local_path_right$=kkcmd_path$
ENDIF
'
@local_set_fonts(3)
@local_set_fonts(4)
@local_set_display(3)
@local_set_display(4)
'
RETURN
> PROCEDURE save_options
LOCAL pos_x&
'
mouse_busy
'
mem_pos%=@mem_init
'
mem_pos%=@mem_put_property("KKCMD_CFG","1.0",mem_pos%)
mem_pos%=@mem_put_property("#","",mem_pos%)
mem_pos%=@mem_put_property("#","DO NOT MODIFY THESE VALUES! USE KKCMD",mem_pos%)
mem_pos%=@mem_put_property("#","",mem_pos%)
'
FOR i&=3 TO 4
IF display_win_pos_save!
win_pos$(i&)=STR$(pwx&(i&))+","+STR$(pwy&(i&))+","+STR$(pwl&(i&))+","+STR$(pwh&(i&))
ELSE
win_pos$(i&)="0,0,0,0"
ENDIF
win_pos$(i&)=win_pos$(i&)+";OPENED"
NEXT i&
'
mem_pos%=@mem_put_property("WIN_POS_LEFT",win_pos$(3),mem_pos%)
mem_pos%=@mem_put_property("WIN_POS_RIGHT",win_pos$(4),mem_pos%)
'
mem_pos%=@mem_put_property("WIN_PATH_LEFT",CHAR{local_path0%},mem_pos%)
mem_pos%=@mem_put_property("WIN_PATH_RIGHT",CHAR{local_path1%},mem_pos%)
'
mem_pos%=@mem_put_property("DISPLAY_WIN_POS_TYPE",STR$(display_win_pos_type&),mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_WIN_POS_SAVE",display_win_pos_save!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_DISK_B",display_disk_b!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_SORT_DISCARD_HIDDEN_FILES",display_sort_discard_hidden_files!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_FILES_WITH_DATE",display_files_with_date!,mem_pos%)
mem_pos%=@mem_put_property("DISPLAY_FILES_DATE_FORMAT",STR$(display_files_date_format&),mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_FILES_WITH_LENGTH",display_files_with_length!,mem_pos%)
mem_pos%=@mem_put_property("DISPLAY_FILES_LENGTH_FORMAT",STR$(display_files_length_format&),mem_pos%)
mem_pos%=@mem_put_property("DISPLAY_FILES_FONT_SIZE",STR$(display_files_font_size&),mem_pos%)
'
mem_pos%=@mem_put_property("DISPLAY_SORT_TYPE",STR$(display_sort_type&),mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_SORT_DISTINGUISH_CASE",display_sort_distinguish_case!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_SORT_FOLDER_AT_TOP",display_sort_folder_at_top!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_SORT_REVERSE",display_sort_reverse!,mem_pos%)
'
mem_pos%=@mem_put_flag("MISC_KEEP_DATE",misc_keep_date!,mem_pos%)
mem_pos%=@mem_put_flag("MISC_VERIFY_FREE_SPACE",misc_verify_free_space!,mem_pos%)
'
mem_pos%=@mem_put_property("MISC_CHOICE_EXISTING_FILE_TYPE",STR$(misc_choice_existing_file_type&),mem_pos%)
'
mem_pos%=@mem_put_flag("MISC_CONFIRM_QUIT",misc_confirm_quit!,mem_pos%)
mem_pos%=@mem_put_flag("MISC_CONFIRM_SEND",misc_confirm_send!,mem_pos%)
mem_pos%=@mem_put_flag("MISC_CONFIRM_DELETE",misc_confirm_delete!,mem_pos%)
'
FOR i&=3 TO 4
str$=""
FOR j&=SUCC(field_bar_obj&(i&)) TO OB_TAIL(adtree%(i&),field_bar_obj&(i&))
IF OB_X(adtree%(i&),j&)>0
pos_x&=ADD(OB_X(adtree%(i&),j&),5)
ELSE
pos_x&=0
ENDIF
IF LEN(str$)>0
str$=str$+","
ENDIF
str$=str$+STR$(pos_x&)
NEXT j&
IF i&=3
mem_pos%=@mem_put_property("DISPLAY_LEFT_FIELDS_POS",str$,mem_pos%)
ELSE IF i&=4
mem_pos%=@mem_put_property("DISPLAY_RIGHT_FIELDS_POS",str$,mem_pos%)
ENDIF
NEXT i&
'
FOR i&=0 TO 9
mem_pos%=@mem_put_property("SHORTCUT_"+STR$(SUCC(i&)),shortcuts$(i&),mem_pos%)
NEXT i&
'
IF mem_pos%>0
~GEMDOS(65,L:V:kkcmd_configuration_file$)
'
file_handle&=GEMDOS(60,L:V:kkcmd_configuration_file$,W:0)
IF file_handle&>0
IF GEMDOS(64,W:file_handle&,L:SUB(mem_pos%,text_mem%),L:text_mem%)<>SUB(mem_pos%,text_mem%)
~@alert(1,5)
ENDIF
~GEMDOS(62,W:file_handle&)
ELSE
~@alert(1,6)
ENDIF
ENDIF
'
~@mem_close
'
mouse_free
'
RETURN
> FUNCTION find$(find_key$)
LOCAL find_len&,find_i&,find_exit!,find_end&
'
~FRE()
~FRE(0)
'
find_value$=""
find_exit!=FALSE
find_key$=find_key$+"="
find_len&=LEN(find_key$)
find_end&=MIN(preferences_text_size&,SUCC(nb_line%))
IF find_ptr&>PRED(find_end&)
find_cursor&=0
ENDIF
old_find_cursor&=find_cursor&
'
FOR find_i&=find_cursor& TO find_end&
IF LEFT$(preferences_text$(find_i&),find_len&)=find_key$
find_value$=MID$(preferences_text$(find_i&),SUCC(find_len&))
find_exit!=TRUE
ENDIF
EXIT IF find_exit!
NEXT find_i&
'
IF NOT find_exit!
find_cursor&=0
IF old_find_cursor&>PRED(find_end&)
FOR find_i&=0 TO old_find_cursor&
IF LEFT$(preferences_text$(find_i&),find_len&)=find_key$
find_value$=MID$(preferences_text$(find_i&),SUCC(find_len&))
find_exit!=TRUE
old_find_cursor&=find_i&
ENDIF
EXIT IF find_exit!
NEXT find_i&
find_cursor&=old_find_cursor&
ENDIF
ENDIF
'
RETURN find_value$
ENDFUNC
> FUNCTION get_flag(find_str$,default_flag!)
'
IF @find$(find_str$)=boolean$(ABS(NOT default_flag!))
RETURN NOT default_flag!
ELSE
RETURN default_flag!
ENDIF
'
ENDFUNC
> PROCEDURE set_win_pos(dial&,str$)
LOCAL str1$
'
IF str$=""
str$="0,0,0,0"
ENDIF
'
IF INSTR(str$,";OPENED")>0
ope!(dial&)=TRUE
str$=LEFT$(str$,PRED(RINSTR(str$,";OPENED")))
ELSE
ope!(dial&)=FALSE
ENDIF
'
str1$=MID$(str$,SUCC(RINSTR(str$,",")))
pwh&(dial&)=VAL(str1$)
str$=LEFT$(str$,PRED(RINSTR(str$,",")))
str1$=MID$(str$,SUCC(RINSTR(str$,",")))
pwl&(dial&)=VAL(str1$)
str$=LEFT$(str$,PRED(RINSTR(str$,",")))
str1$=MID$(str$,SUCC(RINSTR(str$,",")))
pwy&(dial&)=VAL(str1$)
str$=LEFT$(str$,PRED(RINSTR(str$,",")))
pwx&(dial&)=VAL(str$)
'
RETURN
> PROCEDURE set_fields_pos(dial&,str$)
LOCAL str1$,obj_field&,obj_default_pos&
exit!=FALSE
'
IF dial&<6
IF field_bar_obj&(dial&)>0
obj_field&=SUCC(field_bar_obj&(dial&))
obj_default_pos&=0
'
FOR j&=1 TO 6
field_x&(dial&,j&)=32000
NEXT j&
'
WHILE NOT exit!
IF INSTR(str$,",")>0
str1$=LEFT$(str$,MAX(0,PRED(INSTR(str$,","))))
str$=MID$(str$,SUCC(INSTR(str$,",")))
ELSE
str1$=str$
ENDIF
IF obj_field&>OB_TAIL(adtree%(dial&),field_bar_obj&(dial&))
exit!=TRUE
ELSE
dummy&=VAL(str1$)
IF dummy&<1
dummy&=obj_default_pos&
ENDIF
OB_X(adtree%(dial&),obj_field&)=MAX(0,SUB(dummy&,5))
field_x&(dial&,SUB(obj_field&,field_bar_obj&(dial&)))=dummy&
ENDIF
INC obj_field&
ADD obj_default_pos&,120
WEND
ENDIF
ENDIF
'
' set_fields_order(dial&)
'
RETURN
> PROCEDURE set_fields_order(dial&)
LOCAL obj_idx&
'
DIM field_x_temp&(7)
'
FOR j&=1 TO 6
field_x_temp&(j&)=field_x&(dial&,j&)
NEXT j&
'
FOR j&=1 TO 6
obj_idx&=1
FOR k&=1 TO 6
IF field_x_temp&(k&)=<field_x_temp&(obj_idx&)
obj_idx&=k&
ENDIF
NEXT k&
field_x_temp&(obj_idx&)=32000
field_order&(dial&,j&)=obj_idx&
NEXT j&
'
ERASE field_x_temp&()
'
RETURN
> PROCEDURE set_fields_width(dial&)
LOCAL field_x_min&
'
FOR j&=1 TO 6
field_x_min&=32000 ! field_x&(dial&,j&)
FOR k&=1 TO 6
IF field_x&(dial&,j&)<field_x&(dial&,k&) AND field_display!(dial&,k&)=TRUE
field_x_min&=MIN(field_x_min&,field_x&(dial&,k&))
ENDIF
NEXT k&
field_w&(dial&,j&)=MAX(0,SUB(field_x_min&,field_x&(dial&,j&)))
NEXT j&
'
RETURN
> PROCEDURE set_fields_display(dial&)
IF dial&>2 AND dial&<5
IF field_bar_obj&(dial&)>0
FOR k&=1 TO 6
field_display!(dial&,k&)=FALSE
IF field_x&(dial&,k&)<32000
IF NOT BTST(OB_FLAGS(adtree%(dial&),ADD(field_bar_obj&(dial&),k&)),7)
field_display!(dial&,k&)=TRUE
ENDIF
ENDIF
NEXT k&
set_fields_width(dial&)
ENDIF
ENDIF
RETURN
> PROCEDURE new_preferences_text
FOR i&=0 TO preferences_text_size&
preferences_text$(i&)=""
NEXT i&
find_ptr&=0
~FRE()
~FRE(0)
RETURN
'
> FUNCTION mem_init
~@mem_close
text_len%=16000
text_mem%=@mxalloc(text_len%,3)
IF text_mem%<0
~@alert(1,1)
text_mem%=0
ENDIF
RETURN text_mem%
ENDFUNC
> FUNCTION mem_close
IF text_mem%>0
@mxfree(text_mem%)
text_mem%=0
text_len%=0
ENDIF
RETURN TRUE
ENDFUNC
> FUNCTION mem_put_string(str$,ptr%)
LOCAL len_str%
'
len_str%=LEN(str$)
IF @mem_add(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
'
RETURN ptr%
ENDFUNC
> FUNCTION mem_put_line(str$,ptr%)
LOCAL len_str%
'
str$=str$+CHR$(13)+CHR$(10)
len_str%=LEN(str$)
IF @mem_add(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
'
RETURN ptr%
ENDFUNC
> FUNCTION mem_put_property(str1$,str2$,ptr%)
LOCAL len_str%
'
str$=str1$+"="+str2$+CHR$(13)+CHR$(10)
'
len_str%=LEN(str$)
IF @mem_add(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
'
RETURN ptr%
ENDFUNC
> FUNCTION mem_put_flag(str1$,flag!,ptr%)
LOCAL len_str%
'
str$=str1$+"="+boolean$(ABS(flag!))+CHR$(13)+CHR$(10)
'
len_str%=LEN(str$)
IF @mem_add(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
'
RETURN ptr%
ENDFUNC
> FUNCTION mem_add(old_pos%,add%,VAR new_pos%)
LOCAL new_text_mem%,new_text_len%
'
IF old_pos%=0
RETURN FALSE
ENDIF
'
IF ADD(SUB(old_pos%,text_mem%),add%)>text_len%
new_text_len%=SHL(SHR(ADD(ADD(text_len%,MAX(ADD(add%,16),16000)),32),4),4)
new_text_mem%=@mxalloc(new_text_len%,3)
IF new_text_mem%>0
BMOVE text_mem%,new_text_mem%,text_len%
@mxfree(text_mem%)
new_pos%=ADD(new_text_mem%,SUB(old_pos%,text_mem%))
text_mem%=new_text_mem%
text_len%=new_text_len%
RETURN TRUE
ELSE
~@alert(1,1)
new_pos%=0
RETURN FALSE
ENDIF
ENDIF
new_pos%=old_pos%
RETURN TRUE
ENDFUNC
'
> FUNCTION pop_up(pop_tree&,pop_obj&,pop_dial&,pop_mouse_pos!)
$F%
'
~OBJC_OFFSET(adtree%(pop_tree&),pop_obj&,pop_obj_x&,pop_obj_y&)
'
IF pop_mouse_pos!
OB_X(adtree%(pop_dial&),0)=MAX(ADD(screenx&,4),MIN(mo_x&,SUB(ADD(screenx&,screenl&),ADD(OB_W(adtree%(pop_dial&),0),20))))
OB_Y(adtree%(pop_dial&),0)=MAX(ADD(screeny&,4),MIN(mo_y&,SUB(ADD(screeny&,screenh&),ADD(OB_H(adtree%(pop_dial&),0),20))))
ELSE
OB_X(adtree%(pop_dial&),0)=pop_obj_x&
ADD pop_obj_y&,2
OB_Y(adtree%(pop_dial&),0)=ADD(pop_obj_y&,OB_H(adtree%(pop_tree&),pop_obj&))
ENDIF
control
get_photo(pop_dial&,0)
v_show_c
pop_result_old&=0
pop_result&=0
DO
IF NOT magic!
evnt&=@ev_multi(&X100010,258,3,0,30,mo_x&,mo_y&,mo_k&,dummy&,dummy&,mo_c&)
ELSE
evnt&=@ev_multi(&X10,2,0,1,30,mo_x&,mo_y&,mo_k&,dummy&,dummy&,mo_c&)
ENDIF
pop_result&=OBJC_FIND(adtree%(pop_dial&),0,OB_TAIL(adtree%(pop_dial&),0),mo_x&,mo_y&)
IF pop_result_old&<>pop_result&
IF pop_result&>0 AND @tst_enabled(pop_dial&,pop_result&)=TRUE
IF pop_result_old&>0
black_white(pop_dial&,pop_result_old&,0)
ENDIF
pop_result_old&=pop_result&
black_white(pop_dial&,pop_result_old&,1)
ELSE
IF pop_result_old&>0
black_white(pop_dial&,pop_result_old&,0)
ENDIF
pop_result_old&=0
ENDIF
ENDIF
LOOP UNTIL mo_c&=1 AND mo_k&=1
v_hide_c
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WHILE mo_c&>0
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WEND
put_photo(pop_dial&)
uncontrol
'
IF pop_result_old&>0
OB_STATE(adtree%(pop_dial&),pop_result_old&)=0
RETURN PRED(pop_result_old&)
ELSE
RETURN -1
ENDIF
ENDFUNC
> FUNCTION pop_up_with_slider(pop_tree&,pop_obj&,dial&,code!,can_be_null!,pop_length&)
$F%
LOCAL item_height&
'
IF head_nbline&(dial&)<1 AND dial&<>6
RETURN -1
ENDIF
'
FOR it&=2 TO 21
obj_unhide(15,it&)
NEXT it&
'
~OBJC_OFFSET(adtree%(pop_tree&),pop_obj&,pop_obj_x&,pop_obj_y&)
'
OB_X(adtree%(15),0)=pop_obj_x&
OB_Y(adtree%(15),0)=ADD(ADD(pop_obj_y&,2),OB_H(adtree%(pop_tree&),pop_obj&))
'
item_height&=OB_H(adtree%(15),2)
pop_up_start&=0
IF dial&=6
pop_up_max&=MAX(1,loc_drive_nb&)
ELSE
pop_up_max&=MAX(1,ADD(head_nbline&(dial&),ABS(can_be_null!)))
ENDIF
'
dummy&=SUB(ADD(screenh&,screeny&),ADD(pop_obj_y&,OB_H(adtree%(pop_tree&),pop_obj&)))
dummy&=DIV(dummy&,item_height&)
IF dummy&<5
dummy&=SUB(pop_obj_y&,screeny&)
pop_up_height&=MIN(20,DIV(dummy&,item_height&),pop_up_max&)
OB_Y(adtree%(15),0)=MAX(SUCC(screeny&),SUB(SUB(pop_obj_y&,4),MUL(pop_up_height&,item_height&)))
ELSE
pop_up_height&=dummy&
ENDIF
'
pop_up_height&=MIN(20,pop_up_height&,pop_up_max&)
'
OB_H(adtree%(15),0)=MUL(pop_up_height&,item_height&)
OB_H(adtree%(15),1)=MUL(pop_up_height&,item_height&)
'
IF pop_up_height&>3
OB_H(adtree%(15),23)=SUB(OB_H(adtree%(15),0),MUL(OB_H(adtree%(15),22),2))
OB_Y(adtree%(15),24)=1
OB_H(adtree%(15),24)=MIN(ADD(OB_H(adtree%(15),23),EVEN(pop_up_max&))*MIN(1,pop_up_height&/MAX(1,pop_up_max&)),SUB(OB_H(adtree%(15),23),2))
OB_Y(adtree%(15),25)=ADD(OB_H(adtree%(15),23),OB_H(adtree%(15),22))
obj_unhide(15,22)
obj_unhide(15,23)
obj_unhide(15,25)
ELSE
obj_hide(15,22)
obj_hide(15,23)
obj_hide(15,25)
ENDIF
'
IF pop_up_height&<20
FOR it&=ADD(pop_up_height&,2) TO 21
obj_hide(15,it&)
NEXT it&
ENDIF
'
OB_W(adtree%(15),1)=MUL(8,pop_length&)
FOR it&=2 TO 21
OB_W(adtree%(15),it&)=MUL(8,pop_length&)
NEXT it&
OB_W(adtree%(15),0)=ADD(OB_W(adtree%(15),1),OB_W(adtree%(15),22))
OB_X(adtree%(15),22)=OB_W(adtree%(15),1)
OB_X(adtree%(15),23)=OB_W(adtree%(15),1)
OB_X(adtree%(15),25)=OB_W(adtree%(15),1)
'
control
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
get_photo(15,0)
win!(15)=TRUE
v_show_c
pop_result_old&=0
pop_result&=0
DO
IF NOT magic!
evnt&=@ev_multi(&X100010,258,3,0,30,mo_x&,mo_y&,mo_k&,dummy&,dummy&,mo_c&)
ELSE
evnt&=@ev_multi(&X10,2,0,1,30,mo_x&,mo_y&,mo_k&,dummy&,dummy&,mo_c&)
ENDIF
pop_result&=OBJC_FIND(adtree%(15),0,OB_TAIL(adtree%(15),0),mo_x&,mo_y&)
IF mo_k&=1
SELECT pop_result&
CASE 22
pop_up_slide_up(dial&,code!,can_be_null!,pop_length&)
mo_k&=0
CASE 23
pop_up_slide_page(dial&,code!,can_be_null!,pop_length&)
mo_k&=0
CASE 24
pop_up_slide_drag(dial&,code!,can_be_null!,pop_length&)
mo_k&=0
CASE 25
pop_up_slide_down(dial&,code!,can_be_null!,pop_length&)
mo_k&=0
ENDSELECT
ENDIF
IF pop_result_old&<>pop_result&
IF pop_result&>0 AND pop_result&<ADD(pop_up_height&,2) AND @tst_enabled(15,pop_result&)
IF pop_result_old&>0
black_white(15,pop_result_old&,0)
ENDIF
pop_result_old&=pop_result&
black_white(15,pop_result_old&,1)
ELSE
IF pop_result_old&>0
black_white(15,pop_result_old&,0)
ENDIF
pop_result_old&=0
ENDIF
ENDIF
LOOP UNTIL mo_c&=1 AND mo_k&=1
v_hide_c
win!(15)=FALSE
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WHILE mo_c&>0
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WEND
put_photo(15)
uncontrol
'
IF pop_result_old&>1 AND pop_result_old&<22
OB_STATE(adtree%(15),pop_result_old&)=0
RETURN pop_up_value&(pop_result_old&)
ELSE
RETURN -1
ENDIF
ENDFUNC
> PROCEDURE pop_up_slide_drag(dial&,code!,can_be_null!,pop_length&)
IF pop_up_max&>pop_up_height&
WHILE mo_c&=1
pop_up_position&=GRAF_SLIDEBOX(adtree%(15),23,24,1)
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WEND
pop_up_start&=(pop_up_position&/1000)*MAX(1,SUB(pop_up_max&,pop_up_height&))
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
ENDIF
RETURN
> PROCEDURE pop_up_slide_up(dial&,code!,can_be_null!,pop_length&)
IF pop_up_start&>0
black_white(15,22,1)
DEC pop_up_start&
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
black_white(15,22,0)
ENDIF
RETURN
> PROCEDURE pop_up_slide_down(dial&,code!,can_be_null!,pop_length&)
IF pop_up_start&<SUB(pop_up_max&,pop_up_height&)
black_white(15,25,1)
INC pop_up_start&
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
black_white(15,25,0)
ENDIF
RETURN
> PROCEDURE pop_up_slide_page(dial&,code!,can_be_null!,pop_length&)
~OBJC_OFFSET(adtree%(15),24,x_ob&,y_ob&)
IF mo_y&<y_ob&
SUB pop_up_start&,pop_up_height&
ENDIF
IF mo_y&>ADD(y_ob&,OB_H(adtree%(22),24))
ADD pop_up_start&,pop_up_height&
ENDIF
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
RETURN
> PROCEDURE pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
pop_up_start&=MAX(0,MIN(pop_up_start&,SUB(pop_up_max&,pop_up_height&)))
'
FOR ip&=0 TO 22
pop_up_value&(ip&)=0
NEXT ip&
'
SELECT dial&
CASE 6
FOR ip&=0 TO MIN(PRED(pop_up_height&),PRED(pop_up_max&))
idx&=ADD(pop_up_start&,ip&)
@obj_enable(15,ADD(2,ip&))
txt$=" "+loc_drive$(idx&)+": "
num&=loc_drive&(idx&)
'
CHAR{{OB_SPEC(adtree%(15),ADD(2,ip&))}}=txt$
pop_up_value&(ADD(2,ip&))=num&
NEXT ip&
ENDSELECT
'
OB_Y(adtree%(15),24)=MAX(1,(pop_up_start&*OB_H(adtree%(15),23))/MAX(1,pop_up_max&))
'
IF win!(15)
black_white(15,1,0)
black_white(15,23,1)
ENDIF
RETURN
'
> FUNCTION pop_up_next_value(dial&,value&,can_be_null!)
$F%
LOCAL head_line&
'
IF head_nbline&(dial&)<1 AND dial&<>6
RETURN -1
ENDIF
'
SELECT dial&
CASE 2
head_line&=SUCC(value&)
IF head_line&>=loc_drive_nb&
head_line&=0
ENDIF
RETURN head_line&
ENDSELECT
'
RETURN 0
ENDFUNC
'
> PROCEDURE get_photo(photo_tree&,photo_obj&)
IF photo_buf%>0
~OBJC_OFFSET(adtree%(photo_tree&),photo_obj&,x9&,y9&)
SUB x9&,3
SUB y9&,3
l9&=ADD(OB_W(adtree%(photo_tree&),photo_obj&),9)
h9&=ADD(OB_H(adtree%(photo_tree&),photo_obj&),9)
IF ADD(x9&,l9&)>ADD(screenx&,screenl&)
l9&=SUB(ADD(screenx&,screenl&),x9&)
ENDIF
IF ADD(y9&,h9&)>ADD(screeny&,screenh&)
h9&=SUB(ADD(screeny&,screenh&),y9&)
ENDIF
'
IF x9&<ADD(screenx&,screenl&) AND y9&<ADD(screeny&,screenh&)
l9_temp&=SHR&(ADD(ADD(l9&,16),15),4)
photo_buf_new_length%=ADD(MUL(MUL(l9_temp&,ADD(h9&,16)),SHL(nb_plan&,1)),16)
IF photo_buf_new_length%>photo_buf_old_length%
mxfree(photo_buf%)
photo_buf%=@mxalloc(photo_buf_new_length%,3)
IF photo_buf%>0
photo_buf_old_length%=photo_buf_new_length%
ELSE
photo_buf%=0
photo_buf_old_length%=0
ENDIF
ENDIF
'
IF photo_buf%>0
LONG{pdesmfdb%}=photo_buf%
WORD{ADD(pdesmfdb%,4)}=ADD(l9&,16)
WORD{ADD(pdesmfdb%,6)}=ADD(h9&,16)
WORD{ADD(pdesmfdb%,8)}=l9_temp&
make_xyarray(x9&,y9&,ADD(x9&,l9&),ADD(y9&,h9&),1,1,l9&,h9&)
vro_cpyfm(pscrmfdb%,pdesmfdb%)
ENDIF
ENDIF
'
ENDIF
black_white(photo_tree&,photo_obj&,0)
RETURN
> PROCEDURE put_photo(photo_tree&)
IF photo_buf%>0
IF x9&<ADD(screenx&,screenl&) AND y9&<ADD(screeny&,screenh&)
make_xyarray(1,1,l9&,h9&,x9&,y9&,ADD(x9&,PRED(l9&)),ADD(y9&,PRED(h9&)))
vro_cpyfm(pdesmfdb%,pscrmfdb%)
ENDIF
ELSE
~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
ENDIF
RETURN
'
> PROCEDURE control
~WIND_UPDATE(1)
~WIND_UPDATE(3)
v_hide_c
RETURN
> PROCEDURE uncontrol
v_show_c
~WIND_UPDATE(2)
~WIND_UPDATE(0)
RETURN
> PROCEDURE control_form(form&)
~WIND_UPDATE(1)
~WIND_UPDATE(3)
~FORM_CENTER(adtree%(form&),xd&(form&),yd&(form&),ld&(form&),hd&(form&))
DEC xd&(form&)
DEC yd&(form&)
ADD ld&(form&),5
ADD hd&(form&),5
~FORM_DIAL(0,0,0,0,0,xd&(form&),yd&(form&),ld&(form&),hd&(form&))
~OBJC_DRAW(adtree%(form&),0,3,screenx&,screeny&,screenl&,screenh&)
RETURN
> PROCEDURE uncontrol_form(form&)
~WIND_UPDATE(2)
~WIND_UPDATE(0)
~FORM_DIAL(3,0,0,0,0,xd&(form&),yd&(form&),ld&(form&),hd&(form&))
RETURN
> PROCEDURE delay
~EVNT_TIMER(75)
RETURN
> PROCEDURE show_error
~FORM_ALERT(1,ERR$(ERR))
RETURN
> PROCEDURE shl_write(mode&,wisgr&,wiscr&,cmd$,tail$)
'
GCONTRL(0)=121
GCONTRL(1)=3
GCONTRL(2)=1
GCONTRL(3)=2
GCONTRL(4)=0
'
GINTIN(0)=mode&
GINTIN(1)=wisgr&
GINTIN(2)=wiscr&
'
ADDRIN(0)=V:tail$
ADDRIN(1)=V:cmd$
'
GEMSYS
'
RETURN
> PROCEDURE close_multi
IF multi!=FALSE
close_all_windows
~MENU_BAR(adtree%(0),0)
ENDIF
RETURN
> PROCEDURE open_multi
@local_set_drive(4)
@local_set_path(4)
@local_set_drive(3)
@local_set_drive(3)
IF multi!=FALSE
~MENU_BAR(adtree%(0),1)
~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
ENDIF
RETURN
'
> PROCEDURE mouse_busy
~GRAF_MOUSE(2,0)
RETURN
> PROCEDURE mouse_free
~GRAF_MOUSE(0,0)
RETURN
> PROCEDURE mouse_pointing
~GRAF_MOUSE(3,0)
RETURN
> PROCEDURE mouse_prompt
~GRAF_MOUSE(1,0)
RETURN
> FUNCTION alert(typ&,id_msg&)
$F%
RETURN FORM_ALERT(typ&,CHAR{OB_SPEC(adtree%(alert_tree&),id_msg&)})
ENDFUNC
> FUNCTION remove_c0$(remove_str1$)
IF RIGHT$(remove_str1$)=c0$
RETURN LEFT$(remove_str1$,MAX(0,PRED(LEN(remove_str1$))))
ELSE
RETURN remove_str1$
ENDIF
ENDFUNC
> FUNCTION remove_slash$(remove_str2$)
LET remove_str2$=@remove_c0$(remove_str2$)
IF RIGHT$(remove_str2$)="\"
RETURN LEFT$(remove_str2$,MAX(0,PRED(LEN(remove_str2$))))
ELSE
RETURN remove_str2$
ENDIF
ENDFUNC
'
> FUNCTION fsl_get_path$(fsl_title&,path$,name$)
LOCAL fsl_path$,fsl_name$,fsl_choice&,fsl_return&
fsl_path$=path$
fsl_name$=name$
fsl_return&=@fsl_input(fsl_title&,fsl_path$,fsl_name$,fsl_choice&)
IF fsl_return&=0 OR fsl_choice&=0
RETURN kkcmd_path$+c0$
ELSE
RETURN LEFT$(fsl_path$,RINSTR(fsl_path$,"\"))+c0$
ENDIF
ENDFUNC
> FUNCTION fsl_get_file$(fsl_title&,path$,name$)
LOCAL fsl_path$,fsl_name$,fsl_choice&,fsl_return&
fsl_path$=path$
fsl_name$=name$
fsl_return&=@fsl_input(fsl_title&,fsl_path$,fsl_name$,fsl_choice&)
IF fsl_return&=0 OR fsl_choice&=0
RETURN c0$
ELSE
RETURN LEFT$(fsl_path$,RINSTR(fsl_path$,"\"))+fsl_name$
ENDIF
ENDFUNC
> FUNCTION fsl_input(fi_title&,VAR fi_path$,fi_name$,fi_choice&)
$F%
'
IF fslx_mem%>0
fi_choice&=0
IF GEMDOS(48)>=&H1500
type_msg&=0
ENDIF
'
IF type_msg&>0
GCONTRL(0)=91
ELSE
GCONTRL(0)=90
ENDIF
GCONTRL(1)=0
GCONTRL(2)=2
IF type_msg&>0
GCONTRL(3)=3
ELSE
GCONTRL(3)=2
ENDIF
GCONTRL(4)=0
'
CHAR{fslx_path%}=LEFT$(fi_path$,257)
CHAR{fslx_name%}=LEFT$(fi_name$,127)
'
LONG{ADDRIN}=fslx_path%
LONG{ADD(ADDRIN,4)}=fslx_name%
IF type_msg&>0
LONG{ADD(ADDRIN,8)}=OB_SPEC(adtree%(alert_tree&),fi_title&)
ENDIF
'
GEMSYS
'
fi_path$=CHAR{fslx_path%}+c0$
fi_name$=CHAR{fslx_name%}+c0$
fi_choice&=INT{ADD(GINTOUT,2)}
'
RETURN INT{GINTOUT}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fslx_init
LOCAL fslx_str$
'
fslx_mem%=@mxalloc(256,3)
IF fslx_mem%>0
fslx_path%=fslx_mem%
fslx_name%=ADD(fslx_path%,128)
ENDIF
'
RETURN
'
> FUNCTION test_cookie(cookie_name$,VAR cookie_valeur%)
$F%
LOCAL read_cook%,nom_cook%,cookie%
'
nom_cook%=CVL(cookie_name$)
cookie%=LPEEK(&H5A0)
cookie_valeur%=0
'
IF cookie%<>0
REPEAT
read_cook%=LPEEK(cookie%)
cookie_valeur%=LPEEK(cookie%+4)
ADD cookie%,8
UNTIL read_cook%=0 OR read_cook%=nom_cook%
IF read_cook%=nom_cook%
RETURN TRUE
ELSE
RETURN FALSE
ENDIF
ELSE
RETURN FALSE
ENDIF
ENDFUNC
> FUNCTION s_exist(exist_name$)
$F%
'
exist_name$=exist_name$+c0$
LOCAL existe&
IF LEN(exist_name$)=0 OR LEFT$(exist_name$)=c0$
RETURN FALSE
ELSE
existe&=GEMDOS(61,L:V:exist_name$,W:0)
IF existe&>0
~GEMDOS(62,W:existe&)
RETURN TRUE
ELSE
RETURN FALSE
ENDIF
ENDIF
ENDFUNC
> FUNCTION d_exist(exist_name$)
$F%
LOCAL d_handle%,d_exist!,d_name$,d_path$,d_err%,d_dta%
'
IF shell_buf%>0
IF FRE()<4000
~FRE(0)
ENDIF
'
d_exist!=FALSE
d_path$=LEFT$(exist_name$,PRED(RINSTR(exist_name$,"\")))+c0$
d_test$=""
d_name$=@remove_c0$(MID$(exist_name$,SUCC(RINSTR(exist_name$,"\"))))
'
IF (magic!=TRUE OR mint!=TRUE) AND xattr_buf%>0
IF LEN(d_path$)=3
d_path$=@remove_c0$(d_path$)+"\"+c0$
ENDIF
'
d_handle%=GEMDOS(296,L:V:d_path$,0)
IF d_handle%>0
'
WHILE GEMDOS(297,W:250,L:d_handle%,L:shell_buf%)=0 AND d_exist!=FALSE
IF UPPER$(CHAR{ADD(shell_buf%,4)})=UPPER$(d_name$)
d_test$=@remove_c0$(d_path$)+"\"+d_name$+c0$
IF GEMDOS(300,W:0,L:V:d_test$,L:xattr_buf%)=0
IF (INT{xattr_buf%} AND &HF000)=&H4000
d_exist!=TRUE
ENDIF
ENDIF
ENDIF
WEND
'
~GEMDOS(299,L:d_handle%)
ENDIF
'
RETURN d_exist!
ELSE
d_path$=@remove_c0$(d_path$)+"\*"+c0$
d_err%=GEMDOS(78,L:V:d_path$,W:16)
DO
EXIT IF d_err%<>0 OR d_exist!=TRUE
d_dta%=FGETDTA()
IF CHAR{ADD(d_dta%,30)}=UPPER$(d_name$) AND BTST(BYTE{ADD(d_dta%,21)},4)=TRUE
d_exist!=TRUE
ENDIF
d_err%=GEMDOS(79)
LOOP
RETURN d_exist!
ENDIF
ENDIF
'
RETURN FALSE
ENDFUNC
> FUNCTION get_env$(get_env$)
LOCAL get_env%
'
get_env%=@mxalloc(512,3)
ret_env$=""
IF RIGHT$(get_env$)<>"="
get_env$=get_env$+"="
ENDIF
'
IF get_env%>0
CHAR{get_env%}=LEFT$(get_env$,31)
'
INT{ADD(GCONTRL,2)}=0
INT{ADD(GCONTRL,4)}=1
INT{ADD(GCONTRL,6)}=2
INT{ADD(GCONTRL,8)}=0
'
LONG{ADDRIN}=ADD(32,get_env%)
LONG{ADD(ADDRIN,4)}=get_env%
'
GEMSYS 125
'
IF LONG{ADD(32,get_env%)}>0
ret_env$=CHAR{{ADD(32,get_env%)}}
IF RIGHT$(ret_env$)<>"\"
ret_env$=ret_env$+"\"
ENDIF
ENDIF
mxfree(get_env%)
ENDIF
RETURN ret_env$
ENDFUNC
> FUNCTION get_app_name$(app_name$)
app_name$=MID$(app_name$,SUCC(RINSTR(app_name$,"\")))
app_name$=LEFT$(app_name$,MAX(0,PRED(RINSTR(app_name$,"."))))
app_name$=app_name$+SPACE$(MAX(0,SUB(8,LEN(app_name$))))
RETURN app_name$
ENDFUNC
'
> FUNCTION ev_multi(em_flags&,em_cl&,em_ma&,em_st&,em_ct%,VAR em_mx&,em_my&,em_mk&,em_kbd&,em_key&,em_click&)
$F%
'
INT{ADD(GCONTRL,2)}=16
INT{ADD(GCONTRL,4)}=7
INT{ADD(GCONTRL,6)}=1
INT{ADD(GCONTRL,8)}=0
'
INT{GINTIN}=em_flags&
INT{ADD(GINTIN,2)}=em_cl&
INT{ADD(GINTIN,4)}=em_ma&
INT{ADD(GINTIN,6)}=em_st&
INT{ADD(GINTIN,8)}=0
INT{ADD(GINTIN,10)}=0
INT{ADD(GINTIN,12)}=0
INT{ADD(GINTIN,14)}=0
INT{ADD(GINTIN,16)}=0
INT{ADD(GINTIN,18)}=0
INT{ADD(GINTIN,20)}=0
INT{ADD(GINTIN,22)}=0
INT{ADD(GINTIN,24)}=0
INT{ADD(GINTIN,26)}=0
INT{ADD(GINTIN,28)}=WORD(em_ct%)
INT{ADD(GINTIN,30)}=WORD(SWAP(em_ct%))
'
LONG{ADDRIN}=m_adr%
'
GEMSYS 25
'
em_mx&=INT{ADD(GINTOUT,2)}
em_my&=INT{ADD(GINTOUT,4)}
em_mk&=INT{ADD(GINTOUT,6)}
em_kbd&=INT{ADD(GINTOUT,8)}
em_key&=INT{ADD(GINTOUT,10)}
em_click&=INT{ADD(GINTOUT,12)}
'
RETURN INT{GINTOUT}
ENDFUNC
> FUNCTION window_create(cp_win_recu&)
$F%
'
INT{ADD(GCONTRL,2)}=5
INT{ADD(GCONTRL,4)}=1
INT{ADD(GCONTRL,6)}=0
INT{ADD(GCONTRL,8)}=0
'
INT{GINTIN}=cp_win_recu&
INT{ADD(GINTIN,2)}=100
INT{ADD(GINTIN,4)}=100
INT{ADD(GINTIN,6)}=100
INT{ADD(GINTIN,8)}=100
'
GEMSYS 100
'
RETURN INT{GINTOUT}
ENDFUNC
> FUNCTION get_top_window
$F%
'
INT{ADD(GCONTRL,2)}=2
INT{ADD(GCONTRL,4)}=5
LONG{ADD(GCONTRL,6)}=0
'
INT{GINTIN}=0
INT{ADD(GINTIN,2)}=10
'
GEMSYS 104
'
RETURN INT{ADD(GINTOUT,2)}
ENDFUNC
'
> FUNCTION v_opnvwk
$F%
'
INT{ADD(CONTRL,2)}=0
INT{ADD(CONTRL,6)}=11
INT{ADD(CONTRL,12)}=@graf_handle
'
INT{INTIN}=1           ! Numro ID du priphrique physique (cran)
INT{ADD(INTIN,2)}=1    ! Type de ligne
INT{ADD(INTIN,4)}=1    ! Index de couleur Polyline
INT{ADD(INTIN,6)}=1    ! Type de marqueur
INT{ADD(INTIN,8)}=1    ! Index de couleur Polymarker
INT{ADD(INTIN,10)}=1   ! Fonte de caractres
INT{ADD(INTIN,12)}=1   ! Index couleur texte
INT{ADD(INTIN,14)}=1   ! Fill interior Style
INT{ADD(INTIN,16)}=1   ! Fill style index
INT{ADD(INTIN,18)}=1   ! Fill index couleur
INT{ADD(INTIN,20)}=2   ! Flag coordonnes NDC ou RC
'
VDISYS 100
'
RETURN INT{ADD(CONTRL,12)}
ENDFUNC
> PROCEDURE v_clsvwk(handle&)
INT{ADD(CONTRL,12)}=handle&
VDISYS 101,0,0
RETURN
> FUNCTION graf_handle
$F%
'
INT{ADD(GCONTRL,2)}=0
INT{ADD(GCONTRL,4)}=5
LONG{ADD(GCONTRL,6)}=0
'
GEMSYS 77
'
RETURN INT{GINTOUT}
ENDFUNC
'
> PROCEDURE make_zero_mfdb(pmfdb%)
LONG{pmfdb%}=0
LONG{ADD(pmfdb%,4)}=0
LONG{ADD(pmfdb%,8)}=0
LONG{ADD(pmfdb%,12)}=0
LONG{ADD(pmfdb%,16)}=0
RETURN
> PROCEDURE make_xywh(x0&,y0&,w0&,h0&,x1&,y1&,w1&,h1&)
WORD{pxyarray%}=x0&
WORD{ADD(pxyarray%,2)}=y0&
WORD{ADD(pxyarray%,4)}=ADD(x0&,PRED(w0&))
WORD{ADD(pxyarray%,6)}=ADD(y0&,PRED(h0&))
WORD{ADD(pxyarray%,8)}=x1&
WORD{ADD(pxyarray%,10)}=y1&
WORD{ADD(pxyarray%,12)}=ADD(x1&,PRED(w1&))
WORD{ADD(pxyarray%,14)}=ADD(y1&,PRED(h1&))
RETURN
> PROCEDURE make_xyarray(xq0&,yq0&,xq1&,yq1&,xz0&,yz0&,xz1&,yz1&)
WORD{pxyarray%}=xq0&
WORD{ADD(pxyarray%,2)}=yq0&
WORD{ADD(pxyarray%,4)}=xq1&
WORD{ADD(pxyarray%,6)}=yq1&
WORD{ADD(pxyarray%,8)}=xz0&
WORD{ADD(pxyarray%,10)}=yz0&
WORD{ADD(pxyarray%,12)}=xz1&
WORD{ADD(pxyarray%,14)}=yz1&
RETURN
> PROCEDURE vro_cpyfm(pscr_mfdb%,pdes_mfdb%)
'
v_hide_c
'
INT{ADD(CONTRL,2)}=4
INT{ADD(CONTRL,6)}=1
INT{ADD(CONTRL,12)}=vdi_handle&
LONG{ADD(CONTRL,14)}=pscr_mfdb%
LONG{ADD(CONTRL,18)}=pdes_mfdb%
INT{INTIN}=3
BMOVE pxyarray%,PTSIN,16
'
VDISYS 109
'
v_show_c
'
RETURN
'
> PROCEDURE v_hide_c
INT{ADD(CONTRL,2)}=0
INT{ADD(CONTRL,6)}=0
INT{ADD(CONTRL,12)}=vdi_handle&
VDISYS 123
RETURN
> PROCEDURE v_show_c
INT{ADD(CONTRL,2)}=0
INT{ADD(CONTRL,6)}=1
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=1
VDISYS 122
RETURN
'
> PROCEDURE v_text(x&,y&,str$,is_vector!)
'
CHAR{vtxt_buf%}=LEFT$(str$,255)
vtext_len&=MIN(LEN(str$),255)
str$=""
'
IF vtext_len&>0
INT{ADD(CONTRL,12)}=vdi_handle&
INT{ADD(PTSIN,2)}=y&
itext_len&=MIN(vtext_len&,255)
INT{PTSIN}=x&
DEC itext_len&
FOR it&=0 TO itext_len&
INT{ADD(INTIN,SHL&(it&,1))}=BYTE{ADD(vtxt_buf%,it&)}
NEXT it&
INC itext_len&
IF is_vector!
VDISYS 241,itext_len&,1,0
ELSE
VDISYS 8,itext_len&,1,0
ENDIF
ENDIF
RETURN
> FUNCTION vqt_extent(str$,is_vector!)
$F%
'
CHAR{vtxt_buf%}=LEFT$(str$,255)
vtext_len&=MIN(LEN(str$),255)
str$=""
'
IF vtext_len&>0
INT{ADD(CONTRL,12)}=vdi_handle&
itext_len&=MIN(vtext_len&,255)
DEC itext_len&
FOR it&=0 TO itext_len&
INT{ADD(INTIN,SHL&(it&,1))}=BYTE{ADD(vtxt_buf%,it&)}
NEXT it&
INC itext_len&
IF is_vector!
VDISYS 240,itext_len&,0,0
ELSE
VDISYS 116,itext_len&,0,0
ENDIF
RETURN SUB(INT{ADD(PTSOUT,4)},INT{PTSOUT})
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE vst_alignment(halign&,valign&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=halign&
INT{ADD(INTIN,2)}=valign&
VDISYS 39,2,0
RETURN
> PROCEDURE vst_color(color&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=color&
VDISYS 22,1,0
RETURN
> PROCEDURE vst_effect(effect&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=effect&
VDISYS 106,1,0
RETURN
> FUNCTION vst_point(point&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=point&
VDISYS 107,1,0
RETURN SUCC(INT{ADD(PTSOUT,6)})
ENDFUNC
'
> PROCEDURE clip(flag&,cx&,cy&,cw&,ch&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=flag&
INT{PTSIN}=cx&
INT{ADD(PTSIN,2)}=cy&
INT{ADD(PTSIN,4)}=ADD(cx&,PRED(cw&))
INT{ADD(PTSIN,6)}=ADD(cy&,PRED(ch&))
VDISYS 129,1,2
RETURN
> PROCEDURE vswr_mode(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 32,1,0
RETURN
'
> PROCEDURE v_bar(cx&,cy&,cw&,ch&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{PTSIN}=cx&
INT{ADD(PTSIN,2)}=cy&
INT{ADD(PTSIN,4)}=ADD(cx&,PRED(cw&))
INT{ADD(PTSIN,6)}=ADD(cy&,PRED(ch&))
VDISYS 11,0,2,1
RETURN
> PROCEDURE vsf_interior(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 23,1,0
RETURN
> PROCEDURE vsf_style(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 24,1,0
RETURN
> PROCEDURE vsf_color(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 25,1,0
RETURN
> PROCEDURE vsf_perimeter(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 104,1,0
RETURN
'
> PROCEDURE record_general_init(n&)
'
record_number_max&=32000
'
DIM head%(n&),head_maxline&(n&),head_nbline&(n&)
'
DIM record_buffer%(n&),record_len&(n&)
DIM record_start&(n&),record_height&(n&),record_selected&(n&)
DIM record_changed!(n&),record_nb_selected&(n&),record_cursor&(n&)
'
DIM field_bar_h&(n&),field_bar_obj&(n&)
DIM field_x&(n&,16),field_w&(n&,16),field_order&(n&,16),field_display!(n&,16)
DIM field_start&(n&)
'
RETURN
> PROCEDURE record_general_exit
'
ERASE head%(),head_maxline&(),head_nbline&()
'
ERASE record_buffer%(),record_len&()
ERASE record_start&(),record_height&(),record_selected&()
ERASE record_changed!(),record_nb_selected&(),record_cursor&()
'
ERASE field_bar_h&(),field_bar_obj&()
ERASE field_x&(),field_w&(),field_order&(),field_display!()
ERASE field_start&()
'
RETURN
> PROCEDURE record_open(tid&,h_size&,r_len&,obj&)
'
~FRE(0)
~@head_init(tid&,h_size&)
~@gxalloc_init(tid&)
'
record_len&(tid&)=MAX(2,r_len&)
record_buffer%(tid&)=@gxalloc(tid&,record_len&(tid&))
record_start&(tid&)=0
record_height&(tid&)=0
record_selected&(tid&)=-1
record_nb_selected&(tid&)=0
record_changed!(tid&)=FALSE
record_cursor&(tid&)=-1
'
field_bar_obj&(tid&)=obj&
'
RETURN
> PROCEDURE record_close(tid&)
'
IF head%(tid&)>0
~@gxalloc_exit(tid&)
~@head_close(tid&)
ENDIF
'
RETURN
'
> PROCEDURE record_set(tid&,line&)
'
BMOVE record_buffer%(tid&),@px(tid&,line&),record_len&(tid&)
'
record_changed!(tid&)=TRUE
'
RETURN
> PROCEDURE record_insert(tid&,line&)
'
IF @head_resize(tid&,SUCC(head_nbline&(tid&)))
IF line&<head_nbline&(tid&)
temp_len%=MUL(SUB(head_nbline&(tid&),line&),6)
temp_ptr%=ADD(head%(tid&),MUL(line&,6))
IF temp_len%>0
BMOVE temp_ptr%,ADD(temp_ptr%,6),temp_len%
ENDIF
ENDIF
'
temp_ptr%=@gxalloc(tid&,record_len&(tid&))
IF temp_ptr%>0
BMOVE record_buffer%(tid&),temp_ptr%,record_len&(tid&)
LONG{ADD(head%(tid&),MUL(line&,6))}=temp_ptr%
INT{ADD(head%(tid&),ADD(MUL(line&,6),4))}=0
INC head_nbline&(tid&)
record_changed!(tid&)=TRUE
ELSE
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: can't insert a record.|][ Ok ]")
ENDIF
'
ENDIF
RETURN
> PROCEDURE record_delete(tid&,line&)
'
temp_len%=MUL(SUB(head_maxline&(tid&),line&),6)
temp_ptr%=ADD(head%(tid&),MUL(line&,6))
IF temp_len%>0
BMOVE ADD(temp_ptr%,6),temp_ptr%,temp_len%
ENDIF
'
head_nbline&(tid&)=MAX(0,PRED(head_nbline&(tid&)))
record_changed!(tid&)=TRUE
'
RETURN
> PROCEDURE record_select(tid&,line&)
LOCAL sx%
IF line&>-1
sx%=ADD(head%(tid&),ADD(MUL(line&,6),4))
IF BYTE{sx%}=1
BYTE{sx%}=0
ELSE
BYTE{sx%}=1
ENDIF
ENDIF
RETURN
> PROCEDURE record_select_all(tid&)
LOCAL sa_i&,sa_end&,sa_ptr%
'
sa_end&=PRED(head_nbline&(tid&))
IF sa_end&>-1
sa_ptr%=ADD(head%(tid&),4)
FOR sa_i&=0 TO sa_end&
BYTE{sa_ptr%}=1
ADD sa_ptr%,6
NEXT sa_i&
ENDIF
'
RETURN
> PROCEDURE record_deselect_all(tid&)
LOCAL da_i&,da_end&,da_ptr%
'
da_end&=PRED(head_nbline&(tid&))
IF da_end&>-1
da_ptr%=ADD(head%(tid&),4)
FOR da_i&=0 TO da_end&
BYTE{da_ptr%}=0
ADD da_ptr%,6
NEXT da_i&
ENDIF
'
RETURN
> FUNCTION is_record_selected(tid&,line&)
$F%
IF BYTE{ADD(head%(tid&),ADD(MUL(line&,6),4))}=1
RETURN TRUE
ELSE
RETURN FALSE
ENDIF
ENDFUNC
> FUNCTION record_get_next_selected(tid&,line&)
$F%
LOCAL gfs_end&,gfs_i&,gfs_ptr%
'
gfs_end&=PRED(head_nbline&(tid&))
INC line&
'
IF line&=<gfs_end& AND gfs_end&>-1
'
gfs_ptr%=ADD(head%(tid&),ADD(MUL(line&,6),4))
'
FOR gfs_i&=line& TO gfs_end&
IF BYTE{gfs_ptr%}>0
RETURN gfs_i&
ENDIF
ADD gfs_ptr%,6
NEXT gfs_i&
ENDIF
'
RETURN -1
ENDFUNC
> FUNCTION record_get_previous_selected(tid&,line&)
$F%
LOCAL gfs_i&,gfs_ptr%
'
DEC line&
'
IF line&>-1
'
gfs_ptr%=ADD(head%(tid&),ADD(MUL(line&,6),4))
'
FOR gfs_i&=line& TO 0 STEP -1
IF BYTE{gfs_ptr%}>0
RETURN gfs_i&
ENDIF
SUB gfs_ptr%,6
NEXT gfs_i&
ENDIF
'
RETURN -1
ENDFUNC
> FUNCTION record_move_to_top(tid&)
$F%
LOCAL current_ptr%,changed_ptr%
'
IF record_selected&(tid&)>0
current_ptr%=@px(tid&,record_selected&(tid&))
changed_ptr%=@px(tid&,PRED(record_selected&(tid&)))
'
head_fix(tid&,PRED(record_selected&(tid&)),current_ptr%)
head_fix(tid&,record_selected&(tid&),changed_ptr%)
'
DEC record_selected&(tid&)
'
get_record_xywh(tid&,record_selected&(tid&),sx&,sy&,sl&,sh&)
force_update_xywh(tid&,record_selected&(tid&),sx&,sy&,sl&,sh&)
get_record_xywh(tid&,SUCC(record_selected&(tid&)),sx&,sy&,sl&,sh&)
force_update_xywh(tid&,SUCC(record_selected&(tid&)),sx&,sy&,sl&,sh&)
'
record_changed!(tid&)=TRUE
RETURN TRUE
ENDIF
RETURN FALSE
ENDFUNC
> FUNCTION record_move_to_bottom(tid&)
$F%
LOCAL current_ptr%,changed_ptr%
'
IF record_selected&(tid&)<PRED(head_nbline&(tid&))
current_ptr%=@px(tid&,record_selected&(tid&))
changed_ptr%=@px(tid&,SUCC(record_selected&(tid&)))
'
head_fix(tid&,SUCC(record_selected&(tid&)),current_ptr%)
head_fix(tid&,record_selected&(tid&),changed_ptr%)
'
INC record_selected&(tid&)
'
get_record_xywh(tid&,record_selected&(tid&),sx&,sy&,sl&,sh&)
force_update_xywh(tid&,record_selected&(tid&),sx&,sy&,sl&,sh&)
get_record_xywh(tid&,PRED(record_selected&(tid&)),sx&,sy&,sl&,sh&)
force_update_xywh(tid&,PRED(record_selected&(tid&)),sx&,sy&,sl&,sh&)
'
record_changed!(tid&)=TRUE
RETURN TRUE
ENDIF
RETURN FALSE
ENDFUNC
> PROCEDURE record_clear_buffer(tid&)
LOCAL ips%,ipe%
'
ips%=record_buffer%(tid&)
ipe%=ADD(ips%,record_len&(tid&))
'
WHILE ips%<ipe%
INT{ips%}=0
ADD ips%,2
WEND
'
RETURN
'
> FUNCTION head_init(tid&,n&)
$F%
~@head_close(tid&)
head%(tid&)=@mxalloc(MUL(ADD(n&,100),6),3)
IF head%(tid&)>0
head_maxline&(tid&)=ADD(n&,100)
head_nbline&(tid&)=0
RETURN TRUE
ELSE
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: failed in creating|memory block for line pointers.][ Ok ]")
head_nbline&(tid&)=0
head_maxline&(tid&)=0
RETURN FALSE
ENDIF
ENDFUNC
> FUNCTION head_close(tid&)
$F%
head_nbline&(tid&)=0
IF head%(tid&)>0
@mxfree(head%(tid&))
head%(tid&)=0
RETURN TRUE
ELSE
head%(tid&)=0
RETURN FALSE
ENDIF
ENDFUNC
> FUNCTION head_resize(tid&,n&)
$F%
IF n&>head_maxline&(tid&)
new_head%=@mxalloc(ADD(MUL(ADD(n&,200),6),6),3)
IF new_head%>0
BMOVE head%(tid&),new_head%,MUL(head_nbline&(tid&),6)
@mxfree(head%(tid&))
head%(tid&)=new_head%
head_maxline&(tid&)=ADD(n&,200)
RETURN TRUE
ELSE
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: can't resize the|memory block for lines.|][ Ok ]")
ENDIF
RETURN FALSE
ENDIF
RETURN TRUE
ENDFUNC
> PROCEDURE head_fix(tid&,line&,head_ptr%)
IF line&>-1 AND line&<head_nbline&(tid&)
LONG{ADD(head%(tid&),MUL(line&,6))}=head_ptr%
ENDIF
RETURN
> FUNCTION px(tid&,line&)
$F%
RETURN LONG{ADD(head%(tid&),MUL(line&,6))}
ENDFUNC
> FUNCTION lx(tid&,line&)
$F%
RETURN INT{ADD(head%(tid&),ADD(MUL(line&,6),4))} AND &X111111111111
ENDFUNC
> FUNCTION cx(tid&,line&)
$F%
RETURN SHR|(BYTE{ADD(head%(tid&),ADD(MUL(line&,6),4))},4) AND &X1111
ENDFUNC
'
> PROCEDURE gxalloc_main_init(n&)
'
gxblk_max&=128
' gxblk_size%=25600
'
DIM gxblk%(n&,PRED(gxblk_max&)),sgxblk%(n&,PRED(gxblk_max&))
DIM gxblk_count&(n&),sgxblk_count&(n&)
DIM last_gxadr%(n&),last_gxlen%(n&)
DIM gxalloc!(n&),gxblk_size%(n&)
'
FOR i&=1 TO n&
gxblk_size%(i&)=25600
NEXT i&
'
RETURN
> PROCEDURE gxalloc_main_exit(n&)
LOCAL in&
'
FOR in&=0 TO n&
~@gxalloc_exit(in&)
NEXT in&
ERASE gxblk%(),sgxblk%()
ERASE gxblk_count&(),sgxblk_count&()
ERASE last_gxadr%(),last_gxlen%()
ERASE gxalloc!(),gxblk_size%()
'
RETURN
> FUNCTION gxalloc_init(tid&)
$F%
LOCAL ig&
'
IF NOT gxalloc!(tid&)
FOR ig&=0 TO PRED(gxblk_max&)
gxblk%(tid&,ig&)=0
sgxblk%(tid&,ig&)=0
NEXT ig&
'
gxblk_count&(tid&)=0
sgxblk_count&(tid&)=0
last_gxadr%(tid&)=0
last_gxlen%(tid&)=0
'
gxblk%(tid&,0)=@mxalloc(ADD(gxblk_size%(tid&),4),3)
IF gxblk%(tid&,0)>0
last_gxadr%(tid&)=gxblk%(tid&,0)
last_gxlen%(tid&)=gxblk_size%(tid&)
gxalloc!(tid&)=TRUE
RETURN 0
ENDIF
RETURN gxblk%(tid&,0)
ENDIF
RETURN -1
'
ENDFUNC
> FUNCTION gxalloc_exit(tid&)
$F%
LOCAL ig&
'
IF gxalloc!(tid&)
gxalloc!(tid&)=FALSE
FOR ig&=0 TO gxblk_count&(tid&)
IF gxblk%(tid&,ig&)>0
IF GEMDOS(73,L:gxblk%(tid&,ig&))=0
gxblk%(tid&,ig&)=0
ELSE
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: can't free|block memory.|][ Ok ]")
ENDIF
ENDIF
NEXT ig&
FOR ig&=0 TO sgxblk_count&(tid&)
IF sgxblk%(tid&,ig&)>0
IF GEMDOS(73,L:sgxblk%(tid&,ig&))=0
sgxblk%(tid&,ig&)=0
ELSE
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: can't free|(big) block memory.|][ Ok ]")
ENDIF
ENDIF
NEXT ig&
RETURN 0
ENDIF
RETURN -1
ENDFUNC
> FUNCTION gxalloc(tid&,wanted_size%)
$F%
'
wanted_size%=ADD(wanted_size%,3) AND -4
wanted_size%=MAX(4,wanted_size%)
'
IF wanted_size%>0
IF wanted_size%<gxblk_size%(tid&)
IF SUB(last_gxlen%(tid&),wanted_size%)<0
temp_adr%=@new_gxblk(tid&)
IF temp_adr%=0
new_gxadr%=last_gxadr%(tid&)
ADD last_gxadr%(tid&),wanted_size%
SUB last_gxlen%(tid&),wanted_size%
RETURN new_gxadr%
ENDIF
RETURN temp_adr%
ELSE
new_gxadr%=last_gxadr%(tid&)
ADD last_gxadr%(tid&),wanted_size%
SUB last_gxlen%(tid&),wanted_size%
RETURN new_gxadr%
ENDIF
ELSE IF wanted_size%=>gxblk_size%(tid&)
IF sgxblk_count&(tid&)<gxblk_max&
temp_adr%=@mxalloc(ADD(wanted_size%,32),3)
IF temp_adr%>0
sgxblk%(tid&,sgxblk_count&(tid&))=temp_adr%
INC sgxblk_count&(tid&)
RETURN temp_adr%
ENDIF
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: can't allocate|a large memory block.][ Ok ]")
RETURN temp_adr%
ENDIF
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: all available (big) memory|blocks are allocated.][ Ok ]")
RETURN -74
ENDIF
ENDIF
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: negative|length for a memory block.][ Ok ]")
RETURN -70
ENDFUNC
> FUNCTION new_gxblk(tid&)
$F%
INC gxblk_count&(tid&)
IF gxblk_count&(tid&)<gxblk_max&
gxblk%(tid&,gxblk_count&(tid&))=@mxalloc(ADD(gxblk_size%(tid&),32),3)
IF gxblk%(tid&,gxblk_count&(tid&))>0
last_gxadr%(tid&)=gxblk%(tid&,gxblk_count&(tid&))
last_gxlen%(tid&)=gxblk_size%(tid&)
RETURN 0
ELSE
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: can't |allocate a block.][ Ok ]")
ENDIF
DEC gxblk_count&(tid&)
RETURN gxblk%(tid&,SUCC(gxblk_count&(tid&)))
ENDIF
~FORM_ALERT(1,"[1][|Litchi's GXALLOC: all available|blocks are allocated.][ Ok ]")
RETURN -74
ENDFUNC
'
> FUNCTION mx_mask
$F%
IF GEMDOS(68,L:-1,0)=-32
RETURN 0
ELSE IF GEMDOS(290,-1)=-32
RETURN 3
ELSE
RETURN -1
ENDIF
ENDFUNC
> FUNCTION mxalloc(mx_len%,mx_mode&)
$F%
IF mx_alloc!
RETURN GEMDOS(68,L:mx_len%,W:mx_mode&)
ENDIF
RETURN GEMDOS(72,L:mx_len%)
ENDFUNC
> FUNCTION mxalloc_global(mx_len%,mx_mode&)
$F%
'
IF mx_mask%<>0
mx_mode&=OR(mx_mode&,&X101000)
RETURN GEMDOS(68,L:mx_len%,W:mx_mode& AND mx_mask%)
ELSE
RETURN GEMDOS(72,L:mx_len%)
ENDIF
ENDFUNC
> PROCEDURE mxfree(mx_adr%)
IF mx_adr%>0
~GEMDOS(73,L:mx_adr%)
ENDIF
RETURN
> PROCEDURE pdomain(domain&)
IF magic! OR mint!
~GEMDOS(281,W:domain&)
ENDIF
RETURN
'
