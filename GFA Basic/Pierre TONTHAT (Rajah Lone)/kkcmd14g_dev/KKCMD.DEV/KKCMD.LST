$m320000
DEFFLT "a-b"
' RESERVE
'
~FRE(0)
IF FRE()<160000
  ~FORM_ALERT(1,"[1][Insufficient or fragmented memory. ][ Quit ]")
  QUIT 0
ELSE
  IF BYTE{ADD(BASEPAGE,128)}>0
    ordre$=LEFT$(CHAR{ADD(BASEPAGE,129)},BYTE{ADD(BASEPAGE,128)})
  ENDIF
  '
  init_start
  init_rsc
  @load_options
  init_end
  main
ENDIF
'
> PROCEDURE ask_leaving
  '
  IF misc_confirm_quit!
    proceed!=(@alert(1,18)=1)
  ELSE
    proceed!=TRUE
  ENDIF
  '
  IF proceed!
    validate
    leave
  ENDIF
RETURN
> PROCEDURE validate
  '
  '
RETURN
> PROCEDURE leave
  mouse_free
  '
  ~@msa_clean_image_in_ram(msa_image_adr%(3))
  ~@msa_clean_image_in_ram(msa_image_adr%(4))
  '
  @zipfile_unload(3)
  @zipfile_unload(4)
  '
  @zlib_close(FALSE)
  @ldg_exit
  '
  IF misc_save_configuration_at_leave!
    @save_options
  ENDIF
  close_all_windows
  '
  @av_exit
  ~@log_exit
  '
  @record_close(1)
  @record_close(3)
  @record_close(4)
  @record_close(5)
  @record_close(6)
  @record_general_exit
  @gxalloc_main_exit(12)
  '
  mxfree(fslx_mem%)
  mxfree(raster_buf%)
  mxfree(photo_buf%)
  mxfree(m_adr%)
  mxfree(shell_buf%)
  mxfree(xattr_buf%)
  mxfree(remote_path%)
  mxfree(va_start%)
  mxfree(vtxt_buf%)
  '
  ~MENU_BAR(adtree%(0),0)
  ~RSRC_FREE()
  '
  v_clsvwk(vdi_handle&)
  ~APPL_EXIT()
  '
  QUIT 0
RETURN
'
> PROCEDURE init_start
  global%=LONG{ADD(GB,4)}
  '
  ap_id&=APPL_INIT()
  '
  vdi_handle&=@v_opnvwk
  '
  mouse_free
  '
  IF ap_id&=-1 OR vdi_handle&<1
    leave
  ENDIF
  '
  IF VAL(HEX$(INT{global%}))>=399
    naes!=TRUE
  ELSE
    naes!=FALSE
  ENDIF
  '
  mx_alloc!=(GEMDOS(48)>=&H1900)
  mx_mask%=@mx_mask
  '
  magic!=@test_cookie("MagX",dummy%)
  mint!=@test_cookie("MiNT",dummy%)
  nvdi!=@test_cookie("NVDI",dummy%)
  '
  IF mint! OR (magic! AND ap_id&>0)
    multi!=TRUE
  ELSE
    multi!=FALSE
  ENDIF
  '
  IF multi!
    shl_write(9,1,0,"","")
  ENDIF
  '
  pdomain(1)
  '
  ~WIND_UPDATE(1)
  ~WIND_UPDATE(3)
  ~WIND_GET(0,4,screenx&,screeny&,screenl&,screenh&)
  '
  @declare_variables
  IF mint!=FALSE
    RESERVE 128000
  ENDIF
  @declare_allocations
  '
  IF @s_exist(kkcmd_path$+kkcmd_rsc$)=TRUE
    IF RSRC_LOAD(kkcmd_path$+kkcmd_rsc$)=0
      ~FORM_ALERT(1,"[1][ KKCMD.RSC couldn't be loaded. ][ Quit ]")
      leave
    ELSE
      FOR i&=0 TO nb_tree&
        ~RSRC_GADDR(0,i&,adtree%(i&))
      NEXT i&
      FOR i&=1 TO nb_tree&
        ~FORM_CENTER(adtree%(i&),xd&(i&),yd&(i&),dummy&,dummy&)
        ld&(i&)=OB_W(adtree%(i&),0)
        hd&(i&)=OB_H(adtree%(i&),0)
      NEXT i&
    ENDIF
  ELSE
    ~FORM_ALERT(1,"[1][ Can't find KKCMD.RSC| put it besides LITCHI.PRG |][ Quit ]")
    leave
  ENDIF
  '
  kkcmd_configuration_file$=kkcmd_path$+kkcmd_inf$
  '
  home_path$=@get_env$("HOME")
  '
  IF LEN(home_path$)>0
    IF @s_exist(home_path$+kkcmd_inf$) ! already in $HOME
      '
      kkcmd_configuration_file$=home_path$+kkcmd_inf$
      '
    ELSE IF @s_exist(kkcmd_path$+kkcmd_inf$) ! already in native directory
      '
      kkcmd_configuration_file$=kkcmd_path$+kkcmd_inf$
      '
    ELSE ! is the home directory is writable?
      '
      kkcmd_tst_file$=home_path$+"kkcmd.tst"+c0$
      dummy&=GEMDOS(60,L:V:kkcmd_tst_file$,W:0)
      IF dummy&>0
        ~GEMDOS(62,W:dummy&)
        ~GEMDOS(65,L:V:kkcmd_tst_file$)
        '
        kkcmd_configuration_file$=home_path$+kkcmd_inf$
        '
      ELSE
        '
        kkcmd_configuration_file$=kkcmd_path$+kkcmd_inf$
        '
      ENDIF
    ENDIF
  ENDIF
  '
  stguide_app$=@get_env$("STGUIDE")
  IF RIGHT$(stguide_app$)="\"
    stguide_app$=LEFT$(stguide_app$,PRED(LEN(stguide_app$)))
  ENDIF
  stguide_app$=stguide_app$+c0$
  '
RETURN
> PROCEDURE init_rsc
  '
  IF nb_plan&<4
    obj_deselect(5,11)
    OB_SPEC(adtree%(5),12)=SUCC(OB_SPEC(adtree%(5),12) AND &X110010000)
    OB_SPEC(adtree%(5),13)=SUCC(OB_SPEC(adtree%(5),13) AND &X110110000)
    obj_deselect(19,6)
    OB_SPEC(adtree%(19),7)=SUCC(OB_SPEC(adtree%(19),7) AND &X110010000)
    obj_deselect(20,4)
    OB_SPEC(adtree%(20),5)=SUCC(OB_SPEC(adtree%(20),5) AND &X110010000)
    obj_deselect(21,3)
    OB_SPEC(adtree%(21),4)=SUCC(OB_SPEC(adtree%(21),4) AND &X110010000)
    obj_deselect(24,4)
    OB_SPEC(adtree%(24),5)=SUCC(OB_SPEC(adtree%(24),5) AND &X110010000)
  ENDIF
  '
  IF screenh&<250
    INT{ADD(OB_SPEC(adtree%(3),2),6)}=8
  ENDIF
  '
  IF screenh&<250
    INT{ADD(OB_SPEC(adtree%(4),2),6)}=8
  ENDIF
  '
  field_bar_h&(3)=SUCC(OB_H(adtree%(3),0))
  field_bar_h&(4)=SUCC(OB_H(adtree%(4),0))
  '
  FOR i&=1 TO nb_tree&
    j&=OB_TAIL(adtree%(i&),OB_TAIL(adtree%(i&),0))
    IF j&=-1
      j&=OB_TAIL(adtree%(i&),0)
    ENDIF
    FOR k&=1 TO j&
      IF (OB_TYPE(adtree%(i&),k&)=30 OR OB_TYPE(adtree%(i&),k&)=29) AND BTST(OB_FLAGS(adtree%(i&),k&),3)
        CHAR{{OB_SPEC(adtree%(i&),k&)}}=""
      ENDIF
      IF NOT magic!
        IF OB_TYPE(adtree%(i&),k&)=26 AND BTST(OB_STATE(adtree%(i&),k&),15)=TRUE AND BTST(OB_STATE(adtree%(i&),k&),6)=TRUE AND BTST(OB_FLAGS(adtree%(i&),k&),9)=FALSE
          CHAR{OB_SPEC(adtree%(i&),k&)}=""
          OB_FLAGS(adtree%(i&),k&)=BSET(OB_FLAGS(adtree%(i&),k&),9)
        ENDIF
      ENDIF
    NEXT k&
  NEXT i&
  '
  OB_Y(adtree%(8),display_subdialog&(0))=SUCC(OB_Y(adtree%(8),display_subdialog&(0)))
  FOR i&=0 TO 2
    IF i&>0
      OB_Y(adtree%(8),display_subdialog&(i&))=OB_Y(adtree%(8),display_subdialog&(0))
    ENDIF
    OB_SPEC(adtree%(8),display_subdialog&(i&))=OB_SPEC(adtree%(8),display_subdialog&(i&)) AND &X1111111111111111
  NEXT i&
  OB_H(adtree%(8),0)=ADD(ADD(OB_H(adtree%(8),display_subdialog&(0)),OB_Y(adtree%(8),display_subdialog&(0))),8)
  hd&(8)=OB_H(adtree%(8),0)
  display_hide_subdialogs
  obj_select(8,1)
  obj_unhide(8,display_subdialog&(0))
  '
  IF screenh&<250
    obj_disable(8,19)
  ENDIF
  '
  OB_Y(adtree%(9),misc_subdialog&(0))=SUCC(OB_Y(adtree%(9),misc_subdialog&(0)))
  FOR i&=0 TO 3
    IF i&>0
      OB_Y(adtree%(9),misc_subdialog&(i&))=OB_Y(adtree%(9),misc_subdialog&(0))
    ENDIF
    OB_SPEC(adtree%(9),misc_subdialog&(i&))=OB_SPEC(adtree%(9),misc_subdialog&(i&)) AND &X1111111111111111
  NEXT i&
  OB_H(adtree%(9),0)=ADD(ADD(OB_H(adtree%(9),misc_subdialog&(0)),OB_Y(adtree%(9),misc_subdialog&(0))),8)
  hd&(9)=OB_H(adtree%(9),0)
  misc_hide_subdialogs
  obj_select(9,1)
  obj_unhide(9,misc_subdialog&(0))
  '
  FOR i&=0 TO 10
    CHAR{{OB_SPEC(adtree%(15),ADD(3,i&))}}=""
  NEXT i&
  '
  str_files$=CHAR{OB_SPEC(adtree%(alert_tree&),14)}
  str_octets$=CHAR{OB_SPEC(adtree%(alert_tree&),15)}
  str_folders$=CHAR{OB_SPEC(adtree%(alert_tree&),16)}
  str_bytes$=CHAR{OB_SPEC(adtree%(alert_tree&),9)}
  str_kilobytes$=CHAR{OB_SPEC(adtree%(alert_tree&),10)}
  str_megabytes$=CHAR{OB_SPEC(adtree%(alert_tree&),11)}
  str_gigabytes$=CHAR{OB_SPEC(adtree%(alert_tree&),12)}
  '
  obj_hide(19,5)
  IF @test_cookie("_FDC",dummy%)
    IF BTST(dummy%,24)
      @obj_unhide(19,5)
    ENDIF
  ENDIF
  '
  FOR i&=1 TO 2
    OB_X(adtree%(nb_tree&),i&)=0
    OB_Y(adtree%(nb_tree&),i&)=0
  NEXT i&
  '
RETURN
> PROCEDURE declare_variables
  '
  crlf$=CHR$(13)+CHR$(10)
  '
  DIM boolean$(1)
  boolean$(0)="FALSE"
  boolean$(1)="TRUE"
  '
  home_path$=SPACE$(256)
  '
  c0$=CHR$(0)
  mask_all$="*.*"+c0$
  '
  kkcmd_path$=CHR$(ADD(GEMDOS(25),65))+":"+DIR$(SUCC(GEMDOS(25)))+"\"
  IF mint!
    kkcmd_prg$="kkcmd.prg"+c0$
    kkcmd_rsc$="kkcmd.rsc"+c0$
    kkcmd_inf$="kkcmd.cfg"+c0$
    kkcmd_hyp$="kkcmd.hyp"+c0$
    kkcmd_log$="kkzip.log"+c0$
    '
    ext_crc$=".crc"+c0$
    ext_zip$=".zip"+c0$
  ELSE
    kkcmd_prg$="KKCMD.PRG"+c0$
    kkcmd_rsc$="KKCMD.RSC"+c0$
    kkcmd_inf$="KKCMD.CFG"+c0$
    kkcmd_hyp$="KKCMD.HYP"+c0$
    kkcmd_log$="KKZIP.LOG"+c0$
    '
    ext_crc$=".CRC"+c0$
    ext_zip$=".ZIP"+c0$
  ENDIF
  kkcmd_configuration_file$=SPACE$(256)
  kkcmd_register_file$=SPACE$(256)
  '
  kkcmd_guide$=kkcmd_path$+kkcmd_hyp$
  kkcmd_logfile$=kkcmd_path$+kkcmd_log$
  '
  ldg_path$=SPACE$(128)
  '
  preferences_text_size&=100
  DIM preferences_text$(preferences_text_size&)
  FOR i&=0 TO PRED(preferences_text_size&)
    preferences_text$(i&)=SPACE$(128)
  NEXT i&
  '
  fi_path$=SPACE$(512)
  fi_name$=SPACE$(128)
  '
  nb_tree&=28
  nb_win&=9
  alert_tree&=11
  '
  DIM bubble_str%(nb_win&,64)
  '
  DIM adtree%(nb_tree&),xd&(nb_tree&),yd&(nb_tree&),ld&(nb_tree&),hd&(nb_tree&)
  DIM hand_win&(nb_tree&),wx&(nb_tree&),wy&(nb_tree&),wl&(nb_tree&),wh&(nb_tree&)
  DIM pwx&(nb_tree&),pwy&(nb_tree&),pwl&(nb_tree&),pwh&(nb_tree&),win_pos$(nb_tree&),ope!(nb_tree&)
  '
  icon!=FALSE
  DIM win!(nb_tree&),aff!(nb_tree&),ful!(nb_tree&),forced!(nb_tree&)
  FOR i&=0 TO nb_tree&
    win!(i&)=FALSE
    aff!(i&)=FALSE
    ful!(i&)=FALSE
    ope!(i&)=FALSE
  NEXT i&
  '
  DIM cp_win&(nb_tree&)
  FOR i&=1 TO nb_win&
    cp_win&(i&)=&X1011
  NEXT i&
  FOR i&=3 TO 4
    cp_win&(i&)=&X111111111101
    IF naes!
      cp_win&(i&)=BSET(cp_win&(i&),14)
    ENDIF
  NEXT i&
  '
  DIM font_files_real_height&(nb_win&),display_files_mode&(nb_win&)
  DIM msa_image_adr%(nb_win&),msa_image_name$(nb_win&),zip_filename$(nb_win&)
  DIM zip_central_offset%(nb_win&),zip_end_central_offset%(nb_win&)
  DIM parent_record_start$(nb_win&)
  '
  parent_record_start$(3)=SPACE$(256)
  parent_record_start$(4)=SPACE$(256)
  '
  msa_disk_adr%=0
  msa_image_adr%(3)=0
  msa_image_name$(3)=SPACE$(256)
  msa_image_adr%(4)=0
  msa_image_name$(4)=SPACE$(256)
  '
  zip_filename$(3)=SPACE$(256)
  zip_filename$(4)=SPACE$(256)
  '
  local_path_left$=SPACE$(256)
  local_path_right$=SPACE$(256)
  '
  loc_drive_nb&=0
  DIM loc_drive$(26),loc_drive&(26)
  DIM local_drive_index&(nb_win&)
  '
  FOR i&=0 TO 26
    loc_drive$(i&)=SPACE$(4)
    loc_drive$(i&)=SPACE$(4)
  NEXT i&
  '
  DIM shortcuts$(10)
  FOR i&=0 TO 10
    shortcuts$(i&)=SPACE$(256)
  NEXT i&
  '
  DIM display_subdialog&(2)
  '
  display_subdialog&(0)=3
  display_subdialog&(1)=20
  '
  display_win_pos_type&=1
  display_win_pos_save!=FALSE
  display_disk_b!=TRUE
  display_sort_discard_hidden_files!=FALSE
  display_files_with_date!=TRUE
  display_files_date_format&=1
  display_files_with_length!=TRUE
  display_files_length_format&=1
  display_files_length_unity$=SPACE$(10)
  display_files_font_size&=0
  '
  display_sort_type&=1
  display_sort_distinguish_case!=TRUE
  display_sort_folder_at_top!=TRUE
  display_sort_reverse!=FALSE
  '
  DIM misc_subdialog&(3)
  '
  misc_subdialog&(0)=5
  misc_subdialog&(1)=8
  misc_subdialog&(2)=13
  misc_subdialog&(3)=17
  '
  misc_keep_date!=TRUE
  misc_save_configuration_at_leave!=FALSE
  misc_verify_free_space!=TRUE
  '
  misc_choice_existing_file_type&=0
  '
  misc_confirm_quit!=TRUE
  misc_confirm_send!=TRUE
  misc_confirm_delete!=TRUE
  '
  misc_zip_use_log!=FALSE
  misc_zip_deflate_level&=2
  '
  base_extension$=".KDB"+c0$
  back_extension$=".BAK"+c0$
  '
  DIM pop_up_value&(22)
  pop_up_start&=0
  pop_up_height&=20
  pop_up_max&=0
  '
  DIM minor|(256)
  FOR i&=0 TO 255
    minor|(i&)=i&
  NEXT i&
  FOR i&=65 TO 90
    minor|(i&)=ADD(i&,32)
  NEXT i&
  minor|(128)=135
  minor|(142)=132
  minor|(143)=134
  minor|(144)=130
  minor|(146)=145
  minor|(153)=148
  minor|(154)=129
  minor|(165)=164
  minor|(178)=179
  minor|(181)=180
  minor|(182)=133
  minor|(183)=176
  minor|(184)=177
  minor|(193)=192
  '
  DIM major|(256)
  FOR i&=0 TO 255
    major|(i&)=i&
  NEXT i&
  FOR i&=97 TO 122
    major|(i&)=SUB(i&,32)
  NEXT i&
  major|(128)=67
  major|(129)=85
  major|(130)=69
  major|(131)=65
  major|(132)=65
  major|(133)=65
  major|(134)=65
  major|(135)=67
  major|(136)=69
  major|(137)=69
  major|(138)=69
  major|(139)=73
  major|(140)=73
  major|(141)=73
  major|(142)=65
  major|(143)=65
  major|(144)=69
  major|(145)=146
  major|(147)=79
  major|(148)=79
  major|(149)=79
  major|(150)=85
  major|(151)=85
  major|(152)=89
  major|(153)=79
  major|(154)=85
  major|(160)=65
  major|(161)=73
  major|(162)=79
  major|(163)=85
  major|(164)=78
  major|(165)=78
  major|(166)=65
  major|(167)=79
  major|(176)=65
  major|(177)=79
  major|(178)=79
  major|(179)=79
  major|(180)=181
  major|(182)=65
  major|(183)=65
  major|(184)=79
  '
  @fmt_init
  '
  split_current_size&=700
  '
  zip_dont_compress_nb&=32
  DIM zip_dont_compress%(32)
  '
  zip_dont_compress%(1)=CVL("ZIP ")
  zip_dont_compress%(2)=CVL("ZOO ")
  zip_dont_compress%(3)=CVL("GZ  ")
  zip_dont_compress%(4)=CVL("LZH ")
  zip_dont_compress%(5)=CVL("RAR ")
  zip_dont_compress%(6)=CVL("DEB ")
  zip_dont_compress%(7)=CVL("LHA ")
  zip_dont_compress%(8)=CVL("PKG ")
  zip_dont_compress%(9)=CVL("7Z  ")
  zip_dont_compress%(10)=CVL("JAR ")
  zip_dont_compress%(11)=CVL("TAR ")
  zip_dont_compress%(12)=CVL("TGZ ")
  zip_dont_compress%(13)=CVL("RPM ")
  zip_dont_compress%(14)=CVL("ARC ")
  zip_dont_compress%(15)=CVL("ARJ ")
  zip_dont_compress%(16)=CVL("WAD ")
  '
  zip_dont_compress%(17)=CVL("GIF ")
  zip_dont_compress%(18)=CVL("JPG ")
  zip_dont_compress%(19)=CVL("JPEG")
  zip_dont_compress%(20)=CVL("MPG ")
  zip_dont_compress%(21)=CVL("MPEG")
  zip_dont_compress%(22)=CVL("MP2 ")
  zip_dont_compress%(23)=CVL("MP3 ")
  zip_dont_compress%(24)=CVL("MP4 ")
  zip_dont_compress%(25)=CVL("AVI ")
  zip_dont_compress%(26)=CVL("HYP ")
  zip_dont_compress%(27)=CVL("PNG ")
  zip_dont_compress%(28)=CVL("WMV ")
  zip_dont_compress%(29)=CVL("SWF ")
  zip_dont_compress%(30)=CVL("DIVX")
  zip_dont_compress%(31)=CVL("JIF ")
  zip_dont_compress%(32)=CVL("SPC ")
  '
  va_start!=FALSE
  '
RETURN
> PROCEDURE declare_allocations
  '
  crc32_init
  '
  FOR i&=0 TO 10
    shortcuts$(i&)=c0$
  NEXT i&
  '
  @gxalloc_main_init(12)
  @record_general_init(6)
  @record_open(1,16,30,0)
  @record_open(3,64,144,5)
  @record_open(4,64,record_len&(3),5)
  @record_open(5,16,32,0)
  @record_close(5)
  @record_open(6,16,record_len&(5),0)
  @record_close(6)
  '
  IF magic! OR mint!
    m_adr%=@mxalloc(32,3)
    bubble_buf%=@mxalloc_global(256,3)
    shell_buf%=@mxalloc_global(256,3)
    xattr_buf%=@mxalloc(64,0)
    vtxt_buf%=@mxalloc(256,0)
    local_path0%=@mxalloc(640,0)
    local_path1%=ADD(local_path0%,256)
    local_info0%=ADD(local_path1%,256)
    local_info1%=ADD(local_info0%,64)
  ELSE
    m_adr%=@mxalloc(32,3)
    bubble_buf%=@mxalloc(256,3)
    shell_buf%=@mxalloc(256,3)
    xattr_buf%=0
    vtxt_buf%=@mxalloc(256,0)
    local_path0%=@mxalloc(512,0)
    local_path1%=ADD(local_path0%,256)
    local_info0%=ADD(local_path1%,256)
    local_info1%=ADD(local_info0%,64)
  ENDIF
  '
  IF local_path0%>0
    BYTE{local_path0%}=0
    BYTE{local_path1%}=0
  ENDIF
  '
  fslx_init
  '
  va_start%=@mxalloc(512,3)
  '
  IF m_adr%=<0
    leave
  ELSE
    ABSOLUTE m_type&,m_adr%
    ABSOLUTE m_ap_id&,ADD(m_adr%,2)
    ABSOLUTE m_title&,ADD(m_adr%,6)
    ABSOLUTE m_win&,ADD(m_adr%,6)
    ABSOLUTE m_item&,ADD(m_adr%,8)
    ABSOLUTE m_x&,ADD(m_adr%,8)
    ABSOLUTE m_y&,ADD(m_adr%,10)
    ABSOLUTE m_l&,ADD(m_adr%,12)
    ABSOLUTE m_h&,ADD(m_adr%,14)
    '
    s_adr%=ADD(m_adr%,16)
  ENDIF
  '
  INT{ADD(CONTRL,12)}=vdi_handle&
  INT{INTIN}=1
  VDISYS 102,1,0
  nb_plan&=INT{ADD(INTOUT,8)}
  IF nb_plan&=0
    nb_plan&=ROUND(LOG(WORK_OUT(13))/LOG(2))
  ENDIF
  '
  photo_buf%=0
  raster_buf%=@mxalloc(128,3)
  IF raster_buf%>0
    '
    photo_buf_old_length%=MUL(8000,nb_plan&)
    photo_buf%=@mxalloc(photo_buf_old_length%,3)
    IF photo_buf%<1
      leave
    ENDIF
    '
    pscrmfdb%=raster_buf%
    pdesmfdb%=ADD(pscrmfdb%,20)
    pxyarray%=ADD(pdesmfdb%,20)
    '
    make_zero_mfdb(pscrmfdb%)
    make_zero_mfdb(pdesmfdb%)
    '
    LONG{pdesmfdb%}=photo_buf%
    WORD{ADD(pdesmfdb%,4)}=320
    WORD{ADD(pdesmfdb%,6)}=200
    WORD{ADD(pdesmfdb%,8)}=20
    WORD{ADD(pdesmfdb%,12)}=nb_plan&
    '
  ENDIF
  '
  log_pos%=@log_init
  '
RETURN
> PROCEDURE init_end
  '
  @av_init
  '
  vsf_interior(1)
  vsf_color(0)
  vsf_style(0)
  vsf_perimeter(0)
  '
  vst_alignment(0,5)
  '
  ~WIND_UPDATE(2)
  ~WIND_UPDATE(0)
  '
  ~MENU_BAR(adtree%(0),1)
  IF multi!=FALSE
    ~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
  ENDIF
  '
  IF av_server!=FALSE
    ~MENU_IENABLE(adtree%(0),20,0)
  ENDIF
  '
  win(4)
  win(3)
  '
  @local_set_drive(4)
  @local_set_drive(3)
  '
  @display_refresh_directories
  '
  va_start!=TRUE
  '
RETURN
'
> PROCEDURE main
  DO
    evnt&=@ev_multi(&X110011,258,3,0,100,mo_x&,mo_y&,mo_k&,m_touche&,m_clavier&,mo_c&)
    IF BTST(evnt&,0)
      manage_keys_general
    ENDIF
    IF BTST(evnt&,1)
      manage_mouse_general
    ENDIF
    IF BTST(evnt&,4)
      SELECT m_type&
      CASE 10
        manage_menu
      CASE 20
        redraw
      CASE 21
        win_topped
      CASE 29
        win_ontop
      CASE 30
      CASE 31
      CASE 33
        win_untopped
      CASE 22
        win_closed
      CASE 23
        win_fulled
      CASE 24
        win_arrowed
      CASE 25
        IF m_win&=@get_top_window
          win_hslided
        ELSE
          FOR i&=1 TO 4
            IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
              force_top(i&)
            ENDIF
          NEXT i&
        ENDIF
      CASE 26
        IF m_win&=@get_top_window
          win_vslided
        ELSE
          FOR i&=1 TO 4
            IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
              force_top(i&)
            ENDIF
          NEXT i&
        ENDIF
      CASE 28
        win_moved
      CASE 27
        win_sized
      CASE 34,36
        iconify
      CASE 35
        uniconify
      CASE 50
        shut_down
      CASE 18193
        IF m_ap_id&<>ap_id&
          va_start
        ENDIF
      CASE 19530
        zlib_close(TRUE)
      CASE 22360
        FOR i&=1 TO nb_win&
          IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
            aff!(i&)=FALSE
          ENDIF
        NEXT i&
      CASE 22361
        FOR i&=1 TO nb_win&
          IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
            aff!(i&)=TRUE
          ENDIF
        NEXT i&
      ENDSELECT
      clear_m
    ENDIF
    IF BTST(evnt&,5)
      INC cpt_garbage&
      IF cpt_garbage&>300
        ~FRE()
        ~FRE(0)
        cpt_garbage&=0
      ENDIF
    ENDIF
    '
  LOOP
RETURN
> PROCEDURE clear_m
  IF m_adr%>0
    LONG{m_adr%}=0
    LONG{ADD(m_adr%,4)}=0
    LONG{ADD(m_adr%,8)}=0
    LONG{ADD(m_adr%,12)}=0
  ENDIF
RETURN
> PROCEDURE clear_keys
  m_clavier&=0
  m_touche&=0
RETURN
> PROCEDURE clear_mouse
  mo_k&=0
  mo_c&=0
RETURN
> PROCEDURE test_evnt
  evnt&=@ev_multi(&X110000,2,1,1,30,mo_x&,mo_y&,mo_k&,m_touche&,m_clavier&,mo_c&)
  IF BTST(evnt&,4)
    SELECT m_type&
    CASE 20
      redraw
    CASE 21
      win_topped
    CASE 22
      win_closed
    CASE 28
      win_moved
    ENDSELECT
  ENDIF
RETURN
'
> PROCEDURE manage_menu
  ~MENU_TNORMAL(adtree%(0),m_title&,1)
  SELECT m_item&
  CASE 9 ! info
    dialog_about
  CASE 18 ! shortcuts
    win(2)
  CASE 20 ! open log
    @log_open
  CASE 22 ! delete log
    @log_clear
  CASE 24 ! quit
    ask_leaving
  CASE 26 ! display
    win(8)
  CASE 27 ! misc
    win(9)
  CASE 29 ! save prefs
    @save_options
  CASE 31 ! stguide
    call_documentation
  ENDSELECT
RETURN
> PROCEDURE manage_mouse_general
  IF mo_c&=1 AND mo_k&=1
    clic_win&=WIND_FIND(mo_x&,mo_y&)
    IF mint!=FALSE
      delay
    ENDIF
    manage_mouse_on_dials
  ELSE IF mo_c&=2 AND mo_k&=1
    clic_win&=WIND_FIND(mo_x&,mo_y&)
    manage_mouse_on_wins(0)
  ELSE IF mo_c&=1 AND mo_k&=2
    clic_win&=WIND_FIND(mo_x&,mo_y&)
    '
    IF icon!=FALSE
      IF clic_win&=hand_win&(3) AND aff!(3)=TRUE
        kk_win&=3
      ELSE IF clic_win&=hand_win&(4) AND aff!(4)=TRUE
        kk_win&=4
      ENDIF
      '
      IF kk_win&=3 OR kk_win&=4
        '
        IF record_nb_selected&(kk_win&)=0
          dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
          IF dummy&>-1
            record_select(kk_win&,dummy&)
            record_nb_selected&(kk_win&)=1
            '
            get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
            force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
          ENDIF
        ENDIF
        '
        @local_action_menu(kk_win&,TRUE)
        '
      ENDIF
    ENDIF
  ENDIF
RETURN
> PROCEDURE manage_mouse_on_dials
  IF clic_win&=hand_win&(2) AND aff!(2)=TRUE
    dialog_shortcuts
  ELSE IF clic_win&=hand_win&(3) AND aff!(3)=TRUE
    IF icon!=FALSE
      dialog_local(3)
    ENDIF
  ELSE IF clic_win&=hand_win&(4) AND aff!(4)=TRUE
    IF icon!=FALSE
      dialog_local(4)
    ENDIF
  ELSE IF clic_win&=hand_win&(8) AND aff!(8)=TRUE
    dialog_display
  ELSE IF clic_win&=hand_win&(9) AND aff!(9)=TRUE
    dialog_misc
  ENDIF
RETURN
> PROCEDURE manage_mouse_on_wins(kc_win&)
  LOCAL double_click_selected&,dc_win&,dc_darkened!
  '
  dc_win&=0
  IF icon!=FALSE
    IF kc_win&>0
      dc_win&=kc_win&
    ELSE IF clic_win&=hand_win&(3) AND aff!(3)=TRUE
      dc_win&=3
    ELSE IF clic_win&=hand_win&(4) AND aff!(4)=TRUE
      dc_win&=4
    ENDIF
  ENDIF
  '
  IF dc_win&=3 OR dc_win&=4
    IF kc_win&>0
      IF record_selected&(kc_win&)>-1
        dummy&=record_selected&(kc_win&)
      ELSE
        dummy&=@record_get_next_selected(kc_win&,-1)
      ENDIF
    ELSE
      dummy&=@get_record_under_cursor(dc_win&,mo_y&,mo_x&)
    ENDIF
    IF dummy&>-1
      '
      LET double_click_selected&=dummy&
      '
      SELECT @loc_get_type(@px(dc_win&,double_click_selected&))
      CASE 0
        IF kc_win&=0
          IF @is_record_selected(dc_win&,double_click_selected&)=FALSE
            record_select(dc_win&,double_click_selected&)
          ENDIF
          get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
        ENDIF
        '
        IF @loc_get_name$(@px(dc_win&,double_click_selected&))=".."
          @local_change_directory(dc_win&,0)
        ENDIF
        '
      CASE 2
        IF kc_win&=0
          IF @is_record_selected(dc_win&,double_click_selected&)=FALSE
            record_select(dc_win&,double_click_selected&)
          ENDIF
          get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
        ENDIF
        '
        @local_change_directory(dc_win&,@px(dc_win&,double_click_selected&))
        '
      CASE 1
        '
        IF display_files_mode&(dc_win&)<1
          IF kc_win&=0 AND @is_record_selected(dc_win&,double_click_selected&)=FALSE
            dc_darkened!=TRUE
            record_select(dc_win&,double_click_selected&)
            get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
            force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          ELSE
            dc_darkened!=FALSE
          ENDIF
          '
          txt$=@loc_get_name$(@px(dc_win&,double_click_selected&))
          ext$=UPPER$(RIGHT$(txt$,4))
          IF ext$=".PRG" OR ext$=".TOS" OR ext$=".TTP" OR ext$=".APP"
            IF dc_win&=3
              txt$=CHAR{local_path0%}+txt$+c0$
            ELSE IF dc_win&=4
              txt$=CHAR{local_path1%}+txt$+c0$
            ENDIF
            @launch(txt$)
          ELSE IF ext$=".MSA" OR RIGHT$(ext$,3)=".ST" OR ext$=".ZIP"
            IF dc_win&=3
              txt$=CHAR{local_path0%}+txt$+c0$
            ELSE IF dc_win&=4
              txt$=CHAR{local_path1%}+txt$+c0$
            ENDIF
            @local_change_directory(dc_win&,@px(dc_win&,double_click_selected&))
          ELSE IF ext$=".CRC"
            IF (dc_win&=3 AND display_files_mode&(4)=0) OR (dc_win&=4 AND display_files_mode&(3)=0)
              IF @alert(1,34)=1
                @unsplit_file(txt$,dc_win&)
              ENDIF
            ENDIF
          ELSE IF av_server!
            IF dc_win&=3
              txt$=CHAR{local_path0%}+txt$+c0$
            ELSE IF dc_win&=4
              txt$=CHAR{local_path1%}+txt$+c0$
            ENDIF
            @av_start_program(txt$)
          ENDIF
          '
          IF dc_darkened!
            IF kc_win&=0 AND @is_record_selected(dc_win&,double_click_selected&)=TRUE
              record_deselect(dc_win&,double_click_selected&)
              get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
              force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
            ENDIF
          ENDIF
        ENDIF
        '
      CASE 11,12
        '
        IF kc_win&=0
          IF @is_record_selected(dc_win&,double_click_selected&)=FALSE
            record_select(dc_win&,double_click_selected&)
          ENDIF
          get_record_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(dc_win&,double_click_selected&,sx&,sy&,sl&,sh&)
        ENDIF
        '
        txt$=@loc_get_name$(@px(dc_win&,double_click_selected&))
        IF dc_win&=3
          CHAR{local_path0%}=txt$+"\\"
        ELSE IF dc_win&=4
          CHAR{local_path1%}=txt$+"\\"
        ENDIF
        '
        @local_change_directory(dc_win&,0)
        '
      ENDSELECT
    ENDIF
  ENDIF
RETURN
> PROCEDURE manage_keys_general
  m_clavier|=BYTE(m_clavier&)
  top_win&=@get_top_window
  '
  SELECT m_clavier|
  CASE 9 ! Tab
    IF m_touche&=0
      IF hand_win&(3)=top_win& AND win!(3)=TRUE AND win!(4)=TRUE
        win(4)
      ELSE IF hand_win&(4)=top_win& AND win!(4)=TRUE AND win!(3)=TRUE
        win(3)
      ENDIF
    ENDIF
  CASE 17 ! ^Q
    ask_leaving
  CASE 19 ! ^S
    @save_options
  CASE 21 ! ^U
    FOR i&=2 TO nb_win&
      IF top_win&=hand_win&(i&) AND aff!(i&)=TRUE AND i&<>3 AND i&<>4
        close_win(i&)
      ENDIF
    NEXT i&
  ENDSELECT
  '
  IF m_clavier&=25088 ! Help
    call_documentation
  ENDIF
  '
  IF icon!=FALSE
    IF m_touche&=0
      SELECT m_clavier&
      CASE 15360 ! F2 swap windows contents
        dummy$=CHAR{local_path1%}
        @local_set_win_info(4,CHAR{local_path0%})
        @local_set_win_info(3,dummy$)
      CASE 16128 ! F5 = copy
        IF top_win&=hand_win&(3) AND win!(3)=TRUE
          IF record_nb_selected&(3)>0
            IF (display_files_mode&(3)<>2 AND display_files_mode&(4)=0) OR (display_files_mode&(3)=0 AND display_files_mode&(4)=2)
              @local_copy_files(3,FALSE)
            ELSE IF display_files_mode&(3)=2 AND display_files_mode&(4)=0
              @zipfile_extract_files(3)
            ENDIF
          ENDIF
        ELSE IF top_win&=hand_win&(4) AND win!(4)=TRUE
          IF record_nb_selected&(4)>0
            IF (display_files_mode&(4)<>2 AND display_files_mode&(3)=0) OR (display_files_mode&(4)=0 OR display_files_mode&(3)=2)
              @local_copy_files(4,FALSE)
            ELSE IF display_files_mode&(4)=2 AND display_files_mode&(3)=0
              @zipfile_extract_files(4)
            ENDIF
          ENDIF
        ENDIF
      CASE 16384 ! F6 = move
        IF top_win&=hand_win&(3) AND win!(3)=TRUE
          IF record_nb_selected&(3)>0
            IF display_files_mode&(3)=0 AND display_files_mode&(4)=0
              @local_copy_files(3,TRUE)
            ELSE IF display_files_mode&(3)=2 AND display_files_mode&(4)=0
              @zipfile_extract_files(3)
            ENDIF
          ENDIF
        ELSE IF top_win&=hand_win&(4) AND win!(4)=TRUE
          IF record_nb_selected&(4)>0
            IF display_files_mode&(4)=0 AND display_files_mode&(3)=0
              @local_copy_files(4,TRUE)
            ELSE IF display_files_mode&(4)=2 AND display_files_mode&(3)=0
              @zipfile_extract_files(4)
            ENDIF
          ENDIF
        ENDIF
      CASE 16640 ! F7 = new dir
        IF top_win&=hand_win&(3) AND win!(3)=TRUE
          IF display_files_mode&(3)=0
            @dialog_new_folder(3)
          ELSE IF display_files_mode&(3)=2
            @zipfile_new_folder(3)
          ENDIF
        ELSE IF top_win&=hand_win&(4) AND win!(4)=TRUE
          IF display_files_mode&(4)=0
            @dialog_new_folder(4)
          ELSE IF display_files_mode&(4)=2
            @zipfile_new_folder(4)
          ENDIF
        ENDIF
      CASE 16896 ! F8 = delete
        IF top_win&=hand_win&(3) AND win!(3)=TRUE
          IF record_nb_selected&(3)>0
            IF misc_confirm_delete!
              IF display_files_mode&(3)=0
                @local_delete_files(3)
              ELSE IF display_files_mode&(3)=2
                @zipfile_delete_files(3)
              ENDIF
            ENDIF
          ENDIF
        ELSE IF top_win&=hand_win&(4) AND win!(4)=TRUE
          IF record_nb_selected&(4)>0
            IF misc_confirm_delete!
              IF display_files_mode&(4)=0
                @local_delete_files(4)
              ELSE IF display_files_mode&(4)=2
                @zipfile_delete_files(4)
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      CASE 17152 ! F9
        IF display_sort_type&=1
          display_set_sort_type(4)
        ELSE
          IF display_sort_type&=4
            display_set_sort_type(3)
          ELSE
            IF display_sort_type&=3
              display_set_sort_type(2)
            ELSE
              IF display_sort_type&=2
                display_set_sort_type(0)
              ELSE
                IF display_sort_type&=0
                  display_set_sort_type(1)
                ENDIF
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      CASE 17408 ! F10
      CASE 28720 ! num 0
        @launch(shortcuts$(0)+c0$)
      CASE 27953 ! num 1
        @launch(shortcuts$(1)+c0$)
      CASE 28210 ! num 2
        @launch(shortcuts$(2)+c0$)
      CASE 28467 ! num 3
        @launch(shortcuts$(3)+c0$)
      CASE 27188 ! num 4
        @launch(shortcuts$(4)+c0$)
      CASE 27445 ! num 5
        @launch(shortcuts$(5)+c0$)
      CASE 27702 ! num 6
        @launch(shortcuts$(6)+c0$)
      CASE 26423 ! num 7
        @launch(shortcuts$(7)+c0$)
      CASE 26680 ! num 8
        @launch(shortcuts$(8)+c0$)
      CASE 26937 ! num 9
        @launch(shortcuts$(9)+c0$)
      ENDSELECT
    ELSE IF m_touche&=4
      SELECT m_clavier&
      CASE 15616 ! F3
        display_set_sort_type(1)
      CASE 15872 ! F4
        display_set_sort_type(4)
      CASE 16128 ! F5
        display_set_sort_type(3)
      CASE 16384 ! F6
        display_set_sort_type(2)
      CASE 16640 ! F7
        display_set_sort_type(0)
      ENDSELECT
    ELSE IF m_touche&=6
      SELECT m_clavier&
      CASE 19742 ! ^->
        IF record_nb_selected&(3)>0
          IF display_files_mode&(3)=0 AND display_files_mode&(4)=0
            @local_copy_files(3,TRUE)
          ELSE IF display_files_mode&(3)=2 AND display_files_mode&(4)=0
            @zipfile_extract_files(3)
          ENDIF
        ENDIF
      CASE 19220 ! ^<-
        IF record_nb_selected&(4)>0
          IF display_files_mode&(4)=0 AND display_files_mode&(3)=0
            @local_copy_files(4,TRUE)
          ELSE IF display_files_mode&(4)=2 AND display_files_mode&(3)=0
            @zipfile_extract_files(4)
          ENDIF
        ENDIF
      ENDSELECT
    ELSE IF m_touche&=8
      SELECT m_clavier&
      CASE 15104 ! F1
        @local_set_win_info(3,"")
      CASE 15360 ! F2
        @local_set_win_info(4,"")
      ENDSELECT
    ENDIF
    '
    IF m_touche&=4
      manage_keys_control
    ELSE
      manage_keys_normal
    ENDIF
  ENDIF
  '
RETURN
> PROCEDURE manage_keys_control
  '
  IF m_clavier&=29696 ! ^Right
    IF record_nb_selected&(3)>0
      IF (display_files_mode&(3)<>2 AND display_files_mode&(4)=0) OR (display_files_mode&(3)=0 AND display_files_mode&(4)=2)
        @local_copy_files(3,FALSE)
      ELSE IF display_files_mode&(3)=2 AND display_files_mode&(4)=0
        @zipfile_extract_files(3)
      ENDIF
    ENDIF
  ELSE IF m_clavier&=29440 ! ^Left
    IF record_nb_selected&(4)>0
      IF (display_files_mode&(4)<>2 AND display_files_mode&(3)=0) OR (display_files_mode&(4)=0 AND display_files_mode&(3)=2)
        @local_copy_files(4,FALSE)
      ELSE IF display_files_mode&(4)=2 AND display_files_mode&(3)=0
        @zipfile_extract_files(4)
      ENDIF
    ENDIF
  ELSE IF m_clavier&=18432 ! ^Up
    key_arrowed(0)
  ELSE IF m_clavier&=20480 ! ^Down
    key_arrowed(1)
  ELSE IF win!(3)=TRUE AND hand_win&(3)=@get_top_window
    SELECT m_clavier|
    CASE 1 ! ^A
      IF display_files_mode&(3)>-1
        @local_select_all_files(3)
      ENDIF
    CASE 8 ! ^Backspace
      IF display_files_mode&(3)>-1
        @local_change_directory(3,0) ! up
      ENDIF
    CASE 9 ! ^I
      IF record_nb_selected&(3)=1 AND display_files_mode&(3)<1
        dialog_informations(3,@record_get_next_selected(3,-1))
      ENDIF
    CASE 11 ! ^K
      IF display_files_mode&(3)=-1
        IF record_nb_selected&(3)=1
          dummy$=LEFT$(@loc_get_name$(@px(3,record_selected&(3))))
          IF dummy$="A" OR dummy$="B"
            @dialog_format(dummy$)
          ENDIF
        ENDIF
      ENDIF
    CASE 13 ! ^M
      IF display_files_mode&(3)=-1 AND display_files_mode&(4)=0
        IF record_nb_selected&(3)=1
          dummy$=LEFT$(@loc_get_name$(@px(3,record_selected&(3))))
          IF dummy$="A" OR dummy$="B"
            IF BYTE{local_path1%}<>ASC(dummy$)
              @dialog_msa_to_file(dummy$,4)
            ENDIF
          ENDIF
        ENDIF
      ELSE IF display_files_mode&(3)=0
        IF record_nb_selected&(3)=1
          dummy$=@loc_get_name$(@px(3,record_selected&(3)))
          IF UPPER$(RIGHT$(dummy$,4))=".MSA" OR UPPER$(RIGHT$(dummy$,3))=".ST"
            @dialog_msa_to_disk(dummy$)
          ENDIF
        ENDIF
      ENDIF
    CASE 14 ! ^N
      IF display_files_mode&(3)=0
        @dialog_new_folder(3)
      ELSE IF display_files_mode&(3)=2
        @zipfile_new_folder(3)
      ENDIF
    CASE 18 ! ^R
      SELECT display_files_mode&(3)
      CASE 2
        @zipfile_get_directory(3)
      CASE 1
        @msa_get_directory(3)
      CASE -1
        @local_get_disks_list(3)
      DEFAULT
        @local_get_directory(3)
      ENDSELECT
    CASE 31 ! ^Delete
      IF display_files_mode&(3)=-1
        IF record_nb_selected&(3)=1
          dummy$=LEFT$(@loc_get_name$(@px(3,record_selected&(3))))
          IF dummy$="A" OR dummy$="B"
            @dialog_format_quick(dummy$)
          ENDIF
        ENDIF
      ELSE IF display_files_mode&(3)=0
        IF record_nb_selected&(3)>0
          IF misc_confirm_delete!
            @local_delete_files(3)
          ENDIF
        ENDIF
      ELSE IF display_files_mode&(3)=2
        IF record_nb_selected&(3)>0
          IF misc_confirm_delete!
            @zipfile_delete_files(3)
          ENDIF
        ENDIF
      ENDIF
    CASE 23 ! ^\
      dummy$=CHAR{local_path0%}
      dummy&=INSTR(dummy$,"\")
      IF dummy&>0
        @local_set_win_info(3,LEFT$(dummy$,dummy&))
      ENDIF
    ENDSELECT
  ELSE IF win!(4)=TRUE AND hand_win&(4)=@get_top_window
    SELECT m_clavier|
    CASE 1 ! ^A
      IF display_files_mode&(4)>-1
        @local_select_all_files(4)
      ENDIF
    CASE 8 ! ^Backspace
      IF display_files_mode&(4)>-1
        @local_change_directory(4,0) ! up
      ENDIF
    CASE 9 ! ^I
      IF record_nb_selected&(4)=1 AND display_files_mode&(4)<1
        dialog_informations(4,@record_get_next_selected(4,0))
      ENDIF
    CASE 11 ! ^K
      IF display_files_mode&(4)=-1
        IF record_nb_selected&(4)=1
          dummy$=LEFT$(@loc_get_name$(@px(4,record_selected&(4))))
          IF dummy$="A" OR dummy$="B"
            @dialog_format(dummy$)
          ENDIF
        ENDIF
      ENDIF
    CASE 13 ! ^M
      IF display_files_mode&(3)=0 AND display_files_mode&(4)=-1
        IF record_nb_selected&(4)=1
          dummy$=LEFT$(@loc_get_name$(@px(4,record_selected&(4))))
          IF dummy$="A" OR dummy$="B"
            IF BYTE{local_path0%}<>ASC(dummy$)
              @dialog_msa_to_file(dummy$,3)
            ENDIF
          ENDIF
        ENDIF
      ELSE IF display_files_mode&(4)=0
        IF record_nb_selected&(4)=1
          dummy$=@loc_get_name$(@px(4,record_selected&(4)))
          IF UPPER$(RIGHT$(dummy$,4))=".MSA" OR UPPER$(RIGHT$(dummy$,3))=".ST"
            @dialog_msa_to_disk(dummy$)
          ENDIF
        ENDIF
      ENDIF
    CASE 14 ! ^N
      IF display_files_mode&(4)=0
        @dialog_new_folder(4)
      ELSE IF display_files_mode&(4)=2
        @zipfile_new_folder(4)
      ENDIF
    CASE 18 ! ^R
      SELECT display_files_mode&(4)
      CASE 2
        @zipfile_get_directory(4)
      CASE 1
        @msa_get_directory(4)
      CASE -1
        @local_get_disks_list(4)
      DEFAULT
        @local_get_directory(4)
      ENDSELECT
    CASE 31 ! ^Delete
      IF display_files_mode&(4)=-1
        IF record_nb_selected&(4)=1
          dummy$=LEFT$(@loc_get_name$(@px(4,record_selected&(4))))
          IF dummy$="A" OR dummy$="B"
            @dialog_format_quick(dummy$)
          ENDIF
        ENDIF
      ELSE IF display_files_mode&(4)=0
        IF record_nb_selected&(4)>0
          IF misc_confirm_delete!
            @local_delete_files(4)
          ENDIF
        ENDIF
      ELSE IF display_files_mode&(4)=2
        IF record_nb_selected&(4)>0
          IF misc_confirm_delete!
            @zipfile_delete_files(4)
          ENDIF
        ENDIF
      ENDIF
    CASE 23 ! ^\
      dummy$=CHAR{local_path1%}
      dummy&=INSTR(dummy$,"\")
      IF dummy&>0
        @local_set_win_info(4,LEFT$(dummy$,dummy&))
      ENDIF
    ENDSELECT
  ENDIF
RETURN
> PROCEDURE manage_keys_normal
  LOCAL space_win&
  '
  SELECT m_clavier&
  CASE 283 ! Esc
    IF hand_win&(3)=@get_top_window AND win!(3)=TRUE
      SELECT display_files_mode&(3)
      CASE 2
        @zipfile_get_directory(3)
      CASE 1
        @msa_get_directory(3)
      CASE -1
        @local_get_disks_list(3)
      DEFAULT
        @local_get_directory(3)
      ENDSELECT
    ELSE IF hand_win&(4)=@get_top_window AND win!(4)=TRUE
      SELECT display_files_mode&(4)
      CASE 2
        @zipfile_get_directory(4)
      CASE 1
        @msa_get_directory(4)
      CASE -1
        @local_get_disks_list(4)
      DEFAULT
        @local_get_directory(4)
      ENDSELECT
    ENDIF
  CASE 7181,29197 ! Return, Enter
    IF hand_win&(3)=@get_top_window AND win!(3)=TRUE
      @manage_mouse_on_wins(3)
    ELSE IF hand_win&(4)=@get_top_window AND win!(4)=TRUE
      @manage_mouse_on_wins(4)
    ENDIF
  CASE 18176 ! ClrHome
    key_arrowed(4)
  CASE 18231 ! ClrHome
    key_arrowed(5)
  CASE 18432 ! Up
    key_arrowed(2)
  CASE 20480 ! Down
    key_arrowed(3)
  CASE 18488 ! Up
    key_arrowed(6)
  CASE 20530 ! Down
    key_arrowed(7)
  CASE 20992 ! Insert
    sp_win&=0
    IF hand_win&(3)=@get_top_window AND win!(3)=TRUE
      sp_win&=3
    ELSE IF hand_win&(4)=@get_top_window AND win!(4)=TRUE
      sp_win&=4
    ENDIF
    '
    IF sp_win&>0
      IF display_files_mode&(sp_win&)>-1 AND record_selected&(sp_win&)>0
        IF @is_record_selected(sp_win&,record_selected&(sp_win&))=FALSE
          record_select(sp_win&,record_selected&(sp_win&))
          INC record_nb_selected&(sp_win&)
        ELSE
          record_deselect(sp_win&,record_selected&(sp_win&))
          DEC record_nb_selected&(sp_win&)
        ENDIF
        record_nb_selected&(sp_win&)=MAX(0,MIN(record_nb_selected&(sp_win&),head_nbline&(sp_win&)))
        get_record_xywh(sp_win&,record_selected&(sp_win&),sx&,sy&,sl&,sh&)
        force_update_xywh(sp_win&,record_selected&(sp_win&),sx&,sy&,sl&,sh&)
      ENDIF
    ENDIF
  CASE 14624 ! Space
    space_win&=WIND_FIND(mo_x&,mo_y&)
    IF hand_win&(3)=space_win& AND win!(3)=TRUE
      sp_win&=3
    ELSE IF hand_win&(4)=space_win& AND win!(4)=TRUE
      sp_win&=4
    ELSE
      sp_win&=0
    ENDIF
    '
    IF sp_win&>0
      dummy&=@get_record_under_cursor(sp_win&,mo_y&,mo_x&)
      IF dummy&>-1 AND display_files_mode&(sp_win&)>-1
        IF record_nb_selected&(sp_win&)=0
          '
          record_select(sp_win&,dummy&)
          INC record_nb_selected&(sp_win&)
          '
        ELSE IF record_nb_selected&(sp_win&)>0
          '
          IF @is_record_selected(sp_win&,dummy&)=FALSE
            record_select(sp_win&,dummy&)
            INC record_nb_selected&(sp_win&)
          ELSE
            record_deselect(sp_win&,dummy&)
            DEC record_nb_selected&(sp_win&)
          ENDIF
          '
        ENDIF
        record_nb_selected&(sp_win&)=MAX(0,MIN(record_nb_selected&(sp_win&),head_nbline&(sp_win&)))
        get_record_xywh(sp_win&,dummy&,sx&,sy&,sl&,sh&)
        force_update_xywh(sp_win&,dummy&,sx&,sy&,sl&,sh&)
      ENDIF
    ENDIF
  DEFAULT
    IF m_clavier|>64 AND m_clavier|<91
      @local_get_drives
      dummy&=@local_get_drive_index_from_letter(m_clavier|)
      IF hand_win&(3)=top_win& AND win!(3)=TRUE
        local_drive_index&(3)=dummy&
        CHAR{{OB_SPEC(adtree%(3),1)}}=" "+loc_drive$(local_drive_index&(3))+": "
        @local_set_win_info(3,loc_drive$(local_drive_index&(3))+":\")
        black_white(3,1,-1)
      ELSE IF hand_win&(4)=top_win& AND win!(4)=TRUE
        local_drive_index&(4)=dummy&
        CHAR{{OB_SPEC(adtree%(4),1)}}=" "+loc_drive$(local_drive_index&(4))+": "
        @local_set_win_info(4,loc_drive$(local_drive_index&(4))+":\")
        black_white(4,1,-1)
      ENDIF
    ENDIF
  ENDSELECT
  '
RETURN
'
> PROCEDURE key_arrowed(cur_code&)
  LOCAL redraw_line!
  '
  FOR key&=3 TO 4
    IF top_win&=hand_win&(key&) AND win!(key&)=TRUE
      SELECT cur_code&
      CASE 0
        IF record_start&(key&)>0
          SUB record_start&(key&),record_height&(key&)
          record_start&(key&)=MAX(0,record_start&(key&))
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDIF
      CASE 1
        IF record_start&(key&)<SUB(PRED(head_nbline&(key&)),record_height&(key&))
          ADD record_start&(key&),record_height&(key&)
          record_start&(key&)=MAX(0,MIN(record_start&(key&),SUB(head_nbline&(key&),record_height&(key&))))
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDIF
      CASE 2
        IF record_selected&(key&)=-1
          record_selected&(key&)=PRED(head_nbline&(key&))
          '
          IF record_selected&(key&)=<ADD(record_start&(key&),record_height&(key&))
            get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
            force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          ELSE
            record_start&(key&)=MAX(0,SUB(head_nbline&(key&),record_height&(key&)))
            @set_vslide(key&)
            record_start&(key&)=@get_vslide(key&)
            ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
            control
            redraw_table(key&)
            uncontrol
          ENDIF
        ELSE
          old_record_selected&=record_selected&(key&)
          record_selected&(key&)=-1
          get_record_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
          '
          record_selected&(key&)=MAX(0,MIN(PRED(old_record_selected&),PRED(head_nbline&(key&))))
          '
          IF record_start&(key&)>0
            IF record_selected&(key&)<record_start&(key&)
              control
              DEC record_start&(key&)
              scroll_to_top(key&)
              uncontrol
            ENDIF
          ENDIF
          '
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
        ENDIF
        '
      CASE 3
        IF record_selected&(key&)=-1
          record_selected&(key&)=0
          '
          IF record_start&(key&)=0
            get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
            force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          ELSE
            record_start&(key&)=0
            @set_vslide(key&)
            record_start&(key&)=@get_vslide(key&)
            ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
            control
            redraw_table(key&)
            uncontrol
          ENDIF
        ELSE
          old_record_selected&=record_selected&(key&)
          record_selected&(key&)=-1
          get_record_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
          force_update_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
          '
          record_selected&(key&)=MAX(0,MIN(SUCC(old_record_selected&),PRED(head_nbline&(key&))))
          '
          IF record_start&(key&)<SUB(PRED(head_nbline&(key&)),record_height&(key&))
            IF record_selected&(key&)>ADD(record_start&(key&),record_height&(key&))
              control
              INC record_start&(key&)
              scroll_to_bottom(key&)
              uncontrol
            ENDIF
          ENDIF
          '
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
        ENDIF
        '
      CASE 4
        IF record_start&(key&)>0
          record_start&(key&)=0
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDIF
      CASE 5
        IF record_start&(key&)<SUB(PRED(head_nbline&(key&)),record_height&(key&))
          record_start&(key&)=MAX(0,SUB(head_nbline&(key&),record_height&(key&)))
          @set_vslide(key&)
          record_start&(key&)=@get_vslide(key&)
          ~WIND_GET(hand_win&(key&),4,rx&,ry&,rl&,rh&)
          control
          redraw_table(key&)
          uncontrol
        ENDIF
      CASE 6
        IF record_selected&(key&)>0 AND display_files_mode&(key&)>-1
          IF @is_record_selected(key&,record_selected&(key&))
            old_record_selected&=record_selected&(key&)
            record_deselect(key&,old_record_selected&)
            DEC record_nb_selected&(key&)
            DEC record_selected&(key&)
            get_record_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
            force_update_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
          ELSE
            old_record_selected&=record_selected&(key&)
            record_select(key&,old_record_selected&)
            INC record_nb_selected&(key&)
            DEC record_selected&(key&)
            get_record_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
            force_update_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
          ENDIF
          '
          IF record_start&(key&)>0
            IF record_selected&(key&)<record_start&(key&)
              control
              DEC record_start&(key&)
              scroll_to_top(key&)
              uncontrol
            ENDIF
          ENDIF
          '
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
        ENDIF
      CASE 7
        IF record_selected&(key&)<head_nbline&(key&) AND display_files_mode&(key&)>-1
          IF @is_record_selected(key&,SUCC(record_selected&(key&)))
            old_record_selected&=record_selected&(key&)
            record_deselect(key&,old_record_selected&)
            DEC record_nb_selected&(key&)
            INC record_selected&(key&)
            get_record_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
            force_update_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
          ELSE
            old_record_selected&=record_selected&(key&)
            record_select(key&,old_record_selected&)
            INC record_nb_selected&(key&)
            INC record_selected&(key&)
            get_record_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
            force_update_xywh(key&,old_record_selected&,sx&,sy&,sl&,sh&)
          ENDIF
          '
          IF record_start&(key&)<SUB(PRED(head_nbline&(key&)),record_height&(key&))
            IF record_selected&(key&)>ADD(record_start&(key&),record_height&(key&))
              control
              INC record_start&(key&)
              scroll_to_bottom(key&)
              uncontrol
            ENDIF
          ENDIF
          '
          get_record_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
          force_update_xywh(key&,record_selected&(key&),sx&,sy&,sl&,sh&)
        ENDIF
      ENDSELECT
    ENDIF
  NEXT key&
RETURN
'
> PROCEDURE win_closed
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      IF i&=6
        IF scan_is_shown!
          scan_is_aborted!=TRUE
        ELSE
          receive_aborted!=TRUE
          send_aborted!=TRUE
        ENDIF
      ELSE
        close_win(i&)
      ENDIF
    ENDIF
  NEXT i&
RETURN
> PROCEDURE win_sized
  '
  m_x&=MAX(SUCC(screenx&),m_x&)
  m_y&=MAX(SUCC(screeny&),m_y&)
  m_l&=MAX(75,m_l&)
  m_h&=MAX(150,m_h&)
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),5,m_x&,m_y&,m_l&,m_h&)
      move_win(i&,m_x&,m_y&,m_l&,m_h&)
      @set_vslide(i&)
      @set_hslide(i&)
      IF record_start&(i&)<>@get_vslide(i&) OR field_start&(i&)<>@get_hslide(i&)
        force_update(i&)
      ENDIF
      record_start&(i&)=@get_vslide(i&)
      field_start&(i&)=@get_hslide(i&)
      ful!(i&)=FALSE
    ENDIF
  NEXT i&
  '
RETURN
> PROCEDURE win_moved
  m_x&=MAX(SUCC(screenx&),m_x&)
  m_y&=MAX(SUCC(screeny&),m_y&)
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),5,m_x&,m_y&,m_l&,m_h&)
      IF icon!=TRUE
        move_win(23,m_x&,m_y&,m_l&,m_h&)
      ELSE
        move_win(i&,m_x&,m_y&,m_l&,m_h&)
        ful!(i&)=FALSE
      ENDIF
    ENDIF
  NEXT i&
RETURN
> PROCEDURE win_topped
  top_win&=@get_top_window
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE AND aff!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),10,0,0,0,0)
    ENDIF
  NEXT i&
RETURN
> PROCEDURE win_untopped
  ~WIND_SET(m_win&,25,0,0,0,0)
RETURN
> PROCEDURE win_ontop
RETURN
> PROCEDURE win_fulled
  '
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      IF ful!(i&)=FALSE
        wx&(i&)=SUCC(screenx&)
        wy&(i&)=SUCC(screeny&)
        wl&(i&)=SUB(screenl&,4)
        wh&(i&)=ADD(SUB(screenh&,4),MUL(20,multi!))
        ful!(i&)=TRUE
      ELSE
        ~WIND_GET(hand_win&(i&),6,wx&(i&),wy&(i&),wl&(i&),wh&(i&))
        ful!(i&)=FALSE
      ENDIF
      move_win(i&,wx&(i&),wy&(i&),wl&(i&),wh&(i&))
      ~WIND_SET(hand_win&(i&),5,wx&(i&),wy&(i&),wl&(i&),wh&(i&))
      @set_vslide(i&)
      @set_hslide(i&)
      IF record_start&(i&)<>@get_vslide(i&) OR field_start&(i&)<>@get_hslide(i&)
        force_update(i&)
      ENDIF
      record_start&(i&)=@get_vslide(i&)
      field_start&(i&)=@get_hslide(i&)
    ENDIF
  NEXT i&
  '
RETURN
> PROCEDURE win_vslided
  '
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),9,m_x&,0,0,0)
      record_start&(i&)=@get_vslide(i&)
      force_update(i&)
    ENDIF
  NEXT i&
  '
RETURN
> PROCEDURE win_hslided
  FOR i&=1 TO nb_win&
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      ~WIND_SET(hand_win&(i&),8,m_x&,0,0,0)
      field_start&(i&)=@get_hslide(i&)
      force_update(i&)
    ENDIF
  NEXT i&
RETURN
> PROCEDURE win_arrowed
  '
  FOR i&=3 TO 5
    IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
      SELECT m_x&
      CASE 0
        SUB record_start&(i&),record_height&(i&)
        record_start&(i&)=MAX(0,record_start&(i&))
        @set_vslide(i&)
        record_start&(i&)=@get_vslide(i&)
        control
        ~WIND_GET(hand_win&(i&),4,m_x&,m_y&,m_l&,m_h&)
        ~WIND_GET(hand_win&(i&),11,rx&,ry&,rl&,rh&)
        WHILE rl&<>0 AND rh&<>0
          IF RC_INTERSECT(m_x&,m_y&,m_l&,m_h&,rx&,ry&,rl&,rh&)
            redraw_table(i&)
          ENDIF
          ~WIND_GET(hand_win&(i&),12,rx&,ry&,rl&,rh&)
        WEND
        uncontrol
      CASE 1
        ADD record_start&(i&),record_height&(i&)
        record_start&(i&)=MAX(0,MIN(record_start&(i&),SUB(head_nbline&(i&),record_height&(i&))))
        @set_vslide(i&)
        record_start&(i&)=@get_vslide(i&)
        control
        ~WIND_GET(hand_win&(i&),4,m_x&,m_y&,m_l&,m_h&)
        ~WIND_GET(hand_win&(i&),11,rx&,ry&,rl&,rh&)
        WHILE rl&<>0 AND rh&<>0
          IF RC_INTERSECT(m_x&,m_y&,m_l&,m_h&,rx&,ry&,rl&,rh&)
            redraw_table(i&)
          ENDIF
          ~WIND_GET(hand_win&(i&),12,rx&,ry&,rl&,rh&)
        WEND
        uncontrol
      CASE 2
        IF m_win&=@get_top_window
          control
          REPEAT
            ~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
            IF record_start&(i&)>0
              DEC record_start&(i&)
              scroll_to_top(i&)
            ENDIF
          UNTIL NOT mo_c&
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 3
        IF m_win&=@get_top_window
          control
          REPEAT
            ~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
            IF record_start&(i&)<SUB(PRED(head_nbline&(i&)),record_height&(i&))
              INC record_start&(i&)
              scroll_to_bottom(i&)
            ENDIF
          UNTIL NOT mo_c&
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 4
        IF m_win&=@get_top_window
          old_dec_x&=MIN(field_start&(i&),150)
          SUB field_start&(i&),old_dec_x&
          control
          scroll_to_left(i&,old_dec_x&)
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 5
        IF m_win&=@get_top_window
          old_dec_x&=MIN(SUB(1000,field_start&(i&)),150)
          ADD field_start&(i&),old_dec_x&
          control
          scroll_to_right(i&,old_dec_x&)
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 6
        IF m_win&=@get_top_window
          control
          REPEAT
            ~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
            IF field_start&(i&)>0
              old_dec_x&=MIN(field_start&(i&),15)
              SUB field_start&(i&),old_dec_x&
              scroll_to_left(i&,old_dec_x&)
            ENDIF
          UNTIL NOT mo_c&
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      CASE 7
        IF m_win&=@get_top_window
          control
          REPEAT
            ~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
            IF field_start&(i&)<1000
              old_dec_x&=MIN(SUB(1000,field_start&(i&)),15)
              ADD field_start&(i&),old_dec_x&
              scroll_to_right(i&,old_dec_x&)
            ENDIF
          UNTIL NOT mo_c&
          uncontrol
        ELSE
          force_top(i&)
        ENDIF
      ENDSELECT
    ENDIF
  NEXT i&
  '
RETURN
> PROCEDURE win_join
  LOCAL screenll&
  '
  IF multi!=TRUE AND screenl&>640
    screenll&=SUB(screenl&,10)
  ELSE
    screenll&=screenl&
  ENDIF
  '
  pwx&(3)=SUCC(screenx&)
  pwy&(3)=SUCC(screeny&)
  pwl&(3)=MAX(157,SUB(screenll&/2,2))
  IF win!(5)
    pwh&(3)=SUB(screenh&,100)
  ELSE
    pwh&(3)=SUB(screenh&,2)
    IF multi!
      SUB pwh&(3),16
    ENDIF
  ENDIF
  '
  IF win!(3)
    ~WIND_SET(hand_win&(3),5,pwx&(3),pwy&(3),pwl&(3),pwh&(3))
    move_win(3,pwx&(3),pwy&(3),pwl&(3),pwh&(3))
    record_start&(3)=0
    set_vslide(3)
    record_start&(3)=@get_vslide(3)
    force_update(3)
  ENDIF
  '
  pwx&(4)=SUCC(ADD(pwx&(3),pwl&(3)))
  pwy&(4)=SUCC(screeny&)
  pwl&(4)=MAX(157,MIN(SUB(screenll&/2,2),SUB(PRED(screenl&),pwx&(4))))
  IF win!(5)
    pwh&(4)=SUB(screenh&,100)
  ELSE
    pwh&(4)=SUB(screenh&,2)
    IF multi!
      SUB pwh&(4),16
    ENDIF
  ENDIF
  '
  IF win!(4)
    ~WIND_SET(hand_win&(4),5,pwx&(4),pwy&(4),pwl&(4),pwh&(4))
    move_win(4,pwx&(4),pwy&(4),pwl&(4),pwh&(4))
    record_start&(4)=0
    set_vslide(4)
    record_start&(4)=@get_vslide(4)
    force_update(4)
  ENDIF
  '
RETURN
> PROCEDURE iconify
  IF naes!
    FOR i&=1 TO nb_win&
      IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
        icon_win&=m_win&
        icon_i&=i&
        ope!(i&)=TRUE
        icon!=TRUE
        ~WIND_GET(hand_win&(i&),5,pwx&(i&),pwy&(i&),pwl&(i&),pwh&(i&))
        ~WIND_SET(hand_win&(i&),26,m_x&,m_y&,m_l&,m_h&)
        ~WIND_GET(hand_win&(i&),5,m_x&,m_y&,m_l&,m_h&)
        move_win(23,m_x&,m_y&,m_l&,m_h&)
      ELSE
        IF win!(i&)=TRUE AND i&>2 AND i&<5
          ope!(i&)=TRUE
        ELSE
          ope!(i&)=FALSE
        ENDIF
        close_win(i&)
      ENDIF
    NEXT i&
  ENDIF
RETURN
> PROCEDURE uniconify
  FOR iw&=1 TO nb_win&
    IF m_win&=hand_win&(iw&) AND win!(iw&)=TRUE AND naes!=TRUE
      ~WIND_SET(hand_win&(iw&),27,m_x&,m_y&,m_l&,m_h&)
      icon!=FALSE
      icon_win&=-1
    ENDIF
  NEXT iw&
  IF icon!=FALSE
    IF ope!(4)=TRUE AND win!(4)=FALSE
      win(4)
    ENDIF
    IF ope!(3)=TRUE AND win!(3)=FALSE
      win(3)
    ENDIF
  ENDIF
RETURN
> PROCEDURE va_start
  LOCAL c_ap_id&,c_adr%,c_path$
  IF va_start!
    c_adr%=LONG{ADD(m_adr%,6)}
    c_ap_id&=INT{ADD(m_adr%,2)}
    IF c_adr%>0
      c_path$=CHAR{c_adr%}
      '
      clear_s
      INT{s_adr%}=18232
      INT{ADD(s_adr%,2)}=ap_id&
      LONG{ADD(s_adr%,6)}=dummy%
      ~APPL_WRITE(c_ap_id&,16,s_adr%)
      '
      IF LEFT$(c_path$)="'" AND RIGHT$(c_path$)="'"
        c_path$=MID$(c_path$,2)
        c_path$=LEFT$(c_path$,PRED(LEN(c_path$)))
      ENDIF
      '
      dummy$=UPPER$(RIGHT$(c_path$,4))
      '
      IF dummy$=".ZIP" OR dummy$=".MSA" OR INSTR(dummy$,".ST")>0
        c_path$=c_path$+">"
        IF c_path$<>CHAR{local_path0%} AND c_path$<>CHAR{local_path1%}
          '
          win(4)
          win(3)
          @local_set_win_info(3,c_path$)
        ENDIF
      ENDIF
    ENDIF
  ENDIF
RETURN
> PROCEDURE redraw
  '
  control
  '
  ~WIND_GET(m_win&,11,rx&,ry&,rl&,rh&)
  WHILE rl&<>0 AND rh&<>0
    IF RC_INTERSECT(m_x&,m_y&,m_l&,m_h&,rx&,ry&,rl&,rh&)
      FOR i&=1 TO nb_win&
        IF m_win&=hand_win&(i&) AND win!(i&)=TRUE
          forced!(i&)=FALSE
          '
          IF icon!=TRUE
            ~OBJC_DRAW(adtree%(23),0,3,rx&,ry&,rl&,rh&)
          ELSE
            ~OBJC_DRAW(adtree%(i&),0,4,rx&,ry&,rl&,rh&)
            IF i&=3 OR i&=4
              redraw_table(i&)
            ENDIF
          ENDIF
        ENDIF
      NEXT i&
    ENDIF
    ~WIND_GET(m_win&,12,rx&,ry&,rl&,rh&)
  WEND
  '
  win_ontop
  '
  uncontrol
RETURN
'
> PROCEDURE redraw_table(dial&)
  redraw_grid(dial&)
  SELECT dial&
  CASE 3
    @local_redraw(3,-1)
  CASE 4
    @local_redraw(4,-1)
  ENDSELECT
  redraw_selected(dial&)
RETURN
> PROCEDURE redraw_grid(dial&)
  '
  local_set_fonts(dial&)
  '
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  ADD yf&,field_bar_h&(dial&)
  SUB hf&,field_bar_h&(dial&)
  v_hide_c
  clip(1,rx&,ry&,rl&,rh&)
  vswr_mode(1)
  vsf_perimeter(0)
  vsf_interior(1)
  vsf_color(0)
  v_bar(xf&,yf&,ADD(lf&,PRED(xf&)),ADD(hf&,PRED(yf&)))
  '
  IF (dial&=3 OR dial&=4) AND nb_plan&<2
    '
    IF head_nbline&(dial&)>0 AND display_files_mode&(dial&)>-1
      '
      vsf_interior(2)
      vsf_color(1)
      vsf_style(1)
      '
      ef&=MIN(SUCC(ADD(record_start&(dial&),record_height&(dial&))),PRED(head_nbline&(dial&)))
      '
      FOR j&=record_start&(dial&) TO ef&
        yp&=MUL(font_files_real_height&(dial&),SUB(SUCC(j&),record_start&(dial&)))
        IF @is_record_selected(dial&,j&)
          v_bar(xf&,ADD(yf&,SUB(SUCC(yp&),font_files_real_height&(dial&))),lf&,PRED(font_files_real_height&(dial&)))
        ENDIF
      NEXT j&
    ENDIF
  ENDIF
  '
  clip(0,rx&,ry&,rl&,rh&)
  v_show_c
  '
RETURN
> PROCEDURE redraw_selected(dial&)
  '
  local_set_fonts(dial&)
  '
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  ADD yf&,field_bar_h&(dial&)
  SUB hf&,field_bar_h&(dial&)
  v_hide_c
  clip(1,rx&,ry&,rl&,rh&)
  '
  IF (dial&=3 OR dial&=4)
    '
    IF head_nbline&(dial&)>0
      ef&=MIN(SUCC(ADD(record_start&(dial&),record_height&(dial&))),PRED(head_nbline&(dial&)))
      '
      IF display_files_mode&(dial&)=-1
        vsf_perimeter(0)
        vsf_interior(1)
        vsf_color(2)
        vsf_style(0)
        vswr_mode(3)
      ELSE
        vsf_perimeter(1)
        vsl_type(2)
        vsf_interior(0)
        vsf_color(1)
        vsf_style(0)
        vswr_mode(3)
      ENDIF
      '
      FOR j&=record_start&(dial&) TO ef&
        yp&=MUL(font_files_real_height&(dial&),SUB(SUCC(j&),record_start&(dial&)))
        IF j&=record_selected&(dial&)
          v_bar(xf&,ADD(yf&,SUB(SUCC(yp&),font_files_real_height&(dial&))),lf&,PRED(font_files_real_height&(dial&)))
        ENDIF
      NEXT j&
    ENDIF
  ENDIF
  '
  clip(0,rx&,ry&,rl&,rh&)
  v_show_c
  '
RETURN
> PROCEDURE scroll_to_bottom(dial&)
  set_vslide_pos(dial&)
  '
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  IF ADD(yf&,hf&)>ADD(screeny&,screenh&)
    ~WIND_GET(hand_win&(dial&),4,rx&,ry&,rl&,rh&)
    redraw_table(dial&)
  ELSE
    ADD yf&,SUCC(field_bar_h&(dial&))
    SUB hf&,SUCC(field_bar_h&(dial&))
    '
    eh&=MUL(font_files_real_height&(dial&),record_height&(dial&))
    make_xywh(xf&,ADD(yf&,font_files_real_height&(dial&)),lf&,eh&,xf&,yf&,lf&,eh&)
    vro_cpyfm(pscrmfdb%,pscrmfdb%)
    '
    IF ADD(record_start&(dial&),record_height&(dial&))<head_nbline&(dial&)
      get_record_xywh(dial&,ADD(record_start&(dial&),record_height&(dial&)),sx&,sy&,sl&,sh&)
      force_update_xywh(dial&,ADD(record_start&(dial&),record_height&(dial&)),sx&,sy&,sl&,sh&)
      IF ADD(record_start&(dial&),SUCC(record_height&(dial&)))<head_nbline&(dial&)
        get_record_xywh(dial&,ADD(record_start&(dial&),SUCC(record_height&(dial&))),sx&,sy&,sl&,sh&)
        force_update_xywh(dial&,ADD(record_start&(dial&),SUCC(record_height&(dial&))),sx&,sy&,sl&,sh&)
      ELSE
        ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
        ADD yf&,SUCC(field_bar_h&(dial&))
        SUB hf&,SUCC(field_bar_h&(dial&))
        eh&=MUL(font_files_real_height&(dial&),SUCC(record_height&(dial&)))
        '
        rx&=xf&
        ry&=ADD(yf&,eh&)
        rl&=lf&
        rh&=SUB(hf&,eh&)
        '
        IF rh&>0
          redraw_table(dial&)
        ENDIF
      ENDIF
    ELSE
      rx&=xf&
      ry&=ADD(yf&,eh&)
      rl&=lf&
      rh&=SUB(hf&,eh&)
      '
      redraw_table(dial&)
    ENDIF
    '
  ENDIF
RETURN
> PROCEDURE scroll_to_top(dial&)
  set_vslide_pos(dial&)
  '
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  ADD yf&,SUCC(field_bar_h&(dial&))
  SUB hf&,SUCC(field_bar_h&(dial&))
  '
  eh&=MAX(0,SUB(hf&,font_files_real_height&(dial&)))
  make_xywh(xf&,yf&,lf&,eh&,xf&,ADD(yf&,font_files_real_height&(dial&)),lf&,eh&)
  vro_cpyfm(pscrmfdb%,pscrmfdb%)
  '
  rx&=xf&
  ry&=yf&
  rl&=lf&
  rh&=font_files_real_height&(dial&)
  force_update_xywh(dial&,record_start&(dial&),rx&,ry&,rl&,rh&)
  '
RETURN
> PROCEDURE scroll_to_left(dial&,offset&)
  IF field_start&(dial&)>-1 AND offset&>0
    set_hslide(dial&)
    '
    ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
    ADD yf&,field_bar_h&(dial&)
    SUB hf&,field_bar_h&(dial&)
    '
    offset&=MIN(offset&,lf&)
    dummy&=SUB(lf&,offset&)
    IF dummy&>0
      make_xywh(xf&,yf&,dummy&,hf&,ADD(xf&,offset&),yf&,dummy&,hf&)
      vro_cpyfm(pscrmfdb%,pscrmfdb%)
    ENDIF
    '
    rx&=xf&
    ry&=yf&
    rl&=offset&
    rh&=hf&
    redraw_table(dial&)
    '
    IF field_bar_obj&(dial&)>0
      black_white(dial&,field_bar_obj&(dial&),-1)
    ENDIF
  ENDIF
RETURN
> PROCEDURE scroll_to_right(dial&,offset&)
  IF offset&>0
    set_hslide(dial&)
    '
    ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
    ADD yf&,field_bar_h&(dial&)
    SUB hf&,field_bar_h&(dial&)
    '
    offset&=MIN(offset&,lf&)
    dummy&=SUB(lf&,offset&)
    IF dummy&>0
      make_xywh(ADD(xf&,offset&),yf&,dummy&,hf&,xf&,yf&,dummy&,hf&)
      vro_cpyfm(pscrmfdb%,pscrmfdb%)
    ENDIF
    '
    rx&=ADD(xf&,dummy&)
    ry&=yf&
    rl&=offset&
    rh&=hf&
    redraw_table(dial&)
    '
    black_white(dial&,field_bar_obj&(dial&),-1)
  ENDIF
RETURN
'
> FUNCTION get_record_under_cursor(dial&,co_y&,co_x&)
LOCAL cu_y&
IF dial&=3 OR dial&=4
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  ADD yf&,field_bar_h&(dial&)
  SUB hf&,field_bar_h&(dial&)
  '
  IF co_y&>yf& AND co_y&<ADD(yf&,hf&) AND co_x&>xf& AND co_x&<ADD(xf&,lf&)
    cu_y&=MAX(0,SUB(co_y&,yf&))/font_files_real_height&(dial&)
    IF cu_y&>PRED(head_nbline&(dial&))
      RETURN -1
    ENDIF
    IF head_nbline&(dial&)>0
      RETURN MAX(0,MIN(ADD(record_start&(dial&),MIN(cu_y&,SUCC(record_height&(dial&)))),PRED(head_nbline&(dial&))))
    ENDIF
  ENDIF
ENDIF
RETURN -1
ENDFUNC
> PROCEDURE get_record_xywh(dial&,record_id&,VAR sx&,sy&,sl&,sh&)
IF dial&=3 OR dial&=4
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  sx&=xf&
  sl&=lf&
  '
  ADD yf&,field_bar_h&(dial&)
  IF record_id&>=record_start&(dial&) AND record_id&=<MIN(ADD(record_start&(dial&),SUCC(record_height&(dial&))),PRED(head_nbline&(dial&)))
    sy&=ADD(yf&,MUL(SUB(record_id&,record_start&(dial&)),font_files_real_height&(dial&)))
    sh&=font_files_real_height&(dial&)
  ELSE
    sy&=0
    sh&=0
  ENDIF
ENDIF
RETURN
> PROCEDURE get_window_xywh(dial&,record_id&,VAR sx&,sy&,sl&,sh&)
IF dial&=3 OR dial&=4
  ~WIND_GET(hand_win&(dial&),4,xf&,yf&,lf&,hf&)
  sx&=xf&
  sl&=lf&
  '
  ADD yf&,field_bar_h&(dial&)
  SUB hf&,field_bar_h&(dial&)
  '
  record_id&=@record_get_next_selected(dial&,-1)
  '
  sy&=ADD(yf&,MUL(SUB(record_id&,record_start&(dial&)),font_files_real_height&(dial&)))
  '
  record_nb&=SUB(@record_get_previous_selected(dial&,head_nbline&(dial&)),PRED(record_id&))
  '
  sh&=MUL(record_nb&,font_files_real_height&(dial&))
  IF sy&<yf&
    SUB sh&,SUB(yf&,sy&)
    sy&=yf&
  ELSE
    SUB hf&,SUB(sy&,yf&)
  ENDIF
  sh&=MIN(sh&,hf&)
  '
ENDIF
RETURN
'
> PROCEDURE shut_down
'
validate
'
IF misc_save_configuration_at_quit!
  @save_options
ENDIF
~APPL_EXIT()
QUIT 0
RETURN
'
> PROCEDURE win(dial&)
IF icon!=TRUE AND icon_win&>0
  m_win&=icon_win&
  m_x&=pwx&(icon_i&)
  m_y&=pwy&(icon_i&)
  m_l&=pwl&(icon_i&)
  m_h&=pwh&(icon_i&)
  uniconify
ELSE IF win!(dial&)
  force_top(dial&)
ELSE
  create_win(dial&)
ENDIF
RETURN
> PROCEDURE create_win(dial&)
hand_win&(dial&)=@window_create(cp_win&(dial&))
IF hand_win&(dial&)>0
  win!(dial&)=TRUE
  IF dial&=2 OR dial&>4
    ~WIND_SET(hand_win&(dial&),2,CARD(SWAP(OB_SPEC(adtree%(10),dial&))),CARD(OB_SPEC(adtree%(10),dial&)),0,0)
  ENDIF
  IF dial&=3
    parent_record_start$(3)="0"
    IF BYTE{local_path0%}=0
      @local_set_win_info(3,local_path_left$)
    ELSE
      @local_set_win_info(3,CHAR{local_path0%})
    ENDIF
  ENDIF
  IF dial&=4
    parent_record_start$(4)="0"
    IF BYTE{local_path1%}=0
      @local_set_win_info(4,local_path_right$)
    ELSE
      @local_set_win_info(4,CHAR{local_path1%})
    ENDIF
  ENDIF
  ~WIND_SET(hand_win&(dial&),24,&X1,0,0,0)
  '
  IF pwx&(dial&)=0 AND pwy&(dial&)=0
    IF dial&=3 OR dial&=4
      xd&(dial&)=ADD(ADD(screenx&,16),MUL(dial&,8))
      yd&(dial&)=ADD(ADD(screeny&,16),MUL(dial&,8))
      ld&(dial&)=300
      hd&(dial&)=200
    ELSE
      ~FORM_CENTER(adtree%(dial&),xd&(dial&),yd&(dial&),ld&(dial&),dummy&)
      IF dial&=6
        ld&(6)=transfert_save_w&
        hd&(6)=ADD(ADD(OB_Y(adtree%(6),2),OB_H(adtree%(6),2)),4)
      ENDIF
    ENDIF
    ~WIND_CALC(0,cp_win&(dial&),xd&(dial&),yd&(dial&),ld&(dial&),hd&(dial&),wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))
  ELSE
    wx&(dial&)=MIN(SUB(screenl&,ADD(screenx&,40)),pwx&(dial&))
    wy&(dial&)=MIN(SUB(screenh&,ADD(screeny&,40)),pwy&(dial&))
    IF dial&=6
      ld&(6)=transfert_save_w&
      hd&(6)=ADD(ADD(OB_Y(adtree%(6),2),OB_H(adtree%(6),2)),4)
    ENDIF
    IF BTST(cp_win&(dial&),5)=FALSE
      ~WIND_CALC(0,cp_win&(dial&),xd&(dial&),yd&(dial&),ld&(dial&),hd&(dial&),dummy&,dummy&,pwl&(dial&),pwh&(dial&))
    ENDIF
    wl&(dial&)=pwl&(dial&)
    wh&(dial&)=pwh&(dial&)
    ~WIND_CALC(1,cp_win&(dial&),wx&(dial&),wy&(dial&),pwl&(dial&),pwh&(dial&),xd&(dial&),yd&(dial&),ld&(dial&),hd&(dial&))
  ENDIF
  '
  wx&(dial&)=MAX(SUCC(screenx&),wx&(dial&))
  wy&(dial&)=MAX(SUCC(screeny&),wy&(dial&))
  '
  move_win(dial&,wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))
  '
  IF WIND_OPEN(hand_win&(dial&),wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))=0
    win!(dial&)=FALSE
  ENDIF
  IF dial&=3 OR dial&=4
    set_vslide(dial&)
    record_start&(dial&)=@get_vslide(dial&)
    ~WIND_SET(hand_win&(dial&),15,-1,dummy&,dummy&,dummy&)
    set_hslide(dial&)
    field_start&(dial&)=@get_hslide(dial&)
  ENDIF
  aff!(dial&)=win!(dial&)
  ful!(dial&)=FALSE
ELSE
  no_more_win(dial&)
ENDIF
RETURN
> PROCEDURE close_all_windows
FOR i&=1 TO nb_win&
  close_win(i&)
NEXT i&
RETURN
> PROCEDURE close_win(dial&)
IF win!(dial&)
  v_hide_c
  ~WIND_CLOSE(hand_win&(dial&))
  ~WIND_DELETE(hand_win&(dial&))
  v_show_c
  win!(dial&)=FALSE
  aff!(dial&)=FALSE
  ful!(dial&)=FALSE
ENDIF
RETURN
> PROCEDURE move_win(dial&,x0&,y0&,l0&,h0&)
IF win!(dial&)
  ~WIND_CALC(1,cp_win&(dial&),x0&,y0&,l0&,h0&,xd&(dial&),yd&(dial&),ld&(dial&),dummy%)
  OB_X(adtree%(dial&),0)=xd&(dial&)
  OB_Y(adtree%(dial&),0)=yd&(dial&)
  OB_W(adtree%(dial&),0)=ld&(dial&)
  '
  IF dial&=3 OR dial&=4
    IF field_bar_obj&(dial&)>0
      OB_X(adtree%(dial&),field_bar_obj&(dial&))=-MAX(0,field_start&(dial&))
      OB_W(adtree%(dial&),field_bar_obj&(dial&))=ADD(ld&(dial&),field_start&(dial&))
    ENDIF
  ELSE IF dial&=6 AND scan_is_shown!=TRUE
    OB_X(adtree%(25),0)=xd&(dial&)
    OB_Y(adtree%(25),0)=yd&(dial&)
  ENDIF
  '
  pwx&(dial&)=x0&
  pwy&(dial&)=y0&
  IF BTST(cp_win&(dial&),5)
    pwl&(dial&)=l0&
    pwh&(dial&)=h0&
  ELSE
    pwl&(dial&)=0
    pwh&(dial&)=0
  ENDIF
  '
ENDIF
IF dial&=23
  ~WIND_CALC(1,&X1001,m_x&,m_y&,m_l&,m_h&,xd&(23),yd&(23),ld&(23),hd&(23))
  OB_X(adtree%(23),0)=xd&(23)
  OB_Y(adtree%(23),0)=yd&(23)
  OB_W(adtree%(23),0)=ld&(23)
  OB_H(adtree%(23),0)=hd&(23)
  OB_X(adtree%(23),1)=SUB(OB_W(adtree%(23),0),OB_W(adtree%(23),1))/2
  OB_Y(adtree%(23),1)=SUB(OB_H(adtree%(23),0),OB_H(adtree%(23),1))/2
ENDIF
RETURN
> PROCEDURE no_more_win(dial&)
~@alert(1,2)
win!(dial&)=FALSE
aff!(dial&)=FALSE
ful!(dial&)=FALSE
RETURN
'
> PROCEDURE set_vslide(dial&)
IF win!(dial&)=TRUE AND dial&>2 AND dial&<5
  ~WIND_GET(hand_win&(dial&),4,dummy&,dummy&,dummy&,hf&)
  SUB hf&,field_bar_h&(dial&)
  record_height&(dial&)=PRED(MIN(INT(hf&/font_files_real_height&(dial&)),head_nbline&(dial&)))
  '
  IF head_nbline&(dial&)>0
    len_slide&=MAX(0,MIN(1000,1000*(SUCC(record_height&(dial&))/MAX(1,head_nbline&(dial&)))))
    ~WIND_SET(hand_win&(dial&),16,len_slide&,dummy&,dummy&,dummy&)
    pos_slide&=MAX(0,MIN(1000,(1000*record_start&(dial&))/(MAX(1,SUB(head_nbline&(dial&),SUCC(record_height&(dial&)))))))
    ~WIND_SET(hand_win&(dial&),9,pos_slide&,dummy&,dummy&,dummy&)
  ELSE
    ~WIND_SET(hand_win&(dial&),16,1000,dummy&,dummy&,dummy&)
    ~WIND_SET(hand_win&(dial&),9,0,dummy&,dummy&,dummy&)
  ENDIF
ENDIF
RETURN
> PROCEDURE set_vslide_pos(dial&)
IF win!(dial&)
  pos_slide&=MAX(0,MIN(1000,(1000*record_start&(dial&))/(MAX(1,SUB(head_nbline&(dial&),SUCC(record_height&(dial&)))))))
  ~WIND_SET(hand_win&(dial&),9,pos_slide&,dummy&,dummy&,dummy&)
ENDIF
RETURN
> FUNCTION get_vslide(dial&)
$F%
IF win!(dial&)=TRUE AND dial&>2 AND dial&<5
~WIND_GET(hand_win&(dial&),4,dummy&,dummy&,dummy&,hf&)
SUB hf&,field_bar_h&(dial&)
record_height&(dial&)=PRED(MIN(INT(hf&/font_files_real_height&(dial&)),head_nbline&(dial&)))
'
~WIND_GET(hand_win&(dial&),9,pos_slide&,dummy&,dummy&,dummy&)
pos_slide&=INT(((SUB(head_nbline&(dial&),SUCC(record_height&(dial&))))*pos_slide&)/1000)
'
RETURN MAX(0,MIN(pos_slide&,SUB(PRED(head_nbline&(dial&)),record_height&(dial&))))
ENDIF
RETURN 0
ENDFUNC
> PROCEDURE set_hslide(dial&)
IF win!(dial&)=TRUE AND dial&>2 AND dial&<5 AND BTST(cp_win&(dial&),10)=TRUE
IF dial&<5
  IF field_bar_obj&(dial&)>0
    OB_X(adtree%(dial&),field_bar_obj&(dial&))=-MAX(0,field_start&(dial&))
    OB_W(adtree%(dial&),field_bar_obj&(dial&))=ADD(ld&(dial&),MAX(0,field_start&(dial&)))
  ENDIF
ENDIF
'
~WIND_SET(hand_win&(dial&),8,MAX(0,field_start&(dial&)),dummy&,dummy&,dummy&)
ENDIF
RETURN
> FUNCTION get_hslide(dial&)
$F%
IF win!(dial&)=TRUE AND dial&>2 AND dial&<5 AND BTST(cp_win&(dial&),10)=TRUE
~WIND_GET(hand_win&(dial&),8,pos_slide&,dummy&,dummy&,dummy&)
pos_slide&=MAX(0,MIN(pos_slide&,1000))
'
IF dial&<6
IF field_bar_obj&(dial&)>0
  OB_X(adtree%(dial&),field_bar_obj&(dial&))=-MAX(0,pos_slide&)
  OB_W(adtree%(dial&),field_bar_obj&(dial&))=ADD(ld&(dial&),pos_slide&)
ENDIF
ENDIF
'
RETURN MAX(0,pos_slide&)
ENDIF
RETURN 0
ENDFUNC
'
> PROCEDURE force_update(bar&)
IF aff!(bar&) AND NOT forced!(bar&)
INT{s_adr%}=20
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,4)}=0
INT{ADD(s_adr%,6)}=hand_win&(bar&)
INT{ADD(s_adr%,8)}=screenx&
INT{ADD(s_adr%,10)}=screeny&
INT{ADD(s_adr%,12)}=screenl&
INT{ADD(s_adr%,14)}=screenh&
~APPL_WRITE(ap_id&,16,s_adr%)
'
forced!(bar&)=TRUE
ENDIF
RETURN
> PROCEDURE force_update_xywh(bar&,fline&,sx&,sy&,sl&,sh&)
IF fline&<-1
IF aff!(bar&) AND sl&>0 AND sh&>0
INT{s_adr%}=20
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,4)}=0
INT{ADD(s_adr%,6)}=hand_win&(bar&)
INT{ADD(s_adr%,8)}=sx&
INT{ADD(s_adr%,10)}=sy&
INT{ADD(s_adr%,12)}=sl&
INT{ADD(s_adr%,14)}=sh&
~APPL_WRITE(ap_id&,16,s_adr%)
ENDIF
ELSE IF fline&>-1
xfe&=sx&
yfe&=sy&
lfe&=sl&
hfe&=sh&
~WIND_GET(hand_win&(bar&),11,rx&,ry&,rl&,rh&)
'
control
WHILE rl&<>0 AND rh&<>0
IF RC_INTERSECT(xfe&,yfe&,lfe&,hfe&,rx&,ry&,rl&,rh&)
  '
  redraw_grid(bar&)
  SELECT bar&
  CASE 3
    @local_redraw(3,fline&)
  CASE 4
    @local_redraw(4,fline&)
  ENDSELECT
  redraw_selected(bar&)
ENDIF
~WIND_GET(hand_win&(bar&),12,rx&,ry&,rl&,rh&)
WEND
uncontrol
'
ENDIF
RETURN
> PROCEDURE force_top(bar&)
IF hand_win&(bar&)<>@get_top_window AND win!(bar&)=TRUE
clear_s
INT{s_adr%}=21
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,6)}=hand_win&(bar&)
~APPL_WRITE(ap_id&,16,s_adr%)
ENDIF
RETURN
> PROCEDURE force_full(bar&)
IF win!(bar&)
clear_s
INT{s_adr%}=23
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,6)}=hand_win&(bar&)
~APPL_WRITE(ap_id&,16,s_adr%)
ENDIF
RETURN
> PROCEDURE clear_s
IF s_adr%>0
LONG{s_adr%}=0
LONG{ADD(s_adr%,4)}=0
LONG{ADD(s_adr%,8)}=0
LONG{ADD(s_adr%,12)}=0
ENDIF
RETURN
'
> PROCEDURE blitter(x1&,l1&,y1&,y2&,h1&)
'
dummy_blit&=MIN(ADD(x1&,PRED(l1&)),ADD(screenx&,PRED(screenl&)))
'
make_xyarray(x1&,y1&,dummy_blit&,ADD(y1&,PRED(h1&)),x1&,y2&,dummy_blit&,ADD(y2&,PRED(h1&)))
'
vro_cpyfm(pscrmfdb%,pscrmfdb%)
'
RETURN
> PROCEDURE noir_blanc(arbre&,fils&,nb_etat&)
IF BTST(OB_FLAGS(adtree%(arbre&),fils&),9)=0
black_white(arbre&,fils&,OB_STATE(adtree%(arbre&),fils&))
ENDIF
black_white(arbre&,fils&,nb_etat&)
RETURN
> PROCEDURE black_white(arbre&,fils&,etat&)
IF fils&>0 AND etat&>-1
SELECT etat&
CASE 0
obj_deselect(arbre&,fils&)
CASE 1
obj_select(arbre&,fils&)
CASE 2
obj_unhide(arbre&,fils&)
CASE 3
obj_hide(arbre&,fils&)
CASE 4
obj_enable(arbre&,fils&)
CASE 5
obj_disable(arbre&,fils&)
ENDSELECT
ENDIF
'
IF win!(arbre&)=TRUE
~WIND_GET(hand_win&(arbre&),4,xf&,yf&,lf&,hf&)
~WIND_GET(hand_win&(arbre&),11,rx&,ry&,rl&,rh&)
ENDIF
'
IF aff!(arbre&)=TRUE OR arbre&>11 OR arbre&=5
IF arbre&>11 OR arbre&=5
xf&=screenx&
yf&=screeny&
lf&=screenl&
hf&=screenh&
rx&=xf&
ry&=yf&
rl&=lf&
rh&=hf&
ENDIF
control
WHILE rl&<>0 AND rh&<>0
IF RC_INTERSECT(xf&,yf&,lf&,hf&,rx&,ry&,rl&,rh&)
  ~OBJC_DRAW(adtree%(arbre&),fils&,3,rx&,ry&,rl&,rh&)
ENDIF
IF arbre&>11 OR arbre&=5
  rl&=0
  rh&=0
ELSE
  ~WIND_GET(hand_win&(arbre&),12,rx&,ry&,rl&,rh&)
ENDIF
WEND
uncontrol
ENDIF
RETURN
> PROCEDURE copy_string(arbre&,fils&,str&)
OB_SPEC(adtree%(arbre&),fils&)=OB_SPEC(adtree%(alert_tree&),str&)
black_white(arbre&,fils&,-1)
RETURN
> PROCEDURE obj_enable(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BCLR(OB_STATE(adtree%(arbre&),fils&),3)
RETURN
> PROCEDURE obj_disable(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BSET(OB_STATE(adtree%(arbre&),fils&),3)
RETURN
> PROCEDURE obj_select(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BSET(OB_STATE(adtree%(arbre&),fils&),0)
RETURN
> PROCEDURE obj_deselect(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BCLR(OB_STATE(adtree%(arbre&),fils&),0)
RETURN
> PROCEDURE obj_check(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BSET(OB_STATE(adtree%(arbre&),fils&),2)
RETURN
> PROCEDURE obj_uncheck(arbre&,fils&)
OB_STATE(adtree%(arbre&),fils&)=BCLR(OB_STATE(adtree%(arbre&),fils&),2)
RETURN
> PROCEDURE obj_set_editable(arbre&,fils&,editable!)
IF editable!
OB_FLAGS(adtree%(arbre&),fils&)=BSET(OB_FLAGS(adtree%(arbre&),fils&),3)
ELSE
OB_FLAGS(adtree%(arbre&),fils&)=BCLR(OB_FLAGS(adtree%(arbre&),fils&),3)
ENDIF
RETURN
> PROCEDURE obj_hide(arbre&,fils&)
OB_FLAGS(adtree%(arbre&),fils&)=BSET(OB_FLAGS(adtree%(arbre&),fils&),7)
RETURN
> PROCEDURE obj_unhide(arbre&,fils&)
OB_FLAGS(adtree%(arbre&),fils&)=BCLR(OB_FLAGS(adtree%(arbre&),fils&),7)
RETURN
> FUNCTION tst_hidden(arbre&,fils&)
$F%
RETURN BTST(OB_FLAGS(adtree%(arbre&),fils&),7)
ENDFUNC
> FUNCTION tst_selected(arbre&,fils&)
$F%
RETURN BTST(OB_STATE(adtree%(arbre&),fils&),0)
ENDFUNC
> FUNCTION tst_enabled(arbre&,fils&)
$F%
RETURN NOT BTST(OB_STATE(adtree%(arbre&),fils&),3)
ENDFUNC
> FUNCTION obj_hslide(arbre&,fils&,fiston&)
$F%
'
obj_slide_old_pos&=obj_slide_pos&
WHILE mo_c&=1
obj_slide_pos&=GRAF_SLIDEBOX(adtree%(arbre&),fils&,fiston&,0)
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WEND
obj_slide_pos&=MAX(0,MIN((OB_W(adtree%(arbre&),fils&)*obj_slide_pos&)/1000,MAX(1,SUB(OB_W(adtree%(arbre&),fils&),OB_W(adtree%(arbre&),fiston&)))))
IF obj_slide_pos&>0
ADD obj_slide_pos&,5
ENDIF
IF fiston&>SUCC(fils&)
obj_slide_pos&=MAX(10,obj_slide_pos&)
ENDIF
IF obj_slide_pos&<>obj_slide_old_pos&
OB_X(adtree%(arbre&),fiston&)=SUB(obj_slide_pos&,5)
IF aff!(arbre&)
black_white(arbre&,fils&,-1)
ENDIF
ENDIF
'
RETURN obj_slide_pos&
ENDFUNC
> PROCEDURE obj_set_template(arbre&,fils&,template$)
CHAR{{ADD(OB_SPEC(adtree%(arbre&),fils&),4)}}=template$
INT{ADD(OB_SPEC(adtree%(arbre&),fils&),26)}=SUCC(LEN(template$))
RETURN
> PROCEDURE obj_set_valid(arbre&,fils&,valid$)
CHAR{{ADD(OB_SPEC(adtree%(arbre&),fils&),8)}}=valid$
INT{ADD(OB_SPEC(adtree%(arbre&),fils&),24)}=SUCC(LEN(valid$))
RETURN
'
> PROCEDURE dialog_about
'
obj_deselect(1,6)
'
control_form(1)
DO
form_result&=FORM_DO(adtree%(1),0)
LOOP UNTIL form_result&=6
uncontrol_form(1)
'
RETURN
'
> PROCEDURE dialog_shortcuts
result&=OBJC_FIND(adtree%(2),0,3,mo_x&,mo_y&)
SELECT result&
CASE 2,4,6,8,10,12,14,16,18,20
black_white(2,result&,1)
dummy$=@fsl_get_file$(8,kkcmd_path$+mask_all$,"DUMMY.PRG"+c0$)
IF @s_exist(dummy$)=FALSE
dummy$=""
ELSE
IF RIGHT$(dummy$)=c0$
dummy$=LEFT$(dummy$,PRED(LEN(dummy$)))
ENDIF
ENDIF
shortcuts$(DIV(SUB(result&,2),2))=dummy$
CHAR{{OB_SPEC(adtree%(2),result&)}}=RIGHT$(shortcuts$(DIV(SUB(result&,2),2)),30)
black_white(2,result&,0)
DEFAULT
win(2)
ENDSELECT
RETURN
> PROCEDURE shortcuts_set_all
LOCAL short_i&
'
FOR short_i&=0 TO 9
CHAR{{OB_SPEC(adtree%(2),ADD(MUL(short_i&,2),2))}}=RIGHT$(shortcuts$(short_i&),30)
NEXT short_i&
'
RETURN
'
> PROCEDURE dialog_new_folder(kk_win&)
LOCAL command_line$,command_pos&
'
OB_SPEC(adtree%(6),1)=OB_SPEC(adtree%(alert_tree&),46)
CHAR{{OB_SPEC(adtree%(6),3)}}=""
obj_set_template(6,3,"________.___")
obj_set_valid(6,3,"fffffffffff")
obj_unhide(6,4)
obj_deselect(6,4)
obj_deselect(6,5)
obj_deselect(6,6)
'
control_form(6)
form_result&=0
DO
form_result&=FORM_DO(adtree%(6),0)
LOOP UNTIL form_result&>4
uncontrol_form(6)
'
IF form_result&=5
folder_new_name$=CHAR{{OB_SPEC(adtree%(6),3)}}
'
IF LEN(folder_new_name$)>0
IF RIGHT$(folder_new_name$)="\"
folder_new_name$=LEFT$(folder_new_name$,PRED(LEN(folder_new_name$)))
ENDIF
'
IF LEN(folder_new_name$)>8
folder_new_ext$=MID$(folder_new_name$,9)
folder_new_name$=LEFT$(folder_new_name$,8)
ELSE
folder_new_ext$=""
ENDIF
folder_new_name$=TRIM$(folder_new_name$)
IF LEN(folder_new_ext$)>0
folder_new_name$=folder_new_name$+"."+TRIM$(folder_new_ext$)
ENDIF
'
IF kk_win&=3
command_line$=CHAR{local_path0%}+folder_new_name$+c0$
ELSE IF kk_win&=4
command_line$=CHAR{local_path1%}+folder_new_name$+c0$
ENDIF
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF GEMDOS(57,L:V:command_line$)=0
IF @tst_selected(6,4)
command_line$=LEFT$(command_line$,PRED(LEN(command_line$)))
@local_set_win_info(kk_win&,command_line$+"\")
ENDIF
'
@display_refresh_directories
ENDIF
ENDIF
ENDIF
'
RETURN
> PROCEDURE dialog_new_empty_file(kk_win&)
LOCAL command_line$,command_pos&,command_handle&
'
OB_SPEC(adtree%(6),1)=OB_SPEC(adtree%(alert_tree&),47)
CHAR{{OB_SPEC(adtree%(6),3)}}=""
obj_set_template(6,3,"________.___")
obj_set_valid(6,3,"fffffffffff")
obj_hide(6,4)
obj_deselect(6,4)
obj_deselect(6,5)
obj_deselect(6,6)
'
control_form(6)
form_result&=0
DO
form_result&=FORM_DO(adtree%(6),0)
LOOP UNTIL form_result&>4
uncontrol_form(6)
'
IF form_result&=5
file_new_name$=CHAR{{OB_SPEC(adtree%(6),3)}}
'
IF LEN(file_new_name$)>0
'
IF LEN(file_new_name$)>8
file_new_ext$=MID$(file_new_name$,9)
file_new_name$=LEFT$(file_new_name$,8)
ELSE
file_new_ext$=""
ENDIF
file_new_name$=TRIM$(file_new_name$)
IF LEN(file_new_ext$)>0
file_new_name$=file_new_name$+"."+TRIM$(file_new_ext$)
ENDIF
'
IF kk_win&=3
command_line$=CHAR{local_path0%}+file_new_name$+c0$
ELSE IF kk_win&=4
command_line$=CHAR{local_path1%}+file_new_name$+c0$
ENDIF
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF @s_exist(command_line$)=FALSE
'
command_handle&=GEMDOS(60,L:V:command_line$,W:0)
IF command_handle&>0
~GEMDOS(62,W:command_handle&)
ENDIF
'
@display_refresh_directories
ENDIF
ENDIF
ENDIF
'
RETURN
'
> PROCEDURE dialog_informations(kk_win&,kk_selected&)
LOCAL info_pathname$,info_name$,info_ext$,info_attr&,info_pos&,info_longname!
LOCAL info_new_name$,info_new_pathname$,info_date%,info_ptr%,info_free$
LOCAL fil_cursor&,fil_cursor_ptr%,info_nb_files&,info_nb_folders&,info_length%,info_i&
'
info_ptr%=@px(kk_win&,kk_selected&)
'
info_name$=@loc_get_name$(info_ptr%)
IF display_files_mode&(kk_win&)=-1
info_pathname$=info_name$+"\"+c0$
FOR info_i&=7 TO 15
obj_hide(7,info_i&)
NEXT info_i&
info_name$=@get_volume_label$(LEFT$(info_pathname$)+":")
obj_unhide(7,9)
obj_unhide(7,10)
ELSE
IF kk_win&=3
info_pathname$=CHAR{local_path0%}+info_name$+c0$
ELSE IF kk_win&=4
info_pathname$=CHAR{local_path1%}+info_name$+c0$
ENDIF
FOR info_i&=7 TO 16
obj_unhide(7,info_i&)
NEXT info_i&
obj_hide(7,9)
obj_hide(7,10)
ENDIF
'
SELECT @loc_get_type(info_ptr%)
CASE 1
obj_hide(7,3)
obj_hide(7,4)
CHAR{OB_SPEC(adtree%(7),6)}=LEFT$(STR$(@loc_get_length(info_ptr%))+str_octets$,22)
CASE 2,11,12
obj_unhide(7,3)
obj_unhide(7,4)
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
fil_set_type(record_buffer%(1),2)
fil_set_listed(record_buffer%(1),FALSE)
IF display_files_mode&(kk_win&)=-1
fil_set_name(1,record_buffer%(1),@loc_get_name$(info_ptr%)+"\"+c0$)
ELSE
IF kk_win&=3
fil_set_name(1,record_buffer%(1),CHAR{local_path0%}+@loc_get_name$(info_ptr%))
ELSE IF kk_win&=4
fil_set_name(1,record_buffer%(1),CHAR{local_path1%}+@loc_get_name$(info_ptr%))
ENDIF
ENDIF
record_insert(1,head_nbline&(1))
'
@mouse_busy
fil_cursor&=0
WHILE fil_cursor&<head_nbline&(1)
fil_cursor_ptr%=@px(1,fil_cursor&)
IF @fil_get_type(fil_cursor_ptr%)=2 AND @fil_is_listed(fil_cursor_ptr%)=FALSE
@local_add_files_in_directories(LONG{ADD(fil_cursor_ptr%,6)})
@fil_set_listed(@px(1,fil_cursor&),TRUE)
ENDIF
INC fil_cursor&
WEND
@mouse_free
'
IF display_files_mode&(kk_win&)=-1
record_delete(1,0)
ENDIF
'
FOR fil_cursor&=PRED(head_nbline&(1)) TO 0 STEP -1
fil_cursor_ptr%=@px(1,fil_cursor&)
SELECT @fil_get_type(fil_cursor_ptr%)
CASE 1
INC info_nb_files&
ADD info_length%,@fil_get_length(fil_cursor_ptr%)
CASE 2
INC info_nb_folders&
ENDSELECT
NEXT fil_cursor&
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
CHAR{OB_SPEC(adtree%(7),4)}=LEFT$(STR$(info_nb_files&)+str_files$+STR$(info_nb_folders&)+str_folders$,22)
IF display_files_mode&(kk_win&)=-1
dummy%=DFREE(SUB(ASC(LEFT$(@loc_get_name$(info_ptr%))),64))
CHAR{OB_SPEC(adtree%(7),10)}=LEFT$(STR$(dummy%)+str_octets$,22)
dummy%=info_length%+dummy%
IF dummy%>0
info_free$=" "+STR$(INT((100*info_length%)/dummy%))+"%"
ENDIF
ELSE
info_free$=""
ENDIF
CHAR{OB_SPEC(adtree%(7),6)}=LEFT$(STR$(info_length%)+str_octets$+info_free$,22)
'
ENDSELECT
'
info_pos&=RINSTR(info_name$,".")
IF info_pos&>0
info_ext$=MID$(info_name$,SUCC(info_pos&))
info_name$=LEFT$(info_name$,PRED(info_pos&))
ELSE
info_ext$=""
ENDIF
IF LEN(info_name$)>8
info_longname!=TRUE
obj_set_template(7,2,STRING$(22,"_"))
obj_set_valid(7,2,STRING$(22,"f"))
CHAR{{OB_SPEC(adtree%(7),2)}}=LEFT$(info_name$+"."+info_ext$,22)
ELSE
info_longname!=FALSE
obj_set_template(7,2,"________.___")
obj_set_valid(7,2,"fffffffffff")
IF LEN(info_name$)<8 AND LEN(info_ext$)>0
info_name$=info_name$+STRING$(SUB(8,LEN(info_name$))," ")
ENDIF
CHAR{{OB_SPEC(adtree%(7),2)}}=TRIM$(info_name$+info_ext$)
ENDIF
IF LEN(info_ext$)>0
info_name$=TRIM$(info_name$)+"."+info_ext$
ELSE
info_name$=TRIM$(info_name$)
ENDIF
'
IF display_files_mode&(kk_win&)>-1
'
info_date%=@loc_get_date(info_ptr%)
CHAR{OB_SPEC(adtree%(7),8)}=LEFT$(@get_troll_date$(info_date%,TRUE)+" "+@get_troll_time$(info_date%,TRUE),22)
'
obj_deselect(7,12)
obj_deselect(7,13)
obj_deselect(7,14)
obj_deselect(7,15)
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF magic! OR mint!
IF GEMDOS(300,W:0,L:V:info_pathname$,L:xattr_buf%)=0
info_attr&=INT{ADD(xattr_buf%,40)}
ENDIF
ELSE
info_attr%=GEMDOS(67,L:V:info_pathname$,W:0,W:0)
info_attr&=CARD(info_attr%)
ENDIF
IF BTST(info_attr&,0)=TRUE
obj_select(7,12)
ENDIF
IF BTST(info_attr&,1)=TRUE
obj_select(7,13)
ENDIF
IF BTST(info_attr&,5)=TRUE
obj_select(7,14)
ENDIF
IF BTST(info_attr&,2)=TRUE
obj_select(7,15)
ENDIF
'
ENDIF
'
obj_deselect(7,16)
obj_deselect(7,17)
control_form(7)
form_result&=0
DO
form_result&=FORM_DO(adtree%(7),0)
LOOP UNTIL form_result&>15
uncontrol_form(7)
'
IF form_result&=16
'
IF info_longname!
info_new_name$=CHAR{{OB_SPEC(adtree%(7),2)}}
ELSE
info_new_name$=CHAR{{OB_SPEC(adtree%(7),2)}}
IF LEN(info_new_name$)>8
info_new_ext$=MID$(info_new_name$,9)
info_new_name$=LEFT$(info_new_name$,8)
ELSE
info_new_ext$=""
ENDIF
info_new_name$=TRIM$(info_new_name$)
IF LEN(info_new_ext$)>0
info_new_name$=info_new_name$+"."+TRIM$(info_new_ext$)
ENDIF
ENDIF
'
IF display_files_mode&(kk_win&)>-1
'
IF @tst_selected(7,12)
info_attr&=BSET(info_attr&,0)
ELSE
info_attr&=BCLR(info_attr&,0)
ENDIF
IF @tst_selected(7,13)
info_attr&=BSET(info_attr&,1)
ELSE
info_attr&=BCLR(info_attr&,1)
ENDIF
IF @tst_selected(7,14)
info_attr&=BSET(info_attr&,5)
ELSE
info_attr&=BCLR(info_attr&,5)
ENDIF
IF @tst_selected(7,15)
info_attr&=BSET(info_attr&,2)
ELSE
info_attr&=BCLR(info_attr&,2)
ENDIF
~GEMDOS(67,L:V:info_pathname$,W:1,W:info_attr&)
'
IF info_name$<>info_new_name$
IF kk_win&=3
info_new_pathname$=CHAR{local_path0%}+info_new_name$+c0$
ELSE IF kk_win&=4
info_new_pathname$=CHAR{local_path1%}+info_new_name$+c0$
ENDIF
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(86,W:0,L:V:info_pathname$,L:V:info_new_pathname$)
'
@display_refresh_directories
ENDIF
'
ELSE
'
IF info_name$<>info_new_name$
@set_volume_label(LEFT$(info_pathname$),info_name$,info_new_name$)
ENDIF
'
ENDIF
ENDIF
'
RETURN
> FUNCTION get_volume_label$(disk_letter$)
LOCAL d_name$,d_path$,d_err%,d_dta%,d_exist!
'
d_exist!=FALSE
d_name$=""
'
d_path$=disk_letter$+"\*"+c0$
d_err%=GEMDOS(78,L:V:d_path$,W:8)
DO
EXIT IF d_err%<>0 OR d_exist!=TRUE
d_dta%=FGETDTA()
IF BTST(BYTE{ADD(d_dta%,21)},3)=TRUE
d_name$=CHAR{ADD(d_dta%,30)}
d_exist!=TRUE
ENDIF
d_err%=GEMDOS(79)
LOOP
'
RETURN d_name$
ENDFUNC
> PROCEDURE set_volume_label(disk_letter$,disk_oldname$,disk_newname$)
LOCAL d_name$,d_path$,fmt_handle&
'
~FRE(0)
'
IF xattr_buf%>0
@fmt_clean_buffer(xattr_buf%,56)
'
CHAR{xattr_buf%}=LEFT$(disk_letter$+":\"+disk_newname$,56)
'
fmt_handle&=GEMDOS(60,L:xattr_buf%,W:8)
IF fmt_handle&>0
~GEMDOS(62,W:fmt_handle&)
ENDIF
ENDIF
'
RETURN
'
> PROCEDURE dialog_local(kk_win&)
result&=OBJC_FIND(adtree%(kk_win&),0,3,mo_x&,mo_y&)
SELECT result&
CASE 1
@local_get_drives
dummy&=@pop_up_with_slider(kk_win&,1,6,FALSE,FALSE,4)
IF dummy&>-1
local_drive_index&(kk_win&)=@local_get_drive_index(dummy&)
CHAR{{OB_SPEC(adtree%(kk_win&),1)}}=" "+loc_drive$(local_drive_index&(kk_win&))+": "
parent_record_start$(kk_win&)="0"
@local_set_win_info(kk_win&,loc_drive$(local_drive_index&(kk_win&))+":\")
black_white(kk_win&,1,-1)
ENDIF
CASE 2
@local_get_drives
dummy&=@pop_up_next_value(6,local_drive_index&(kk_win&),FALSE)
IF dummy&>-1
local_drive_index&(kk_win&)=dummy&
CHAR{{OB_SPEC(adtree%(kk_win&),1)}}=" "+loc_drive$(local_drive_index&(kk_win&))+": "
parent_record_start$(kk_win&)="0"
@local_set_win_info(kk_win&,loc_drive$(local_drive_index&(kk_win&))+":\")
black_white(kk_win&,1,-1)
ENDIF
CASE 3
@local_action_menu(kk_win&,FALSE)
CASE 4
@local_add_menu(kk_win&,FALSE)
CASE 6 TO 11
field_x&(kk_win&,SUB(result&,field_bar_obj&(kk_win&)))=@obj_hslide(kk_win&,field_bar_obj&(kk_win&),result&)
set_fields_width(kk_win&)
force_update(kk_win&)
DEFAULT
IF (m_touche&=1 OR m_touche&=2) AND display_files_mode&(kk_win&)>-1
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
IF dummy&>-1
IF record_nb_selected&(kk_win&)=0
'
record_select(kk_win&,dummy&)
INC record_nb_selected&(kk_win&)
'
ELSE IF record_nb_selected&(kk_win&)>0
'
IF @is_record_selected(kk_win&,dummy&)=FALSE
record_select(kk_win&,dummy&)
INC record_nb_selected&(kk_win&)
ELSE
record_deselect(kk_win&,dummy&)
DEC record_nb_selected&(kk_win&)
ENDIF
'
ENDIF
record_nb_selected&(kk_win&)=MAX(0,MIN(record_nb_selected&(kk_win&),head_nbline&(kk_win&)))
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
ENDIF
ELSE IF m_touche&=8 AND display_files_mode&(kk_win&)>-1
IF record_nb_selected&(kk_win&)=1
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
first_selected&=@record_get_next_selected(kk_win&,0)
IF dummy&>0 AND first_selected&>0
IF dummy&>first_selected&
DO
INC record_nb_selected&(kk_win&)
record_select(kk_win&,dummy&)
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
DEC dummy&
LOOP UNTIL dummy&=first_selected&
ELSE IF dummy&<first_selected&
DO
INC record_nb_selected&(kk_win&)
record_select(kk_win&,dummy&)
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
INC dummy&
LOOP UNTIL dummy&=first_selected&
ENDIF
ENDIF
ENDIF
ELSE
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
IF dummy&>-1
IF @is_record_selected(kk_win&,dummy&)
delay
IF record_nb_selected&(kk_win&)=1
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
ELSE
get_window_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
ENDIF
delay
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,mo_kbd&)
IF mo_c&>0
dummy&=@local_drag_selection(kk_win&,sx&,sy&,sl&,sh&,mo_x&,mo_y&,mov_win&)
IF dummy&=1
IF mo_kbd&=0
~GRAF_MKSTATE(dummy&,dummy&,dummy&,mo_kbd&)
ENDIF
IF (display_files_mode&(3)<>2 AND display_files_mode&(4)=0) OR (display_files_mode&(3)=0 AND display_files_mode&(4)=2)
@local_copy_files(3,BTST(mo_kbd&,2))
ELSE IF display_files_mode&(3)=2 AND display_files_mode&(4)=0
@zipfile_extract_files(3)
ENDIF
ELSE IF dummy&=-1
IF mo_kbd&=0
~GRAF_MKSTATE(dummy&,dummy&,dummy&,mo_kbd&)
ENDIF
IF (display_files_mode&(4)<>2 AND display_files_mode&(3)=0) OR (display_files_mode&(4)=0 AND display_files_mode&(3)=2)
@local_copy_files(4,BTST(mo_kbd&,2))
ELSE IF display_files_mode&(4)=2 AND display_files_mode&(3)=0
@zipfile_extract_files(4)
ENDIF
ENDIF
ELSE
@local_mouse_select(kk_win&)
ENDIF
ELSE
delay
delay
~GRAF_MKSTATE(mm_x&,mm_y&,mo_c&,mo_kbd&)
IF mo_c&>0 AND display_files_mode&(kk_win&)>-1
IF mm_x&=mo_x& AND mm_y&=mo_y& AND record_nb_selected&(kk_win&)=0
'
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
IF dummy&>-1
'
record_nb_selected&(kk_win&)=1
record_select(kk_win&,dummy&)
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
'
dummy&=@local_drag_selection(kk_win&,sx&,sy&,sl&,sh&,mo_x&,mo_y&,mov_win&)
IF dummy&=1
  IF mo_kbd&=0
    ~GRAF_MKSTATE(dummy&,dummy&,dummy&,mo_kbd&)
  ENDIF
  IF (display_files_mode&(3)<>2 AND display_files_mode&(4)=0) OR (display_files_mode&(3)=0 AND display_files_mode&(4)=2)
    @local_copy_files(3,BTST(mo_kbd&,2))
  ELSE IF display_files_mode&(3)=2 AND display_files_mode&(4)=0
    @zipfile_extract_files(3)
  ENDIF
ELSE IF dummy&=-1
  IF mo_kbd&=0
    ~GRAF_MKSTATE(dummy&,dummy&,dummy&,mo_kbd&)
  ENDIF
  IF (display_files_mode&(4)<>2 AND display_files_mode&(3)=0) OR (display_files_mode&(4)=0 AND display_files_mode&(3)=2)
    @local_copy_files(4,BTST(mo_kbd&,2))
  ELSE IF display_files_mode&(4)=2 AND display_files_mode&(3)=0
    @zipfile_extract_files(4)
  ENDIF
ENDIF
'
ENDIF
ELSE
~@local_rubber_selection(kk_win&,mo_x&,mo_y&)
ENDIF
ELSE
@local_mouse_select(kk_win&)
ENDIF
ENDIF
ELSE
@local_mouse_select(kk_win&)
ENDIF
ENDIF
ENDSELECT
RETURN
> PROCEDURE local_action_menu(kk_win&,right_click!)
'
SELECT display_files_mode&(kk_win&)
CASE 2
'
obj_enable(25,1)
obj_disable(25,3)
obj_disable(25,5)
'
IF record_nb_selected&(kk_win&)>0
IF record_nb_selected&(kk_win&)=1 AND @is_record_selected(kk_win&,0)=TRUE
ELSE
IF kk_win&=3 AND display_files_mode&(4)=0
obj_enable(25,3)
ELSE IF kk_win&=4 AND display_files_mode&(3)=0
obj_enable(25,3)
ENDIF
obj_enable(25,5)
ENDIF
ENDIF
'
SELECT @pop_up(kk_win&,3,25,right_click!)
CASE 0
@zipfile_get_directory(kk_win&)
CASE 2 ! copy
IF (kk_win&=3 AND display_files_mode&(4)=0) OR (kk_win&=4 AND display_files_mode&(3)=0)
@zipfile_extract_files(3)
ENDIF
CASE 4 ! delete
@zipfile_delete_files(kk_win&)
ENDSELECT
'
CASE 1
'
obj_enable(22,1)
obj_disable(22,3)
'
IF record_nb_selected&(kk_win&)>0
IF record_nb_selected&(kk_win&)=1 AND @is_record_selected(kk_win&,0)=TRUE
ELSE
IF kk_win&=3 AND display_files_mode&(4)=0
obj_enable(22,3)
ELSE IF kk_win&=4 AND display_files_mode&(3)=0
obj_enable(22,3)
ENDIF
ENDIF
ENDIF
'
SELECT @pop_up(kk_win&,3,22,right_click!)
CASE 0 ! refresh
@msa_get_directory(kk_win&)
CASE 2 ! copy
@local_copy_files(kk_win&,FALSE)
ENDSELECT
'
CASE -1
'
obj_disable(18,1)
obj_disable(18,3)
obj_disable(18,5)
obj_disable(18,7)
obj_disable(18,8)
obj_disable(18,10)
'
IF record_nb_selected&(kk_win&)=1 AND record_selected&(kk_win&)>-1
obj_enable(18,1)
IF (kk_win&=3 AND (display_files_mode&(4)=0 OR display_files_mode&(4)=2)) OR (kk_win&=4 AND (display_files_mode&(3)=0 OR display_files_mode&(3)=2))
obj_enable(18,3)
ENDIF
IF (kk_win&=3 AND display_files_mode&(4)=0) OR (kk_win&=4 AND display_files_mode&(3)=0)
obj_enable(18,5)
ENDIF
dummy$=LEFT$(@loc_get_name$(@px(kk_win&,record_selected&(kk_win&))))
IF dummy$="A" OR dummy$="B"
obj_enable(18,7)
obj_enable(18,8)
ENDIF
IF dummy$="A"
IF kk_win&=3 AND BYTE{local_path1%}<>65 AND display_files_mode&(4)=0
obj_enable(18,10)
ELSE IF kk_win&=4 AND BYTE{local_path0%}<>65 AND display_files_mode&(3)=0
obj_enable(18,10)
ENDIF
ELSE IF dummy$="B"
IF kk_win&=3 AND BYTE{local_path1%}<>66 AND display_files_mode&(4)=0
obj_enable(18,10)
ELSE IF kk_win&=4 AND BYTE{local_path0%}<>66 AND display_files_mode&(3)=0
obj_enable(18,10)
ENDIF
ENDIF
ENDIF
'
SELECT @pop_up(kk_win&,3,18,right_click!)
CASE 0 ! informations
@dialog_informations(kk_win&,record_selected&(kk_win&))
CASE 2 ! copy
IF (kk_win&=3 AND (display_files_mode&(4)=0 OR display_files_mode&(4)=2)) OR (kk_win&=4 AND (display_files_mode&(3)=0 OR display_files_mode&(3)=2))
@local_copy_files(kk_win&,FALSE)
ENDIF
CASE 4 ! move
IF (kk_win&=3 AND display_files_mode&(4)=0) OR (kk_win&=4 AND display_files_mode&(3)=0)
@local_copy_files(kk_win&,TRUE)
ENDIF
CASE 6
@dialog_format_quick(LEFT$(@loc_get_name$(@px(kk_win&,record_selected&(kk_win&)))))
CASE 7
@dialog_format(LEFT$(@loc_get_name$(@px(kk_win&,record_selected&(kk_win&)))))
CASE 9
IF kk_win&=3
@dialog_msa_to_file(LEFT$(@loc_get_name$(@px(kk_win&,record_selected&(kk_win&)))),4)
ELSE IF kk_win&=4
@dialog_msa_to_file(LEFT$(@loc_get_name$(@px(kk_win&,record_selected&(kk_win&)))),3)
ENDIF
ENDSELECT
'
DEFAULT
'
obj_enable(17,1)
obj_disable(17,3)
obj_disable(17,5)
obj_disable(17,7)
obj_disable(17,9)
obj_disable(17,11)
obj_disable(17,13)
'
IF record_nb_selected&(kk_win&)>0
IF record_nb_selected&(kk_win&)=1 AND @is_record_selected(kk_win&,0)=TRUE
ELSE
IF (kk_win&=3 AND display_files_mode&(4)=0) OR (kk_win&=4 AND display_files_mode&(3)=0)
obj_enable(17,5)
obj_enable(17,7)
ENDIF
obj_enable(17,9)
ENDIF
ENDIF
'
IF record_nb_selected&(kk_win&)=1
first_selected&=@record_get_next_selected(kk_win&,0)
IF first_selected&>0
obj_enable(17,3)
IF UPPER$(RIGHT$(@loc_get_name$(@px(kk_win&,first_selected&)),4))=".MSA"
obj_enable(17,11)
ELSE IF UPPER$(RIGHT$(@loc_get_name$(@px(kk_win&,first_selected&)),3))=".ST"
obj_enable(17,11)
ENDIF
IF (kk_win&=3 AND display_files_mode&(4)=0) OR (kk_win&=4 AND display_files_mode&(3)=0)
obj_enable(17,13)
ENDIF
ENDIF
ENDIF
'
SELECT @pop_up(kk_win&,3,17,right_click!)
CASE 0
@local_get_directory(kk_win&)
CASE 2
@dialog_informations(kk_win&,@record_get_next_selected(kk_win&,0))
CASE 4
IF (kk_win&=3 AND (display_files_mode&(4)=0 OR display_files_mode&(4)=2)) OR (kk_win&=4 AND (display_files_mode&(3)=0 OR display_files_mode&(3)=2))
@local_copy_files(kk_win&,FALSE)
ENDIF
CASE 6
IF (kk_win&=3 AND display_files_mode&(4)=0) OR (kk_win&=4 AND display_files_mode&(3)=0)
@local_copy_files(kk_win&,TRUE)
ENDIF
CASE 8
@local_delete_files(kk_win&)
CASE 10
@dialog_msa_to_disk(@loc_get_name$(@px(kk_win&,@record_get_next_selected(kk_win&,0))))
CASE 12
@dialog_split_file(@loc_get_name$(@px(kk_win&,@record_get_next_selected(kk_win&,0))),kk_win&)
ENDSELECT
'
ENDSELECT
'
RETURN
> PROCEDURE local_add_menu(kk_win&,right_click!)
'
SELECT display_files_mode&(kk_win&)
CASE 2
'
obj_enable(26,1)
obj_enable(26,3)
obj_disable(26,5)
'
SELECT @pop_up(kk_win&,4,26,right_click!)
CASE 0 ! new folder
@zipfile_new_folder(kk_win&)
CASE 2 ! new empty file
@zipfile_new_empty_file(kk_win&)
ENDSELECT
'
CASE 1,-1
'
obj_disable(26,1)
obj_disable(26,3)
obj_disable(26,5)
'
SELECT @pop_up(kk_win&,4,26,right_click!)
ENDSELECT
'
DEFAULT
'
obj_enable(26,1)
obj_enable(26,3)
obj_enable(26,5)
'
SELECT @pop_up(kk_win&,4,26,right_click!)
CASE 0
@dialog_new_folder(kk_win&)
CASE 2
@dialog_new_empty_file(kk_win&)
CASE 4
@zipfile_new(kk_win&)
ENDSELECT
'
ENDSELECT
'
RETURN
> PROCEDURE local_mouse_select(kk_win&)
dummy&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
IF dummy&>-1
IF display_files_mode&(kk_win&)=-1
'
old_record_selected&=record_selected&(kk_win&)
IF old_record_selected&>-1
record_deselect(kk_win&,record_selected&(kk_win&))
ENDIF
IF record_selected&(kk_win&)=dummy&
dummy&=-1
record_nb_selected&(kk_win&)=0
ELSE
record_nb_selected&(kk_win&)=1
ENDIF
'
record_selected&(kk_win&)=dummy&
get_record_xywh(kk_win&,record_selected&(kk_win&),sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,record_selected&(kk_win&),sx&,sy&,sl&,sh&)
IF old_record_selected&>-1
get_record_xywh(kk_win&,old_record_selected&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,old_record_selected&,sx&,sy&,sl&,sh&)
ENDIF
'
ELSE
'
IF @is_record_selected(kk_win&,dummy&)
record_deselect(kk_win&,dummy&)
DEC record_nb_selected&(kk_win&)
ELSE
record_select(kk_win&,dummy&)
INC record_nb_selected&(kk_win&)
ENDIF
'
get_record_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,dummy&,sx&,sy&,sl&,sh&)
'
ENDIF
ELSE IF OBJC_FIND(adtree%(kk_win&),0,0,mo_x&,mo_y&)<>-1
win(kk_win&)
ELSE IF record_nb_selected&(kk_win&)>0
@local_deselect_all_files(kk_win&)
ELSE
win(kk_win&)
ENDIF
RETURN
> FUNCTION local_drag_selection(kk_win&,mov_x&,mov_y&,mov_l&,mov_h&,VAR mo_x&,mo_y&,mov_win&)
$F%
'
~WIND_UPDATE(1)
~WIND_UPDATE(3)
~GRAF_MOUSE(4,0)
~GRAF_DRAGBOX(mov_l&,mov_h&,mov_x&,mov_y&,screenx&,screeny&,screenl&,screenh&)
~GRAF_MKSTATE(mo_x&,mo_y&,dummy&,dummy&)
mov_win&=WIND_FIND(mo_x&,mo_y&)
~GRAF_MOUSE(0,0)
~WIND_UPDATE(0)
~WIND_UPDATE(2)
'
IF mov_win&=hand_win&(4) AND win!(4)=TRUE AND kk_win&=3
RETURN 1
ELSE IF mov_win&=hand_win&(3) AND win!(3)=TRUE AND kk_win&=4
RETURN -1
ENDIF
RETURN 0
ENDFUNC
> FUNCTION local_rubber_selection(kk_win&,VAR mo_x&,mo_y&)
$F%
LOCAL ro_x&,ro_y&,ro_start&,ro_end&
'
ro_x&=mo_x&
ro_y&=mo_y&
'
~WIND_UPDATE(1)
~WIND_UPDATE(3)
~GRAF_MOUSE(3,0)
'
~GRAF_RUBBERBOX(ro_x&,ro_y&,1,1)
~GRAF_MKSTATE(mo_x&,mo_y&,dummy&,mo_kbd&)
'
~GRAF_MOUSE(0,0)
~WIND_UPDATE(0)
~WIND_UPDATE(2)
'
IF BTST(mo_kbd&,0) OR BTST(mo_kbd&,1)
'
ro_start&=@get_record_under_cursor(kk_win&,ro_y&,ro_x&)
ro_end&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
'
IF ro_start&>-1
DO
IF @is_record_selected(kk_win&,ro_start&)=FALSE
INC record_nb_selected&(kk_win&)
record_select(kk_win&,ro_start&)
get_record_xywh(kk_win&,ro_start&,sx&,sy&,sl&,sh&)
force_update_xywh(kk_win&,ro_start&,sx&,sy&,sl&,sh&)
ENDIF
INC ro_start&
LOOP UNTIL ro_start&>ro_end&
ENDIF
'
ELSE
record_deselect_all(kk_win&)
record_selected&(kk_win&)=-1
record_nb_selected&(kk_win&)=0
'
ro_start&=@get_record_under_cursor(kk_win&,ro_y&,ro_x&)
ro_end&=@get_record_under_cursor(kk_win&,mo_y&,mo_x&)
'
IF ro_start&>-1
DO
INC record_nb_selected&(kk_win&)
record_select(kk_win&,ro_start&)
INC ro_start&
LOOP UNTIL ro_start&>ro_end&
ENDIF
'
IF head_nbline&(kk_win&)>0
force_update(kk_win&)
ENDIF
'
ENDIF
'
RETURN 0
ENDFUNC
> PROCEDURE local_select_all_files(kk_win&)
LOCAL rec_i&
'
record_deselect_all(kk_win&)
record_selected&(kk_win&)=-1
record_nb_selected&(kk_win&)=0
'
IF head_nbline&(kk_win&)>0
'
FOR rec_i&=0 TO PRED(head_nbline&(kk_win&))
IF @loc_get_type(@px(kk_win&,rec_i&))=1 OR @loc_get_type(@px(kk_win&,rec_i&))=2
record_select(kk_win&,rec_i&)
INC record_nb_selected&(kk_win&)
ENDIF
NEXT rec_i&
'
force_update(kk_win&)
ENDIF
'
RETURN
> PROCEDURE local_deselect_all_files(kk_win&)
'
record_deselect_all(kk_win&)
record_selected&(kk_win&)=-1
record_nb_selected&(kk_win&)=0
'
IF head_nbline&(kk_win&)>0
force_update(kk_win&)
ENDIF
'
RETURN
> PROCEDURE local_set_fonts(kk_win&)
'
IF display_files_mode&(kk_win&)=-1
'
~@vst_point(6)
IF screenh&<250
font_files_real_height&(kk_win&)=SUCC(MUL(OB_H(adtree%(nb_tree&),1),2))
ELSE
font_files_real_height&(kk_win&)=SUCC(OB_H(adtree%(nb_tree&),1))
ENDIF
'
ELSE
'
IF screenh&<250
SELECT display_files_font_size&
CASE 1
font_files_real_height&(kk_win&)=SUCC(@vst_point(6))
CASE 0,2,3
font_files_real_height&(kk_win&)=SUCC(@vst_point(9))
ENDSELECT
ELSE
SELECT display_files_font_size&
CASE 1
font_files_real_height&(kk_win&)=SUCC(@vst_point(6))
CASE 2
font_files_real_height&(kk_win&)=SUCC(@vst_point(9))
CASE 0,3
font_files_real_height&(kk_win&)=SUCC(@vst_point(13))
ENDSELECT
ENDIF
'
field_length_max_width&=@vqt_extent("100000000000MM",FALSE)
ENDIF
'
RETURN
> PROCEDURE local_set_display(kk_win&)
'
IF display_files_with_date!
obj_unhide(kk_win&,ADD(field_bar_obj&(kk_win&),2))
ELSE
obj_hide(kk_win&,ADD(field_bar_obj&(kk_win&),2))
ENDIF
IF display_files_with_length!
obj_unhide(kk_win&,ADD(field_bar_obj&(kk_win&),3))
ELSE
obj_hide(kk_win&,ADD(field_bar_obj&(kk_win&),3))
ENDIF
'
set_fields_display(kk_win&)
'
RETURN
> PROCEDURE local_set_win_info(kk_win&,str$)
IF local_path0%>0
IF kk_win&=3
CHAR{local_path0%}=LEFT$(str$,250)
IF win!(3)
~WIND_SET(hand_win&(3),2,CARD(SWAP(local_path0%)),CARD(local_path0%),0,0)
ENDIF
record_start&(3)=0
ELSE IF kk_win&=4
CHAR{local_path1%}=LEFT$(str$,250)
IF win!(4)
~WIND_SET(hand_win&(4),2,CARD(SWAP(local_path1%)),CARD(local_path1%),0,0)
ENDIF
record_start&(4)=0
ENDIF
IF UPPER$(RIGHT$(str$,4))=".ZIP" OR INSTR(UPPER$(str$),".ZIP>")>0
'
display_files_mode&(kk_win&)=2
'
msa_image_adr%(kk_win&)=@msa_clean_image_in_ram(msa_image_adr%(kk_win&))
msa_image_name$(kk_win&)=SPACE$(3)
'
@local_set_fonts(kk_win&)
@local_set_drive(kk_win&)
@zipfile_get_directory(kk_win&)
'
ELSE IF UPPER$(RIGHT$(str$,4))=".MSA" OR INSTR(UPPER$(str$),".MSA>")>0 OR UPPER$(RIGHT$(str$,3))=".ST" OR INSTR(UPPER$(str$),".ST>")>0
'
display_files_mode&(kk_win&)=1
'
@zipfile_unload(kk_win&)
zip_filename$(kk_win&)=SPACE$(3)
'
@local_set_fonts(kk_win&)
@local_set_drive(kk_win&)
@msa_get_directory(kk_win&)
'
ELSE IF LEN(str$)>0
'
display_files_mode&(kk_win&)=0
'
msa_image_adr%(kk_win&)=@msa_clean_image_in_ram(msa_image_adr%(kk_win&))
msa_image_name$(kk_win&)=SPACE$(3)
@zipfile_unload(kk_win&)
zip_filename$(kk_win&)=SPACE$(3)
'
@local_set_fonts(kk_win&)
@local_set_drive(kk_win&)
@local_set_path(kk_win&)
@local_get_directory(kk_win&)
'
ELSE
'
display_files_mode&(kk_win&)=-1
'
msa_image_adr%(kk_win&)=@msa_clean_image_in_ram(msa_image_adr%(kk_win&))
msa_image_name$(kk_win&)=SPACE$(3)
@zipfile_unload(kk_win&)
zip_filename$(kk_win&)=SPACE$(3)
'
@local_set_fonts(kk_win&)
@local_get_disks_list(kk_win&)
'
ENDIF
ENDIF
RETURN
> PROCEDURE local_redraw(kk_win&,rline&)
LOCAL local_length%,local_date%,local_changed!
'
IF head_nbline&(kk_win&)>0
'
IF FRE()<4000
~FRE(0)
ENDIF
'
v_hide_c
vswr_mode(3)
vst_effect(0)
vst_color(1)
'
~WIND_GET(hand_win&(kk_win&),4,xf&,yf&,lf&,hf&)
ADD yf&,field_bar_h&(kk_win&)
SUB hf&,field_bar_h&(kk_win&)
'
IF display_files_mode&(kk_win&)=-1
'
ex&=ADD(xf&,1)
ed&=ADD(ex&,3)
ey&=yf&
el&=SUB(lf&,1)
eh&=hf&
IF rline&<0
ee&=ADD(yf&,2)
ef&=MIN(ADD(record_start&(kk_win&),SUCC(record_height&(kk_win&))),PRED(head_nbline&(kk_win&)))
ei&=record_start&(kk_win&)
ELSE
ee&=ADD(ADD(yf&,2),MUL(font_files_real_height&(kk_win&),SUB(rline&,record_start&(kk_win&))))
ef&=rline&
ei&=rline&
ENDIF
'
local_text_dy&=MAX(0,DIV(SUB(font_files_real_height&(kk_win&),6),2))
'
IF el&>0
IF RC_INTERSECT(rx&,ry&,rl&,rh&,ex&,ey&,el&,eh&)
clip(1,ex&,ey&,el&,eh&)
FOR j&=ei& TO ef&
yp&=MUL(font_files_real_height&(kk_win&),SUB(j&,ei&))
ptr%=@px(kk_win&,j&)
txt$=" "+@loc_get_name$(ptr%)
'
OB_X(adtree%(nb_tree&),0)=ed&
OB_Y(adtree%(nb_tree&),0)=PRED(ADD(ee&,yp&))
clip(0,ex&,ey&,el&,eh&)
SELECT @loc_get_type(ptr%)
CASE 11
~OBJC_DRAW(adtree%(nb_tree&),1,0,ex&,ey&,el&,eh&)
CASE 12
~OBJC_DRAW(adtree%(nb_tree&),2,0,ex&,ey&,el&,eh&)
ENDSELECT
clip(1,ex&,ey&,el&,eh&)
'
v_text(ADD(ed&,ADD(OB_W(adtree%(nb_tree&),1),2)),ADD(ADD(ee&,yp&),local_text_dy&),txt$,FALSE)
'
NEXT j&
clip(0,ex&,ey&,el&,eh&)
ENDIF
ENDIF
'
ELSE
'
ex&=ADD(xf&,SUB(field_x&(kk_win&,1),field_start&(kk_win&)))
ed&=ADD(ex&,3)
ey&=yf&
el&=field_w&(kk_win&,1)
eh&=hf&
IF rline&<0
ee&=ADD(yf&,2)
ef&=MIN(ADD(record_start&(kk_win&),SUCC(record_height&(kk_win&))),PRED(head_nbline&(kk_win&)))
ei&=record_start&(kk_win&)
ELSE
ee&=ADD(ADD(yf&,2),MUL(font_files_real_height&(kk_win&),SUB(rline&,record_start&(kk_win&))))
ef&=rline&
ei&=rline&
ENDIF
'
IF el&>0
IF RC_INTERSECT(rx&,ry&,rl&,rh&,ex&,ey&,el&,eh&)
clip(1,ex&,ey&,el&,eh&)
FOR j&=ei& TO ef&
yp&=MUL(font_files_real_height&(kk_win&),SUB(j&,ei&))
ptr%=@px(kk_win&,j&)
txt$=@loc_get_name$(ptr%)
ext$=UPPER$(RIGHT$(txt$,4))
IF @loc_get_type(ptr%)=2
txt$=" "+txt$
ELSE IF ext$=".PRG" OR ext$=".TOS" OR ext$=".TTP"
IF display_files_mode&(kk_win&)=0
txt$=". "+txt$
ELSE
txt$="  "+txt$
ENDIF
ELSE IF ext$=".ZIP" OR ext$=".MSA" OR INSTR(ext$,".ST")=2
IF display_files_mode&(kk_win&)=0
txt$=" "+txt$
ELSE
txt$="  "+txt$
ENDIF
ELSE
txt$="  "+txt$
ENDIF
'
local_changed!=FALSE
IF @is_record_selected(kk_win&,j&)
local_changed!=TRUE
vswr_mode(2)
IF nb_plan&>1
vst_color(2)
ENDIF
ENDIF
IF @loc_get_type(ptr%)=4
vst_effect(&X10)
ENDIF
'
v_text(ed&,ADD(ee&,yp&),txt$,FALSE)
'
IF @loc_get_type(ptr%)=4
vst_effect(0)
ENDIF
IF local_changed!
vswr_mode(3)
vst_color(1)
ENDIF
'
NEXT j&
clip(0,ex&,ey&,el&,eh&)
ENDIF
ENDIF
'
ex&=ADD(xf&,SUB(field_x&(kk_win&,2),field_start&(kk_win&)))
ed&=ADD(ex&,3)
ey&=yf&
el&=MIN(field_w&(kk_win&,2),field_length_max_width&)
em&=SUB(el&,3)
eh&=hf&
'
vst_alignment(2,5)
'
IF el&>0 AND display_files_with_length!
IF RC_INTERSECT(rx&,ry&,rl&,rh&,ex&,ey&,el&,eh&)
clip(1,ex&,ey&,el&,eh&)
FOR j&=ei& TO ef&
yp&=MUL(font_files_real_height&(kk_win&),SUB(j&,ei&))
'
LET local_length%=@loc_get_length(@px(kk_win&,j&))
SELECT display_files_length_format&
CASE 0
IF local_length%>1073741824
LET local_length%=MAX(1,local_length%/1073741824)
txt$=STR$(local_length%)+str_kilobytes$
ELSE IF local_length%>1048576
LET local_length%=MAX(1,local_length%/1048576)
txt$=STR$(local_length%)+str_megabytes$
ELSE IF local_length%>1024
LET local_length%=MAX(1,local_length%/1024)
txt$=STR$(local_length%)+str_kilobytes$
ELSE IF local_length%>0
txt$=STR$(local_length%)+str_bytes$
ELSE
txt$=""
ENDIF
CASE 1
IF local_length%=0
txt$=""
ELSE
LET local_length%=MAX(1,local_length%/1024)
txt$=STR$(local_length%)+str_kilobytes$
ENDIF
CASE 2
IF local_length%>0
txt$=STR$(local_length%)+" " ! +str_bytes$
ELSE
txt$=""
ENDIF
ENDSELECT
'
local_changed!=FALSE
IF @is_record_selected(kk_win&,j&)
local_changed!=TRUE
vswr_mode(2)
IF nb_plan&>1
vst_color(2)
ENDIF
ENDIF
'
v_text(ADD(ed&,em&),ADD(ee&,yp&),txt$,FALSE)
'
IF local_changed!
vswr_mode(3)
vst_color(1)
ENDIF
'
NEXT j&
clip(0,ex&,ey&,el&,eh&)
ENDIF
ENDIF
'
vst_alignment(0,5)
'
ex&=ADD(xf&,SUB(field_x&(kk_win&,3),field_start&(kk_win&)))
ed&=ADD(ex&,3)
ey&=yf&
el&=field_w&(kk_win&,3)
eh&=hf&
'
IF el&>0 AND display_files_with_date!
IF RC_INTERSECT(rx&,ry&,rl&,rh&,ex&,ey&,el&,eh&)
clip(1,ex&,ey&,el&,eh&)
FOR j&=ei& TO ef&
yp&=MUL(font_files_real_height&(kk_win&),SUB(j&,ei&))
LET local_date%=@loc_get_date(@px(kk_win&,j&))
'
local_changed!=FALSE
IF @is_record_selected(kk_win&,j&)
local_changed!=TRUE
vswr_mode(2)
IF nb_plan&>1
vst_color(2)
ENDIF
ENDIF
'
v_text(ed&,ADD(ee&,yp&),@get_troll_date$(local_date%,TRUE)+" "+@get_troll_time$(local_date%,TRUE),FALSE)
'
IF local_changed!
vswr_mode(3)
vst_color(1)
ENDIF
'
NEXT j&
clip(0,ex&,ey&,el&,eh&)
ENDIF
ENDIF
'
ENDIF
'
v_show_c
'
ENDIF
'
RETURN
> PROCEDURE local_get_drives
LOCAL dgetdrive%,dget_i&
'
dgetdrive%=BIOS(10)
loc_drive_nb&=0
'
FOR dget_i&=0 TO 25
IF BTST(dgetdrive%,dget_i&)
IF dget_i&<>1 OR display_disk_b!=TRUE
loc_drive$(loc_drive_nb&)=CHR$(ADD(65,dget_i&))
loc_drive&(loc_drive_nb&)=dget_i&
INC loc_drive_nb&
ENDIF
ENDIF
NEXT dget_i&
'
RETURN
> FUNCTION local_get_drive_index(number&)
FOR ipo&=0 TO PRED(loc_drive_nb&)
IF loc_drive&(ipo&)=number&
RETURN ipo&
ENDIF
NEXT ipo&
RETURN 0
ENDFUNC
> FUNCTION local_get_drive_index_from_letter(number|)
FOR ipo&=0 TO PRED(loc_drive_nb&)
IF loc_drive$(ipo&)=CHR$(number|)
RETURN ipo&
ENDIF
NEXT ipo&
RETURN 0
ENDFUNC
> PROCEDURE local_change_directory(kk_win&,loc_ptr%)
LOCAL d_folder$,d_separator$,d_pos&
'
IF loc_ptr%=0
IF kk_win&=3
d_folder$=CHAR{local_path0%}
ELSE IF kk_win&=4
d_folder$=CHAR{local_path1%}
ENDIF
d_folder$=LEFT$(d_folder$,PRED(LEN(d_folder$)))
d_pos&=MAX(RINSTR(d_folder$,"\"),RINSTR(d_folder$,">"))
IF d_pos&>0
d_folder$=LEFT$(d_folder$,d_pos&)
IF RIGHT$(d_folder$)=":"
d_folder$=d_folder$+"\"
ENDIF
local_set_win_info(kk_win&,d_folder$)
ELSE
local_set_win_info(kk_win&,"")
ENDIF
'
d_pos&=RINSTR(parent_record_start$(kk_win&),"|")
IF d_pos&>0
record_start&(kk_win&)=VAL(MID$(parent_record_start$(kk_win&),SUCC(d_pos&)))
parent_record_start$(kk_win&)=LEFT$(parent_record_start$(kk_win&),PRED(d_pos&))
ELSE
record_start&(kk_win&)=0
parent_record_start$(kk_win&)="0"
ENDIF
record_start&(kk_win&)=MAX(0,MIN(record_start&(kk_win&),SUB(PRED(head_nbline&(kk_win&)),record_height&(kk_win&))))
set_vslide(kk_win&)
'
ELSE
IF @loc_get_type(loc_ptr%)=1
d_separator$=">"
ELSE
d_separator$="\"
ENDIF
parent_record_start$(kk_win&)=parent_record_start$(kk_win&)+"|"+STR$(record_start&(kk_win&))
IF kk_win&=3
local_set_win_info(3,CHAR{local_path0%}+@loc_get_name$(loc_ptr%)+d_separator$)
ELSE IF kk_win&=4
local_set_win_info(4,CHAR{local_path1%}+@loc_get_name$(loc_ptr%)+d_separator$)
ENDIF
ENDIF
'
RETURN
> PROCEDURE local_set_drive(kk_win&)
IF kk_win&=3
~GEMDOS(14,W:SUB(BYTE{local_path0%},65))
CHAR{{OB_SPEC(adtree%(3),1)}}=" "+CHR$(BYTE{local_path0%})+": "
black_white(3,1,-1)
ELSE IF kk_win&=4
~GEMDOS(14,W:SUB(BYTE{local_path1%},65))
CHAR{{OB_SPEC(adtree%(4),1)}}=" "+CHR$(BYTE{local_path1%})+": "
black_white(4,1,-1)
ENDIF
RETURN
> PROCEDURE local_set_path(kk_win&)
IF kk_win&=3
~GEMDOS(59;L:local_path0%)
ELSE IF kk_win&=4
~GEMDOS(59;L:local_path1%)
ENDIF
RETURN
> PROCEDURE local_get_directory(kk_win&)
$F%
LOCAL d_handle%,d_err%,d_test$,d_dta%,d_folder$,d_length%,d_date%
LOCAL loc_mem_i&,loc_mem_j&,loc_mem_k&
'
d_total_length%=0
'
IF shell_buf%>0
'
old_record_start&=record_start&(kk_win&)
'
@mouse_busy
@record_close(kk_win&)
@record_open(kk_win&,64,record_len&(kk_win&),field_bar_obj&(kk_win&))
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF kk_win&=3
d_folder$=CHAR{local_path0%}
ELSE IF kk_win&=4
d_folder$=CHAR{local_path1%}
ENDIF
d_folder$=LEFT$(d_folder$,PRED(LEN(d_folder$)))
loc_add_ptr%=record_buffer%(kk_win&)
'
IF (magic!=TRUE OR mint!=TRUE) AND xattr_buf%>0
'
d_handle%=GEMDOS(296,L:V:d_folder$,0)
IF d_handle%>0
'
WHILE GEMDOS(297,W:250,L:d_handle%,L:shell_buf%)=0
'
record_clear_buffer(kk_win&)
'
d_type&=-1
d_file$=CHAR{ADD(shell_buf%,4)}
'
IF d_file$<>"." AND d_file$<>".."
'
@loc_set_number(loc_add_ptr%,0)
@loc_set_name(loc_add_ptr%,d_file$)
'
d_test$=d_folder$+"\"+d_file$+c0$
IF GEMDOS(300,W:0,L:V:d_test$,L:xattr_buf%)=0
SELECT (INT{xattr_buf%} AND &HF000)
CASE &H4000
d_type&=2
CASE &H8000
d_type&=1
CASE &HE000
d_type&=3
ENDSELECT
'
@loc_set_type(loc_add_ptr%,d_type&)
@loc_set_length(loc_add_ptr%,LONG{ADD(xattr_buf%,16)})
IF d_type&=1
ADD d_total_length%,LONG{ADD(xattr_buf%,16)}
ENDIF
@loc_set_date(loc_add_ptr%,@gemdos_to_troll_timestamp(INT{ADD(xattr_buf%,30)},INT{ADD(xattr_buf%,28)}))
ENDIF
ENDIF
IF display_sort_discard_hidden_files!
IF LEFT$(d_file$)="."
d_type&=-1
ENDIF
ENDIF
'
IF d_type&>0
SELECT display_sort_type&
CASE 0
record_insert(kk_win&,head_nbline&(kk_win&))
CASE 1
record_insert(kk_win&,@local_sort_by_name(kk_win&,ADD(loc_add_ptr%,8)))
CASE 2
record_insert(kk_win&,@local_sort_by_length(kk_win&,@loc_get_length(loc_add_ptr%)))
CASE 3
record_insert(kk_win&,@local_sort_by_date(kk_win&,@loc_get_date(loc_add_ptr%)))
CASE 4
record_insert(kk_win&,@local_sort_by_type(kk_win&,ADD(loc_add_ptr%,4)))
ENDSELECT
ENDIF
'
WEND
'
~GEMDOS(299,L:d_handle%)
ENDIF
'
ELSE
'
IF kk_win&=3
d_folder$=CHAR{local_path0%}+"*.*"+c0$
ELSE IF kk_win&=4
d_folder$=CHAR{local_path1%}+"*.*"+c0$
ENDIF
d_err%=GEMDOS(78,L:V:d_folder$,W:&X110111)
DO
EXIT IF d_err%<>0
d_dta%=FGETDTA()
'
record_clear_buffer(kk_win&)
'
d_type&=-1
d_file$=CHAR{ADD(d_dta%,30)}
'
IF d_file$<>"." AND d_file$<>".."
'
@loc_set_number(loc_add_ptr%,0)
@loc_set_name(loc_add_ptr%,d_file$)
'
IF BTST(BYTE{ADD(d_dta%,21)},4)
d_type&=2
ELSE IF BTST(BYTE{ADD(d_dta%,21)},1)
IF display_sort_discard_hidden_files!
d_type&=-1
ELSE
d_type&=4
ENDIF
ELSE
d_type&=1
ENDIF
'
@loc_set_type(loc_add_ptr%,d_type&)
@loc_set_length(loc_add_ptr%,LONG{ADD(d_dta%,26)})
IF d_type&=1
ADD d_total_length%,LONG{ADD(d_dta%,26)}
ENDIF
@loc_set_date(loc_add_ptr%,@gemdos_to_troll_timestamp(INT{ADD(d_dta%,24)},INT{ADD(d_dta%,22)}))
ENDIF
IF display_sort_discard_hidden_files!
IF LEFT$(d_file$)="."
d_type&=-1
ENDIF
ENDIF
'
IF d_type&>0
SELECT display_sort_type&
CASE 0
record_insert(kk_win&,head_nbline&(kk_win&))
CASE 1
record_insert(kk_win&,@local_sort_by_name(kk_win&,ADD(loc_add_ptr%,8)))
CASE 2
record_insert(kk_win&,@local_sort_by_length(kk_win&,@loc_get_length(loc_add_ptr%)))
CASE 3
record_insert(kk_win&,@local_sort_by_date(kk_win&,@loc_get_date(loc_add_ptr%)))
CASE 4
record_insert(kk_win&,@local_sort_by_type(kk_win&,ADD(loc_add_ptr%,4)))
ENDSELECT
ENDIF
'
d_err%=GEMDOS(79)
LOOP
'
ENDIF
'
@local_sort_folders_at_top(kk_win&)
@local_set_win_stats(kk_win&)
@local_add_cd_up(kk_win&)
'
@mouse_free
'
IF win!(kk_win&)
record_start&(kk_win&)=old_record_start&
set_vslide(kk_win&)
record_start&(kk_win&)=@get_vslide(kk_win&)
force_update(kk_win&)
ENDIF
ENDIF
RETURN
> PROCEDURE local_sort_folders_at_top(kk_win&)
'
IF display_sort_folder_at_top!
IF head_nbline&(kk_win&)>0
LET loc_mem_i&=0
LET loc_mem_k&=PRED(head_nbline&(kk_win&))
IF loc_mem_k&>loc_mem_i&
FOR loc_mem_j&=loc_mem_i& TO loc_mem_k&
IF @loc_get_type(@px(kk_win&,loc_mem_j&))=2
IF loc_mem_i&<loc_mem_j&
record_clear_buffer(kk_win&)
BMOVE @px(kk_win&,loc_mem_j&),record_buffer%(kk_win&),record_len&(kk_win&)
record_delete(kk_win&,loc_mem_j&)
record_insert(kk_win&,loc_mem_i&)
INC loc_mem_i&
ELSE IF loc_mem_i&=loc_mem_j&
INC loc_mem_i&
ENDIF
ENDIF
NEXT loc_mem_j&
ENDIF
ENDIF
ENDIF
'
RETURN
> PROCEDURE local_set_win_stats(kk_win&)
'
IF kk_win&=3 AND win!(3)=TRUE
CHAR{local_info0%}=LEFT$(" "+STR$(head_nbline&(3))+str_files$+STR$(d_total_length%)+str_octets$,63)
~WIND_SET(hand_win&(3),3,CARD(SWAP(local_info0%)),CARD(local_info0%),0,0)
ELSE IF kk_win&=4 AND win!(4)=TRUE
CHAR{local_info1%}=LEFT$(" "+STR$(head_nbline&(4))+str_files$+STR$(d_total_length%)+str_octets$,63)
~WIND_SET(hand_win&(4),3,CARD(SWAP(local_info1%)),CARD(local_info1%),0,0)
ENDIF
'
RETURN
> PROCEDURE local_add_cd_up(kk_win&)
'
record_clear_buffer(kk_win&)
@loc_set_number(record_buffer%(kk_win&),0)
@loc_set_type(record_buffer%(kk_win&),0)
@loc_set_name(record_buffer%(kk_win&),"..")
record_insert(kk_win&,0)
'
RETURN
> PROCEDURE local_get_disks_list(kk_win&)
LOCAL dgetdrive%,dget_i&
'
dgetdrive%=BIOS(10)
loc_drive_nb&=0
'
@mouse_busy
@record_close(kk_win&)
@record_open(kk_win&,64,record_len&(kk_win&),field_bar_obj&(kk_win&))
'
IF FRE()<4000
~FRE(0)
ENDIF
'
loc_add_ptr%=record_buffer%(kk_win&)
'
FOR dget_i&=0 TO 25
IF BTST(dgetdrive%,dget_i&)
IF dget_i&<>1 OR display_disk_b!=TRUE
'
record_clear_buffer(kk_win&)
'
@loc_set_number(loc_add_ptr%,0)
@loc_set_name(loc_add_ptr%,CHR$(ADD(65,dget_i&))+":")
IF dget_i&<2
@loc_set_type(loc_add_ptr%,11)
ELSE
@loc_set_type(loc_add_ptr%,12)
ENDIF
@loc_set_length(loc_add_ptr%,0)
'
record_insert(kk_win&,head_nbline&(kk_win&))
'
ENDIF
ENDIF
NEXT dget_i&
'
IF kk_win&=3 AND win!(3)=TRUE
CHAR{local_info0%}=""
~WIND_SET(hand_win&(3),3,CARD(SWAP(local_info0%)),CARD(local_info0%),0,0)
ELSE IF kk_win&=4 AND win!(4)=TRUE
CHAR{local_info1%}=""
~WIND_SET(hand_win&(4),3,CARD(SWAP(local_info1%)),CARD(local_info1%),0,0)
ENDIF
'
@mouse_free
'
IF win!(kk_win&)
set_vslide(kk_win&)
record_start&(kk_win&)=@get_vslide(kk_win&)
force_update(kk_win&)
ENDIF
'
RETURN
> PROCEDURE local_create_folders(kk_folders$,sep_pos&)
LOCAL loc_folder$
'
loc_folder$=LEFT$(kk_folders$,PRED(LEN(kk_folders$)))+c0$
'
IF LEN(kk_folders$)>0
IF @d_exist(loc_folder$)=FALSE
DO
IF MID$(kk_folders$,sep_pos&,1)="\"
loc_folder$=LEFT$(kk_folders$,PRED(sep_pos&))+c0$
IF @d_exist(loc_folder$)=FALSE
loc_folder$=loc_folder$
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(57,L:V:loc_folder$)
ENDIF
ENDIF
INC sep_pos&
LOOP UNTIL sep_pos&>LEN(kk_folders$)
ENDIF
ENDIF
'
RETURN
> FUNCTION local_sort_by_name(kk_win&,loc_name_ptr%)
$F%
LOCAL loc_ins_j&,loc_ins_exit!
'
IF head_nbline&(kk_win&)=0
RETURN 0
ENDIF
'
loc_ins_j&=0
loc_ins_exit!=FALSE
'
IF display_sort_reverse!
WHILE loc_ins_j&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @compare_name(loc_name_ptr%,ADD(@px(kk_win&,loc_ins_j&),8))=2
INC loc_ins_j&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ELSE
WHILE loc_ins_j&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @compare_name(loc_name_ptr%,ADD(@px(kk_win&,loc_ins_j&),8))=1
INC loc_ins_j&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ENDIF
'
RETURN MAX(0,MIN(loc_ins_j&,head_nbline&(kk_win&)))
ENDFUNC
> FUNCTION local_sort_by_length(kk_win&,loc_len%)
$F%
LOCAL loc_ins_i&,loc_ins_exit!
'
IF head_nbline&(kk_win&)=0
RETURN 0
ENDIF
'
loc_ins_i&=0
loc_ins_exit!=FALSE
'
IF display_sort_reverse!
WHILE loc_ins_i&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @loc_get_length(@px(kk_win&,loc_ins_i&))<loc_len%
INC loc_ins_i&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ELSE
WHILE loc_ins_i&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @loc_get_length(@px(kk_win&,loc_ins_i&))>loc_len%
INC loc_ins_i&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ENDIF
'
RETURN MAX(0,MIN(loc_ins_i&,head_nbline&(kk_win&)))
ENDFUNC
> FUNCTION local_sort_by_date(kk_win&,loc_dat%)
$F%
LOCAL loc_ins_i&,loc_ins_exit!
'
IF head_nbline&(kk_win&)=0
RETURN 0
ENDIF
'
loc_ins_i&=0
loc_ins_exit!=FALSE
'
IF display_sort_reverse!
WHILE loc_ins_i&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @loc_get_date(@px(kk_win&,loc_ins_i&))<loc_dat%
INC loc_ins_i&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ELSE
WHILE loc_ins_i&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @loc_get_date(@px(kk_win&,loc_ins_i&))>loc_dat%
INC loc_ins_i&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ENDIF
'
RETURN MAX(0,MIN(loc_ins_i&,head_nbline&(kk_win&)))
ENDFUNC
> FUNCTION local_sort_by_type(kk_win&,loc_type_ptr%)
$F%
LOCAL loc_ins_j&,loc_ins_exit!
'
IF head_nbline&(kk_win&)=0
RETURN 0
ENDIF
'
loc_ins_j&=0
loc_ins_exit!=FALSE
'
IF display_sort_reverse!
WHILE loc_ins_j&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @compare_name(loc_type_ptr%,ADD(@px(kk_win&,loc_ins_j&),4))=2
INC loc_ins_j&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ELSE
WHILE loc_ins_j&<head_nbline&(kk_win&) AND loc_ins_exit!=FALSE
IF @compare_name(loc_type_ptr%,ADD(@px(kk_win&,loc_ins_j&),4))=1
INC loc_ins_j&
ELSE
loc_ins_exit!=TRUE
ENDIF
WEND
ENDIF
'
RETURN MAX(0,MIN(loc_ins_j&,head_nbline&(kk_win&)))
ENDFUNC
> FUNCTION compare_name(str1_ptr%,str2_ptr%)
$F%
gscn_exit!=FALSE
gscn_ret&=0
gscn_i1%=str1_ptr%
gscn_i2%=str2_ptr%
'
IF display_sort_distinguish_case!
DO
gscn_b1|=major|(BYTE{gscn_i1%})
gscn_b2|=major|(BYTE{gscn_i2%})
IF gscn_b1|=0 OR gscn_b2|=0
gscn_exit!=TRUE
ELSE IF gscn_b1|>gscn_b2|
gscn_exit!=TRUE
gscn_ret&=1
ELSE IF gscn_b1|<gscn_b2|
gscn_exit!=TRUE
gscn_ret&=2
ELSE
INC gscn_i1%
INC gscn_i2%
ENDIF
LOOP UNTIL gscn_exit!
ELSE
DO
gscn_b1|=BYTE{gscn_i1%}
gscn_b2|=BYTE{gscn_i2%}
IF gscn_b1|=0 OR gscn_b2|=0
gscn_exit!=TRUE
ELSE IF gscn_b1|>gscn_b2|
gscn_exit!=TRUE
gscn_ret&=1
ELSE IF gscn_b1|<gscn_b2|
gscn_exit!=TRUE
gscn_ret&=2
ELSE
INC gscn_i1%
INC gscn_i2%
ENDIF
LOOP UNTIL gscn_exit!
ENDIF
'
RETURN gscn_ret&
ENDFUNC
> PROCEDURE local_copy_files(kk_win&,move_action!)
LOCAL fil_cursor&,fil_cursor_ptr%,fil_folders!,cmd_i&,cmd_del_ptr%,fil_rename!,all_disk!
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
copy_root&=0
copy_nb_files&=0
copy_nb_folders&=0
copy_current_length%=0
copy_total_length%=0
copy_from_msa_win&=0
copy_to_zip_win&=0
'
IF kk_win&=3
IF display_files_mode&(3)=1 AND display_files_mode&(4)=0
copy_from_msa_win&=3
ENDIF
IF display_files_mode&(4)=2 AND display_files_mode&(3)<1
copy_to_zip_win&=4
ENDIF
IF display_files_mode&(3)=-1
copy_root&=3
ELSE
copy_root&=SUCC(LEN(CHAR{local_path0%}))
ENDIF
receive_folder$=CHAR{local_path1%}
ELSE IF kk_win&=4
IF display_files_mode&(4)=1 AND display_files_mode&(3)=0
copy_from_msa_win&=4
ENDIF
IF display_files_mode&(3)=2 AND display_files_mode&(4)<1
copy_to_zip_win&=3
ENDIF
IF display_files_mode&(4)=-1
copy_root&=3
ELSE
copy_root&=SUCC(LEN(CHAR{local_path1%}))
ENDIF
receive_folder$=CHAR{local_path0%}
ENDIF
'
IF copy_from_msa_win&>0 OR copy_to_zip_win&>0
move_action!=FALSE
ENDIF
'
fil_folders!=FALSE
fil_rename!=FALSE
'
IF display_files_mode&(kk_win&)=-1
FOR ipo&=0 TO PRED(head_nbline&(kk_win&))
IF @is_record_selected(kk_win&,ipo&)
record_clear_buffer(1)
SELECT @loc_get_type(@px(kk_win&,ipo&))
CASE 11,12
fil_set_type(record_buffer%(1),2)
fil_set_listed(record_buffer%(1),FALSE)
fil_folders!=TRUE
all_disk!=TRUE
ENDSELECT
fil_set_name(1,record_buffer%(1),@loc_get_name$(@px(kk_win&,ipo&)))
record_insert(1,head_nbline&(1))
ENDIF
NEXT ipo&
fil_rename!=FALSE
ELSE
FOR ipo&=0 TO PRED(head_nbline&(kk_win&))
IF @is_record_selected(kk_win&,ipo&)
record_clear_buffer(1)
SELECT @loc_get_type(@px(kk_win&,ipo&))
CASE 2
fil_set_type(record_buffer%(1),2)
fil_set_listed(record_buffer%(1),FALSE)
fil_set_date(record_buffer%(1),@loc_get_date(@px(kk_win&,ipo&)))
fil_folders!=TRUE
CASE 1
fil_set_type(record_buffer%(1),1)
fil_set_listed(record_buffer%(1),TRUE)
fil_set_length(record_buffer%(1),@loc_get_length(@px(kk_win&,ipo&)))
fil_set_date(record_buffer%(1),@loc_get_date(@px(kk_win&,ipo&)))
fil_set_cluster_start(record_buffer%(1),@loc_get_cluster_start(@px(kk_win&,ipo&)))
ENDSELECT
IF kk_win&=3
fil_set_name(1,record_buffer%(1),CHAR{local_path0%}+@loc_get_name$(@px(kk_win&,ipo&)))
ELSE IF kk_win&=4
fil_set_name(1,record_buffer%(1),CHAR{local_path1%}+@loc_get_name$(@px(kk_win&,ipo&)))
ENDIF
record_insert(1,head_nbline&(1))
ENDIF
NEXT ipo&
'
IF display_files_mode&(3)=0 AND display_files_mode&(4)=0
IF BYTE{local_path0%}=BYTE{local_path1%} AND move_action!=TRUE AND GEMDOS(48)>=&H1500
fil_rename!=TRUE
ENDIF
ENDIF
ENDIF
'
IF fil_folders!
'
@mouse_busy
fil_cursor&=0
WHILE fil_cursor&<head_nbline&(1)
fil_cursor_ptr%=@px(1,fil_cursor&)
IF @fil_get_type(fil_cursor_ptr%)=2 AND @fil_is_listed(fil_cursor_ptr%)=FALSE
IF copy_from_msa_win&>0
@msa_add_files_in_directories(copy_from_msa_win&,LONG{ADD(fil_cursor_ptr%,6)})
ELSE
@local_add_files_in_directories(LONG{ADD(fil_cursor_ptr%,6)})
ENDIF
@fil_set_listed(@px(1,fil_cursor&),TRUE)
ENDIF
INC fil_cursor&
WEND
@mouse_free
'
ENDIF
'
IF all_disk!
IF head_nbline&(1)>0
@record_delete(1,0)
ENDIF
ENDIF
'
IF head_nbline&(1)>0
IF fil_rename!
'
IF misc_confirm_delete!
proceed!=(@alert(1,19)=1)
ELSE
proceed!=TRUE
ENDIF
'
IF proceed!
@mouse_busy
@rename_files
'
FOR cmd_i&=PRED(head_nbline&(1)) TO 0 STEP -1
cmd_del_ptr%=@px(1,cmd_i&)
SELECT @fil_get_type(cmd_del_ptr%)
CASE 2
~GEMDOS(58,L:LONG{ADD(cmd_del_ptr%,6)})
ENDSELECT
NEXT cmd_i&
@mouse_free
ENDIF
'
ELSE
'
FOR fil_cursor&=PRED(head_nbline&(1)) TO 0 STEP -1
fil_cursor_ptr%=@px(1,fil_cursor&)
SELECT @fil_get_type(fil_cursor_ptr%)
CASE 1
INC copy_nb_files&
ADD copy_total_length%,@fil_get_length(fil_cursor_ptr%)
CASE 2
INC copy_nb_folders&
ENDSELECT
NEXT fil_cursor&
'
obj_unhide(5,14)
obj_enable(5,14)
obj_deselect(5,14)
obj_unhide(5,15)
obj_enable(5,15)
obj_deselect(5,15)
IF move_action!
obj_deselect(5,1)
obj_select(5,2)
ELSE
obj_select(5,1)
obj_deselect(5,2)
ENDIF
CHAR{{OB_SPEC(adtree%(5),4)}}=LEFT$(STR$(copy_nb_files&)+" / "+STR$(copy_total_length%)+str_octet$,31)
CHAR{{OB_SPEC(adtree%(5),6)}}=LEFT$(STR$(copy_nb_folders&),31)
CHAR{{OB_SPEC(adtree%(5),8)}}=""
CHAR{{OB_SPEC(adtree%(5),10)}}=""
OB_W(adtree%(5),12)=2
OB_W(adtree%(5),13)=2
'
IF copy_from_msa_win&>0
CHAR{OB_SPEC(adtree%(5),1)}=CHAR{OB_SPEC(adtree%(alert_tree&),43)}
@obj_hide(5,2)
ELSE IF copy_to_zip_win&>0
CHAR{OB_SPEC(adtree%(5),1)}=CHAR{OB_SPEC(adtree%(alert_tree&),42)}
@obj_hide(5,2)
ELSE
CHAR{OB_SPEC(adtree%(5),1)}=CHAR{OB_SPEC(adtree%(alert_tree&),40)}
CHAR{OB_SPEC(adtree%(5),2)}=CHAR{OB_SPEC(adtree%(alert_tree&),41)}
@obj_unhide(5,2)
ENDIF
'
proceed!=FALSE
IF misc_confirm_send!
control_form(5)
form_result&=0
DO
form_result&=FORM_DO(adtree%(5),0)
LOOP UNTIL form_result&=14 OR form_result&=15
IF form_result&=14
move_action!=@tst_selected(5,2)
obj_disable(5,14)
obj_deselect(5,14)
black_white(5,14,-1)
proceed!=TRUE
ELSE IF result&=15
obj_deselect(5,15)
proceed!=FALSE
ENDIF
ELSE
obj_hide(5,14)
obj_hide(5,15)
control_form(5)
proceed!=TRUE
ENDIF
'
IF proceed!=TRUE
@mouse_busy
'
IF copy_to_zip_win&>0
IF @zipfile_add_files(copy_to_zip_win&,TRUE)
zip_filename$(copy_to_zip_win&)=SPACE$(3)
ENDIF
ELSE
@copy_files(move_action!,copy_from_msa_win&)
ENDIF
'
IF move_action!
FOR cmd_i&=PRED(head_nbline&(1)) TO 0 STEP -1
cmd_del_ptr%=@px(1,cmd_i&)
SELECT @fil_get_type(cmd_del_ptr%)
CASE 2
~GEMDOS(58,L:LONG{ADD(cmd_del_ptr%,6)})
ENDSELECT
NEXT cmd_i&
ENDIF
@mouse_free
ENDIF
'
uncontrol_form(5)
ENDIF
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
IF proceed!
@display_refresh_directories
ENDIF
ENDIF
'
RETURN
> PROCEDURE local_add_files_in_directories(enquiry_path%)
LOCAL d_handle%,d_err%,d_test$,d_folder$,d_dta%
'
IF shell_buf%>0
'
IF FRE()<4000
~FRE(0)
ENDIF
'
d_folder$=CHAR{enquiry_path%}+"\"
d_folder$=LEFT$(d_folder$,PRED(LEN(d_folder$)))
fil_add_ptr%=record_buffer%(1)
'
IF (magic!=TRUE OR mint!=TRUE) AND xattr_buf%>0
'
d_handle%=GEMDOS(296,L:V:d_folder$,0)
IF d_handle%>0
'
WHILE GEMDOS(297,W:250,L:d_handle%,L:shell_buf%)=0
'
record_clear_buffer(1)
'
d_type&=-1
d_file$=CHAR{ADD(shell_buf%,4)}
'
IF d_file$<>"." AND d_file$<>".."
'
@fil_set_name(1,fil_add_ptr%,CHAR{enquiry_path%}+"\"+d_file$)
'
d_test$=d_folder$+"\"+d_file$+c0$
IF GEMDOS(300,W:0,L:V:d_test$,L:xattr_buf%)=0
SELECT (INT{xattr_buf%} AND &HF000)
CASE &H4000
d_type&=2
@fil_set_type(fil_add_ptr%,2)
@fil_set_listed(fil_add_ptr%,FALSE)
@fil_set_date(fil_add_ptr%,@gemdos_to_troll_timestamp(INT{ADD(xattr_buf%,30)},INT{ADD(xattr_buf%,28)}))
CASE &H8000
d_type&=1
@fil_set_type(fil_add_ptr%,1)
@fil_set_listed(fil_add_ptr%,TRUE)
@fil_set_length(fil_add_ptr%,LONG{ADD(xattr_buf%,16)})
@fil_set_date(fil_add_ptr%,@gemdos_to_troll_timestamp(INT{ADD(xattr_buf%,30)},INT{ADD(xattr_buf%,28)}))
ENDSELECT
ENDIF
ENDIF
'
IF d_type&>0
record_insert(1,head_nbline&(1))
ENDIF
'
WEND
'
~GEMDOS(299,L:d_handle%)
ENDIF
'
ELSE
'
d_folder$=CHAR{enquiry_path%}+"\*.*"+c0$
d_err%=GEMDOS(78,L:V:d_folder$,W:&X110111)
DO
EXIT IF d_err%<>0
d_dta%=FGETDTA()
'
record_clear_buffer(1)
'
d_type&=-1
d_file$=CHAR{ADD(d_dta%,30)}
'
IF d_file$<>"." AND d_file$<>".."
'
@fil_set_name(1,fil_add_ptr%,CHAR{enquiry_path%}+"\"+d_file$)
'
IF BTST(BYTE{ADD(d_dta%,21)},1)
ELSE IF BTST(BYTE{ADD(d_dta%,21)},4)
d_type&=2
@fil_set_type(fil_add_ptr%,2)
@fil_set_listed(fil_add_ptr%,FALSE)
@fil_set_date(fil_add_ptr%,@gemdos_to_troll_timestamp(INT{ADD(d_dta%,24)},INT{ADD(d_dta%,22)}))
ELSE
d_type&=1
@fil_set_type(fil_add_ptr%,1)
@fil_set_listed(fil_add_ptr%,TRUE)
@fil_set_length(fil_add_ptr%,LONG{ADD(d_dta%,26)})
@fil_set_date(fil_add_ptr%,@gemdos_to_troll_timestamp(INT{ADD(d_dta%,24)},INT{ADD(d_dta%,22)}))
ENDIF
ENDIF
'
IF d_type&>0
record_insert(1,head_nbline&(1))
ENDIF
'
d_err%=GEMDOS(79)
LOOP
'
ENDIF
'
ENDIF
RETURN
> PROCEDURE local_delete_files(kk_win&)
LOCAL cmd_i&,cmd_del_ptr%,fil_folders!,fil_cursor&
'
IF misc_confirm_delete!
proceed!=(@alert(1,13)=1)
ELSE
proceed!=TRUE
ENDIF
'
IF proceed!
'
@mouse_busy
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
fil_folders!=FALSE
FOR ipo&=0 TO PRED(head_nbline&(kk_win&))
IF @is_record_selected(kk_win&,ipo&)
record_clear_buffer(1)
IF @loc_get_type(@px(kk_win&,ipo&))=2
fil_set_type(record_buffer%(1),2)
fil_set_listed(record_buffer%(1),FALSE)
fil_folders!=TRUE
ELSE
fil_set_type(record_buffer%(1),1)
fil_set_listed(record_buffer%(1),TRUE)
ENDIF
IF kk_win&=3
fil_set_name(1,record_buffer%(1),CHAR{local_path0%}+@loc_get_name$(@px(kk_win&,ipo&)))
ELSE IF kk_win&=4
fil_set_name(1,record_buffer%(1),CHAR{local_path1%}+@loc_get_name$(@px(kk_win&,ipo&)))
ENDIF
record_insert(1,head_nbline&(1))
ENDIF
NEXT ipo&
'
IF fil_folders!
'
@mouse_busy
fil_cursor&=0
WHILE fil_cursor&<head_nbline&(1)
fil_cursor_ptr%=@px(1,fil_cursor&)
IF @fil_get_type(fil_cursor_ptr%)=2 AND @fil_is_listed(fil_cursor_ptr%)=FALSE
@local_add_files_in_directories(LONG{ADD(fil_cursor_ptr%,6)})
@fil_set_listed(@px(1,fil_cursor&),TRUE)
ENDIF
INC fil_cursor&
WEND
@mouse_free
'
ENDIF
'
IF head_nbline&(1)>0
FOR cmd_i&=PRED(head_nbline&(1)) TO 0 STEP -1
cmd_del_ptr%=@px(1,cmd_i&)
SELECT @fil_get_type(cmd_del_ptr%)
CASE 1
~GEMDOS(65,L:LONG{ADD(cmd_del_ptr%,6)})
CASE 2
~GEMDOS(58,L:LONG{ADD(cmd_del_ptr%,6)})
ENDSELECT
NEXT cmd_i&
ENDIF
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
@mouse_free
@display_refresh_directories
IF kk_win&=3 AND display_files_mode&(4)=2
@zipfile_get_directory(4)
ELSE IF kk_win&=4 AND display_files_mode&(3)=2
@zipfile_get_directory(3)
ENDIF
ENDIF
'
RETURN
'
> PROCEDURE rename_files
LOCAL rec_handle&,rec_i&,rec_ptr%
LOCAL receive_is_file!,receive_filename$,send_filename$
LOCAL rec_date%,rec_behavior&,rec_choice&,rec_remain&
LOCAL rec_drive&
'
receive_file_length%=0
receive_buffer_adr%=0
rec_drive&=SUB(ASC(LEFT$(receive_folder$)),64)
'
FOR rec_i&=0 TO PRED(head_nbline&(1))
'
rec_ptr%=@px(1,rec_i&)
receive_is_file!=FALSE
'
SELECT @fil_get_type(rec_ptr%)
CASE 1 ! file
IF @fil_get_length(rec_ptr%)>0
'
receive_is_file!=TRUE
'
ENDIF
CASE 2 ! folder, created emptied if not existing
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)
IF RIGHT$(receive_filename$)="\"
receive_filename$=LEFT$(receive_filename$,PRED(LEN(receive_filename$)))
ENDIF
receive_filename$=receive_filename$+c0$
IF @d_exist(receive_filename$)=FALSE
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(57,L:V:receive_filename$)
ENDIF
'
ENDSELECT
'
rec_behavior&=-1
receive_continue!=FALSE
'
IF receive_is_file!
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)+c0$
send_filename$=@fil_get_name$(rec_ptr%)+c0$
'
rec_handle&=GEMDOS(61,L:V:receive_filename$,W:0)
IF rec_handle&>0
~GEMDOS(62,W:rec_handle&)
'
IF misc_choice_existing_file_type&=0
'
CHAR{{OB_SPEC(adtree%(16),2)}}=LEFT$(@fil_get_name$(rec_ptr%),30)
rec_remain&=10
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
obj_deselect(16,7)
'
control_form(16)
form_result&=0
DO
evnt&=@ev_multi(&X100011,258,3,0,1000,mo_x&,mo_y&,mo_k&,dummy&,m_clavier&,mo_c&)
IF BTST(evnt&,0)
IF BYTE(m_clavier&)=13
black_white(16,7,1)
form_result&=8
ENDIF
ENDIF
IF BTST(evnt&,1)
IF mo_k&=1 AND mo_c&=1
delay
pop_result&=OBJC_FIND(adtree%(16),0,9,mo_x&,mo_y&)
IF pop_result&>2 AND pop_result&<8
IF @tst_enabled(16,pop_result&)
IF rec_choice&>0
black_white(16,ADD(2,rec_choice&),0)
ENDIF
rec_choice&=SUB(pop_result&,2)
black_white(16,ADD(2,rec_choice&),1)
ENDIF
ELSE IF pop_result&=8
black_white(16,7,1)
form_result&=8
ENDIF
ENDIF
ENDIF
IF BTST(evnt&,5)
DEC rec_remain&
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
black_white(16,9,-1)
ENDIF
LOOP UNTIL form_result&=7 OR rec_remain&=0
uncontrol_form(16)
obj_deselect(16,7)
'
rec_behavior&=rec_choice&
ELSE
rec_behavior&=misc_choice_existing_file_type&
ENDIF
'
SELECT rec_behavior&
CASE 1 ! rewrite
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(65,L:V:send_filename$)
CASE 2 ! rename
receive_filename$=@get_other_name$(receive_filename$)
CASE 3 ! skip
receive_is_file!=FALSE
CASE 4 ! abort
receive_aborted!=TRUE
ENDSELECT
'
ENDIF
'
IF receive_is_file!=TRUE AND receive_aborted!=FALSE
receive_continue!=TRUE
ENDIF
'
IF receive_continue!=TRUE
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(86,W:0,L:V:send_filename$,L:V:receive_filename$)
ENDIF
ENDIF
EXIT IF receive_aborted!
NEXT rec_i&
'
RETURN
> PROCEDURE copy_files(move_action!,from_msa_win&)
LOCAL rec_handle&,rec_i&,rec_ptr%
LOCAL receive_is_file!,receive_filename$,send_filename$
LOCAL rec_date%,rec_behavior&,rec_choice&,rec_remain&
LOCAL rec_drive&
'
receive_file_length%=0
receive_buffer_adr%=0
rec_drive&=SUB(ASC(LEFT$(receive_folder$)),64)
'
FOR rec_i&=0 TO PRED(head_nbline&(1))
'
rec_ptr%=@px(1,rec_i&)
receive_is_file!=FALSE
'
CHAR{{OB_SPEC(adtree%(5),4)}}=LEFT$(STR$(copy_nb_files&)+" / "+STR$(copy_total_length%)+str_octets$+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),6)}}=LEFT$(STR$(copy_nb_folders&)+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),8)}}=LEFT$(RIGHT$(@fil_get_name$(rec_ptr%),31)+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),10)}}=""
black_white(5,4,-1)
black_white(5,6,-1)
black_white(5,8,-1)
black_white(5,10,-1)
'
SELECT @fil_get_type(rec_ptr%)
CASE 1 ! file
IF @fil_get_length(rec_ptr%)>0
'
receive_is_file!=TRUE
'
ELSE IF @fil_get_length(rec_ptr%)=0 ! just create empty if not exist
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)+c0$
IF @s_exist(receive_filename$)=FALSE
rec_handle&=GEMDOS(60,L:V:receive_filename$,W:0)
IF rec_handle&>0
IF misc_keep_date!
rec_date%=@to_gemdos_timedate(@fil_get_date(rec_ptr%))
ELSE
rec_date%=SHL(GEMDOS(42),16) OR GEMDOS(44)
ENDIF
~GEMDOS(87,L:V:rec_date%,W:rec_handle&,W:1)
~GEMDOS(62,W:rec_handle&)
ENDIF
ENDIF
IF move_action!
~GEMDOS(65,L:LONG{ADD(rec_ptr%,6)})
ENDIF
DEC copy_nb_files&
'
ENDIF
CASE 2 ! folder, created emptied if not existing
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)
IF RIGHT$(receive_filename$)="\"
receive_filename$=LEFT$(receive_filename$,PRED(LEN(receive_filename$)))
ENDIF
receive_filename$=receive_filename$+c0$
IF @d_exist(receive_filename$)=FALSE
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(57,L:V:receive_filename$)
ENDIF
DEC copy_nb_folders&
'
ENDSELECT
'
rec_behavior&=-1
receive_continue!=FALSE
'
IF receive_is_file!
'
receive_filename$=receive_folder$+MID$(@fil_get_name$(rec_ptr%),copy_root&)+c0$
send_filename$=@fil_get_name$(rec_ptr%)+c0$
'
rec_handle&=GEMDOS(61,L:V:receive_filename$,W:0)
IF rec_handle&>0
~GEMDOS(62,W:rec_handle&)
'
IF misc_choice_existing_file_type&=0
'
CHAR{{OB_SPEC(adtree%(16),2)}}=LEFT$(@fil_get_name$(rec_ptr%),30)
rec_remain&=10
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
obj_deselect(16,7)
'
uncontrol_form(5)
control_form(16)
form_result&=0
DO
evnt&=@ev_multi(&X100011,258,3,0,1000,mo_x&,mo_y&,mo_k&,dummy&,m_clavier&,mo_c&)
IF BTST(evnt&,0)
IF BYTE(m_clavier&)=13
black_white(16,7,1)
form_result&=7
ENDIF
ENDIF
IF BTST(evnt&,1)
IF mo_k&=1 AND mo_c&=1
delay
pop_result&=OBJC_FIND(adtree%(16),0,9,mo_x&,mo_y&)
IF pop_result&>2 AND pop_result&<7
IF @tst_enabled(16,pop_result&)
IF rec_choice&>0
black_white(16,ADD(2,rec_choice&),0)
ENDIF
rec_choice&=SUB(pop_result&,2)
black_white(16,ADD(2,rec_choice&),1)
ENDIF
ELSE IF pop_result&=7
black_white(16,7,1)
form_result&=7
ENDIF
ENDIF
ENDIF
IF BTST(evnt&,5)
DEC rec_remain&
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
black_white(16,9,-1)
ENDIF
LOOP UNTIL form_result&=7 OR rec_remain&=0
uncontrol_form(16)
control_form(5)
obj_deselect(16,7)
'
rec_behavior&=rec_choice&
ELSE
rec_behavior&=misc_choice_existing_file_type&
ENDIF
'
SELECT rec_behavior&
CASE 1 ! rewrite
CASE 2 ! rename
receive_filename$=@get_other_name$(receive_filename$)
CASE 3 ! skip
receive_is_file!=FALSE
CASE 4 ! abort
receive_aborted!=TRUE
ENDSELECT
'
ENDIF
'
IF receive_is_file!=TRUE AND receive_aborted!=FALSE
receive_continue!=TRUE
ENDIF
'
IF receive_continue!=TRUE
'
receive_file_length%=@fil_get_length(rec_ptr%)
'
IF receive_buffer_adr%>0
@mxfree(receive_buffer_adr%)
receive_buffer_adr%=0
ENDIF
receive_buffer_adr%=@copy_mxalloc(receive_file_length%,from_msa_win&)
IF receive_buffer_adr%>0 OR (receive_buffer_adr%=0 AND from_msa_win&>0)
'
IF @s_exist(receive_filename$)
~GEMDOS(65,L:V:receive_filename$)
ENDIF
rec_handle&=0
rec_handle&=GEMDOS(60,L:V:receive_filename$,W:0)
IF rec_handle&>0
~GEMDOS(62,W:rec_handle&)
ENDIF
'
IF @has_enough_space(rec_drive&,receive_file_length%)=TRUE
'
IF from_msa_win&>0
CHAR{{OB_SPEC(adtree%(5),10)}}="EXTRACTING..."
ELSE IF move_action!
CHAR{{OB_SPEC(adtree%(5),10)}}="MOVING..."
ELSE
CHAR{{OB_SPEC(adtree%(5),10)}}="COPYING..."
ENDIF
black_white(5,10,-1)
'
@copy(receive_buffer_adr%,receive_file_length%,send_filename$,receive_filename$,from_msa_win&,@fil_get_cluster_start(rec_ptr%))
'
IF FRE()<4000
~FRE(0)
ENDIF
'
rec_handle&=0
rec_handle&=GEMDOS(61,L:V:receive_filename$,W:0)
IF rec_handle&>0
receive_file_length%=GEMDOS(66,L:0,W:rec_handle&,W:2) !=receive_file_length%
IF misc_keep_date!
rec_date%=@to_gemdos_timedate(@fil_get_date(rec_ptr%))
ELSE
rec_date%=SHL(GEMDOS(42),16) OR GEMDOS(44)
ENDIF
~GEMDOS(87,L:V:rec_date%,W:rec_handle&,W:1)
~GEMDOS(62,W:rec_handle&)
ENDIF
IF move_action!
~GEMDOS(65,L:LONG{ADD(rec_ptr%,6)})
ENDIF
DEC copy_nb_files&
'
ADD copy_current_length%,receive_file_length%
'
OB_W(adtree%(5),12)=2
OB_W(adtree%(5),13)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,copy_current_length%/MAX(1,copy_total_length%))))
black_white(5,11,-1)
ENDIF
ENDIF
ENDIF
ENDIF
EXIT IF receive_aborted!
NEXT rec_i&
'
IF receive_buffer_adr%>0
@mxfree(receive_buffer_adr%)
receive_buffer_adr%=0
ENDIF
'
RETURN
> FUNCTION get_other_name$(str$)
LOCAL ron_name$,ron_folder$,ron_ext$,ron_pos&,ron_final$
'
ron_pos&=RINSTR(str$,"\")
IF ron_pos&>0
ron_folder$=LEFT$(str$,ron_pos&)
str$=MID$(str$,SUCC(ron_pos&))
ENDIF
'
ron_pos&=RINSTR(str$,".")
IF ron_pos&
ron_name$=LEFT$(str$,PRED(ron_pos&))
ron_ext$=MID$(str$,SUCC(ron_pos&))
ENDIF
'
IF LEN(ron_name$)>8
ron_pos&=2
ron_final$=ron_folder$+ron_name$+"("+STR$(ron_pos&)+")."+ron_ext$
'
WHILE @s_exist(ron_final$)=TRUE
INC ron_pos&
IF FRE()<4000
~FRE(0)
ENDIF
ron_final$=ron_folder$+ron_name$+"("+STR$(ron_pos&)+")."+ron_ext$
WEND
ELSE
ron_pos&=2
ron_final$=ron_folder$+LEFT$(ron_name$,6)+"~"+STR$(ron_pos&)+"."+ron_ext$
'
WHILE @s_exist(ron_final$)=TRUE
INC ron_pos&
IF FRE()<4000
~FRE(0)
ENDIF
ron_final$=ron_folder$+LEFT$(ron_name$,SUB(7,LEN(STR$(ron_pos&))))+"~"+STR$(ron_pos&)+"."+ron_ext$
WEND
ENDIF
'
RETURN ron_final$
ENDFUNC
> FUNCTION has_enough_space(used_drive&,needed_space%)
'
IF misc_verify_free_space!
@clear_s
IF GEMDOS(54,L:s_adr%,W:used_drive&)=0
IF LONG{ADD(s_adr%,12)}>0                   ! Raj (20121028)
DIV needed_space%,LONG{ADD(s_adr%,12)}    ! new method (from DGEM)
ENDIF                                       ! for large spaces
IF LONG{ADD(s_adr%,8)}>0                    ! (such as Aranym HOSTFS)
DIV needed_space%,LONG{ADD(s_adr%,8)}     !
ENDIF                                       !
IF LONG{s_adr%}<needed_space%               !
RETURN FALSE                              !
ENDIF
ENDIF
ENDIF
'
RETURN TRUE
ENDFUNC
> PROCEDURE copy(data_copy_adr%,data_copy_len%,source_filename$,target_filename$,data_from_msa_win&,data_cluster_start&)
LOCAL data_copy_offset%,data_copy_min%,msa_extract_handle&
'
OB_W(adtree%(5),12)=2
black_white(5,12,-1)
'
IF data_from_msa_win&>0
'
CHAR{{OB_SPEC(adtree%(5),10)}}="EXTRACTING..."
black_white(5,10,-1)
msa_image_ptr%=0
'
IF data_from_msa_win&=3
msa_image_ptr%=msa_image_adr%(3)
ELSE IF data_from_msa_win&=4
msa_image_ptr%=msa_image_adr%(4)
ENDIF
'
IF msa_image_ptr%>0
'
bpb_recsiz&=SHL&(BYTE{ADD(msa_image_ptr%,12)},8) OR BYTE{ADD(msa_image_ptr%,11)}
bpb_clsiz&=BYTE{ADD(msa_image_ptr%,13)}
fmt_reserved&=MAX(1,SHL&(BYTE{ADD(msa_image_ptr%,15)},8) OR BYTE{ADD(msa_image_ptr%,14)})
fmt_nfats&=BYTE{ADD(msa_image_ptr%,16)}
fmt_ndirs&=SHL&(BYTE{ADD(msa_image_ptr%,18)},8) OR BYTE{ADD(msa_image_ptr%,17)}
fmt_nsecs&=SHL&(BYTE{ADD(msa_image_ptr%,20)},8) OR BYTE{ADD(msa_image_ptr%,19)}
fmt_spf&=SHL&(BYTE{ADD(msa_image_ptr%,23)},8) OR BYTE{ADD(msa_image_ptr%,22)}
fmt_spt&=SHL&(BYTE{ADD(msa_image_ptr%,25)},8) OR BYTE{ADD(msa_image_ptr%,24)}
fmt_numside&=BYTE{ADD(msa_image_ptr%,26)}
'
fmt_track&=DIV(DIV(fmt_nsecs&,fmt_numside&),fmt_spt&)
bpb_rdlen&=DIV(fmt_ndirs&,16)
bpb_datrec&=fmt_reserved&+MUL(fmt_nfats&,fmt_spf&)+bpb_rdlen&
'
bpb_cluster_length%=MUL(bpb_recsiz&,bpb_clsiz&)
'
msa_fat_adr%=ADD(msa_image_ptr%,MUL(fmt_reserved&,bpb_recsiz&))
'
IF FRE()<4000
~FRE(0)
ENDIF
'
msa_extract_handle&=GEMDOS(61,L:V:target_filename$,W:1)
IF msa_extract_handle&>0
~GEMDOS(66,L:0,W:msa_extract_handle&,W:2)
'
LET data_copy_offset%=0
WHILE data_copy_offset%<data_copy_len% AND data_cluster_start&>&H1 AND data_cluster_start&<&HFF0
'
LET data_sector_start&=ADD(bpb_datrec&,MUL(SUB(data_cluster_start&,2),bpb_clsiz&))
LET data_copy_adr%=ADD(msa_image_ptr%,MUL(data_sector_start&,bpb_recsiz&))
LET data_copy_min%=MIN(SUB(data_copy_len%,data_copy_offset%),bpb_cluster_length%)
'
IF data_copy_min%>0
~GEMDOS(64,W:msa_extract_handle&,L:data_copy_min%,L:data_copy_adr%)
ENDIF
'
ADD data_copy_offset%,data_copy_min%
'
OB_W(adtree%(5),12)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,data_copy_offset%/MAX(1,data_copy_len%))))
black_white(5,12,-1)
'
IF data_copy_offset%<data_copy_len%
LET data_cluster_start&=@fmt_get_fat(msa_fat_adr%,data_cluster_start&)
ELSE
LET data_cluster_start&=&HFF8
ENDIF
WEND
~GEMDOS(62,W:msa_extract_handle&)
ENDIF
ENDIF
'
ELSE
IF copy_buffer_mode&=1
LET data_copy_offset%=0
WHILE data_copy_offset%<data_copy_len%
LET data_copy_min%=MIN(copy_buffer_length%,SUB(data_copy_len%,copy_buffer_length%))
copy_load_file_into_buffer(data_copy_adr%,source_filename$,data_copy_offset%,data_copy_min%)
copy_save_file_from_buffer(data_copy_adr%,target_filename$,data_copy_min%)
ADD data_copy_offset%,copy_buffer_length%
'
OB_W(adtree%(5),12)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,data_copy_offset%/MAX(1,data_copy_len%))))
black_white(5,12,-1)
WEND
ELSE
copy_load_file_into_buffer(data_copy_adr%,source_filename$,0,data_copy_len%)
copy_save_file_from_buffer(data_copy_adr%,target_filename$,data_copy_len%)
ENDIF
ENDIF
'
OB_W(adtree%(5),12)=PRED(OB_W(adtree%(5),11))
black_white(5,12,-1)
'
RETURN
> FUNCTION copy_mxalloc(copy_buffer_asked_length%,data_from_msa_win&)
$F%
'
IF data_from_msa_win&>0
copy_buffer_value%=0
copy_buffer_mode&=0
copy_buffer_adr%=0
RETURN copy_buffer_adr%
ENDIF
'
copy_buffer_value%=384000
copy_buffer_length%=0
copy_buffer_mode&=0
copy_buffer_adr%=@mxalloc(SHL(ADD(SHR(copy_buffer_asked_length%,4),4),4),3)
'
IF copy_buffer_adr%>0
copy_buffer_length%=copy_buffer_asked_length%
ELSE
copy_buffer_mode&=1
copy_buffer_adr%=@mxalloc(ADD(copy_buffer_value%,4),3)
WHILE copy_buffer_adr%<1 AND copy_buffer_value%>0
INC copy_buffer_mode&
SUB copy_buffer_value%,19200
copy_buffer_adr%=@mxalloc(ADD(copy_buffer_value%,4),3)
WEND
IF copy_buffer_value%<38400
IF copy_buffer_adr%>0
@mxfree(copy_buffer_adr%)
ENDIF
copy_buffer_adr%=-1
ELSE
copy_buffer_length%=copy_buffer_value%
ENDIF
ENDIF
'
RETURN copy_buffer_adr%
ENDFUNC
> PROCEDURE copy_load_file_into_buffer(slb_adr%,slb_filename$,slb_offset%,slb_length%)
LOCAL slb_handle&,slb_actually_read%
'
IF FRE()<4000
~FRE(0)
ENDIF
'
slb_handle&=GEMDOS(61,L:V:slb_filename$,W:0)
IF slb_handle&>0
~GEMDOS(66,L:slb_offset%,W:slb_handle&,W:0)
slb_actually_read%=GEMDOS(63,W:slb_handle&,L:slb_length%,L:slb_adr%)
~GEMDOS(62,W:slb_handle&)
ENDIF
'
RETURN
> PROCEDURE copy_save_file_from_buffer(rlb_adr%,rlb_filename$,rlb_length%)
LOCAL rlb_handle&
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF rlb_length%>0
IF LEN(rlb_filename$)>1
rlb_handle&=GEMDOS(61,L:V:rlb_filename$,W:1)
IF rlb_handle&>0
~GEMDOS(66,L:0,W:rlb_handle&,W:2)
~GEMDOS(64,W:rlb_handle&,L:rlb_length%,L:rlb_adr%)
~GEMDOS(62,W:rlb_handle&)
ENDIF
ENDIF
ENDIF
'
RETURN
'
> FUNCTION loc_search_name(name$)
$F%
'
IF name$=""
RETURN -1
ENDIF
'
LET name$=UPPER$(name$)
dummy&=PRED(head_nbline&(4))
IF dummy&>-1
FOR ipo&=0 TO dummy&
IF UPPER$(@loc_get_name$(@px(4,ipo&)))=name$
RETURN ipo&
ENDIF
NEXT ipo&
ENDIF
RETURN -1
ENDFUNC
> PROCEDURE loc_set_number(loc_ptr%,number&)
INT{loc_ptr%}=number&
RETURN
> FUNCTION loc_get_number(loc_ptr%)
$F%
RETURN INT{loc_ptr%}
ENDFUNC
> PROCEDURE loc_set_type(loc_ptr%,value|)
BYTE{ADD(loc_ptr%,2)}=value|
RETURN
> FUNCTION loc_get_type(loc_ptr%)
$F%
IF loc_ptr%>0
RETURN BYTE{ADD(loc_ptr%,2)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE loc_set_name(loc_ptr%,name$)
'
CHAR{ADD(loc_ptr%,8)}=TRIM$(LEFT$(name$,124))
'
dot_pos&=RINSTR(name$,".")
IF dot_pos&>0
LET name$=MID$(name$,SUCC(dot_pos&))
ELSE
LET name$=""
ENDIF
LONG{ADD(loc_ptr%,4)}=CVL(UPPER$(LEFT$(name$+"    ",4)))
RETURN
> FUNCTION loc_get_name$(loc_ptr%)
IF loc_ptr%>0
RETURN CHAR{ADD(loc_ptr%,8)}
ENDIF
RETURN ""
ENDFUNC
> FUNCTION loc_get_extension$(loc_ptr%)
IF loc_ptr%>0
RETURN TRIM$(MKL$(LONG{ADD(loc_ptr%,4)}))
ELSE
RETURN ""
ENDIF
ENDFUNC
> PROCEDURE loc_set_length(loc_ptr%,value%)
LONG{ADD(loc_ptr%,134)}=value%
RETURN
> FUNCTION loc_get_length(loc_ptr%)
$F%
IF loc_ptr%>0
RETURN LONG{ADD(loc_ptr%,134)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE loc_set_date(loc_ptr%,value%)
LONG{ADD(loc_ptr%,138)}=value%
RETURN
> FUNCTION loc_get_date(loc_ptr%)
$F%
IF loc_ptr%>0
RETURN LONG{ADD(loc_ptr%,138)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE loc_set_cluster_start(loc_ptr%,value&)
INT{ADD(loc_ptr%,142)}=value&
RETURN
> FUNCTION loc_get_cluster_start(loc_ptr%)
$F%
IF loc_ptr%>0
RETURN INT{ADD(loc_ptr%,142)}
ELSE
RETURN 0
ENDIF
ENDFUNC
'
> PROCEDURE fil_set_type(fil_ptr%,value|)
BYTE{fil_ptr%}=value|
RETURN
> FUNCTION fil_get_type(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN BYTE{fil_ptr%}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_listed(fil_ptr%,value!)
IF value!
BYTE{SUCC(fil_ptr%)}=1
ELSE
BYTE{SUCC(fil_ptr%)}=0
ENDIF
RETURN
> FUNCTION fil_is_listed(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN BYTE{SUCC(fil_ptr%)}>0
ELSE
RETURN FALSE
ENDIF
ENDFUNC
> PROCEDURE fil_set_length(fil_ptr%,value%)
LONG{ADD(fil_ptr%,2)}=value%
RETURN
> FUNCTION fil_get_length(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN LONG{ADD(fil_ptr%,2)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_name(fil_win&,fil_ptr%,name$)
fil_str_ptr%=@gxalloc(ADD(6,fil_win&),SUCC(LEN(name$)))
IF fil_str_ptr%>0
LONG{ADD(fil_ptr%,6)}=fil_str_ptr%
CHAR{fil_str_ptr%}=name$
ENDIF
RETURN
> FUNCTION fil_get_name$(fil_ptr%)
IF fil_ptr%>0
fil_str_ptr%=LONG{ADD(fil_ptr%,6)}
IF fil_str_ptr%>0
RETURN CHAR{fil_str_ptr%}
ENDIF
ENDIF
RETURN ""
ENDFUNC
> PROCEDURE fil_set_date(fil_ptr%,value%)
LONG{ADD(fil_ptr%,10)}=value%
RETURN
> FUNCTION fil_get_date(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN LONG{ADD(fil_ptr%,10)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_cluster_start(fil_ptr%,value&)
INT{ADD(fil_ptr%,14)}=value&
RETURN
> FUNCTION fil_get_cluster_start(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN INT{ADD(fil_ptr%,14)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_offset(fil_ptr%,value%)
LONG{ADD(fil_ptr%,16)}=value%
RETURN
> FUNCTION fil_get_offset(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN LONG{ADD(fil_ptr%,16)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_compressed_size(fil_ptr%,value%)
LONG{ADD(fil_ptr%,20)}=value%
RETURN
> FUNCTION fil_get_compressed_size(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN LONG{ADD(fil_ptr%,20)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_crc32(fil_ptr%,value%)
LONG{ADD(fil_ptr%,24)}=value%
RETURN
> FUNCTION fil_get_crc32(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN LONG{ADD(fil_ptr%,24)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fil_set_method(fil_ptr%,value&)
INT{ADD(fil_ptr%,28)}=value&
RETURN
> FUNCTION fil_get_method(fil_ptr%)
$F%
IF fil_ptr%>0
RETURN INT{ADD(fil_ptr%,28)}
ELSE
RETURN 0
ENDIF
ENDFUNC
'
> FUNCTION get_troll_date$(td_timestamp%,separator!)
LOCAL td_year$,td_month$,td_day$
'
IF td_timestamp%=0
RETURN ""
ENDIF
'
td_year$=STR$(SHR(td_timestamp%,20) AND &X11111111111111)
IF LEN(td_year$)<4
td_year$=STRING$(SUB(4,LEN(td_year$)),"0")+td_year$
ENDIF
td_month$=STR$(SHR(td_timestamp%,16) AND &X1111)
IF LEN(td_month$)<2
td_month$=STRING$(SUB(2,LEN(td_month$)),"0")+td_month$
ENDIF
td_day$=STR$(SHR(td_timestamp%,11) AND &X11111)
IF LEN(td_day$)<2
td_day$=STRING$(SUB(2,LEN(td_day$)),"0")+td_day$
ENDIF
'
IF separator!
SELECT display_files_date_format&
CASE 1
RETURN td_day$+"/"+td_month$+"/"+td_year$
CASE 2
RETURN td_day$+"/"+td_month$+"/"+RIGHT$(td_year$,2)
CASE 3
RETURN td_month$+"-"+td_day$+"-"+td_year$
CASE 4
RETURN td_month$+"-"+td_day$+"-"+RIGHT$(td_year$,2)
ENDSELECT
ELSE
SELECT display_files_date_format&
CASE 1
RETURN td_day$+td_month$+td_year$
CASE 2
RETURN td_day$+td_month$+RIGHT$(td_year$,2)
CASE 3
RETURN td_month$+td_day$+td_year$
CASE 4
RETURN td_month$+td_day$+RIGHT$(td_year$,2)
ENDSELECT
ENDIF
RETURN td_year$+"/"+td_month$+"/"+td_day$
ENDFUNC
> FUNCTION get_troll_time$(td_timestamp%,separator!)
LOCAL td_hour$,td_minute$
'
IF AND(td_timestamp%,&X11111111111)=0
RETURN ""
ENDIF
'
td_hour$=STR$(SHR(td_timestamp%,6) AND &X11111)
IF LEN(td_hour$)<4
td_hour$=STRING$(SUB(2,LEN(td_hour$)),"0")+td_hour$
ENDIF
td_minute$=STR$(td_timestamp% AND &X111111)
IF LEN(td_minute$)<2
td_minute$=STRING$(SUB(2,LEN(td_minute$)),"0")+td_minute$
ENDIF
'
IF separator!
RETURN td_hour$+":"+td_minute$
ENDIF
RETURN td_hour$+td_minute$
ENDFUNC
> FUNCTION gemdos_to_troll_timestamp(td_date%,td_time%)
$F%
LOCAL td_year&,td_month&,td_day&,td_hour&,td_minute&,td_timestamp%
'
td_year&=ADD(SHR&(td_date%,9) AND &X1111111,1980)
td_month&=SHR&(td_date%,5) AND &X1111
td_day&=td_date% AND &X11111
td_hour&=SHR&(td_time%,11) AND &X11111
td_minute&=SHR&(td_time%,5) AND &X111111
'
td_timestamp%=0
'
ADD td_timestamp%,SHL(MAX(1,MIN(td_day&,31)),11)
ADD td_timestamp%,SHL(MAX(1,MIN(td_month&,12)),16)
ADD td_timestamp%,SHL(td_year&,20)
ADD td_timestamp%,SHL(td_hour&,6)
ADD td_timestamp%,td_minute&
'
RETURN td_timestamp%
ENDFUNC
> FUNCTION to_gemdos_timedate(td_timestamp%)
$F%
LOCAL td_year&,td_month&,td_day&,td_hour&,td_minute&,td_second&,td_gemdos%
'
td_gemdos%=0
'
td_hour&=(SHR(td_timestamp%,6) AND &X11111)
td_minute&=(td_timestamp% AND &X111111)
td_second&=0
'
td_hour&=SHL&(MAX(0,MIN(td_hour&,23)),11)
td_minute&=SHL&(MAX(0,MIN(td_minute&,59)),5)
td_second&=MAX(0,MIN(SHR&(td_second&,1),29))
'
ADD td_gemdos%,SHL(ADD(td_hour&,ADD(td_minute&,td_second&)),16)
'
td_year&=(SHR(td_timestamp%,20) AND &X11111111111111)
td_month&=(SHR(td_timestamp%,16) AND &X1111)
td_day&=(SHR(td_timestamp%,11) AND &X11111)
'
td_year&=SHL&(MAX(0,MIN(SUB(td_year&,1980),119)),9)
td_month&=SHL&(MAX(1,MIN(td_month&,12)),5)
td_day&=MAX(1,MIN(td_day&,31))
'
ADD td_gemdos%,ADD(td_year&,ADD(td_month&,td_day&))
'
RETURN td_gemdos%
ENDFUNC
'
> PROCEDURE launch(launch_name$)
IF @s_exist(launch_name$)=TRUE
close_multi
num_drive&=SUB(ASC(launch_name$),65)
~GEMDOS(14,W:num_drive&)
CHDIR LEFT$(launch_name$,RINSTR(launch_name$,"\"))+c0$
IF multi!
'
SELECT LEFT$(RIGHT$(launch_name$,4),3)
CASE "ACC","ACX"
shl_write(3,1,100,dummy$,launch_name$)
CASE "TOS","TTP"
shl_write(1,0,100,dummy$,launch_name$)
DEFAULT
shl_write(1,1,100,dummy$,launch_name$)
ENDSELECT
FOR i&=0 TO 7
delay
NEXT i&
'
ELSE
~EXEC(0,launch_name$,c0$,c0$+c0$)
OUT 4,8
v_show_c
ENDIF
open_multi
IF multi!=FALSE
win(3)
win(4)
ENDIF
ENDIF
RETURN
'
> PROCEDURE dialog_format(fmt_d$)
LOCAL fmt_diskname$,fmt_single!,fmt_high!
'
obj_set_template(19,2,"________.___")
obj_set_valid(19,2,"fffffffffff")
CHAR{{OB_SPEC(adtree%(19),2)}}=""
obj_deselect(19,3)
obj_select(19,4)
obj_deselect(19,5)
OB_W(adtree%(19),7)=2
obj_hide(19,7)
obj_deselect(19,8)
obj_deselect(19,9)
'
control_form(19)
form_result&=0
DO
form_result&=FORM_DO(adtree%(19),0)
LOOP UNTIL form_result&=8 OR form_result&=9
'
IF form_result&=8
'
fmt_diskname$=CHAR{{OB_SPEC(adtree%(19),2)}}
IF LEN(fmt_diskname$)>8
fmt_diskname$=LEFT$(fmt_diskname$,8)+"."+MID$(fmt_diskname$,9,3)
ENDIF
fmt_single!=@tst_selected(19,3)
fmt_high!=@tst_selected(19,5)
'
@fmt_disk_format(fmt_d$,fmt_single!,fmt_high!,fmt_diskname$)
ENDIF
'
uncontrol_form(19)
RETURN
> PROCEDURE dialog_format_quick(fmt_d$)
IF @alert(1,24)=1
@fmt_disk_init(fmt_d$)
ENDIF
RETURN
> PROCEDURE fmt_init
'
DIM fmt_bad_sectors&(16)
'
RETURN
> PROCEDURE fmt_disk_format(fmt_disk$,fmt_single_sided!,fmt_high_density!,fmt_name$)
' LOCAL table_skew_ptr%,fmt_skew&,fmt_skew_i&,fmt_virgin&
LOCAL fmt_bad_sectors_nb&,fmt_bad_sector&,fmt_ret&,fmt_virgin&
'
obj_unhide(19,7)
OB_W(adtree%(19),7)=2
'
IF fmt_disk$="A"
fmt_devno&=0
ELSE IF fmt_disk$="B"
fmt_devno&=1
ELSE
fmt_devno&=-1
ENDIF
'
IF fmt_devno&>-1
'
fmt_buffer%=@mxalloc(16384,0)
' table_skew_ptr%=@mxalloc(72,0)
'
IF fmt_buffer%>0 ! AND table_skew_ptr%>0
'
mouse_busy
'
fmt_failure!=FALSE
fmt_bad_sectors_nb&=0
'
fmt_track&=80
fmt_spf&=5
fmt_spt&=9
fmt_numside&=2
fmt_disktype&=3
fmt_ndirs&=112
'
' INT{ADD(table_skew_ptr%,0)}=1
' INT{ADD(table_skew_ptr%,2)}=2
' INT{ADD(table_skew_ptr%,4)}=3
' INT{ADD(table_skew_ptr%,6)}=4
' INT{ADD(table_skew_ptr%,8)}=5
' INT{ADD(table_skew_ptr%,10)}=6
' INT{ADD(table_skew_ptr%,12)}=7
' INT{ADD(table_skew_ptr%,14)}=8
' INT{ADD(table_skew_ptr%,16)}=9
' INT{ADD(table_skew_ptr%,18)}=1
' INT{ADD(table_skew_ptr%,20)}=2
' INT{ADD(table_skew_ptr%,22)}=3
' INT{ADD(table_skew_ptr%,24)}=4
' INT{ADD(table_skew_ptr%,26)}=5
' INT{ADD(table_skew_ptr%,28)}=6
' INT{ADD(table_skew_ptr%,30)}=7
' INT{ADD(table_skew_ptr%,32)}=8
' INT{ADD(table_skew_ptr%,34)}=9
'
IF fmt_single_sided!
fmt_numside&=1
fmt_disktype&=2
ELSE IF fmt_high_density!
fmt_spt&=18
fmt_disktype&=4
'
' INT{ADD(table_skew_ptr%,0)}=1
' INT{ADD(table_skew_ptr%,2)}=2
' INT{ADD(table_skew_ptr%,4)}=3
' INT{ADD(table_skew_ptr%,6)}=4
' INT{ADD(table_skew_ptr%,8)}=5
' INT{ADD(table_skew_ptr%,10)}=6
' INT{ADD(table_skew_ptr%,12)}=7
' INT{ADD(table_skew_ptr%,14)}=8
' INT{ADD(table_skew_ptr%,16)}=9
' INT{ADD(table_skew_ptr%,18)}=10
' INT{ADD(table_skew_ptr%,20)}=11
' INT{ADD(table_skew_ptr%,22)}=12
' INT{ADD(table_skew_ptr%,24)}=13
' INT{ADD(table_skew_ptr%,26)}=14
' INT{ADD(table_skew_ptr%,28)}=15
' INT{ADD(table_skew_ptr%,30)}=16
' INT{ADD(table_skew_ptr%,32)}=17
' INT{ADD(table_skew_ptr%,34)}=18
' INT{ADD(table_skew_ptr%,36)}=1
' INT{ADD(table_skew_ptr%,38)}=2
' INT{ADD(table_skew_ptr%,40)}=3
' INT{ADD(table_skew_ptr%,42)}=4
' INT{ADD(table_skew_ptr%,44)}=5
' INT{ADD(table_skew_ptr%,46)}=6
' INT{ADD(table_skew_ptr%,48)}=7
' INT{ADD(table_skew_ptr%,50)}=8
' INT{ADD(table_skew_ptr%,52)}=9
' INT{ADD(table_skew_ptr%,54)}=10
' INT{ADD(table_skew_ptr%,56)}=11
' INT{ADD(table_skew_ptr%,58)}=12
' INT{ADD(table_skew_ptr%,60)}=13
' INT{ADD(table_skew_ptr%,62)}=14
' INT{ADD(table_skew_ptr%,64)}=15
' INT{ADD(table_skew_ptr%,66)}=16
' INT{ADD(table_skew_ptr%,68)}=17
' INT{ADD(table_skew_ptr%,70)}=18
'
ENDIF
'
' IF fmt_numside&=1 OR fmt_disktype&=4
' fmt_skew&=3
' ELSE
' fmt_skew&=2
' ENDIF
'
' fmt_skew_i&=0
fmt_virgin&=0
'
fmt_track_n&=0
DO
fmt_side_n&=0
DO
'
IF fmt_track_n&<2
fmt_virgin&=&H0
ELSE
fmt_virgin&=&HE5E5
ENDIF
'
' SUB fmt_skew_i&,fmt_skew&
' IF fmt_skew_i&<0
' ADD fmt_skew_i&,fmt_spt&
' ENDIF
'
' IF fmt_single_sided! OR fmt_high_density!
fmt_ret&=XBIOS(10,L:fmt_buffer%,L:0,W:fmt_devno&,W:fmt_spt&,W:fmt_track_n&,W:fmt_side_n&,W:1,L:&H87654321,W:fmt_virgin&)
' ELSE
' fmt_ret&=XBIOS(10,L:fmt_buffer%,L:ADD(table_skew_ptr%,MUL(fmt_skew_i&,2)),W:fmt_devno&,W:fmt_spt&,W:fmt_track_n&,W:fmt_side_n&,W:-1,L:&H87654321,W:fmt_virgin&)
' ENDIF
IF fmt_ret&=-16
IF fmt_track_n&<2 OR fmt_bad_sectors_nb&>15
fmt_failure!=TRUE
ELSE
fmt_i&=0
DO
fmt_bad_sector&=INT{ADD(fmt_buffer%,MUL(fmt_i&,2))}
IF fmt_bad_sector&<>0 AND fmt_bad_sectors_nb&<16
fmt_bad_sectors&(fmt_bad_cluster_nb&)=(fmt_track_n&*fmt_numside&*fmt_spt&)+(fmt_side_n&*fmt_spt&)+PRED(fmt_bad_sector&)
INC fmt_bad_sectors_nb&
ENDIF
INC fmt_i&
LOOP UNTIL fmt_bad_sector&<>0 OR fmt_bad_sectors_nb&>15
ENDIF
ENDIF
'
INC fmt_side_n&
LOOP UNTIL fmt_side_n&=fmt_numside& OR fmt_failure!=TRUE
'
OB_W(adtree%(19),7)=MAX(2,PRED(OB_W(adtree%(19),6)*MIN(1,fmt_track_n&/MAX(1,fmt_track&))))
black_white(19,6,-1)
'
INC fmt_track_n&
LOOP UNTIL fmt_track_n&=fmt_track& OR fmt_failure!=TRUE
'
IF fmt_failure!=FALSE
@fmt_clean_buffer(fmt_buffer%,1024)
'
~XBIOS(18,L:fmt_buffer%,L:&H1000000,W:fmt_disktype&,W:0)
BYTE{ADD(fmt_buffer%,0)}=&HE9
BYTE{ADD(fmt_buffer%,1)}=&H0
BYTE{ADD(fmt_buffer%,2)}=&H4E
'
BYTE{ADD(fmt_buffer%,11)}=&H0
BYTE{ADD(fmt_buffer%,12)}=&H2
BYTE{ADD(fmt_buffer%,13)}=2
BYTE{ADD(fmt_buffer%,14)}=1
BYTE{ADD(fmt_buffer%,15)}=0
BYTE{ADD(fmt_buffer%,16)}=2
'
dummy&=fmt_ndirs&
BYTE{ADD(fmt_buffer%,17)}=(dummy& AND &HFF)
BYTE{ADD(fmt_buffer%,18)}=(SHR&(dummy&,8) AND &HFF)
'
dummy&=MUL(MUL(fmt_track&,fmt_spt&),fmt_numside&)
BYTE{ADD(fmt_buffer%,19)}=(dummy& AND &HFF)
BYTE{ADD(fmt_buffer%,20)}=(SHR&(dummy&,8) AND &HFF)
'
IF fmt_numside&=2
BYTE{ADD(fmt_buffer%,21)}=&HF9
ELSE
BYTE{ADD(fmt_buffer%,21)}=&HF8
ENDIF
'
dummy&=fmt_spf&
BYTE{ADD(fmt_buffer%,22)}=(dummy& AND &HFF)
BYTE{ADD(fmt_buffer%,23)}=(SHR&(dummy&,8) AND &HFF)
'
dummy&=fmt_spt&
BYTE{ADD(fmt_buffer%,24)}=(dummy& AND &HFF)
BYTE{ADD(fmt_buffer%,25)}=(SHR&(dummy&,8) AND &HFF)
'
BYTE{ADD(fmt_buffer%,26)}=(fmt_numside& AND &HFF)
BYTE{ADD(fmt_buffer%,27)}=0
'
IF XBIOS(9,L:fmt_buffer%,L:0,W:fmt_devno&,W:1,W:0,W:0,W:1)<0
fmt_failure!=TRUE
ENDIF
ENDIF
'
IF fmt_failure!=FALSE AND LEN(fmt_name$)>0
@fmt_clean_buffer(fmt_buffer%,16)
'
CHAR{fmt_buffer%}=fmt_disk$+":\"+fmt_name$
'
fmt_handle&=GEMDOS(60,L:fmt_buffer%,W:8)
IF fmt_handle&>0
~GEMDOS(62,W:fmt_handle&)
ENDIF
ENDIF
'
bpb_recsiz&=512
bpb_clsiz&=2
bpb_rdlen&=DIV(fmt_ndirs&,16)
bpb_fsiz&=fmt_spf&
bpb_datrec&=1+MUL(2,fmt_spf&)+bpb_rdlen&
'
IF fmt_failure!=FALSE
@fmt_clean_buffer(fmt_buffer%,MIN(MUL(bpb_fsiz&,bpb_recsiz&),16380))
'
IF fmt_numside&=2
INT{ADD(fmt_buffer%,0)}=&HF9FF
ELSE
INT{ADD(fmt_buffer%,0)}=&HF8FF
ENDIF
INT{ADD(fmt_buffer%,2)}=&HFF00
'
IF fmt_bad_sectors_nb&>0
FOR fmt_i&=0 TO PRED(fmt_bad_sectors_nb&)
@fmt_set_fat(fmt_buffer%,((fmt_bad_sectors&(fmt_i&)-bpb_datrec&)/bpb_clsiz&)+2,&HFF7)
NEXT fmt_i&
ENDIF
'
~@fmt_write_sectors(fmt_devno&,fmt_buffer%,1,bpb_fsiz&)
~@fmt_write_sectors(fmt_devno&,fmt_buffer%,SUCC(bpb_fsiz&),bpb_fsiz&)
ENDIF
'
IF fmt_failure!=FALSE
~FSFIRST(fmt_disk$+":\@@$$%%&&",17)
ENDIF
'
mouse_free
'
IF fmt_bad_sectors_nb&>0
IF fmt_failure!
~@alert(1,21)
ELSE
~@alert(1,23)
ENDIF
ELSE IF fmt_failure!
~@alert(1,20)
ENDIF
'
@mxfree(fmt_buffer%)
@mxfree(table_bad_ptr%)
' @mxfree(table_skew_ptr%)
ENDIF
ENDIF
RETURN
> PROCEDURE fmt_disk_init(fmt_disk$)
LOCAL fmt_rdir_start&
'
IF fmt_disk$="A"
fmt_devno&=0
ELSE IF fmt_disk$="B"
fmt_devno&=1
ELSE
fmt_devno&=-1
ENDIF
'
IF fmt_devno&>-1
fmt_buffer%=@mxalloc(16384,0)
IF fmt_buffer%>0
'
mouse_busy
'
@fmt_clean_buffer(fmt_buffer%,1024)
'
IF BIOS(4,W:2,L:fmt_buffer%,W:1,W:0,fmt_devno&,L:0)<0
fmt_failure!=TRUE
ENDIF
'
IF fmt_failure!=FALSE
'
bpb_recsiz&=SHL&(BYTE{ADD(fmt_buffer%,12)},8) OR BYTE{ADD(fmt_buffer%,11)}
bpb_clsiz&=BYTE{ADD(fmt_buffer%,13)}
fmt_reserved&=MAX(1,SHL&(BYTE{ADD(fmt_buffer%,15)},8) OR BYTE{ADD(fmt_buffer%,14)})
fmt_nfats&=BYTE{ADD(fmt_buffer%,16)}
fmt_ndirs&=SHL&(BYTE{ADD(fmt_buffer%,18)},8) OR BYTE{ADD(fmt_buffer%,17)}
fmt_nsecs&=SHL&(BYTE{ADD(fmt_buffer%,20)},8) OR BYTE{ADD(fmt_buffer%,19)}
fmt_spf&=SHL&(BYTE{ADD(fmt_buffer%,23)},8) OR BYTE{ADD(fmt_buffer%,22)}
fmt_spt&=SHL&(BYTE{ADD(fmt_buffer%,25)},8) OR BYTE{ADD(fmt_buffer%,24)}
fmt_numside&=BYTE{ADD(fmt_buffer%,26)}
fmt_track&=DIV(DIV(fmt_nsecs&,fmt_numside&),fmt_spt&)
'
fmt_rdir_start&=ADD(fmt_reserved&,MUL(fmt_nfats&,fmt_spf&))
bpb_rdlen&=DIV(fmt_ndirs&,16)
bpb_datrec&=fmt_reserved&+MUL(fmt_nfats&,fmt_spf&)+bpb_rdlen&
'
@fmt_clean_buffer(fmt_buffer%,MUL(bpb_recsiz&,bpb_rdlen&))
~@fmt_write_sectors(fmt_devno&,fmt_buffer%,PRED(fmt_rdir_start&),bpb_rdlen&)
'
@fmt_clean_buffer(fmt_buffer%,MIN(MUL(bpb_recsiz&,fmt_spf&),16384))
~@fmt_read_sectors(fmt_devno&,fmt_buffer%,fmt_reserved&,fmt_spf&)
'
bpb_numcl&=(fmt_nsecs&-bpb_datrec&)/bpb_clsiz&
'
FOR fmt_i&=2 TO bpb_numcl&
IF @fmt_get_fat(fmt_buffer%,fmt_i&)<>&HFF7
@fmt_set_fat(fmt_buffer%,fmt_i&,0)
ENDIF
NEXT fmt_i&
'
~@fmt_write_sectors(fmt_devno&,fmt_buffer%,fmt_reserved&,fmt_spf&)
IF fmt_nfats&>1
~@fmt_write_sectors(fmt_devno&,fmt_buffer%,ADD(fmt_reserved&,fmt_spf&),fmt_spf&)
ENDIF
ENDIF
'
IF fmt_failure!=FALSE
~FSFIRST(fmt_disk$+":\@@$$%%&&",17)
ENDIF
'
mouse_free
'
@mxfree(fmt_buffer%)
ENDIF
ENDIF
'
RETURN
> PROCEDURE fmt_set_fat(fmt_adr%,fmt_cluster&,fmt_value&)
LOCAL fmt_pos&,fmt_pq|,fmt_r|,fmt_s|,fmt_tu|
'
IF fmt_cluster&>1
IF ODD(fmt_cluster&)
fmt_pos&=(PRED(fmt_cluster&)*3)/2
'
fmt_r|=SHL|(fmt_value& AND &HF,4)
fmt_s|=BYTE{ADD(fmt_adr%,SUCC(fmt_pos&))} AND &HF
fmt_tu|=SHR&(fmt_value&,4) AND &HFF
'
BYTE{ADD(fmt_adr%,SUCC(fmt_pos&))}=fmt_r| OR fmt_s|
BYTE{ADD(fmt_adr%,ADD(fmt_pos&,2))}=fmt_tu|
ELSE
fmt_pos&=(fmt_cluster&*3)/2
'
fmt_pq|=fmt_value& AND &HFF
fmt_r|=BYTE{ADD(fmt_adr%,SUCC(fmt_pos&))} AND &HF0
fmt_s|=SHR&(fmt_value&,8) AND &HF
'
BYTE{ADD(fmt_adr%,fmt_pos&)}=fmt_pq|
BYTE{ADD(fmt_adr%,SUCC(fmt_pos&))}=fmt_r| OR fmt_s|
ENDIF
ENDIF
RETURN
> FUNCTION fmt_get_fat(fmt_adr%,fmt_cluster&)
$F%
LOCAL fmt_pos&
'
IF fmt_cluster&>1
IF ODD(fmt_cluster&)
fmt_pos&=(PRED(fmt_cluster&)*3)/2
'
fmt_value&=SHL&(BYTE{ADD(fmt_adr%,ADD(fmt_pos&,2))},4) OR SHR|(BYTE{ADD(fmt_adr%,SUCC(fmt_pos&))} AND &HF0,4)
ELSE
fmt_pos&=(fmt_cluster&*3)/2
'
fmt_value&=SHL&(BYTE{ADD(fmt_adr%,SUCC(fmt_pos&))} AND &HF,8) OR BYTE{ADD(fmt_adr%,fmt_pos&)}
ENDIF
RETURN fmt_value&
ENDIF
RETURN 0
ENDFUNC
> FUNCTION fmt_read_sectors(fmt_dev&,fmt_adr%,fmt_start&,fmt_nb&)
DO
fmt_ret&=BIOS(4,W:&X10,L:fmt_adr%,W:1,W:fmt_start&,W:fmt_dev&)
ADD fmt_adr%,bpb_recsiz&
INC fmt_start&
DEC fmt_nb&
LOOP UNTIL fmt_nb&=0 OR fmt_ret&<0
RETURN fmt_ret&
ENDFUNC
> FUNCTION fmt_write_sectors(fmt_dev&,fmt_adr%,fmt_start&,fmt_nb&)
DO
fmt_ret&=BIOS(4,W:&X11,L:fmt_adr%,W:1,W:fmt_start&,W:fmt_dev&)
ADD fmt_adr%,bpb_recsiz&
INC fmt_start&
DEC fmt_nb&
LOOP UNTIL fmt_nb&=0 OR fmt_ret&<0
RETURN fmt_ret&
ENDFUNC
> PROCEDURE fmt_clean_buffer(fmt_adr%,fmt_len%)
DO
LONG{fmt_adr%}=0
ADD fmt_adr%,4
SUB fmt_len%,4
LOOP UNTIL fmt_len%=<0
RETURN
'
> PROCEDURE dialog_msa_to_disk(msa_name$)
'
CHAR{OB_SPEC(adtree%(20),1)}=RIGHT$(msa_name$,15)+" ->"
obj_enable(20,2)
obj_enable(20,3)
IF LEFT$(msa_name$)="A"
obj_disable(20,2)
obj_deselect(20,2)
obj_select(20,3)
obj_unhide(20,3)
ELSE IF LEFT$(msa_name$)="B"
obj_select(20,2)
obj_deselect(20,3)
IF display_disk_b!
obj_disable(20,3)
obj_unhide(20,3)
ELSE
obj_hide(20,3)
ENDIF
ELSE
obj_select(20,2)
obj_deselect(20,3)
IF display_disk_b!
obj_unhide(20,3)
ELSE
obj_hide(20,3)
ENDIF
ENDIF
OB_W(adtree%(20),5)=2
obj_hide(20,5)
obj_deselect(20,6)
obj_deselect(20,7)
'
control_form(20)
form_result&=0
DO
form_result&=FORM_DO(adtree%(20),0)
LOOP UNTIL form_result&=6 OR form_result&=7
'
IF form_result&=6
IF @tst_selected(20,3)
@msa_file_to_disk("B",msa_name$)
ELSE
@msa_file_to_disk("A",msa_name$)
ENDIF
ENDIF
'
uncontrol_form(20)
'
RETURN
> PROCEDURE dialog_msa_to_file(fmt_disk$,msa_win&)
'
obj_set_template(21,2,"________")
obj_set_valid(21,2,"ffffffff")
CHAR{{OB_SPEC(adtree%(21),2)}}=""
OB_W(adtree%(21),4)=2
obj_hide(21,4)
obj_deselect(21,5)
obj_deselect(21,6)
'
control_form(21)
form_result&=0
DO
form_result&=FORM_DO(adtree%(21),0)
LOOP UNTIL form_result&=5 OR form_result&=6
'
IF form_result&=5
'
msa_name$=CHAR{{OB_SPEC(adtree%(21),2)}}
IF msa_win&=3
msa_name$=CHAR{local_path0%}+msa_name$+".MSA"
ELSE IF msa_win&=4
msa_name$=CHAR{local_path1%}+msa_name$+".MSA"
ENDIF
'
@msa_disk_to_file(fmt_disk$,msa_name$)
ENDIF
'
uncontrol_form(21)
'
IF form_result&=5
display_refresh_directories
ENDIF
'
RETURN
> PROCEDURE msa_disk_to_file(fmt_disk$,msa_filename$)
LOCAL msa_proceed!
'
msa_filename$=msa_filename$+c0$
msa_failure!=FALSE
'
IF @s_exist(msa_filename$)
msa_proceed!=(@alert(1,25)=1)
IF msa_proceed!
~GEMDOS(65,L:V:msa_filename$)
ENDIF
ELSE
msa_proceed!=TRUE
ENDIF
'
IF fmt_disk$="A"
fmt_devno&=0
ELSE IF fmt_disk$="B"
fmt_devno&=1
ELSE
fmt_devno&=-1
ENDIF
'
IF fmt_devno&<0
msa_proceed!=FALSE
ENDIF
'
IF msa_proceed!
msa_buffer%=@mxalloc(25600,0)
IF msa_buffer%>0
msa_buffer2%=ADD(msa_buffer%,12800)
'
IF XBIOS(8,L:msa_buffer%,L:0,W:fmt_devno&,W:1,W:0,W:0,W:1)=0
'
~FRE(0)
msa_handle&=GEMDOS(60,L:V:msa_filename$,W:0)
IF msa_handle&>0
mouse_busy
obj_unhide(21,4)
'
msa_recsiz&=SHL&(BYTE{ADD(msa_buffer%,12)},8) OR BYTE{ADD(msa_buffer%,11)}
IF msa_recsiz&=512
'
msa_nsecs&=SHL&(BYTE{ADD(msa_buffer%,20)},8) OR BYTE{ADD(msa_buffer%,19)}
msa_spt&=SHL&(BYTE{ADD(msa_buffer%,25)},8) OR BYTE{ADD(msa_buffer%,24)}
msa_numside&=PRED(BYTE{ADD(msa_buffer%,26)})
msa_starting_track&=0
msa_ending_track&=PRED(DIV(DIV(msa_nsecs&,SUCC(msa_numside&)),msa_spt&))
'
INT{ADD(msa_buffer%,0)}=&HE0F
INT{ADD(msa_buffer%,2)}=msa_spt&
INT{ADD(msa_buffer%,4)}=msa_numside&
INT{ADD(msa_buffer%,6)}=msa_starting_track&
INT{ADD(msa_buffer%,8)}=msa_ending_track&
'
~GEMDOS(64,W:msa_handle&,L:10,L:msa_buffer%)
'
ELSE
msa_failure!=TRUE
ENDIF
'
IF msa_failure!=FALSE
'
fmt_track_n&=msa_starting_track&
DO
fmt_side_n&=0
DO
'
IF XBIOS(8,L:msa_buffer%,L:0,W:fmt_devno&,W:1,W:fmt_track_n&,W:fmt_side_n&,W:msa_spt&)=0
IF @msa_can_compress
~GEMDOS(64,W:msa_handle&,L:2,L:msa_buffer2%)
~GEMDOS(64,W:msa_handle&,L:CARD{msa_buffer2%},L:ADD(msa_buffer2%,2))
ELSE
CARD{msa_buffer2%}=MUL(512,msa_spt&)
~GEMDOS(64,W:msa_handle&,L:2,L:msa_buffer2%)
~GEMDOS(64,W:msa_handle&,L:MUL(512,msa_spt&),L:msa_buffer%)
ENDIF
ELSE
msa_failure!=TRUE
ENDIF
'
INC fmt_side_n&
LOOP UNTIL fmt_side_n&>msa_numside& OR msa_failure!=TRUE
'
OB_W(adtree%(21),4)=MAX(2,PRED(OB_W(adtree%(21),3)*MIN(1,fmt_track_n&/MAX(1,msa_ending_track&))))
black_white(21,3,-1)
'
INC fmt_track_n&
LOOP UNTIL fmt_track_n&>msa_ending_track& OR msa_failure!=TRUE
ENDIF
'
IF msa_failure!
~@alert(1,27)
ENDIF
'
~GEMDOS(62,W:msa_handle&)
mouse_free
ELSE
~@alert(1,27)
ENDIF
ELSE
~@alert(1,28)
ENDIF
ELSE
~@alert(1,26)
ENDIF
ENDIF
'
RETURN
> FUNCTION msa_can_compress
LOCAL msa_is_compressed!,msa_src_ptr%,msa_dst_ptr%
'
msa_is_compressed!=FALSE
'
msa_src_ptr%=msa_buffer%
msa_src_end%=ADD(msa_buffer%,MUL(512,msa_spt&))
msa_dst_ptr%=ADD(msa_buffer2%,2)
msa_dst_lim%=ADD(msa_dst_ptr%,MUL(512,msa_spt&))
'
DO
msa_bytes_val|=BYTE{msa_src_ptr%}
msa_bytes_nb%=1
msa_bytes_suite!=TRUE
'
WHILE ADD(msa_src_ptr%,msa_bytes_nb%)<msa_src_end% AND msa_bytes_suite!=TRUE
IF BYTE{ADD(msa_src_ptr%,msa_bytes_nb%)}=msa_bytes_val|
INC msa_bytes_nb%
ELSE
msa_bytes_suite!=FALSE
ENDIF
WEND
'
SELECT msa_bytes_nb%
CASE 1
'
IF msa_bytes_val|=&HE5
BYTE{msa_dst_ptr%}=&HE5
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=&HE5
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=0
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=1
INC msa_dst_ptr%
ELSE
BYTE{msa_dst_ptr%}=msa_bytes_val|
INC msa_dst_ptr%
ENDIF
'
INC msa_src_ptr%
'
CASE 2
'
IF msa_bytes_val|=&HE5
BYTE{msa_dst_ptr%}=&HE5
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=&HE5
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=0
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=2
INC msa_dst_ptr%
ELSE
BYTE{msa_dst_ptr%}=msa_bytes_val|
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=msa_bytes_val|
INC msa_dst_ptr%
ENDIF
'
ADD msa_src_ptr%,2
'
CASE 3
'
IF msa_bytes_val|=&HE5
BYTE{msa_dst_ptr%}=&HE5
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=&HE5
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=0
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=3
INC msa_dst_ptr%
ELSE
BYTE{msa_dst_ptr%}=msa_bytes_val|
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=msa_bytes_val|
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=msa_bytes_val|
INC msa_dst_ptr%
ENDIF
'
ADD msa_src_ptr%,3
'
DEFAULT
'
BYTE{msa_dst_ptr%}=&HE5
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=msa_bytes_val|
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=SHR(msa_bytes_nb%,8) AND &HFF
INC msa_dst_ptr%
BYTE{msa_dst_ptr%}=msa_bytes_nb% AND &HFF
INC msa_dst_ptr%
'
ADD msa_src_ptr%,msa_bytes_nb%
'
ENDSELECT
'
LOOP UNTIL msa_src_ptr%>=msa_src_end% OR msa_dst_ptr%>=msa_dst_lim%
'
IF msa_dst_ptr%<msa_dst_lim%
CARD{msa_buffer2%}=SUB(msa_dst_ptr%,ADD(msa_buffer2%,2))
msa_is_compressed!=TRUE
ELSE
msa_is_compressed!=FALSE
ENDIF
'
RETURN msa_is_compressed!
ENDFUNC
> PROCEDURE msa_file_to_disk(fmt_disk$,msa_filename$)
'
IF fmt_disk$="A"
fmt_devno&=0
ELSE IF fmt_disk$="B"
fmt_devno&=1
ELSE
fmt_devno&=-1
ENDIF
'
IF fmt_devno&>-1
msa_disk_adr%=0
msa_disk_adr%=@msa_file_to_ram(msa_filename$,msa_disk_adr%)
IF msa_disk_adr%>0
'
mouse_busy
'
msa_image_ptr%=msa_disk_adr%
msa_recsiz&=SHL&(BYTE{ADD(msa_disk_adr%,12)},8) OR BYTE{ADD(msa_disk_adr%,11)}
msa_spt&=SHL&(BYTE{ADD(msa_disk_adr%,25)},8) OR BYTE{ADD(msa_disk_adr%,24)}
'
fmt_failure!=FALSE
obj_unhide(20,5)
'
fmt_buffer%=@mxalloc(16384,0)
IF fmt_buffer%>0
'
fmt_track_n&=msa_starting_track&
DO
fmt_side_n&=0
DO
'
IF fmt_track_n&<2
fmt_virgin&=&H0
ELSE
fmt_virgin&=&HE5E5
ENDIF
'
IF XBIOS(10,L:fmt_buffer%,L:0,W:fmt_devno&,W:msa_spt&,W:fmt_track_n&,W:fmt_side_n&,W:1,L:&H87654321,W:fmt_virgin&)=-16
fmt_failure!=TRUE
ELSE
IF XBIOS(9,L:msa_image_ptr%,L:0,W:fmt_devno&,W:1,W:fmt_track_n&,W:fmt_side_n&,W:msa_spt&)=0
IF XBIOS(19,L:msa_image_ptr%,L:0,W:fmt_devno&,W:1,W:fmt_track_n&,W:fmt_side_n&,W:msa_spt&)=0
ADD msa_image_ptr%,MUL(msa_recsiz&,msa_spt&)
ELSE
fmt_failure!=TRUE
ENDIF
ELSE
fmt_failure!=TRUE
ENDIF
ENDIF
'
INC fmt_side_n&
LOOP UNTIL fmt_side_n&>msa_numside& OR fmt_failure!=TRUE
'
OB_W(adtree%(20),5)=MAX(2,PRED(OB_W(adtree%(20),4)*MIN(1,fmt_track_n&/MAX(1,msa_ending_track&))))
black_white(20,4,-1)
'
INC fmt_track_n&
LOOP UNTIL fmt_track_n&>msa_ending_track& OR fmt_failure!=TRUE
ENDIF
'
IF fmt_failure!
~@alert(1,27)
ENDIF
'
mouse_free
'
msa_disk_adr%=@msa_clean_image_in_ram(msa_disk_adr%)
ENDIF
ENDIF
'
RETURN
> FUNCTION msa_file_to_ram(msa_filename$,msa_img_adr%)
$F%
'
msa_failure!=FALSE
msa_marker&=0
msa_spt&=0
msa_numside&=0
msa_starting_track&=0
msa_ending_track&=0
msa_track_len&=0
'
msa_img_adr%=@msa_clean_image_in_ram(msa_img_adr%)
'
msa_buffer%=@mxalloc(4610,0)
IF msa_buffer%>0
'
msa_is_st_file!=(UPPER$(RIGHT$(msa_filename$,3))=".ST")
msa_is_msa_file!=(UPPER$(RIGHT$(msa_filename$,4))=".MSA")
'
msa_filename$=msa_filename$+c0$
~FRE(0)
msa_handle&=GEMDOS(61,L:V:msa_filename$,W:0)
IF msa_handle&>0
mouse_busy
'
~GEMDOS(66,L:0,W:msa_handle&,W:0)
'
IF msa_is_msa_file!
'
IF GEMDOS(63,W:msa_handle&,L:10,L:msa_buffer%)=10
'
msa_marker&=INT{ADD(msa_buffer%,0)}
msa_spt&=CARD{ADD(msa_buffer%,2)}
msa_numside&=CARD{ADD(msa_buffer%,4)}
msa_starting_track&=CARD{ADD(msa_buffer%,6)}
msa_ending_track&=CARD{ADD(msa_buffer%,8)}
'
msa_track_len%=MUL(512,msa_spt&)
'
IF msa_track_len%>4610
@mxfree(msa_buffer%)
msa_buffer%=@mxalloc(ADD(msa_track_len%,2),0)
ENDIF
'
IF msa_buffer%>0
IF msa_marker&=&HE0F
'
msa_image_len%=MUL(MUL(SUCC(msa_ending_track&),msa_track_len%),SUCC(msa_numside&))
'
msa_img_adr%=@mxalloc(msa_image_len%,0)
IF msa_img_adr%>0
'
msa_image_ptr%=msa_img_adr%
msa_image_end%=ADD(msa_image_ptr%,msa_image_len%)
'
msa_track_n&=msa_starting_track&
DO
msa_side_n&=0
DO
IF GEMDOS(63,W:msa_handle&,L:2,L:msa_buffer%)=2
msa_track_data_len%=CARD{msa_buffer%}
'
IF msa_track_data_len%=msa_track_len% ! not compressed
IF ODD(msa_image_ptr%)
msa_failure!=TRUE
ELSE IF GEMDOS(63,W:msa_handle&,L:msa_track_data_len%,L:msa_image_ptr%)=msa_track_data_len%
ADD msa_image_ptr%,msa_track_data_len%
ELSE
msa_failure!=TRUE
ENDIF
ELSE ! RLE compressed
IF GEMDOS(63,W:msa_handle&,L:msa_track_data_len%,L:msa_buffer%)=msa_track_data_len%
'
msa_buffer_ptr%=msa_buffer%
msa_buffer_end%=ADD(msa_buffer%,msa_track_data_len%)
msa_byte_val|=0
msa_bytes_val|=0
msa_bytes_nb%=0
'
DO
msa_byte_val|=BYTE{msa_buffer_ptr%}
IF msa_byte_val|=&HE5
msa_bytes_val|=BYTE{SUCC(msa_buffer_ptr%)}
msa_bytes_nb%=SHL&(BYTE{ADD(msa_buffer_ptr%,2)},8) OR BYTE{ADD(msa_buffer_ptr%,3)}
DO
BYTE{msa_image_ptr%}=msa_bytes_val|
INC msa_image_ptr%
DEC msa_bytes_nb%
LOOP UNTIL msa_bytes_nb%=0 OR msa_image_ptr%>msa_image_end%
ADD msa_buffer_ptr%,4
ELSE
BYTE{msa_image_ptr%}=msa_byte_val|
INC msa_image_ptr%
INC msa_buffer_ptr%
ENDIF
LOOP UNTIL msa_buffer_ptr%>=msa_buffer_end% OR msa_image_ptr%>msa_image_end%
'
ELSE
msa_failure!=TRUE
ENDIF
ENDIF
ELSE
msa_failure!=TRUE
ENDIF
'
INC msa_side_n&
LOOP UNTIL msa_side_n&>msa_numside& OR msa_failure!=TRUE
'
INC msa_track_n&
LOOP UNTIL msa_track_n&>msa_ending_track& OR msa_failure!=TRUE
'
ELSE
msa_failure!=TRUE
~@alert(1,26)
ENDIF
'
ELSE
msa_failure!=TRUE
~@alert(1,27)
ENDIF
ELSE
msa_failure!=TRUE
~@alert(1,26)
ENDIF
ENDIF
'
ELSE IF msa_is_st_file!
'
msa_image_len%=GEMDOS(66,L:0,W:msa_handle&,W:2)
~GEMDOS(66,L:0,W:msa_handle&,W:0)
'
msa_img_adr%=@mxalloc(msa_image_len%,0)
IF msa_img_adr%>0
IF GEMDOS(63,W:msa_handle&,L:msa_image_len%,L:msa_img_adr%)=msa_image_len%
'
msa_recsiz&=SHL&(BYTE{ADD(msa_img_adr%,12)},8) OR BYTE{ADD(msa_img_adr%,11)}
msa_spt&=SHL&(BYTE{ADD(msa_img_adr%,25)},8) OR BYTE{ADD(msa_img_adr%,24)}
msa_numside&=PRED(BYTE{ADD(msa_img_adr%,26)})
'
DIV msa_image_len%,msa_recsiz&
DIV msa_image_len%,msa_spt&
DIV msa_image_len%,SUCC(msa_numside&)
'
msa_starting_track&=0
msa_ending_track&=PRED(msa_image_len%)
'
ELSE
msa_failure!=TRUE
ENDIF
ELSE
msa_failure!=TRUE
~@alert(1,26)
ENDIF
ENDIF
'
~GEMDOS(62,W:msa_handle&)
'
mouse_free
ELSE
msa_failure!=TRUE
~@alert(1,27)
ENDIF
'
@mxfree(msa_buffer%)
ELSE
msa_failure!=TRUE
~@alert(1,26)
ENDIF
'
IF msa_failure!=TRUE
msa_img_adr%=@msa_clean_image_in_ram(msa_img_adr%)
ENDIF
'
RETURN msa_img_adr%
ENDFUNC
> FUNCTION msa_clean_image_in_ram(msa_mem_adr%)
IF msa_mem_adr%>0
@mxfree(msa_mem_adr%)
ENDIF
msa_mem_adr%=0
RETURN 0
ENDFUNC
> PROCEDURE msa_get_directory(kk_win&)
LOCAL d_val&,d_val|,d_val%,msa_add_ptr%,d_dta%,d_ext$
'
d_total_length%=0
'
IF kk_win&=3
msa_filename$=CHAR{local_path0%}
ELSE IF kk_win&=4
msa_filename$=CHAR{local_path1%}
ENDIF
'
dummy$=UPPER$(msa_filename$)
'
IF RIGHT$(dummy$,4)=".MSA" OR INSTR(dummy$,".MSA>")>0 OR RIGHT$(dummy$,3)=".ST" OR INSTR(dummy$,".ST>")>0
'
msa_pos&=RINSTR(dummy$,">")
IF msa_pos&>0
msa_inside_path$=MID$(msa_filename$,SUCC(msa_pos&))
msa_filename$=LEFT$(msa_filename$,PRED(msa_pos&))
ENDIF
'
msa_image_ptr%=0
'
IF kk_win&=3
IF msa_filename$<>msa_image_name$(3)
msa_image_name$(3)=msa_filename$
msa_image_adr%(3)=@msa_clean_image_in_ram(msa_image_adr%(3))
msa_image_ptr%=@msa_file_to_ram(msa_image_name$(3),msa_image_adr%(3))
IF msa_image_ptr%>0
msa_image_adr%(3)=msa_image_ptr%
ENDIF
ELSE IF msa_image_adr%(3)>0
msa_image_ptr%=msa_image_adr%(3)
ENDIF
ELSE IF kk_win&=4
IF msa_filename$<>msa_image_name$(4)
msa_image_name$(4)=msa_filename$
msa_image_adr%(4)=@msa_clean_image_in_ram(msa_image_adr%(4))
msa_image_ptr%=@msa_file_to_ram(msa_image_name$(4),msa_image_adr%(4))
IF msa_image_ptr%>0
msa_image_adr%(4)=msa_image_ptr%
ENDIF
ELSE IF msa_image_adr%(4)>0
msa_image_ptr%=msa_image_adr%(4)
ENDIF
ENDIF
'
msa_add_ptr%=record_buffer%(kk_win&)
'
old_record_start&=record_start&(kk_win&)
'
@record_close(kk_win&)
@record_open(kk_win&,64,record_len&(kk_win&),field_bar_obj&(kk_win&))
'
IF msa_image_ptr%>0
'
@mouse_busy
'
@msa_goto_directory
'
msa_exit!=FALSE
WHILE BYTE{msa_dta_ptr%}<>0 AND msa_exit!=FALSE
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF BYTE{msa_dta_ptr%}<>&HE5 AND BYTE{msa_dta_ptr%}<>0
'
d_val|=BYTE{ADD(msa_dta_ptr%,11)}
'
d_type&=1
IF BTST(d_val|,4)
d_type&=2
ELSE IF BTST(d_val|,1)
IF display_sort_discard_hidden_files!
d_type&=-1
ELSE
d_type&=4
ENDIF
ELSE IF BTST(d_val|,3)
d_type&=-1
ENDIF
'
IF d_type&>0
record_clear_buffer(kk_win&)
'
d_file$=TRIM$(MKL$(LONG{msa_dta_ptr%})+MKL$(LONG{ADD(msa_dta_ptr%,4)}))
d_ext$=TRIM$(LEFT$(MKL$(LONG{ADD(msa_dta_ptr%,8)}),3))
'
IF LEN(d_ext$)>0
d_file$=d_file$+"."+d_ext$
ENDIF
'
IF d_file$<>"." AND d_file$<>".."
'
@loc_set_number(msa_add_ptr%,0)
@loc_set_name(msa_add_ptr%,d_file$)
@loc_set_type(msa_add_ptr%,d_type&)
'
d_val%=0
ADD d_val%,BYTE{ADD(msa_dta_ptr%,28)}
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,29)},8)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,30)},16)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,31)},24)
@loc_set_length(msa_add_ptr%,d_val%)
'
IF d_type&=1
ADD d_total_length%,d_val%
ENDIF
'
d_val%=0
ADD d_val%,BYTE{ADD(msa_dta_ptr%,22)}
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,23)},8)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,24)},16)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,25)},24)
@loc_set_date(msa_add_ptr%,@gemdos_to_troll_timestamp(SHR(d_val% AND &HFFFF0000,16),d_val% AND &HFFFF))
'
d_val&=0
ADD d_val&,BYTE{ADD(msa_dta_ptr%,26)}
ADD d_val&,SHL&(BYTE{ADD(msa_dta_ptr%,27)},8)
@loc_set_cluster_start(msa_add_ptr%,d_val&)
ELSE
d_type&=-1
ENDIF
IF display_sort_discard_hidden_files!
IF LEFT$(d_file$)="."
d_type&=-1
ENDIF
ENDIF
'
IF d_type&>0
SELECT display_sort_type&
CASE 0
record_insert(kk_win&,head_nbline&(kk_win&))
CASE 1
record_insert(kk_win&,@local_sort_by_name(kk_win&,ADD(msa_add_ptr%,8)))
CASE 2
record_insert(kk_win&,@local_sort_by_length(kk_win&,@loc_get_length(msa_add_ptr%)))
CASE 3
record_insert(kk_win&,@local_sort_by_date(kk_win&,@loc_get_date(msa_add_ptr%)))
CASE 4
record_insert(kk_win&,@local_sort_by_type(kk_win&,ADD(msa_add_ptr%,4)))
ENDSELECT
ENDIF
ENDIF
ENDIF
'
ADD msa_dta_ptr%,32
'
IF msa_dta_ptr%>=msa_dta_end%
IF msa_root!=TRUE
msa_exit!=TRUE
ELSE
msa_cluster_start&=@fmt_get_fat(msa_fat_adr%,msa_cluster_start&)
IF msa_cluster_start&>&H1 AND msa_cluster_start&<&HFF0
msa_sector_start&=ADD(bpb_datrec&,MUL(SUB(msa_cluster_start&,2),bpb_clsiz&))
msa_dta_ptr%=ADD(msa_image_ptr%,MUL(msa_sector_start&,bpb_recsiz&))
msa_dta_end%=ADD(msa_dta_ptr%,MUL(bpb_recsiz&,bpb_clsiz&))
ELSE
msa_exit!=TRUE
ENDIF
ENDIF
ENDIF
WEND
'
ENDIF
'
@local_sort_folders_at_top(kk_win&)
@local_set_win_stats(kk_win&)
@local_add_cd_up(kk_win&)
'
@mouse_free
'
IF win!(kk_win&)
record_start&(kk_win&)=old_record_start&
set_vslide(kk_win&)
record_start&(kk_win&)=@get_vslide(kk_win&)
force_update(kk_win&)
ENDIF
ENDIF
'
RETURN
> PROCEDURE msa_add_files_in_directories(kk_win&,enquiry_path%)
LOCAL d_val&,d_val|,d_val%,msa_add_ptr%,d_dta%,d_ext$
'
IF FRE()<4000
~FRE(0)
ENDIF
'
dummy$=UPPER$(CHAR{enquiry_path%})+"\"
'
IF RIGHT$(dummy$,4)=".MSA" OR INSTR(dummy$,".MSA>")>0 OR RIGHT$(dummy$,3)=".ST" OR INSTR(dummy$,".ST>")>0
'
msa_filename$=CHAR{enquiry_path%}+"\"
'
msa_pos&=RINSTR(dummy$,">")
IF msa_pos&>0
msa_inside_path$=MID$(msa_filename$,SUCC(msa_pos&))
msa_filename$=LEFT$(msa_filename$,PRED(msa_pos&))
ENDIF
'
msa_image_ptr%=0
'
IF kk_win&=3
msa_image_ptr%=msa_image_adr%(3)
ELSE IF kk_win&=4
msa_image_ptr%=msa_image_adr%(4)
ENDIF
'
IF msa_image_ptr%>0
'
@msa_goto_directory
'
msa_add_ptr%=record_buffer%(1)
'
msa_exit!=FALSE
WHILE BYTE{msa_dta_ptr%}<>0 AND msa_exit!=FALSE
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF BYTE{msa_dta_ptr%}<>&HE5 AND BYTE{msa_dta_ptr%}<>0
'
record_clear_buffer(1)
'
d_file$=TRIM$(MKL$(LONG{msa_dta_ptr%})+MKL$(LONG{ADD(msa_dta_ptr%,4)}))
d_ext$=TRIM$(LEFT$(MKL$(LONG{ADD(msa_dta_ptr%,8)}),3))
'
IF LEN(d_ext$)>0
d_file$=d_file$+"."+d_ext$
ENDIF
'
d_type&=0
'
IF d_file$<>"." AND d_file$<>".."
'
d_val|=BYTE{ADD(msa_dta_ptr%,11)}
'
IF BTST(d_val|,1)
ELSE IF BTST(d_val|,3)
ELSE IF BTST(d_val|,4)
d_type&=2
@fil_set_name(1,msa_add_ptr%,CHAR{enquiry_path%}+"\"+d_file$)
@fil_set_type(msa_add_ptr%,2)
@fil_set_listed(msa_add_ptr%,FALSE)
@fil_set_length(msa_add_ptr%,0)
'
d_val%=0
ADD d_val%,BYTE{ADD(msa_dta_ptr%,22)}
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,23)},8)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,24)},16)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,25)},24)
@fil_set_date(msa_add_ptr%,@gemdos_to_troll_timestamp(SHR(d_val% AND &HFFFF0000,16),d_val% AND &HFFFF))
ELSE
d_type&=1
@fil_set_name(1,msa_add_ptr%,CHAR{enquiry_path%}+"\"+d_file$)
@fil_set_type(msa_add_ptr%,1)
@fil_set_listed(msa_add_ptr%,TRUE)
'
d_val%=0
ADD d_val%,BYTE{ADD(msa_dta_ptr%,28)}
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,29)},8)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,30)},16)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,31)},24)
@fil_set_length(msa_add_ptr%,d_val%)
'
d_val%=0
ADD d_val%,BYTE{ADD(msa_dta_ptr%,22)}
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,23)},8)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,24)},16)
ADD d_val%,SHL(BYTE{ADD(msa_dta_ptr%,25)},24)
@fil_set_date(msa_add_ptr%,@gemdos_to_troll_timestamp(SHR(d_val% AND &HFFFF0000,16),d_val% AND &HFFFF))
'
d_val&=0
ADD d_val&,BYTE{ADD(msa_dta_ptr%,26)}
ADD d_val&,SHL&(BYTE{ADD(msa_dta_ptr%,27)},8)
@fil_set_cluster_start(msa_add_ptr%,d_val&)
ENDIF
ENDIF
'
IF d_type&>0
record_insert(1,head_nbline&(1))
ENDIF
ENDIF
'
ADD msa_dta_ptr%,32
'
IF msa_dta_ptr%>=msa_dta_end%
IF msa_root!=TRUE
msa_exit!=TRUE
ELSE
msa_cluster_start&=@fmt_get_fat(msa_fat_adr%,msa_cluster_start&)
IF msa_cluster_start&>&H1 AND msa_cluster_start&<&HFF0
msa_sector_start&=ADD(bpb_datrec&,MUL(SUB(msa_cluster_start&,2),bpb_clsiz&))
msa_dta_ptr%=ADD(msa_image_ptr%,MUL(msa_sector_start&,bpb_recsiz&))
msa_dta_end%=ADD(msa_dta_ptr%,MUL(bpb_recsiz&,bpb_clsiz&))
ELSE
msa_exit!=TRUE
ENDIF
ENDIF
ENDIF
WEND
'
ENDIF
ENDIF
'
RETURN
> PROCEDURE msa_goto_directory
'
IF FRE()<4000
~FRE(0)
ENDIF
'
bpb_recsiz&=SHL&(BYTE{ADD(msa_image_ptr%,12)},8) OR BYTE{ADD(msa_image_ptr%,11)}
bpb_clsiz&=BYTE{ADD(msa_image_ptr%,13)}
fmt_reserved&=MAX(1,SHL&(BYTE{ADD(msa_image_ptr%,15)},8) OR BYTE{ADD(msa_image_ptr%,14)})
fmt_nfats&=BYTE{ADD(msa_image_ptr%,16)}
fmt_ndirs&=SHL&(BYTE{ADD(msa_image_ptr%,18)},8) OR BYTE{ADD(msa_image_ptr%,17)}
fmt_nsecs&=SHL&(BYTE{ADD(msa_image_ptr%,20)},8) OR BYTE{ADD(msa_image_ptr%,19)}
fmt_spf&=SHL&(BYTE{ADD(msa_image_ptr%,23)},8) OR BYTE{ADD(msa_image_ptr%,22)}
fmt_spt&=SHL&(BYTE{ADD(msa_image_ptr%,25)},8) OR BYTE{ADD(msa_image_ptr%,24)}
fmt_numside&=BYTE{ADD(msa_image_ptr%,26)}
'
IF fmt_nfats&=0 OR fmt_spt&=0
~@alert(1,29)
ENDIF
'
fmt_track&=DIV(DIV(fmt_nsecs&,fmt_numside&),fmt_spt&)
bpb_rdlen&=DIV(fmt_ndirs&,16)
bpb_datrec&=fmt_reserved&+MUL(fmt_nfats&,fmt_spf&)+bpb_rdlen&
'
fmt_rdir_start&=ADD(fmt_reserved&,MUL(fmt_nfats&,fmt_spf&))
'
msa_dta_ptr%=ADD(msa_image_ptr%,MUL(fmt_rdir_start&,bpb_recsiz&))
msa_dta_end%=ADD(msa_dta_ptr%,MUL(fmt_ndirs&,32))
msa_cluster_start&=0
msa_fat_adr%=ADD(msa_image_ptr%,MUL(fmt_reserved&,bpb_recsiz&))
msa_root!=TRUE
'
WHILE LEN(msa_inside_path$)>0
'
IF FRE()<4000
~FRE(0)
ENDIF
'
msa_pos&=INSTR(msa_inside_path$,"\")
IF msa_pos&>0
msa_inside_folder$=UPPER$(LEFT$(msa_inside_path$,PRED(msa_pos&)))
msa_inside_path$=MID$(msa_inside_path$,SUCC(msa_pos&))
ELSE
msa_inside_folder$=UPPER$(msa_inside_path$)
ENDIF
'
msa_exit!=FALSE
WHILE BYTE{msa_dta_ptr%}<>0 AND msa_exit!=FALSE
'
IF BYTE{msa_dta_ptr%}<>&HE5 AND BYTE{msa_dta_ptr%}<>0
'
IF BTST(BYTE{ADD(msa_dta_ptr%,11)},4)
'
d_file$=TRIM$(MKL$(LONG{msa_dta_ptr%})+MKL$(LONG{ADD(msa_dta_ptr%,4)}))
d_ext$=TRIM$(LEFT$(MKL$(LONG{ADD(msa_dta_ptr%,8)}),3))
'
IF LEN(d_ext$)>0
d_file$=d_file$+"."+d_ext$
ENDIF
'
IF msa_inside_folder$=UPPER$(d_file$)
msa_cluster_start&=SHL&(BYTE{ADD(msa_dta_ptr%,27)},8) OR BYTE{ADD(msa_dta_ptr%,26)}
IF msa_cluster_start&>&H1 AND msa_cluster_start&<&HFF0
msa_sector_start&=ADD(bpb_datrec&,MUL(SUB(msa_cluster_start&,2),bpb_clsiz&))
msa_dta_ptr%=ADD(msa_image_ptr%,MUL(msa_sector_start&,bpb_recsiz&))
msa_dta_end%=ADD(msa_dta_ptr%,MUL(bpb_recsiz&,bpb_clsiz&))
msa_root!=FALSE
ENDIF
msa_exit!=TRUE
ENDIF
ENDIF
ENDIF
'
IF msa_exit!=FALSE
ADD msa_dta_ptr%,32
IF msa_dta_ptr%>msa_dta_end% AND msa_root!=FALSE
msa_cluster_start&=@fmt_get_fat(msa_fat_adr%,msa_cluster_start&)
IF msa_cluster_start&>&H1 AND msa_cluster_start&<&HFF0
msa_sector_start&=ADD(bpb_datrec&,MUL(SUB(msa_cluster_start&,2),bpb_clsiz&))
msa_dta_ptr%=ADD(msa_image_ptr%,MUL(msa_sector_start&,bpb_recsiz&))
msa_dta_end%=ADD(msa_dta_ptr%,MUL(bpb_recsiz&,bpb_clsiz&))
ELSE
msa_exit!=TRUE
ENDIF
ENDIF
ENDIF
WEND
WEND
'
IF msa_root!=TRUE
msa_dta_end%=ADD(msa_dta_ptr%,MUL(fmt_ndirs&,32))
ELSE
msa_dta_end%=ADD(msa_dta_ptr%,MUL(bpb_recsiz&,bpb_clsiz&))
ENDIF
'
RETURN
'
> PROCEDURE dialog_split_file(split_name$,split_win&)
LOCAL split_filename$,split_fragname$,split_pos&
'
CHAR{{OB_SPEC(adtree%(24),2)}}=LEFT$(STR$(split_current_size&),4)
OB_W(adtree%(24),5)=2
obj_hide(24,5)
obj_deselect(24,6)
obj_deselect(24,7)
'
control_form(24)
form_result&=0
DO
form_result&=FORM_DO(adtree%(24),0)
LOOP UNTIL form_result&=6 OR form_result&=7
'
IF form_result&=6
'
split_current_size&=VAL(CHAR{{OB_SPEC(adtree%(24),2)}})
'
split_applied_count%=MAX(1,DIV(MUL(split_current_size&,1024),32000))
'
IF split_win&=3
split_filename$=CHAR{local_path0%}+split_name$+c0$
ELSE IF split_win&=4
split_filename$=CHAR{local_path1%}+split_name$+c0$
ENDIF
'
split_pos&=RINSTR(split_name$,".")
IF split_pos&>0
IF split_win&=3
split_fragname$=CHAR{local_path1%}+LEFT$(split_name$,PRED(split_pos&))
ELSE IF split_win&=4
split_fragname$=CHAR{local_path0%}+LEFT$(split_name$,PRED(split_pos&))
ENDIF
ELSE
IF split_win&=3
split_fragname$=CHAR{local_path1%}+split_name$
ELSE IF split_win&=4
split_fragname$=CHAR{local_path0%}+split_name$
ENDIF
ENDIF
'
@split_file(split_filename$,split_fragname$,split_name$)
ENDIF
'
uncontrol_form(24)
'
IF form_result&=6
@display_refresh_directories
ENDIF
'
RETURN
> PROCEDURE split_file(sp_filename$,sp_fragname$,sp_name$)
LOCAL sp_okay!,sp_size%,sp_handle&,sp_len%,sp_ptr%,sp_count&,sp_adr%
LOCAL up_num&,up_count&,up_filename$,up_handle&,up_drive&
'
sp_okay!=TRUE
sp_count&=INT(split_applied_count%)
up_num&=1
up_drive&=SUB(ASC(LEFT$(sp_fragname$)),64)
obj_unhide(24,5)
'
split_crc_value%=0
'
sp_adr%=@mxalloc(32016,3)
IF sp_adr%<1
sp_okay!=FALSE
ENDIF
'
IF @s_exist(sp_filename$)=TRUE AND sp_okay!=TRUE
'
IF FRE()<4000
~FRE(0)
ENDIF
'
sp_handle&=GEMDOS(61,L:V:sp_filename$,W:0)
IF sp_handle&>0
mouse_busy
'
sp_size%=GEMDOS(66,L:0,W:sp_handle&,W:2)
'
IF @has_enough_space(up_drive&,sp_size%+256)=FALSE
~@alert(1,32)
sp_okay!=FALSE
ENDIF
'
IF sp_size%=<MUL(sp_count&,32000)
~@alert(1,33)
sp_okay!=FALSE
ENDIF
'
IF sp_okay!=TRUE
'
up_filename$=STR$(up_num&)
up_filename$=STRING$(SUB(3,LEN(up_filename$)),"0")+up_filename$
up_filename$=sp_fragname$+"."+up_filename$+c0$
'
IF @s_exist(up_filename$)
~GEMDOS(65,L:V:up_filename$)
ENDIF
'
IF FRE()<4000
~FRE(0)
ENDIF
'
up_handle&=GEMDOS(60,L:V:up_filename$,W:0)
IF up_handle&<1
sp_okay!=FALSE
ENDIF
'
~GEMDOS(66,L:0,W:sp_handle&,W:0)
'
sp_ptr%=0
up_count&=1
WHILE sp_ptr%<sp_size% AND sp_okay!=TRUE
'
sp_len%=MIN(32000,SUB(sp_size%,sp_ptr%))
IF GEMDOS(63,W:sp_handle&,L:sp_len%,L:sp_adr%)=sp_len%
'
split_crc_value%=@crc32(split_crc_value%,sp_adr%,sp_len%)
'
IF GEMDOS(64,W:up_handle&,L:sp_len%,L:sp_adr%)=sp_len%
INC up_count&
'
IF up_count&>sp_count& AND ADD(sp_ptr%,sp_len%)<sp_size%
~GEMDOS(62,W:up_handle&)
up_handle&=0
'
INC up_num&
IF up_num&<1000
up_count&=1
'
up_filename$=STR$(up_num&)
up_filename$=STRING$(SUB(3,LEN(up_filename$)),"0")+up_filename$
up_filename$=sp_fragname$+"."+up_filename$+c0$
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF @s_exist(up_filename$)
~GEMDOS(65,L:V:up_filename$)
ENDIF
'
up_handle&=GEMDOS(60,L:V:up_filename$,W:0)
IF up_handle&<1
sp_okay!=FALSE
ENDIF
ELSE
sp_okay!=FALSE
ENDIF
ENDIF
ELSE
sp_okay!=FALSE
ENDIF
'
OB_W(adtree%(24),5)=MAX(2,PRED(OB_W(adtree%(24),4)*MIN(1,sp_ptr%/MAX(1,sp_size%))))
black_white(24,4,-1)
'
ELSE
sp_okay!=FALSE
ENDIF
ADD sp_ptr%,32000
WEND
'
IF up_handle&>0
~GEMDOS(62,W:up_handle&)
ENDIF
'
INC up_num&
IF up_num&<1000
up_filename$=STR$(up_num&)
up_filename$=STRING$(SUB(3,LEN(up_filename$)),"0")+up_filename$
up_filename$=sp_fragname$+"."+up_filename$+c0$
'
IF @s_exist(up_filename$)
~GEMDOS(65,L:V:up_filename$)
ENDIF
ENDIF
'
ELSE
sp_okay!=FALSE
ENDIF
~GEMDOS(62,W:sp_handle&)
'
mouse_free
ELSE
sp_okay!=FALSE
ENDIF
ELSE
sp_okay!=FALSE
ENDIF
'
IF sp_adr%>0
@mxfree(sp_adr%)
ENDIF
'
IF sp_okay!=TRUE AND sp_size%>0
'
mouse_busy
'
mem_pos%=@mem_init
'
mem_pos%=@mem_put_property("filename",sp_name$,mem_pos%)
mem_pos%=@mem_put_property("size",STR$(sp_size%),mem_pos%)
mem_pos%=@mem_put_property("crc32",HEX$(split_crc_value%,8),mem_pos%)
'
sp_name$=sp_fragname$+ext_crc$
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF mem_pos%>0
IF @s_exist(sp_name$)
~GEMDOS(65,L:V:sp_name$)
ENDIF
'
file_handle&=GEMDOS(60,L:V:sp_name$,W:0)
IF file_handle&>0
~GEMDOS(64,W:file_handle&,L:SUB(mem_pos%,text_mem%),L:text_mem%)
~GEMDOS(62,W:file_handle&)
ENDIF
ENDIF
'
~@mem_close
'
mouse_free
ENDIF
'
RETURN
> PROCEDURE unsplit_file(crc_filename$,split_win&)
LOCAL sp_okay!,sp_size%,sp_handle&,up_len%,up_ptr%,up_size%,up_adr%
LOCAL up_num&,up_filename$,up_handle&,sp_drive&,sp_pos&,sp_final%
'
sp_okay!=TRUE
'
IF split_win&=3
crc_filename$=CHAR{local_path0%}+crc_filename$+c0$
ELSE IF split_win&=4
crc_filename$=CHAR{local_path1%}+crc_filename$+c0$
ENDIF
'
IF @s_exist(crc_filename$)=TRUE
'
@new_preferences_text
OPEN "i",#4,crc_filename$
RECALL #4,preferences_text$(),-1,nb_line%
CLOSE #4
'
sp_name$=@find$("filename")
sp_size%=MAX(0,VAL(@find$("size")))
sp_crc%=VAL("&H"+@find$("crc32"))
'
@new_preferences_text
'
IF split_win&=3
sp_filename$=CHAR{local_path1%}+sp_name$+c0$
ELSE IF split_win&=4
sp_filename$=CHAR{local_path0%}+sp_name$+c0$
ENDIF
'
sp_pos&=RINSTR(sp_name$,".")
IF sp_pos&>0
IF sp_win&=3
sp_fragname$=CHAR{local_path0%}+LEFT$(sp_name$,PRED(sp_pos&))
ELSE IF split_win&=4
sp_fragname$=CHAR{local_path1%}+LEFT$(sp_name$,PRED(sp_pos&))
ENDIF
ELSE
IF split_win&=3
sp_fragname$=CHAR{local_path0%}+sp_name$
ELSE IF split_win&=4
sp_fragname$=CHAR{local_path1%}+sp_name$
ENDIF
ENDIF
'
sp_drive&=SUB(ASC(LEFT$(sp_filename$)),64)
'
ELSE
sp_name$=""
sp_size%=0
sp_crc%=0
ENDIF
'
split_crc_value%=0
'
up_adr%=@mxalloc(32016,3)
IF up_adr%<1
sp_okay!=FALSE
ENDIF
'
IF sp_size%>0
IF @has_enough_space(sp_drive&,sp_size%)=TRUE
ELSE
~@alert(1,32)
sp_okay!=FALSE
ENDIF
ELSE
sp_okay!=FALSE
ENDIF
'
IF sp_okay!=TRUE
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF @s_exist(sp_filename$)
~GEMDOS(65,L:V:sp_filename$)
ENDIF
'
sp_handle&=GEMDOS(60,L:V:sp_filename$,W:0)
IF sp_handle&>0
mouse_busy
'
up_num&=1
up_filename$=STR$(up_num&)
up_filename$=STRING$(SUB(3,LEN(up_filename$)),"0")+up_filename$
up_filename$=sp_fragname$+"."+up_filename$+c0$
'
WHILE @s_exist(up_filename$)=TRUE AND sp_okay!=TRUE
'
IF FRE()<4000
~FRE(0)
ENDIF
'
up_handle&=GEMDOS(61,L:V:up_filename$,W:0)
IF up_handle&>0
'
up_size%=GEMDOS(66,L:0,W:up_handle&,W:2)
~GEMDOS(66,L:0,W:up_handle&,W:0)
'
up_ptr%=0
WHILE up_ptr%<up_size% AND sp_okay!=TRUE
'
up_len%=MIN(32000,SUB(up_size%,up_ptr%))
IF GEMDOS(63,W:up_handle&,L:up_len%,L:up_adr%)=up_len%
'
split_crc_value%=@crc32(split_crc_value%,up_adr%,up_len%)
'
IF GEMDOS(64,W:sp_handle&,L:up_len%,L:up_adr%)=up_len%
ADD sp_final%,up_len%
ELSE
sp_okay!=FALSE
ENDIF
ELSE
sp_okay!=FALSE
ENDIF
ADD up_ptr%,32000
WEND
~GEMDOS(62,W:up_handle&)
ELSE
sp_okay!=FALSE
ENDIF
'
INC up_num&
up_filename$=STR$(up_num&)
up_filename$=STRING$(SUB(3,LEN(up_filename$)),"0")+up_filename$
up_filename$=sp_fragname$+"."+up_filename$+c0$
WEND
mouse_free
'
~GEMDOS(62,W:sp_handle&)
ENDIF
ENDIF
'
IF up_adr%>0
@mxfree(up_adr%)
ENDIF
'
IF sp_size%>0
IF sp_size%<>sp_final%
~@alert(1,35)
ENDIF
ENDIF
'
IF sp_crc%>0
IF sp_crc%<>split_crc_value%
~@alert(1,36)
ENDIF
ENDIF
'
@display_refresh_directories
RETURN
'
> PROCEDURE zipfile_new(kk_win&)
LOCAL zip_new$,command_handle&
'
OB_SPEC(adtree%(6),1)=OB_SPEC(adtree%(alert_tree&),48)
CHAR{{OB_SPEC(adtree%(6),3)}}=""
obj_set_template(6,3,"________")
obj_set_valid(6,3,"fffffff")
obj_unhide(6,4)
obj_deselect(6,4)
obj_deselect(6,5)
obj_deselect(6,6)
'
control_form(6)
form_result&=0
DO
form_result&=FORM_DO(adtree%(6),0)
LOOP UNTIL form_result&>4
uncontrol_form(6)
'
IF form_result&=5
'
zip_new$=CHAR{{OB_SPEC(adtree%(6),3)}}
'
IF LEN(zip_new$)>0
'
zip_new$=zip_new$+ext_zip$
IF kk_win&=3
zip_new$=CHAR{local_path0%}+zip_new$
ELSE IF kk_win&=4
zip_new$=CHAR{local_path1%}+zip_new$
ENDIF
'
IF @s_exist(zip_new$)=FALSE
'
IF FRE()<4000
~FRE(0)
ENDIF
'
command_handle&=GEMDOS(60,L:V:zip_new$,W:0)
zip_buf%=@mxalloc(64,3)
IF command_handle&>0 AND zip_buf%>0
'
LONG{zip_buf%}=&H504B0506
FOR zip_i&=1 TO 6
LONG{ADD(zip_buf%,MUL(zip_i&,4))}=0
NEXT zip_i&
'
~GEMDOS(64,W:command_handle&,L:22,L:zip_buf%)
'
ENDIF
IF command_handle&>0
~GEMDOS(62,W:command_handle&)
ENDIF
IF zip_buf%>0
@mxfree(zip_buf%)
zip_buf%=0
ENDIF
'
ENDIF
'
IF @tst_selected(6,4)
zip_new$=@remove_c0$(zip_new$)
IF kk_win&=3
@local_set_win_info(3,zip_new$+">")
ELSE IF kk_win&=4
@local_set_win_info(4,zip_new$+">")
ENDIF
zip_filename$(kk_win&)=SPACE$(3)
@zipfile_get_directory(kk_win&)
ELSE
@display_refresh_directories
ENDIF
ENDIF
ENDIF
'
RETURN
> PROCEDURE zipfile_new_folder(kk_win&)
LOCAL zip_new_folder$
'
OB_SPEC(adtree%(6),1)=OB_SPEC(adtree%(alert_tree&),46)
CHAR{{OB_SPEC(adtree%(6),3)}}=""
obj_set_template(6,3,"______________________")
obj_set_valid(6,3,"aaaaaaaaaaaaaaaaaaaaaa")
obj_unhide(6,4)
obj_deselect(6,4)
obj_deselect(6,5)
obj_deselect(6,6)
'
control_form(6)
form_result&=0
DO
form_result&=FORM_DO(adtree%(6),0)
LOOP UNTIL form_result&>4
uncontrol_form(6)
'
IF form_result&=5
'
zip_new_folder$=CHAR{{OB_SPEC(adtree%(6),3)}}
'
IF LEN(zip_new_folder$)>0
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
record_clear_buffer(1)
'
fil_set_type(record_buffer%(1),2)
IF kk_win&=3
fil_set_name(1,record_buffer%(1),CHAR{local_path0%}+zip_new_folder$)
copy_root&=SUCC(LEN(CHAR{local_path0%}))
ELSE IF kk_win&=4
fil_set_name(1,record_buffer%(1),CHAR{local_path1%}+zip_new_folder$)
copy_root&=SUCC(LEN(CHAR{local_path1%}))
ENDIF
fil_set_date(record_buffer%(1),SHL(GEMDOS(42),16) OR GEMDOS(44))
'
record_insert(1,head_nbline&(1))
'
IF @zipfile_add_files(kk_win&,FALSE)
IF @tst_selected(6,4)
IF kk_win&=3
@local_set_win_info(3,CHAR{local_path0%}+zip_new_folder$+"\")
ELSE IF kk_win&=4
@local_set_win_info(4,CHAR{local_path1%}+zip_new_folder$+"\")
ENDIF
ENDIF
zip_filename$(kk_win&)=SPACE$(3)
@zipfile_get_directory(kk_win&)
ENDIF
ENDIF
ENDIF
'
RETURN
> PROCEDURE zipfile_new_empty_file(kk_win&)
LOCAL zip_new_empty_file$
'
OB_SPEC(adtree%(6),1)=OB_SPEC(adtree%(alert_tree&),47)
CHAR{{OB_SPEC(adtree%(6),3)}}=""
obj_set_template(6,3,"______________________")
obj_set_valid(6,3,"aaaaaaaaaaaaaaaaaaaaaa")
obj_hide(6,4)
obj_deselect(6,4)
obj_deselect(6,5)
obj_deselect(6,6)
'
control_form(6)
form_result&=0
DO
form_result&=FORM_DO(adtree%(6),0)
LOOP UNTIL form_result&>4
uncontrol_form(6)
'
IF form_result&=5
'
zip_new_empty_file$=CHAR{{OB_SPEC(adtree%(6),3)}}
'
IF LEN(zip_new_empty_file$)>0
'
@record_close(1)
@record_open(1,48,record_len&(1),0)
'
record_clear_buffer(1)
'
fil_set_type(record_buffer%(1),1)
IF kk_win&=3
fil_set_name(1,record_buffer%(1),CHAR{local_path0%}+zip_new_empty_file$)
copy_root&=SUCC(LEN(CHAR{local_path0%}))
ELSE IF kk_win&=4
fil_set_name(1,record_buffer%(1),CHAR{local_path1%}+zip_new_empty_file$)
copy_root&=SUCC(LEN(CHAR{local_path1%}))
ENDIF
fil_set_date(record_buffer%(1),SHL(GEMDOS(42),16) OR GEMDOS(44))
'
record_insert(1,head_nbline&(1))
'
IF @zipfile_add_files(kk_win&,FALSE)
zip_filename$(kk_win&)=SPACE$(3)
@zipfile_get_directory(kk_win&)
ENDIF
ENDIF
ENDIF
'
RETURN
'
> FUNCTION zipfile_add_files(kk_win&,zip_showed!)
$F%
LOCAL zip_new_entry_name$,zip_fil_ptr%,zip_deflate_error!
'
zip_okay!=FALSE
zip_deflate_error!=FALSE
'
IF kk_win&=3
zip_name$=CHAR{local_path0%}
ELSE IF kk_win&=4
zip_name$=CHAR{local_path1%}
ELSE
zip_name$=""
ENDIF
'
zip_win&=ADD(2,kk_win&)
'
dummy$=UPPER$(zip_name$)
'
IF RIGHT$(dummy$,4)=".ZIP" OR INSTR(dummy$,".ZIP>")>0
'
zip_pos&=RINSTR(dummy$,">")
IF zip_pos&>0
zip_inside_path$=MID$(zip_name$,SUCC(zip_pos&))
zip_name$=LEFT$(zip_name$,PRED(zip_pos&))
ELSE
zip_inside_path$=""
ENDIF
'
zip_inside_path$=@zipfile_get_slashed_path$(zip_inside_path$)
zip_name$=zip_name$+c0$
'
IF @zipfile_select_same_entries(zip_win&)
zip_result!=@zipfile_delete_entries(kk_win&,FALSE)
ELSE
zip_result!=TRUE
ENDIF
'
IF zip_result!
'
@zipfile_clean_folders_for_entries
IF ldgm_cookie!=FALSE
@ldg_init
ENDIF
IF head_nbline&(1)=1
IF @fil_get_type(@px(1,0))=2
ELSE IF zlib_opened!=FALSE
zlib_opened!=@zlib_open
ENDIF
ELSE IF zlib_opened!=FALSE
zlib_opened!=@zlib_open
ENDIF
IF zlib_opened!
@zipfile_set_method_for_new_entries
ENDIF
'
zip_central_offset%(kk_win&)=0
zip_end_central_offset%(kk_win&)=0
'
IF FRE()<4000
~FRE(0)
ENDIF
'
zip_handle&=GEMDOS(61,L:V:zip_name$,W:2)
zip_buf%=@mxalloc_global(96016,3)
IF zip_handle&>0 AND zip_buf%>0
'
log_pos%=@log_add("--- Archive"+crlf$,log_pos%)
log_pos%=@log_add("Opened: "+@remove_c0$(zip_name$)+crlf$,log_pos%)
@log_write
'
zip_cbuf%=ADD(zip_buf%,&H8002)
'
zipfile_size%=GEMDOS(66,L:0,W:zip_handle&,W:2)
zip_pos%=SUB(zipfile_size%,22)
'
IF zip_pos%>0
DO
~GEMDOS(66,L:zip_pos%,W:zip_handle&,W:0)
~GEMDOS(63,W:zip_handle&,L:4,L:zip_buf%)
DEC zip_pos%
LOOP UNTIL LONG{zip_buf%}=&H504B0506 OR zip_pos%<0
INC zip_pos%
ENDIF
'
zip_end_central_offset%(kk_win&)=zip_pos%
'
IF zip_end_central_offset%(kk_win&)>-1
'
zip_end_cd_size%=SUB(zipfile_size%,zip_pos%)
'
zip_buf_end_cd%=@mxalloc(SHL(SHR(ADD(zip_end_cd_size%,16),4),4),3)
IF zip_buf_end_cd%>0
~GEMDOS(66,L:zip_end_central_offset%(kk_win&),W:zip_handle&,W:0)
~GEMDOS(63,W:zip_handle&,L:zip_end_cd_size%,L:zip_buf_end_cd%)
'
zip_entries_nb&=@zipfile_read_int(ADD(zip_buf_end_cd%,10))
zip_cd_size%=@zipfile_read_long(ADD(zip_buf_end_cd%,12))
zip_central_offset%(kk_win&)=@zipfile_read_long(ADD(zip_buf_end_cd%,16))
ENDIF
ENDIF
'
IF zip_central_offset%(kk_win&)>-1 AND zip_cd_size%>0
'
zip_buf_cd%=@mxalloc(SHL(SHR(ADD(zip_cd_size%,16),4),4),3)
IF zip_buf_cd%>0
~GEMDOS(66,L:zip_central_offset%(kk_win&),W:zip_handle&,W:0)
~GEMDOS(63,W:zip_handle&,L:zip_cd_size%,L:zip_buf_cd%)
ENDIF
ENDIF
'
IF zip_central_offset%(kk_win&)>-1
'
~GEMDOS(66,L:zip_central_offset%(kk_win&),W:zip_handle&,W:0)
'
FOR ipo&=0 TO PRED(head_nbline&(1))
'
zip_fil_ptr%=@px(1,ipo&)
'
IF zip_showed!
CHAR{{OB_SPEC(adtree%(5),4)}}=LEFT$(STR$(copy_nb_files&)+" / "+STR$(copy_total_length%)+str_octets$+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),6)}}=LEFT$(STR$(copy_nb_folders&)+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),8)}}=LEFT$(RIGHT$(@fil_get_name$(zip_fil_ptr%),31)+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),10)}}=""
black_white(5,4,-1)
black_white(5,6,-1)
black_white(5,8,-1)
black_white(5,10,-1)
ENDIF
'
IF FRE()<4000
~FRE(0)
ENDIF
'
@fil_set_offset(zip_fil_ptr%,zip_central_offset%(kk_win&))
'
zip_new_entry_name$=MID$(@fil_get_name$(zip_fil_ptr%),copy_root&)
zip_new_entry_name$=@zipfile_get_slashed_path$(zip_new_entry_name$)
zip_new_entry_name$=zip_inside_path$+zip_new_entry_name$
IF @fil_get_type(zip_fil_ptr%)=2
zip_new_entry_name$=zip_new_entry_name$+"/"
DEC copy_nb_folders&
ELSE
DEC copy_nb_files&
ENDIF
'
zip_name_len&=LEN(zip_new_entry_name$)
zip_has_data_descriptor!=FALSE
'
zip_file_name$=@fil_get_name$(zip_fil_ptr%)+c0$
'
LONG{zip_buf%}=&H504B0304
@zipfile_write_int(ADD(zip_buf%,4),&H14)
IF @fil_get_method(zip_fil_ptr%)=8
'
dummy&=0
SELECT misc_zip_deflate_level&
CASE 1
dummy&=dummy& OR 6
CASE 2
dummy&=dummy& OR 4
CASE 8,9
dummy&=dummy& OR 2
ENDSELECT
'
@zipfile_write_int(ADD(zip_buf%,6),dummy&)
ELSE
@zipfile_write_int(ADD(zip_buf%,6),0)
ENDIF
@zipfile_write_int(ADD(zip_buf%,8),@fil_get_method(zip_fil_ptr%))
@zipfile_write_int(ADD(zip_buf%,10),AND(SHR(@to_gemdos_timedate(@fil_get_date(zip_fil_ptr%)),16),&HFFFF))
@zipfile_write_int(ADD(zip_buf%,12),AND(@to_gemdos_timedate(@fil_get_date(zip_fil_ptr%)),&HFFFF))
@zipfile_write_long(ADD(zip_buf%,14),0)
@zipfile_write_long(ADD(zip_buf%,18),0)
@zipfile_write_long(ADD(zip_buf%,22),0)
@zipfile_write_int(ADD(zip_buf%,26),zip_name_len&)
@zipfile_write_int(ADD(zip_buf%,28),0)
'
CHAR{ADD(zip_buf%,30)}=zip_new_entry_name$
'
~GEMDOS(64,W:zip_handle&,L:ADD(30,zip_name_len&),L:zip_buf%)
'
SELECT @fil_get_type(zip_fil_ptr%)
CASE 1
IF @fil_get_length(zip_fil_ptr%)=0
log_pos%=@log_add("Stored empty file: "+zip_new_entry_name$+crlf$,log_pos%)
@log_write
ELSE
zip_file_handle&=GEMDOS(61,L:V:zip_file_name$,W:0)
IF zip_file_handle&>0
'
zip_file_len%=GEMDOS(66,L:0,W:zip_file_handle&,W:2)
'
~GEMDOS(66,L:0,W:zip_file_handle&,W:0)
'
zip_file_ptr%=0
'
IF @fil_get_method(zip_fil_ptr%)=8
IF zlib_opened!
'
log_pos%=@log_add("Deflating: "+zip_new_entry_name$+crlf$,log_pos%)
@log_write
'
CHAR{{OB_SPEC(adtree%(5),10)}}="DEFLATING..."
black_white(5,10,-1)
'
zlib_src_todo%=zip_file_len%
zlib_src_done%=0
zlib_dst_done%=0
zlib_ret&=0
zlib_crc_val%=0
'
@zlib_clear_stream
'
LONG{ADD(zlib_stream%,0)}=zip_buf%
'
~@zlib_deflate_init(misc_zip_deflate_level&)
'
DO
zip_all_read_before%=LONG{ADD(zlib_stream%,8)}
'
zip_buf_len%=MAX(0,MIN(&H8000,zlib_src_todo%))
~GEMDOS(63,W:zip_file_handle&,L:zip_buf_len%,L:zip_buf%)
'
zlib_crc_val%=@zlib_crc(zlib_crc_val%,zip_buf%,zip_buf_len%)
'
LONG{ADD(zlib_stream%,0)}=zip_buf%
LONG{ADD(zlib_stream%,4)}=zip_buf_len%
LONG{ADD(zlib_stream%,12)}=zip_cbuf%
LONG{ADD(zlib_stream%,16)}=&HC200
'
IF zip_buf_len%=zlib_src_todo%
zlib_ret&=@zlib_deflate(4)
ELSE
zlib_ret&=@zlib_deflate(2)
ENDIF
'
IF zlib_ret&>-1
zip_buf_clen%=SUB(LONG{ADD(zlib_stream%,20)},zlib_dst_done%)
ADD zlib_dst_done%,zip_buf_clen%
'
~GEMDOS(64,W:zip_handle&,L:zip_buf_clen%,L:zip_cbuf%)
ENDIF
'
ADD zlib_src_done%,SUB(LONG{ADD(zlib_stream%,8)},zip_all_read_before%)
SUB zlib_src_todo%,SUB(LONG{ADD(zlib_stream%,8)},zip_all_read_before%)
'
IF zip_showed!
OB_W(adtree%(5),12)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,zlib_src_done%/MAX(1,zip_file_len%))))
black_white(5,12,-1)
ENDIF
LOOP UNTIL zlib_ret&<>0
'
~@zlib_deflate_end
'
IF zlib_ret&<0 OR zlib_ret&=2
log_pos%=@log_add("Deflate error: "+STR$(zlib_ret&)+" ("+@zlib_get_error$(zlib_ret&)+")"+crlf$,log_pos%)
zip_deflate_error!=TRUE
ELSE
log_pos%=@log_add("Deflate succeeded: "+STR$(zip_file_len%)+" bytes ("+STR$(zlib_dst_done%)+" deflated)"+crlf$,log_pos%)
ENDIF
'
@fil_set_crc32(zip_fil_ptr%,zlib_crc_val%)
@fil_set_compressed_size(zip_fil_ptr%,zlib_dst_done%)
@fil_set_length(zip_fil_ptr%,zip_file_len%)
ENDIF
'
ELSE
'
log_pos%=@log_add("Storing: "+zip_new_entry_name$+crlf$,log_pos%)
'
CHAR{{OB_SPEC(adtree%(5),10)}}="STORING..."
black_white(5,10,-1)
'
file_crc_value%=0
'
WHILE zip_file_ptr%<zip_file_len%
'
zip_buf_len%=MIN(16000,SUB(zip_file_len%,zip_file_ptr%))
~GEMDOS(63,W:zip_file_handle&,L:zip_buf_len%,L:zip_buf%)
IF zlib_crc%>0
file_crc_value%=@zlib_crc(file_crc_value%,zip_buf%,zip_buf_len%)
ELSE
file_crc_value%=@crc32(file_crc_value%,zip_buf%,zip_buf_len%)
ENDIF
~GEMDOS(64,W:zip_handle&,L:zip_buf_len%,L:zip_buf%)
'
ADD zip_file_ptr%,16000
'
IF zip_showed!
OB_W(adtree%(5),12)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,MIN(zip_file_ptr%,zip_file_len%)/MAX(1,zip_file_len%))))
black_white(5,12,-1)
ENDIF
WEND
'
log_pos%=@log_add("Store done:"+STR$(zip_file_len%)+" bytes"+crlf$,log_pos%)
'
@fil_set_crc32(zip_fil_ptr%,file_crc_value%)
@fil_set_compressed_size(zip_fil_ptr%,zip_file_len%)
@fil_set_length(zip_fil_ptr%,zip_file_len%)
ENDIF
'
~GEMDOS(62,W:zip_file_handle&)
'
@log_write
ENDIF
ENDIF
CASE 2
log_pos%=@log_add("Stored folder: "+zip_new_entry_name$+crlf$,log_pos%)
@log_write
ENDSELECT
'
ADD zip_central_offset%(kk_win&),30
ADD zip_central_offset%(kk_win&),zip_name_len&
ADD zip_central_offset%(kk_win&),@fil_get_compressed_size(zip_fil_ptr%)
'
IF @fil_get_type(zip_fil_ptr%)=1 AND @fil_get_compressed_size(zip_fil_ptr%)>0
~GEMDOS(66,L:ADD(@fil_get_offset(zip_fil_ptr%),14),W:zip_handle&,W:0)
'
@zipfile_write_long(ADD(zip_buf%,0),@fil_get_crc32(zip_fil_ptr%))
@zipfile_write_long(ADD(zip_buf%,4),@fil_get_compressed_size(zip_fil_ptr%))
@zipfile_write_long(ADD(zip_buf%,8),@fil_get_length(zip_fil_ptr%))
'
~GEMDOS(64,W:zip_handle&,L:12,L:zip_buf%)
'
~GEMDOS(66,L:zip_central_offset%(kk_win&),W:zip_handle&,W:0)
ENDIF
'
IF zip_showed!
ADD copy_current_length%,@fil_get_length(zip_fil_ptr%)
'
OB_W(adtree%(5),12)=2
OB_W(adtree%(5),13)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,copy_current_length%/MAX(1,copy_total_length%))))
black_white(5,11,-1)
ENDIF
'
NEXT ipo&
'
IF zip_cd_size%>0 AND zip_buf_cd%>0
~GEMDOS(64,W:zip_handle&,L:zip_cd_size%,L:zip_buf_cd%)
ENDIF
'
FOR ipo&=0 TO PRED(head_nbline&(1))
'
zip_fil_ptr%=@px(1,ipo&)
'
zip_new_entry_name$=MID$(@fil_get_name$(zip_fil_ptr%),copy_root&)
zip_new_entry_name$=@zipfile_get_slashed_path$(zip_new_entry_name$)
zip_new_entry_name$=zip_inside_path$+zip_new_entry_name$
IF @fil_get_type(zip_fil_ptr%)=2
zip_new_entry_name$=zip_new_entry_name$+"/"
ENDIF
'
zip_name_len&=LEN(zip_new_entry_name$)
'
LONG{zip_buf%}=&H504B0102
@zipfile_write_int(ADD(zip_buf%,4),&H514)
@zipfile_write_int(ADD(zip_buf%,6),&H14)
IF @fil_get_method(zip_fil_ptr%)=8
'
dummy&=0
SELECT misc_zip_deflate_level&
CASE 1
dummy&=dummy& OR 6
CASE 2
dummy&=dummy& OR 4
CASE 8,9
dummy&=dummy& OR 2
ENDSELECT
'
@zipfile_write_int(ADD(zip_buf%,8),dummy&)
ELSE
@zipfile_write_int(ADD(zip_buf%,8),0)
ENDIF
@zipfile_write_int(ADD(zip_buf%,10),@fil_get_method(zip_fil_ptr%))
@zipfile_write_int(ADD(zip_buf%,12),AND(SHR(@to_gemdos_timedate(@fil_get_date(zip_fil_ptr%)),16),&HFFFF))
@zipfile_write_int(ADD(zip_buf%,14),AND(@to_gemdos_timedate(@fil_get_date(zip_fil_ptr%)),&HFFFF))
@zipfile_write_long(ADD(zip_buf%,16),@fil_get_crc32(zip_fil_ptr%))
@zipfile_write_long(ADD(zip_buf%,20),@fil_get_compressed_size(zip_fil_ptr%))
@zipfile_write_long(ADD(zip_buf%,24),@fil_get_length(zip_fil_ptr%))
@zipfile_write_int(ADD(zip_buf%,28),zip_name_len&)
@zipfile_write_int(ADD(zip_buf%,30),0)
@zipfile_write_int(ADD(zip_buf%,32),0)
@zipfile_write_int(ADD(zip_buf%,34),0)
@zipfile_write_int(ADD(zip_buf%,36),0)
@zipfile_write_long(ADD(zip_buf%,38),0)
@zipfile_write_long(ADD(zip_buf%,42),@fil_get_offset(zip_fil_ptr%))
'
CHAR{ADD(zip_buf%,46)}=zip_new_entry_name$
'
~GEMDOS(64,W:zip_handle&,L:ADD(46,zip_name_len&),L:zip_buf%)
'
INC zip_entries_nb&
ADD zip_cd_size%,46
ADD zip_cd_size%,zip_name_len&
'
NEXT ipo&
'
log_pos%=@log_add("Updated central directory"+crlf$,log_pos%)
'
@zipfile_write_int(ADD(zip_buf_end_cd%,8),zip_entries_nb&)
@zipfile_write_int(ADD(zip_buf_end_cd%,10),zip_entries_nb&)
@zipfile_write_long(ADD(zip_buf_end_cd%,12),zip_cd_size%)
@zipfile_write_long(ADD(zip_buf_end_cd%,16),zip_central_offset%(kk_win&))
'
IF zip_end_cd_size%>0 AND zip_buf_end_cd%>0
~GEMDOS(64,W:zip_handle&,L:zip_end_cd_size%,L:zip_buf_end_cd%)
'
log_pos%=@log_add("Updated end of central directory"+crlf$,log_pos%)
ENDIF
'
zip_okay!=TRUE
ENDIF
ENDIF
'
IF zip_handle&>0
~GEMDOS(62,W:zip_handle&)
ENDIF
IF zip_buf%>0
@mxfree(zip_buf%)
zip_buf%=0
ENDIF
IF zip_buf_cd%>0
@mxfree(zip_buf_cd%)
zip_buf_cd%=0
ENDIF
IF zip_buf_end_cd%>0
@mxfree(zip_buf_end_cd%)
zip_buf_end_cd%=0
ENDIF
'
log_pos%=@log_add(crlf$,log_pos%)
@log_write
ENDIF
ENDIF
'
IF zip_deflate_error!=TRUE OR zip_okay!=FALSE
~@alert(1,38)
ENDIF
'
RETURN zip_okay!
ENDFUNC
> FUNCTION zipfile_select_same_entries(zip_win&)
$F%
LOCAL zip_delete_is_needed!
'
zip_delete_is_needed!=FALSE
'
IF head_nbline&(zip_win&)>0
'
record_deselect_all(zip_win&)
'
FOR ipo&=0 TO PRED(head_nbline&(1))
IF FRE()<4000
~FRE(0)
ENDIF
txt$=MID$(@fil_get_name$(@px(1,ipo&)),copy_root&)
txt$=@zipfile_get_slashed_path$(txt$)
txt$=zip_inside_path$+txt$
IF @fil_get_type(@px(1,ipo&))=2
txt$=txt$+"/"
ENDIF
'
ipe&=0
DO
IF FRE()<4000
~FRE(0)
ENDIF
IF @zipentry_get_name$(@px(zip_win&,ipe&))=txt$
record_select(zip_win&,ipe&)
ipe&=head_nbline&(zip_win&)
zip_delete_is_needed!=TRUE
ELSE
INC ipe&
ENDIF
LOOP UNTIL ipe&=head_nbline&(zip_win&)
NEXT ipo&
'
ENDIF
'
RETURN zip_delete_is_needed!
ENDFUNC
> PROCEDURE zipfile_clean_folders_for_entries
LOCAL zip_cand_i&,zip_cand_j&,zip_cand_ptr%,zip_cand_clr!
'
zip_cand_i&=0
DO
zip_cand_clr!=FALSE
zip_cand_ptr%=@px(1,zip_cand_i&)
IF @fil_get_type(zip_cand_ptr%)=2
IF SUCC(zip_cand_i&)=<PRED(head_nbline&(1))
IF FRE()<4000
~FRE(0)
ENDIF
txt$=@fil_get_name$(zip_cand_ptr%)
FOR zip_cand_j&=SUCC(zip_cand_i&) TO PRED(head_nbline&(1))
IF INSTR(@fil_get_name$(@px(1,zip_cand_j&)),txt$)=1
zip_cand_clr!=TRUE
ENDIF
EXIT IF zip_cand_clr!
NEXT zip_cand_j&
ENDIF
ENDIF
IF zip_cand_clr!
record_delete(1,zip_cand_i&)
ELSE
INC zip_cand_i&
ENDIF
LOOP UNTIL zip_cand_i&>=PRED(head_nbline&(1))
'
RETURN
> PROCEDURE zipfile_set_method_for_new_entries
LOCAL zip_cand_i&,zip_cand_j&,zip_cand_ptr%,zip_cand_ext%,zip_cand_clr!
'
zip_cand_i&=0
DO
zip_cand_ptr%=@px(1,zip_cand_i&)
'
IF @fil_get_type(zip_cand_ptr%)=2 OR @fil_get_length(zip_cand_ptr%)<257
@fil_set_method(zip_cand_ptr%,0)
ELSE
IF FRE()<4000
~FRE(0)
ENDIF
'
txt$=@fil_get_name$(zip_cand_ptr%)
dot_pos&=RINSTR(txt$,"\")
IF dot_pos&>0
txt$=MID$(txt$,SUCC(dot_pos&))
ENDIF
dot_pos&=RINSTR(txt$,".")
IF dot_pos&>0
LET txt$=MID$(txt$,SUCC(dot_pos&))
ELSE
LET txt$=""
ENDIF
zip_cand_ext%=CVL(UPPER$(LEFT$(txt$+"    ",4)))
'
zip_cand_clr!=FALSE
FOR zip_cand_j&=1 TO zip_dont_compress_nb&
IF zip_cand_ext%=zip_dont_compress%(zip_cand_j&)
zip_cand_clr!=TRUE
ENDIF
EXIT IF zip_cand_clr!
NEXT zip_cand_j&
'
IF zip_cand_clr!
@fil_set_method(zip_cand_ptr%,0)
ELSE
@fil_set_method(zip_cand_ptr%,8)
ENDIF
ENDIF
INC zip_cand_i&
LOOP UNTIL zip_cand_i&=head_nbline&(1)
'
RETURN
'
> PROCEDURE zipfile_extract_files(kk_win&)
'
~@zipfile_select_entries(kk_win&)
'
obj_unhide(5,14)
obj_enable(5,14)
obj_deselect(5,14)
obj_unhide(5,15)
obj_enable(5,15)
obj_deselect(5,15)
'
obj_select(5,1)
CHAR{OB_SPEC(adtree%(5),1)}=CHAR{OB_SPEC(adtree%(alert_tree&),43)}
obj_hide(5,2)
'
CHAR{{OB_SPEC(adtree%(5),4)}}=LEFT$(STR$(zipe_nb_files&)+" / "+STR$(zipe_total_length%)+str_octet$,31)
CHAR{{OB_SPEC(adtree%(5),6)}}=LEFT$(STR$(zipe_nb_folders&),31)
CHAR{{OB_SPEC(adtree%(5),8)}}=""
CHAR{{OB_SPEC(adtree%(5),10)}}=""
OB_W(adtree%(5),12)=2
OB_W(adtree%(5),13)=2
'
proceed!=FALSE
IF misc_confirm_send!
control_form(5)
form_result&=0
DO
form_result&=FORM_DO(adtree%(5),0)
LOOP UNTIL form_result&=14 OR form_result&=15
IF form_result&=14
obj_disable(5,14)
obj_deselect(5,14)
black_white(5,14,-1)
proceed!=TRUE
ELSE IF result&=15
obj_deselect(5,15)
proceed!=FALSE
ENDIF
ELSE
obj_hide(5,14)
obj_hide(5,15)
control_form(5)
proceed!=TRUE
ENDIF
'
IF proceed!=TRUE
'
~@zipfile_extract_entries(kk_win&,TRUE)
'
@display_refresh_directories
ENDIF
'
@uncontrol_form(5)
'
RETURN
> FUNCTION zipfile_extract_entries(kk_win&,zip_showed!)
LOCAL zip_ext_ptr%,zip_ext_name$,zip_is_folder!,zip_ext_offset%,zip_ext_date%
LOCAL rec_handle&,rec_behavior&,rec_choice&,rec_remain&,rec_drive&
LOCAL extract_is_file!,extract_aborted!
$F%
zip_okay!=TRUE
'
zip_all_selected!=@zipfile_select_entries(kk_win&)
'
IF kk_win&=3
zip_name$=CHAR{local_path0%}
ELSE IF kk_win&=4
zip_name$=CHAR{local_path1%}
ELSE
zip_name$=""
ENDIF
'
zip_win&=ADD(2,kk_win&)
'
dummy$=UPPER$(zip_name$)
'
IF RIGHT$(dummy$,4)=".ZIP" OR INSTR(dummy$,".ZIP>")>0
'
zip_pos&=RINSTR(dummy$,">")
IF zip_pos&>0
zip_inside_path$=MID$(zip_name$,SUCC(zip_pos&))
zip_name$=LEFT$(zip_name$,PRED(zip_pos&))
ELSE
zip_inside_path$=""
ENDIF
'
zip_inside_path$=@zipfile_get_slashed_path$(zip_inside_path$)
zip_inside_root&=SUCC(LEN(zip_inside_path$))
zip_name$=zip_name$+c0$
'
zip_handle&=GEMDOS(61,L:V:zip_name$,W:0)
zip_buf%=@mxalloc_global(&H10010,3)
IF zip_handle&>0 AND zip_buf%>0
'
log_pos%=@log_add("--- Extract"+crlf$,log_pos%)
log_pos%=@log_add("Opened: "+@remove_c0$(zip_name$)+crlf$,log_pos%)
@log_write
'
zip_ubuf%=ADD(zip_buf%,&H8002)
'
rec_behavior&=-1
extract_aborted!=FALSE
'
FOR ipo&=0 TO PRED(head_nbline&(zip_win&))
IF @is_record_selected(zip_win&,ipo&)=TRUE
'
zip_ext_ptr%=@px(zip_win&,ipo&)
'
IF zip_showed!
CHAR{{OB_SPEC(adtree%(5),4)}}=LEFT$(STR$(zipe_nb_files&)+" / "+STR$(zipe_total_length%)+str_octets$+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),6)}}=LEFT$(STR$(zipe_nb_folders&)+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),8)}}=LEFT$(RIGHT$(@zipentry_get_name$(zip_ext_ptr%),31)+SPACE$(31),31)
CHAR{{OB_SPEC(adtree%(5),10)}}=""
black_white(5,4,-1)
black_white(5,6,-1)
black_white(5,8,-1)
black_white(5,10,-1)
ENDIF
'
IF FRE()<4000
~FRE(0)
ENDIF
'
zip_ext_name$=@zipentry_get_name$(zip_ext_ptr%)
zip_is_folder!=(RIGHT$(zip_ext_name$)="/")
zip_ext_name$=MID$(zip_ext_name$,zip_inside_root&)
IF kk_win&=3
zip_ext_name$=CHAR{local_path1%}+zip_ext_name$
zip_ext_root&=LEN(CHAR{local_path1%})
rec_drive&=SUB(BYTE{local_path1%},64)
ELSE IF kk_win&=4
zip_ext_name$=CHAR{local_path0%}+zip_ext_name$
zip_ext_root&=LEN(CHAR{local_path0%})
rec_drive&=SUB(BYTE{local_path0%},64)
ENDIF
zip_ext_name$=@zipfile_get_antislashed_path$(zip_ext_name$)
'
IF misc_keep_date!
zip_ext_date%=@zipentry_get_date(zip_ext_ptr%)
zip_ext_date%=@gemdos_to_troll_timestamp(SHR(zip_ext_date% AND &HFFFF0000,16),zip_ext_date% AND &HFFFF)
zip_ext_date%=@to_gemdos_timedate(zip_ext_date%)
ELSE
zip_ext_date%=SHL(GEMDOS(42),16) OR GEMDOS(44)
ENDIF
'
IF zip_is_folder!
@local_create_folders(zip_ext_name$,zip_ext_root&)
zipe_nb_folders&=MAX(0,PRED(zipe_nb_folders&))
'
log_pos%=@log_add("Created folders: "+zip_ext_name$+crlf$,log_pos%)
@log_write
ELSE
'
zip_ext_name$=zip_ext_name$+c0$
'
zip_pos&=RINSTR(zip_ext_name$,"\")
IF zip_pos&>0
@local_create_folders(LEFT$(zip_ext_name$,zip_pos&),zip_ext_root&)
ENDIF
'
extract_skip!=FALSE
'
IF @has_enough_space(rec_drive&,@zipentry_get_size(zip_ext_ptr%))=FALSE
extract_skip!=TRUE
log_pos%=@log_add("Not enough space on disk to extract: "+@remove_c0$(zip_ext_name$)+crlf$,log_pos%)
@log_write
ENDIF
'
IF extract_skip!=FALSE
rec_handle&=GEMDOS(61,L:V:zip_ext_name$,W:0)
IF rec_handle&>0
~GEMDOS(62,W:rec_handle&)
'
IF misc_choice_existing_file_type&=0
'
CHAR{{OB_SPEC(adtree%(16),2)}}=LEFT$(@zipentry_get_name$(zip_ext_ptr%),30)
rec_remain&=10
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
obj_deselect(16,7)
'
uncontrol_form(5)
control_form(16)
form_result&=0
DO
evnt&=@ev_multi(&X100011,258,3,0,1000,mo_x&,mo_y&,mo_k&,dummy&,m_clavier&,mo_c&)
IF BTST(evnt&,0)
IF BYTE(m_clavier&)=13
black_white(16,7,1)
form_result&=7
ENDIF
ENDIF
IF BTST(evnt&,1)
IF mo_k&=1 AND mo_c&=1
delay
pop_result&=OBJC_FIND(adtree%(16),0,9,mo_x&,mo_y&)
IF pop_result&>2 AND pop_result&<7
IF @tst_enabled(16,pop_result&)
IF rec_choice&>0
black_white(16,ADD(2,rec_choice&),0)
ENDIF
rec_choice&=SUB(pop_result&,2)
black_white(16,ADD(2,rec_choice&),1)
ENDIF
ELSE IF pop_result&=7
black_white(16,7,1)
form_result&=7
ENDIF
ENDIF
ENDIF
IF BTST(evnt&,5)
DEC rec_remain&
CHAR{{OB_SPEC(adtree%(16),9)}}=LEFT$(STR$(rec_remain&)+"s  ",6)
black_white(16,9,-1)
ENDIF
LOOP UNTIL form_result&=7 OR rec_remain&=0
uncontrol_form(16)
control_form(5)
obj_deselect(16,7)
'
rec_behavior&=rec_choice&
ELSE
rec_behavior&=misc_choice_existing_file_type&
ENDIF
'
SELECT rec_behavior&
CASE 1 ! rewrite
CASE 2 ! rename
zip_ext_name$=@get_other_name$(zip_ext_name$)
CASE 3 ! skip
extract_skip!=TRUE
CASE 4 ! abort
extract_aborted!=TRUE
ENDSELECT
'
ENDIF
ENDIF
'
'
IF extract_skip!=FALSE AND extract_aborted!=FALSE
'
zip_ext_offset%=@zipentry_get_offset(zip_ext_ptr%)
'
~GEMDOS(66,L:zip_ext_offset%,W:zip_handle&,W:0)
'
~GEMDOS(63,W:zip_handle&,L:30,L:zip_buf%)
'
IF LONG{zip_buf%}=&H504B0304
'
ADD zip_ext_offset%,30
ADD zip_ext_offset%,@zipfile_read_int(ADD(zip_buf%,26))
ADD zip_ext_offset%,@zipfile_read_int(ADD(zip_buf%,28))
'
~GEMDOS(66,L:zip_ext_offset%,W:zip_handle&,W:0)
'
IF BTST(@zipentry_get_flags(zip_ext_ptr%),0)
log_pos%=@log_add("Decrypration is not supported: "+@remove_c0$(zip_ext_name$)+crlf$,log_pos%)
@log_write
ELSE
'
SELECT @zipentry_get_method(zip_ext_ptr%)
CASE 8
'
log_pos%=@log_add("Inflating: "+@remove_c0$(zip_ext_name$)+crlf$,log_pos%)
@log_write
'
CHAR{{OB_SPEC(adtree%(5),10)}}="INFLATING..."
black_white(5,10,-1)
'
IF ldgm_cookie!=FALSE
@ldg_init
ENDIF
IF zlib_opened!=FALSE
zlib_opened!=@zlib_open
ENDIF
'
IF zlib_opened!
'
IF @s_exist(zip_ext_name$)
~GEMDOS(65,L:V:zip_ext_name$)
'
log_pos%=@log_add("Deleted old file"+crlf$,log_pos%)
@log_write
ENDIF
'
zip_file_len%=@zipentry_get_size(zip_ext_ptr%)
zip_file_clen%=@zipentry_get_compressed_size(zip_ext_ptr%)
'
zip_file_handle&=GEMDOS(60,L:V:zip_ext_name$,W:0)
IF zip_file_handle&>0
'
zlib_src_todo%=zip_file_clen%
zlib_src_done%=0
zlib_dst_done%=0
zlib_ret&=0
zlib_crc_val%=0
'
@zlib_clear_stream
'
LONG{ADD(zlib_stream%,0)}=zip_buf%
LONG{ADD(zlib_stream%,4)}=&H8000
LONG{ADD(zlib_stream%,12)}=zip_ubuf%
LONG{ADD(zlib_stream%,16)}=&H8000
'
~@zlib_inflate_init
'
DO
~GEMDOS(66,L:ADD(zip_ext_offset%,zlib_src_done%),W:zip_handle&,W:0)
'
zip_buf_clen%=MIN(&H8000,zlib_src_todo%)
~GEMDOS(63,W:zip_handle&,L:zip_buf_clen%,L:zip_buf%)
'
LONG{ADD(zlib_stream%,0)}=zip_buf%
LONG{ADD(zlib_stream%,4)}=zip_buf_clen%
LONG{ADD(zlib_stream%,12)}=zip_ubuf%
LONG{ADD(zlib_stream%,16)}=&H8000
'
zlib_ret&=@zlib_inflate(5)
'
IF zlib_ret&>-1
zip_buf_len%=MAX(0,SUB(&H8000,LONG{ADD(zlib_stream%,16)}))
IF zip_buf_len%>0
'
ADD zlib_dst_done%,zip_buf_len%
'
zlib_crc_val%=@zlib_crc(zlib_crc_val%,zip_ubuf%,zip_buf_len%)
'
~GEMDOS(64,W:zip_file_handle&,L:zip_buf_len%,L:zip_ubuf%)
ENDIF
ENDIF
'
zlib_src_done%=LONG{ADD(zlib_stream%,8)}
zlib_src_todo%=SUB(zip_file_clen%,LONG{ADD(zlib_stream%,8)})
'
IF zip_showed!
OB_W(adtree%(5),12)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,zlib_dst_done%/MAX(1,zip_file_len%))))
black_white(5,12,-1)
ENDIF
'
LOOP UNTIL zlib_ret&<>0 OR zlib_dst_done%>=zip_file_len%
'
~@zlib_inflate_end
'
IF zlib_ret&<0 OR zlib_ret&=2
log_pos%=@log_add("Inflate error: "+STR$(zlib_ret&)+" ("+@zlib_get_error$(zlib_ret&)+")"+crlf$,log_pos%)
ELSE
log_pos%=@log_add("Inflate succeeded: "+STR$(zip_file_len%)+" bytes ("+STR$(zip_file_clen%)+" deflated)"+crlf$,log_pos%)
ENDIF
'
IF zlib_crc_val%<>0
IF @zipentry_get_crc32(zip_ext_ptr%)<>zlib_crc_val%
log_pos%=@log_add("CRC error, read:"+HEX$(zlib_crc_val%,8)+" expected:"+HEX$(@zipentry_get_crc32(zip_ext_ptr%),8)+crlf$,log_pos%)
zip_okay!=FALSE
ENDIF
ENDIF
'
~GEMDOS(87,L:V:zip_ext_date%,W:zip_file_handle&,W:1)
'
~GEMDOS(62,W:zip_file_handle&)
ENDIF
'
zipe_nb_files&=MAX(0,PRED(zipe_nb_files&))
ELSE
log_pos%=@log_add("Could not inflate file: library not loaded"+crlf$,log_pos%)
ENDIF
@log_write
'
CASE 0
'
log_pos%=@log_add("Restoring: "+@remove_c0$(zip_ext_name$)+crlf$,log_pos%)
'
CHAR{{OB_SPEC(adtree%(5),10)}}="RESTORING..."
black_white(5,10,-1)
'
IF @s_exist(zip_ext_name$)
~GEMDOS(65,L:V:zip_ext_name$)
'
log_pos%=@log_add("Deleted old file"+crlf$,log_pos%)
ENDIF
'
zip_file_handle&=GEMDOS(60,L:V:zip_ext_name$,W:0)
IF zip_file_handle&>0
'
zip_file_len%=@zipentry_get_size(zip_ext_ptr%)
'
file_crc_value%=0
zip_file_ptr%=0
WHILE zip_file_ptr%<zip_file_len%
'
zip_buf_len%=MIN(16000,SUB(zip_file_len%,zip_file_ptr%))
~GEMDOS(63,W:zip_handle&,L:zip_buf_len%,L:zip_buf%)
IF zlib_crc%>0
file_crc_value%=@zlib_crc(file_crc_value%,zip_buf%,zip_buf_len%)
ELSE
file_crc_value%=@crc32(file_crc_value%,zip_buf%,zip_buf_len%)
ENDIF
~GEMDOS(64,W:zip_file_handle&,L:zip_buf_len%,L:zip_buf%)
'
ADD zip_file_ptr%,16000
'
IF zip_showed!
OB_W(adtree%(5),12)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,MIN(zip_file_ptr%,zip_file_len%)/MAX(1,zip_file_len%))))
black_white(5,12,-1)
ENDIF
WEND
'
~GEMDOS(87,L:V:zip_ext_date%,W:zip_file_handle&,W:1)
'
~GEMDOS(62,W:zip_file_handle&)
'
log_pos%=@log_add("Restore done: "+STR$(zip_file_len%)+" bytes"+crlf$,log_pos%)
'
zipe_nb_files&=MAX(0,PRED(zipe_nb_files&))
'
IF @zipentry_get_crc32(zip_ext_ptr%)<>file_crc_value%
log_pos%=@log_add("CRC error, read:"+HEX$(file_crc_value%,8)+" expected:"+HEX$(@zipentry_get_crc32(zip_ext_ptr%),8)+crlf$,log_pos%)
zip_okay!=FALSE
ENDIF
'
ENDIF
@log_write
DEFAULT
log_pos%=@log_add("Method of compression not supported ("+STR$(@zipentry_get_method(zip_ext_ptr%))+")"+crlf$,log_pos%)
@log_write
zip_okay!=FALSE
ENDSELECT
ENDIF
ENDIF
'
ENDIF
'
IF zip_showed!
ADD zipe_current_length%,@zipentry_get_size(zip_ext_ptr%)
'
OB_W(adtree%(5),12)=2
OB_W(adtree%(5),13)=MAX(2,PRED(OB_W(adtree%(5),11)*MIN(1,zipe_current_length%/MAX(1,zipe_total_length%))))
black_white(5,11,-1)
ENDIF
ENDIF
ENDIF
EXIT IF extract_aborted!
NEXT ipo&
'
IF zip_okay!=FALSE
~@alert(1,39)
ENDIF
'
ENDIF
IF zip_handle&>0
~GEMDOS(62,W:zip_handle&)
ENDIF
IF zip_buf%>0
@mxfree(zip_buf%)
zip_buf%=0
ENDIF
'
ENDIF
'
log_pos%=@log_add(crlf$,log_pos%)
@log_write
'
RETURN zip_okay!
ENDFUNC
'
> PROCEDURE zipfile_delete_files(kk_win&)
LOCAL zip_handle_new&,zip_handle_old&,zip_entry_ptr%,zip_keep_offset%
'
IF misc_confirm_delete!
proceed!=(@alert(1,37)=1)
ELSE
proceed!=TRUE
ENDIF
'
IF proceed!
'
IF @zipfile_delete_entries(kk_win&,@zipfile_select_entries(kk_win&))
zip_filename$(kk_win&)=SPACE$(3)
@zipfile_get_directory(kk_win&)
ENDIF
'
ENDIF
'
RETURN
> FUNCTION zipfile_delete_entries(kke_win&,zip_keep_folder!)
$F%
LOCAL zip_handle_new&,zip_handle_old&,zip_entry_ptr%,zip_keep_offset%
'
zip_pos&=RINSTR(UPPER$(zip_name$),".ZIP")
IF zip_pos&>0
zip_temp_name$=LEFT$(zip_name$,PRED(zip_pos&))
ELSE
zip_temp_name$=zip_name$
ENDIF
zip_temp_name$=zip_temp_name$+".ZMP"+c0$
zip_name$=zip_name$+c0$
'
IF FRE()<4000
~FRE(0)
ENDIF
'
IF @s_exist(zip_temp_name$)
~GEMDOS(65,L:V:zip_temp_name$)
ENDIF
'
zip_okay!=FALSE
'
zip_handle_new&=GEMDOS(60,L:V:zip_temp_name$,W:0)
zip_handle_old&=GEMDOS(61,L:V:zip_name$,W:0)
zip_buf%=@mxalloc(64016,3)
IF zip_handle_new&>0 AND zip_handle_old&>0 AND zip_buf%>0
'
log_pos%=@log_add("--- Delete"+crlf$,log_pos%)
log_pos%=@log_add("Opened: "+@remove_c0$(zip_name$)+crlf$,log_pos%)
log_pos%=@log_add("Created temp achive: "+@remove_c0$(zip_temp_name$)+crlf$,log_pos%)
@log_write
'
zipfile_size%=0
zip_new_entries_nb&=0
zip_new_central_offset%=0
zip_new_central_size%=0
zip_okay!=TRUE
'
FOR zip_i&=0 TO PRED(head_nbline&(zip_win&))
IF @is_record_selected(zip_win&,zip_i&)=FALSE
INC zip_new_entries_nb&
'
zip_entry_ptr%=@px(zip_win&,zip_i&)
'
zip_offset%=@zipentry_get_offset(zip_entry_ptr%)
'
IF zip_offset%>-1
'
~GEMDOS(66,L:zip_offset%,W:zip_handle_old&,W:0)
~GEMDOS(63,W:zip_handle_old&,L:30,L:zip_buf%)
'
IF LONG{zip_buf%}=&H504B0304
'
zip_offset%=zipfile_size%
'
ADD zipfile_size%,30
zip_name_len&=@zipfile_read_int(ADD(zip_buf%,26))
ADD zipfile_size%,zip_name_len&
zip_temp_len&=zip_name_len&
zip_extra_len&=@zipfile_read_int(ADD(zip_buf%,28))
ADD zipfile_size%,zip_extra_len&
ADD zip_temp_len&,zip_extra_len&
'
IF zip_temp_len&>0
~GEMDOS(63,W:zip_handle_old&,L:zip_temp_len&,L:ADD(zip_buf%,30))
ENDIF
~GEMDOS(64,W:zip_handle_new&,L:ADD(30,zip_temp_len&),L:zip_buf%)
'
zip_file_len%=@zipfile_read_long(ADD(zip_buf%,18))
zip_has_data_descriptor!=BTST(@zipfile_read_int(ADD(zip_buf%,6)),3)
'
IF zip_file_len%>-1
ADD zipfile_size%,zip_file_len%
'
zip_file_ptr%=0
WHILE zip_file_ptr%<zip_file_len%
zip_buf_len%=MIN(64000,SUB(zip_file_len%,zip_file_ptr%))
~GEMDOS(63,W:zip_handle_old&,L:zip_buf_len%,L:zip_buf%)
~GEMDOS(64,W:zip_handle_new&,L:zip_buf_len%,L:zip_buf%)
ADD zip_file_ptr%,64000
WEND
'
IF zip_has_data_descriptor!
~GEMDOS(63,W:zip_handle_old&,L:16,L:zip_buf%)
IF LONG{zip_buf%}=&H504B0708
ADD zipfile_size%,16
~GEMDOS(64,W:zip_handle_new&,L:16,L:zip_buf%)
ELSE
ADD zipfile_size%,12
~GEMDOS(64,W:zip_handle_new&,L:12,L:zip_buf%)
ENDIF
ENDIF
'
@zipentry_set_offset(zip_entry_ptr%,zip_offset%)
ELSE
zip_okay!=FALSE
ENDIF
ENDIF
ELSE
zip_okay!=FALSE
ENDIF
'
ENDIF
EXIT IF zip_okay!=FALSE
NEXT zip_i&
'
IF zip_okay!=TRUE
'
log_pos%=@log_add("Copied relevant entries in temp archive"+crlf$,log_pos%)
@log_write
'
IF zip_keep_folder!
IF LEN(zip_inside_path$)>0
zip_keep_offset%=zipfile_size%
INC zip_new_entries_nb&
'
zip_name_len&=LEN(zip_inside_path$)
'
LONG{zip_buf%}=&H504B0304
@zipfile_write_int(ADD(zip_buf%,4),10)
@zipfile_write_int(ADD(zip_buf%,6),0)
@zipfile_write_int(ADD(zip_buf%,8),0)
@zipfile_write_long(ADD(zip_buf%,10),SHL(GEMDOS(42),16) OR GEMDOS(44))
@zipfile_write_long(ADD(zip_buf%,14),0)
@zipfile_write_long(ADD(zip_buf%,18),0)
@zipfile_write_long(ADD(zip_buf%,22),0)
@zipfile_write_int(ADD(zip_buf%,26),zip_name_len&)
@zipfile_write_int(ADD(zip_buf%,28),0)
'
CHAR{ADD(zip_buf%,30)}=zip_inside_path$
'
~GEMDOS(64,W:zip_handle_new&,L:ADD(30,zip_name_len&),L:zip_buf%)
'
ADD zipfile_size%,30
ADD zipfile_size%,zip_name_len&
'
log_pos%=@log_add("Added entry: "+zip_inside_path$+" (empty folder)"+crlf$,log_pos%)
@log_write
'
ENDIF
ENDIF
ENDIF
'
IF zip_okay!
'
zip_new_central_offset%=zipfile_size%
zip_new_central_size%=0
'
FOR zip_i&=0 TO PRED(head_nbline&(zip_win&))
IF @is_record_selected(zip_win&,zip_i&)=FALSE
'
zip_entry_ptr%=@px(zip_win&,zip_i&)
'
~GEMDOS(66,L:@zipentry_get_pointer(zip_entry_ptr%),W:zip_handle_old&,W:0)
~GEMDOS(63,W:zip_handle_old&,L:46,L:zip_buf%)
'
IF LONG{zip_buf%}=&H504B0102
'
@zipfile_write_int(ADD(zip_buf%,34),0)
@zipfile_write_long(ADD(zip_buf%,42),@zipentry_get_offset(zip_entry_ptr%))
'
zip_name_len&=@zipfile_read_int(ADD(zip_buf%,28))
zip_temp_len&=zip_name_len&
zip_extra_len&=@zipfile_read_int(ADD(zip_buf%,30))
ADD zip_temp_len&,zip_extra_len&
zip_comment_len&=@zipfile_read_int(ADD(zip_buf%,32))
ADD zip_temp_len&,zip_comment_len&
'
IF zip_temp_len&>0
~GEMDOS(63,W:zip_handle_old&,L:zip_temp_len&,L:ADD(zip_buf%,46))
ENDIF
~GEMDOS(64,W:zip_handle_new&,L:ADD(46,zip_temp_len&),L:zip_buf%)
'
ADD zip_new_central_size%,46
ADD zip_new_central_size%,zip_temp_len&
'
ELSE IF LONG{zip_buf%}=&H504B0505
'
zip_sign_len&=@zipfile_read_int(ADD(zip_buf%,4))
'
IF zip_sign_len&>0
~GEMDOS(63,W:zip_handle_old&,L:zip_sign_len&,L:ADD(zip_buf%,6))
ENDIF
~GEMDOS(64,W:zip_handle_new&,L:ADD(6,zip_sign_len&),L:zip_buf%)
'
ADD zip_new_central_size%,6
ADD zip_new_central_size%,zip_sign_len&
'
ELSE
zip_okay!=FALSE
ENDIF
ENDIF
EXIT IF zip_okay!=FALSE
NEXT zip_i&
ENDIF
'
IF zip_okay!=TRUE
'
log_pos%=@log_add("Updated central directory"+crlf$,log_pos%)
@log_write
'
IF zip_keep_folder!
IF LEN(zip_inside_path$)>0
'
zip_name_len&=LEN(zip_inside_path$)
'
LONG{zip_buf%}=&H504B0102
@zipfile_write_int(ADD(zip_buf%,4),&H51A)
@zipfile_write_int(ADD(zip_buf%,6),&H14)
@zipfile_write_int(ADD(zip_buf%,8),0)
@zipfile_write_int(ADD(zip_buf%,10),0)
@zipfile_write_int(ADD(zip_buf%,12),SHL(GEMDOS(42),16) OR GEMDOS(44))
@zipfile_write_long(ADD(zip_buf%,16),0)
@zipfile_write_long(ADD(zip_buf%,20),0)
@zipfile_write_long(ADD(zip_buf%,24),0)
@zipfile_write_int(ADD(zip_buf%,28),zip_name_len&)
@zipfile_write_int(ADD(zip_buf%,30),0)
@zipfile_write_int(ADD(zip_buf%,32),0)
@zipfile_write_int(ADD(zip_buf%,34),0)
@zipfile_write_int(ADD(zip_buf%,36),0)
@zipfile_write_long(ADD(zip_buf%,38),0)
@zipfile_write_long(ADD(zip_buf%,42),zip_keep_offset%)
'
CHAR{ADD(zip_buf%,46)}=zip_inside_path$
'
~GEMDOS(64,W:zip_handle_new&,L:ADD(46,zip_name_len&),L:zip_buf%)
'
ADD zip_new_central_size%,46
ADD zip_new_central_size%,zip_name_len&
'
log_pos%=@log_add("Added entry: "+zip_inside_path$+" (in central directory)"+crlf$,log_pos%)
@log_write
'
ENDIF
ENDIF
ENDIF
'
IF zip_okay!
'
~GEMDOS(66,L:zip_end_central_offset%(kke_win&),W:zip_handle_old&,W:0)
~GEMDOS(63,W:zip_handle_old&,L:22,L:zip_buf%)
'
IF LONG{zip_buf%}=&H504B0506
'
@zipfile_write_int(ADD(zip_buf%,4),0)
@zipfile_write_int(ADD(zip_buf%,6),0)
@zipfile_write_int(ADD(zip_buf%,8),zip_new_entries_nb&)
@zipfile_write_int(ADD(zip_buf%,10),zip_new_entries_nb&)
@zipfile_write_long(ADD(zip_buf%,12),zip_new_central_size%)
@zipfile_write_long(ADD(zip_buf%,16),zip_new_central_offset%)
'
zip_comment_len&=@zipfile_read_int(ADD(zip_buf%,20))
IF zip_comment_len&>0
~GEMDOS(63,W:zip_handle_old&,L:zip_comment_len&,L:ADD(zip_buf%,22))
ENDIF
~GEMDOS(64,W:zip_handle_new&,L:ADD(22,zip_comment_len&),L:zip_buf%)
'
log_pos%=@log_add("Updated end of central directory"+crlf$,log_pos%)
@log_write
'
ENDIF
ENDIF
ENDIF
IF zip_handle_new&>0
~GEMDOS(62,W:zip_handle_new&)
ENDIF
IF zip_handle_old&>0
~GEMDOS(62,W:zip_handle_old&)
ENDIF
IF zip_buf%>0
@mxfree(zip_buf%)
zip_buf%=0
ENDIF
'
IF zip_okay!
IF FRE()<4000
~FRE(0)
ENDIF
~GEMDOS(65,L:V:zip_name$)
~GEMDOS(86,W:0,L:V:zip_temp_name$,L:V:zip_name$)
'
log_pos%=@log_add("Deleted old archive, renamed temp file into new archive"+crlf$,log_pos%)
ENDIF
'
log_pos%=@log_add(crlf$,log_pos%)
@log_write
'
RETURN zip_okay!
ENDFUNC
> FUNCTION zipfile_select_entries(kk_win&)
$F%
LOCAL zip_all_item_selected!
'
zipe_nb_files&=0
zipe_nb_folders&=0
zipe_current_length%=0
zipe_total_length%=0
'
zip_all_item_selected!=TRUE
'
IF kk_win&=3
zip_name$=CHAR{local_path0%}
ELSE IF kk_win&=4
zip_name$=CHAR{local_path1%}
ELSE
zip_name$=""
ENDIF
'
dummy$=UPPER$(zip_name$)
'
IF RIGHT$(dummy$,4)=".ZIP" OR INSTR(dummy$,".ZIP>")>0
'
zip_pos&=RINSTR(dummy$,">")
IF zip_pos&>0
zip_inside_path$=MID$(zip_name$,SUCC(zip_pos&))
zip_name$=LEFT$(zip_name$,PRED(zip_pos&))
ELSE
zip_inside_path$=""
ENDIF
'
zip_inside_path$=@zipfile_get_slashed_path$(zip_inside_path$)
'
zip_win&=ADD(2,kk_win&)
'
IF head_nbline&(zip_win&)>0
'
record_deselect_all(zip_win&)
'
FOR ipo&=0 TO PRED(head_nbline&(kk_win&))
IF FRE()<4000
~FRE(0)
ENDIF
txt$=@loc_get_name$(@px(kk_win&,ipo&))
IF txt$=".."
ELSE IF @is_record_selected(kk_win&,ipo&)
txt$=zip_inside_path$+txt$
SELECT @loc_get_type(@px(kk_win&,ipo&))
CASE 2
txt$=txt$+"/"
FOR ipe&=0 TO PRED(head_nbline&(zip_win&))
IF FRE()<4000
~FRE(0)
ENDIF
IF INSTR(@zipentry_get_name$(@px(zip_win&,ipe&)),txt$)=1
INC zipe_nb_files&
ADD zipe_total_length%,@zipentry_get_size(@px(zip_win&,ipe&))
record_select(zip_win&,ipe&)
ENDIF
NEXT ipe&
CASE 1
ipe&=0
DO
IF FRE()<4000
~FRE(0)
ENDIF
IF @zipentry_get_name$(@px(zip_win&,ipe&))=txt$
INC zipe_nb_files&
ADD zipe_total_length%,@zipentry_get_size(@px(zip_win&,ipe&))
record_select(zip_win&,ipe&)
ipe&=head_nbline&(zip_win&)
ELSE
INC ipe&
ENDIF
LOOP UNTIL ipe&=head_nbline&(zip_win&)
ENDSELECT
ELSE
zip_all_item_selected!=FALSE
ENDIF
NEXT ipo&
'
ENDIF
ENDIF
'
RETURN zip_all_item_selected!
ENDFUNC
'
> PROCEDURE zipfile_get_directory(kk_win&)
LOCAL zip_add_ptr%,zip_ent_adr%
'
d_total_length%=0
'
IF kk_win&=3
zip_name$=CHAR{local_path0%}
ELSE IF kk_win&=4
zip_name$=CHAR{local_path1%}
ENDIF
'
dummy$=UPPER$(zip_name$)
'
IF RIGHT$(dummy$,4)=".ZIP" OR INSTR(dummy$,".ZIP>")>0
'
zip_inside_path$=""
'
zip_pos&=RINSTR(zip_name$,">")
IF zip_pos&>0
zip_inside_path$=@zipfile_get_slashed_path$(MID$(zip_name$,SUCC(zip_pos&)))
zip_name$=LEFT$(zip_name$,PRED(zip_pos&))
ENDIF
'
IF kk_win&=3
IF zip_name$<>zip_filename$(3)
zip_filename$(3)=zip_name$
~@zipfile_read_entries(zip_name$,3)
ENDIF
ELSE IF kk_win&=4
IF zip_name$<>zip_filename$(4)
zip_filename$(4)=zip_name$
~@zipfile_read_entries(zip_name$,4)
ENDIF
ENDIF
'
zip_add_ptr%=record_buffer%(kk_win&)
'
zip_win&=ADD(2,kk_win&)
'
old_record_start&=record_start&(kk_win&)
'
@record_close(kk_win&)
@record_open(kk_win&,64,record_len&(kk_win&),field_bar_obj&(kk_win&))
'
IF head_nbline&(zip_win&)>0
'
@mouse_busy
'
FOR zip_i&=0 TO PRED(head_nbline&(zip_win&))
'
IF FRE()<4000
~FRE(0)
ENDIF
record_clear_buffer(kk_win&)
'
zip_ent_adr%=@px(zip_win&,zip_i&)
'
z_type&=-1
z_file$=@zipentry_get_name$(zip_ent_adr%)
'
IF LEN(zip_inside_path$)>0
zip_pos&=INSTR(z_file$,zip_inside_path$)
IF zip_pos&=1
z_file$=MID$(z_file$,SUCC(LEN(zip_inside_path$)))
ELSE
z_file$=""
ENDIF
ENDIF
'
IF LEN(z_file$)>0
zip_pos&=INSTR(z_file$,"/")
'
IF zip_pos&>0 ! folder
'
z_file$=LEFT$(z_file$,PRED(zip_pos&))
'
IF @zipfile_is_already_listed(kk_win&,z_file$)=FALSE
z_type&=2
'
@loc_set_number(zip_add_ptr%,0)
@loc_set_name(zip_add_ptr%,z_file$)
@loc_set_type(zip_add_ptr%,z_type&)
ENDIF
'
ELSE ! file
z_type&=1
'
@loc_set_number(zip_add_ptr%,0)
@loc_set_name(zip_add_ptr%,z_file$)
@loc_set_type(zip_add_ptr%,z_type&)
'
z_val%=@zipentry_get_size(zip_ent_adr%)
ADD d_total_length%,z_val%
@loc_set_length(zip_add_ptr%,z_val%)
'
z_val%=@zipentry_get_date(zip_ent_adr%)
@loc_set_date(zip_add_ptr%,@gemdos_to_troll_timestamp(SHR(z_val% AND &HFFFF0000,16),z_val% AND &HFFFF))
'
ENDIF
ENDIF
'
IF z_type&>0
SELECT display_sort_type&
CASE 0
record_insert(kk_win&,head_nbline&(kk_win&))
CASE 1
record_insert(kk_win&,@local_sort_by_name(kk_win&,ADD(zip_add_ptr%,8)))
CASE 2
record_insert(kk_win&,@local_sort_by_length(kk_win&,@loc_get_length(zip_add_ptr%)))
CASE 3
record_insert(kk_win&,@local_sort_by_date(kk_win&,@loc_get_date(zip_add_ptr%)))
CASE 4
record_insert(kk_win&,@local_sort_by_type(kk_win&,ADD(zip_add_ptr%,4)))
ENDSELECT
ENDIF
'
NEXT zip_i&
'
ENDIF
'
@local_sort_folders_at_top(kk_win&)
@local_set_win_stats(kk_win&)
@local_add_cd_up(kk_win&)
'
@mouse_free
'
IF win!(kk_win&)
record_start&(kk_win&)=old_record_start&
set_vslide(kk_win&)
record_start&(kk_win&)=@get_vslide(kk_win&)
force_update(kk_win&)
ENDIF
ENDIF
'
RETURN
> FUNCTION zipfile_is_already_listed(kki_win&,has_name$)
$F%
LOCAL has_i&
'
has_name$=UPPER$(has_name$)
'
IF head_nbline&(kki_win&)>0
FOR has_i&=0 TO PRED(head_nbline&(kki_win&))
IF @loc_get_type(@px(kki_win&,has_i&))=2
IF UPPER$(@loc_get_name$(@px(kki_win&,has_i&)))=has_name$
RETURN TRUE
ENDIF
ENDIF
NEXT has_i&
ENDIF
'
RETURN FALSE
ENDFUNC
> FUNCTION zipfile_read_entries(zip_filename0$,kk_win&)
$F%
LOCAL zip_handle&
'
zip_win&=ADD(2,kk_win&)
'
@record_close(zip_win&)
'
zipfile_size%=0
zip_central_offset%(kk_win&)=0
zip_end_central_offset%(kk_win&)=0
'
IF @s_exist(zip_filename0$)=FALSE
RETURN FALSE
ENDIF
'
zip_buf%=@mxalloc(6400,3)
IF zip_buf%>0
zip_filename0$=zip_filename0$+c0$
~FRE(0)
zip_handle&=GEMDOS(61,L:V:zip_filename0$,W:0)
IF zip_handle&>0
mouse_busy
'
zipfile_size%=GEMDOS(66,L:0,W:zip_handle&,W:2)
zip_pos%=SUB(zipfile_size%,22)
'
IF zip_pos%>0
DO
~GEMDOS(66,L:zip_pos%,W:zip_handle&,W:0)
~GEMDOS(63,W:zip_handle&,L:4,L:zip_buf%)
DEC zip_pos%
LOOP UNTIL LONG{zip_buf%}=&H504B0506 OR zip_pos%<0
INC zip_pos%
ENDIF
'
IF zip_pos%>0
'
zip_end_central_offset%(kk_win&)=zip_pos%
'
~GEMDOS(66,L:zip_pos%,W:zip_handle&,W:0)
~GEMDOS(63,W:zip_handle&,L:20,L:zip_buf%)
'
zip_entries_nb&=@zipfile_read_int(ADD(zip_buf%,10))
zip_central_offset%(kk_win&)=@zipfile_read_long(ADD(zip_buf%,16))
'
IF zip_entries_nb&>0 AND zip_central_offset%(kk_win&)>0
'
@record_open(zip_win&,zip_entries_nb&,record_len&(zip_win&),0)
zip_entry_adr%=record_buffer%(zip_win&)
zip_offset%=zip_central_offset%(kk_win&)
'
FOR zip_i&=1 TO zip_entries_nb&
'
~GEMDOS(66,L:zip_offset%,W:zip_handle&,W:0)
~GEMDOS(63,W:zip_handle&,L:46,L:zip_buf%)
'
IF LONG{zip_buf%}=&H504B0102
'
record_clear_buffer(zip_win&)
'
@zipentry_set_pointer(zip_entry_adr%,zip_offset%)
@zipentry_set_flags(zip_entry_adr%,@zipfile_read_int(ADD(zip_buf%,8)))
@zipentry_set_method(zip_entry_adr%,@zipfile_read_int(ADD(zip_buf%,10)))
@zipentry_set_date(zip_entry_adr%,@zipfile_read_long(ADD(zip_buf%,12)))
@zipentry_set_crc32(zip_entry_adr%,@zipfile_read_long(ADD(zip_buf%,16)))
@zipentry_set_compressed_size(zip_entry_adr%,@zipfile_read_long(ADD(zip_buf%,20)))
@zipentry_set_size(zip_entry_adr%,@zipfile_read_long(ADD(zip_buf%,24)))
@zipentry_set_offset(zip_entry_adr%,@zipfile_read_long(ADD(zip_buf%,42)))
'
zip_name_len&=@zipfile_read_int(ADD(zip_buf%,28))
zip_ext_len&=@zipfile_read_int(ADD(zip_buf%,30))
zip_comment_len&=@zipfile_read_int(ADD(zip_buf%,32))
'
~GEMDOS(63,W:zip_handle&,L:MIN(zip_name_len&,1024),L:zip_buf%)
BYTE{ADD(zip_buf%,MIN(zip_name_len&,1024))}=0
@zipentry_set_name(zip_win&,zip_entry_adr%,CHAR{zip_buf%})
'
record_insert(zip_win&,head_nbline&(zip_win&))
'
ADD zip_offset%,46
ADD zip_offset%,zip_name_len&
ADD zip_offset%,zip_ext_len&
ADD zip_offset%,zip_comment_len&
'
ELSE IF LONG{zip_buf%}=&H504B0505
'
zip_sign_len&=@zipfile_read_int(ADD(zip_buf%,4))
'
ADD zip_offset%,6
ADD zip_offset%,zip_sign_len&
DEC zip_i&
'
ENDIF
NEXT zip_i&
ENDIF
ENDIF
'
mouse_free
~GEMDOS(62,W:zip_handle&)
ENDIF
@mxfree(zip_buf%)
zip_buf%=0
ENDIF
'
IF head_nbline&(zip_win&)>0
RETURN TRUE
ENDIF
RETURN FALSE
ENDFUNC
> FUNCTION zipfile_read_int(zipe_ptr%)
$F%
RETURN BYTE{zipe_ptr%} OR SHL&(BYTE{SUCC(zipe_ptr%)},8)
ENDFUNC
> FUNCTION zipfile_read_long(zipe_ptr%)
$F%
RETURN BYTE{zipe_ptr%} OR SHL(BYTE{SUCC(zipe_ptr%)},8) OR SHL(BYTE{ADD(zipe_ptr%,2)},16) OR SHL(BYTE{ADD(zipe_ptr%,3)},24)
ENDFUNC
> PROCEDURE zipfile_write_int(zipe_ptr%,value&)
IF value&=0
INT{zipe_ptr%}=0
ELSE
BYTE{zipe_ptr%}=value& AND &HFF
INC zipe_ptr%
BYTE{zipe_ptr%}=SHR&(value&,8) AND &HFF
ENDIF
RETURN
> PROCEDURE zipfile_write_long(zipe_ptr%,value%)
IF value%=0
LONG{zipe_ptr%}=0
ELSE
BYTE{zipe_ptr%}=value% AND &HFF
INC zipe_ptr%
BYTE{zipe_ptr%}=SHR(value%,8) AND &HFF
INC zipe_ptr%
BYTE{zipe_ptr%}=SHR(value%,16) AND &HFF
INC zipe_ptr%
BYTE{zipe_ptr%}=SHR(value%,24) AND &HFF
ENDIF
RETURN
> FUNCTION zipfile_get_slashed_path$(name$)
LOCAL znts_pos&
IF LEN(name$)>0
DO
znts_pos&=INSTR(name$,"\")
IF znts_pos&>0
MID$(name$,znts_pos&)="/"
ENDIF
LOOP UNTIL znts_pos&=0
ENDIF
RETURN name$
ENDFUNC
> FUNCTION zipfile_get_antislashed_path$(name$)
LOCAL znts_pos&
IF LEN(name$)>0
DO
znts_pos&=INSTR(name$,"/")
IF znts_pos&>0
MID$(name$,znts_pos&)="\"
ENDIF
LOOP UNTIL znts_pos&=0
ENDIF
RETURN name$
ENDFUNC
> PROCEDURE zipfile_unload(kk_win&)
IF head_nbline&(ADD(2,kk_win&))>0
@record_close(ADD(2,kk_win&))
ENDIF
RETURN
'
> PROCEDURE zipentry_set_pointer(zipe_ptr%,value%)
LONG{zipe_ptr%}=value%
RETURN
> FUNCTION zipentry_get_pointer(zipe_ptr%)
$F%
IF zipe_ptr%>0
RETURN LONG{zipe_ptr%}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE zipentry_set_name(zipe_win&,zipe_ptr%,name$)
zipe_str_ptr%=@gxalloc(ADD(6,zipe_win&),SUCC(LEN(name$)))
IF zipe_str_ptr%>0
LONG{ADD(zipe_ptr%,4)}=zipe_str_ptr%
CHAR{zipe_str_ptr%}=name$
ENDIF
RETURN
> FUNCTION zipentry_get_name$(zipe_ptr%)
IF zipe_ptr%>0
zipe_str_ptr%=LONG{ADD(zipe_ptr%,4)}
IF zipe_str_ptr%>0
RETURN CHAR{zipe_str_ptr%}
ENDIF
ENDIF
RETURN ""
ENDFUNC
> PROCEDURE zipentry_set_size(zipe_ptr%,value%)
LONG{ADD(zipe_ptr%,8)}=value%
RETURN
> FUNCTION zipentry_get_size(zipe_ptr%)
$F%
IF zipe_ptr%>0
RETURN LONG{ADD(zipe_ptr%,8)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE zipentry_set_compressed_size(zipe_ptr%,value%)
LONG{ADD(zipe_ptr%,12)}=value%
RETURN
> FUNCTION zipentry_get_compressed_size(zipe_ptr%)
$F%
IF zipe_ptr%>0
RETURN LONG{ADD(zipe_ptr%,12)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE zipentry_set_crc32(zipe_ptr%,value%)
LONG{ADD(zipe_ptr%,16)}=value%
RETURN
> FUNCTION zipentry_get_crc32(zipe_ptr%)
$F%
IF zipe_ptr%>0
RETURN LONG{ADD(zipe_ptr%,16)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE zipentry_set_date(zipe_ptr%,value%)
LONG{ADD(zipe_ptr%,20)}=value%
RETURN
> FUNCTION zipentry_get_date(zipe_ptr%)
$F%
IF zipe_ptr%>0
RETURN LONG{ADD(zipe_ptr%,20)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE zipentry_set_method(zipe_ptr%,value&)
INT{ADD(zipe_ptr%,24)}=value&
RETURN
> FUNCTION zipentry_get_method(zipe_ptr%)
$F%
IF zipe_ptr%>0
RETURN INT{ADD(zipe_ptr%,24)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE zipentry_set_offset(zipe_ptr%,value%)
LONG{ADD(zipe_ptr%,26)}=value%
RETURN
> FUNCTION zipentry_get_offset(zipe_ptr%)
$F%
IF zipe_ptr%>0
RETURN LONG{ADD(zipe_ptr%,26)}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE zipentry_set_flags(zipe_ptr%,value&)
INT{ADD(zipe_ptr%,30)}=value&
RETURN
> FUNCTION zipentry_get_flags(zipe_ptr%)
$F%
IF zipe_ptr%>0
RETURN INT{ADD(zipe_ptr%,30)}
ELSE
RETURN 0
ENDIF
ENDFUNC
'
> PROCEDURE dialog_display
result&=OBJC_FIND(adtree%(8),0,3,mo_x&,mo_y&)
SELECT result&
CASE 1 TO 2
display_change_subdialog(result&)
CASE 5
display_win_pos_type&=0
black_white(8,5,1)
black_white(8,6,0)
CASE 6
display_win_pos_type&=1
black_white(8,5,0)
black_white(8,6,1)
CASE 7
IF display_win_pos_save!
display_win_pos_save!=FALSE
ELSE
display_win_pos_save!=TRUE
ENDIF
black_white(8,7,ABS(display_win_pos_save!))
CASE 8
IF display_disk_b!
display_disk_b!=FALSE
ELSE
display_disk_b!=TRUE
ENDIF
black_white(8,8,ABS(display_disk_b!))
CASE 9
IF display_sort_discard_hidden_files!
display_sort_discard_hidden_files!=FALSE
ELSE
display_sort_discard_hidden_files!=TRUE
ENDIF
black_white(8,9,ABS(display_sort_discard_hidden_files!))
@display_refresh_directories
CASE 10
IF display_files_with_date!
display_files_with_date!=FALSE
ELSE
display_files_with_date!=TRUE
ENDIF
black_white(8,10,ABS(display_files_with_date!))
@local_set_display(3)
@local_set_display(4)
force_update(3)
force_update(4)
CASE 12
dummy&=@pop_up(8,12,13,FALSE)
IF dummy&>-1
display_files_date_format&=MAX(0,MIN(dummy&,4))
CHAR{{OB_SPEC(adtree%(8),12)}}=CHAR{{OB_SPEC(adtree%(13),SUCC(display_files_date_format&))}}
black_white(8,12,-1)
IF display_files_with_date!
force_update(3)
force_update(4)
ENDIF
ENDIF
CASE 13
IF display_files_with_length!
display_files_with_length!=FALSE
ELSE
display_files_with_length!=TRUE
ENDIF
black_white(8,13,ABS(display_files_with_length!))
@local_set_display(3)
@local_set_display(4)
force_update(3)
force_update(4)
CASE 15
dummy&=@pop_up(8,15,14,FALSE)
IF dummy&>-1
display_files_length_format&=MAX(0,MIN(dummy&,3))
CHAR{{OB_SPEC(adtree%(8),15)}}=CHAR{{OB_SPEC(adtree%(14),SUCC(display_files_length_format&))}}
black_white(8,15,-1)
IF display_files_with_length!
force_update(3)
force_update(4)
ENDIF
ENDIF
CASE 17
IF @tst_selected(8,17)
display_files_font_size&=0
black_white(8,17,0)
ELSE
display_files_font_size&=1
black_white(8,17,1)
black_white(8,18,0)
black_white(8,19,0)
ENDIF
local_set_fonts(3)
local_set_fonts(4)
set_vslide(3)
record_start&(3)=@get_vslide(3)
set_vslide(4)
record_start&(4)=@get_vslide(4)
force_update(3)
force_update(4)
CASE 18
IF @tst_selected(8,18)
display_files_font_size&=0
black_white(8,18,0)
ELSE
display_files_font_size&=2
black_white(8,17,0)
black_white(8,18,1)
black_white(8,19,0)
ENDIF
local_set_fonts(3)
local_set_fonts(4)
set_vslide(3)
record_start&(3)=@get_vslide(3)
set_vslide(4)
record_start&(4)=@get_vslide(4)
force_update(3)
force_update(4)
CASE 19
IF screenh&>250
IF @tst_selected(8,19)
display_files_font_size&=0
black_white(8,19,0)
ELSE
display_files_font_size&=3
black_white(8,17,0)
black_white(8,18,0)
black_white(8,19,1)
ENDIF
local_set_fonts(3)
local_set_fonts(4)
set_vslide(3)
record_start&(3)=@get_vslide(3)
set_vslide(4)
record_start&(4)=@get_vslide(4)
force_update(3)
force_update(4)
ENDIF
CASE 22
display_set_sort_type(0)
CASE 23
display_set_sort_type(1)
CASE 24
display_set_sort_type(2)
CASE 25
display_set_sort_type(3)
CASE 26
display_set_sort_type(4)
CASE 27
IF display_sort_distinguish_case!
display_sort_distinguish_case!=FALSE
ELSE
display_sort_distinguish_case!=TRUE
ENDIF
black_white(8,27,ABS(display_sort_distinguish_case!))
IF display_sort_type&=4 OR display_sort_type&=1
@display_refresh_directories
ENDIF
CASE 28
IF display_sort_folder_at_top!
display_sort_folder_at_top!=FALSE
ELSE
display_sort_folder_at_top!=TRUE
ENDIF
black_white(8,28,ABS(display_sort_folder_at_top!))
@display_refresh_directories
CASE 29
IF display_sort_reverse!
display_sort_reverse!=FALSE
ELSE
display_sort_reverse!=TRUE
ENDIF
black_white(8,29,ABS(display_sort_reverse!))
@display_refresh_directories
DEFAULT
win(8)
ENDSELECT
RETURN
> PROCEDURE display_hide_subdialogs
FOR i&=0 TO 1
obj_deselect(8,SUCC(i&))
obj_hide(8,display_subdialog&(i&))
NEXT i&
RETURN
> PROCEDURE display_change_subdialog(obj&)
IF NOT @tst_selected(8,obj&)
display_hide_subdialogs
obj_select(8,obj&)
obj_unhide(8,display_subdialog&(PRED(obj&)))
force_update(8)
ENDIF
RETURN
> PROCEDURE display_set_all
'
SELECT display_win_pos_type&
CASE 1
obj_deselect(8,5)
obj_select(8,6)
DEFAULT
obj_select(8,5)
obj_deselect(8,6)
ENDSELECT
IF display_win_pos_save!
obj_select(8,7)
ELSE
obj_deselect(8,7)
ENDIF
IF display_disk_b!
obj_select(8,8)
ELSE
obj_deselect(8,8)
ENDIF
IF display_sort_discard_hidden_files!
obj_select(8,9)
ELSE
obj_deselect(8,9)
ENDIF
IF display_files_with_date!
obj_select(8,10)
ELSE
obj_deselect(8,10)
ENDIF
CHAR{{OB_SPEC(adtree%(8),12)}}=CHAR{{OB_SPEC(adtree%(13),SUCC(display_files_date_format&))}}
IF display_files_with_length!
obj_select(8,13)
ELSE
obj_deselect(8,13)
ENDIF
CHAR{{OB_SPEC(adtree%(8),15)}}=CHAR{{OB_SPEC(adtree%(14),SUCC(display_files_length_format&))}}
'
obj_deselect(8,17)
obj_deselect(8,18)
obj_deselect(8,19)
IF display_files_font_size&>0
obj_select(8,ADD(16,display_files_font_size&))
ENDIF
'
obj_deselect(8,22)
obj_deselect(8,23)
obj_deselect(8,24)
obj_deselect(8,25)
obj_deselect(8,26)
SELECT display_sort_type&
CASE 1
obj_select(8,23)
CASE 2
obj_select(8,24)
CASE 3
obj_select(8,25)
CASE 4
obj_select(8,26)
DEFAULT
obj_select(8,22)
ENDSELECT
IF display_sort_distinguish_case!
obj_select(8,27)
ELSE
obj_deselect(8,27)
ENDIF
IF display_sort_folder_at_top!
obj_select(8,28)
ELSE
obj_deselect(8,28)
ENDIF
IF display_sort_reverse!
obj_select(8,29)
ELSE
obj_deselect(8,29)
ENDIF
'
IF win!(8)
force_update(8)
ENDIF
RETURN
> PROCEDURE display_set_sort_type(value&)
black_white(8,ADD(22,display_sort_type&),0)
display_sort_type&=value&
black_white(8,ADD(22,display_sort_type&),1)
@display_refresh_directories
RETURN
> PROCEDURE display_refresh_directories
SELECT display_files_mode&(3)
CASE 2
@zipfile_get_directory(3)
CASE 1
@msa_get_directory(3)
CASE -1
@local_get_disks_list(3)
DEFAULT
@local_get_directory(3)
ENDSELECT
'
SELECT display_files_mode&(4)
CASE 2
@zipfile_get_directory(4)
CASE 1
@msa_get_directory(4)
CASE -1
@local_get_disks_list(4)
DEFAULT
@local_get_directory(4)
ENDSELECT
RETURN
'
> PROCEDURE dialog_misc
result&=OBJC_FIND(adtree%(9),0,3,mo_x&,mo_y&)
SELECT result&
CASE 1 TO 4
misc_change_subdialog(result&)
CASE 6
IF misc_keep_date!
misc_keep_date!=FALSE
ELSE
misc_keep_date!=TRUE
ENDIF
black_white(9,6,ABS(misc_keep_date!))
CASE 7
IF misc_verify_free_space!
misc_verify_free_space!=FALSE
ELSE
misc_verify_free_space!=TRUE
ENDIF
black_white(9,7,ABS(misc_verify_free_space!))
CASE 9
misc_choice_existing_file_type&=0
black_white(9,9,1)
black_white(9,10,0)
black_white(9,11,0)
black_white(9,12,0)
CASE 10
misc_choice_existing_file_type&=1
black_white(9,9,0)
black_white(9,10,1)
black_white(9,11,0)
black_white(9,12,0)
CASE 11
misc_choice_existing_file_type&=2
black_white(9,9,0)
black_white(9,10,0)
black_white(9,11,1)
black_white(9,12,0)
CASE 12
misc_choice_existing_file_type&=3
black_white(9,9,0)
black_white(9,10,0)
black_white(9,11,0)
black_white(9,12,1)
CASE 14
IF misc_confirm_quit!
misc_confirm_quit!=FALSE
ELSE
misc_confirm_quit!=TRUE
ENDIF
black_white(9,14,ABS(misc_confirm_quit!))
CASE 15
IF misc_confirm_send!
misc_confirm_send!=FALSE
ELSE
misc_confirm_send!=TRUE
ENDIF
black_white(9,15,ABS(misc_confirm_send!))
CASE 16
IF misc_confirm_delete!
misc_confirm_delete!=FALSE
ELSE
misc_confirm_delete!=TRUE
ENDIF
black_white(9,16,ABS(misc_confirm_delete!))
CASE 18
IF misc_zip_use_log!
misc_zip_use_log!=FALSE
ELSE
misc_zip_use_log!=TRUE
ENDIF
black_white(9,18,ABS(misc_zip_use_log!))
CASE 19
black_white(9,19,1)
dummy$=@fsl_get_file$(44,kkcmd_path$+mask_all$,kkcmd_log$)
IF LEN(dummy$)>2
kkcmd_logfile$=dummy$+c0$
CHAR{{OB_SPEC(adtree%(9),19)}}=RIGHT$(kkcmd_logfile$,30)
ENDIF
black_white(9,19,0)
CASE 21
dummy&=@pop_up(9,21,27,FALSE)
IF dummy&>-1
misc_zip_deflate_level&=MAX(0,MIN(dummy&,9))
CHAR{{OB_SPEC(adtree%(9),21)}}=" "+LEFT$(STR$(misc_zip_deflate_level&))
black_white(9,21,-1)
ENDIF
CASE 22
INC misc_zip_deflate_level&
IF misc_zip_deflate_level&>9
misc_zip_deflate_level&=1
ENDIF
CHAR{{OB_SPEC(adtree%(9),21)}}=" "+LEFT$(STR$(misc_zip_deflate_level&))
black_white(9,21,-1)
DEFAULT
win(9)
ENDSELECT
RETURN
> PROCEDURE misc_hide_subdialogs
FOR i&=0 TO 3
obj_deselect(9,SUCC(i&))
obj_hide(9,misc_subdialog&(i&))
NEXT i&
RETURN
> PROCEDURE misc_change_subdialog(obj&)
IF NOT @tst_selected(9,obj&)
misc_hide_subdialogs
obj_select(9,obj&)
obj_unhide(9,misc_subdialog&(PRED(obj&)))
force_update(9)
ENDIF
RETURN
> PROCEDURE misc_set_all
'
IF misc_keep_date!
obj_select(9,6)
ELSE
obj_deselect(9,6)
ENDIF
IF misc_verify_free_space!
obj_select(9,7)
ELSE
obj_deselect(9,7)
ENDIF
'
obj_deselect(9,9)
obj_deselect(9,10)
obj_deselect(9,11)
obj_deselect(9,12)
SELECT misc_choice_existing_file_type&
CASE 1
obj_select(9,10)
CASE 2
obj_select(9,11)
CASE 3
obj_select(9,12)
DEFAULT
obj_select(9,9)
ENDSELECT
'
IF misc_confirm_quit!
obj_select(9,14)
ELSE
obj_deselect(9,14)
ENDIF
IF misc_confirm_send!
obj_select(9,15)
ELSE
obj_deselect(9,15)
ENDIF
IF misc_confirm_delete!
obj_select(9,16)
ELSE
obj_deselect(9,16)
ENDIF
'
IF misc_zip_use_log!
obj_select(9,18)
ELSE
obj_deselect(9,18)
ENDIF
CHAR{{OB_SPEC(adtree%(9),19)}}=RIGHT$(kkcmd_logfile$,30)
CHAR{{OB_SPEC(adtree%(9),21)}}=" "+LEFT$(STR$(misc_zip_deflate_level&))
'
IF win!(9)
force_update(9)
ENDIF
RETURN
'
> PROCEDURE load_options
new_preferences_text
IF @s_exist(kkcmd_configuration_file$)=TRUE
'
OPEN "i",#4,kkcmd_configuration_file$
RECALL #4,preferences_text$(),-1,nb_line%
CLOSE #4
'
IF LEFT$(preferences_text$(0),9)="KKCMD_CFG"
'
display_win_pos_type&=MAX(0,MIN(VAL(@find$("DISPLAY_WIN_POS_TYPE")),1))
display_win_pos_save!=@get_flag("DISPLAY_WIN_POS_SAVE",TRUE)
'
IF display_win_pos_save!
@set_win_pos(3,@find$("WIN_POS_LEFT"))
@set_win_pos(4,@find$("WIN_POS_RIGHT"))
ELSE
FOR i&=3 TO 4
@set_win_pos(i&,"0,0,0,0")
NEXT i&
ENDIF
IF display_win_pos_type&>0
win_join
ENDIF
'
local_path_left$=@find$("WIN_PATH_LEFT")
local_path_right$=@find$("WIN_PATH_RIGHT")
'
display_disk_b!=@get_flag("DISPLAY_DISK_B",TRUE)
display_sort_discard_hidden_files!=@get_flag("DISPLAY_SORT_DISCARD_HIDDEN_FILES",FALSE)
display_files_with_date!=@get_flag("DISPLAY_FILES_WITH_DATE",FALSE)
display_files_date_format&=MAX(0,MIN(VAL(@find$("DISPLAY_FILES_DATE_FORMAT")),4))
display_files_with_length!=@get_flag("DISPLAY_FILES_WITH_LENGTH",TRUE)
display_files_length_format&=MAX(0,MIN(VAL(@find$("DISPLAY_FILES_LENGTH_FORMAT")),2))
display_files_font_size&=MAX(0,MIN(VAL(@find$("DISPLAY_FILES_FONT_SIZE")),3))
'
display_sort_type&=MAX(0,MIN(VAL(@find$("DISPLAY_SORT_TYPE")),2))
display_sort_distinguish_case!=@get_flag("DISPLAY_SORT_DISTINGUISH_CASE",TRUE)
display_sort_folder_at_top!=@get_flag("DISPLAY_SORT_FOLDER_AT_TOP",TRUE)
display_sort_reverse!=@get_flag("DISPLAY_SORT_REVERSE",FALSE)
'
display_set_all
'
misc_keep_date!=@get_flag("MISC_KEEP_DATE",TRUE)
misc_verify_free_space!=@get_flag("MISC_VERIFY_FREE_SPACE",TRUE)
'
misc_choice_existing_file_type&=MAX(0,MIN(VAL(@find$("MISC_CHOICE_EXISTING_FILE")),3))
'
misc_confirm_quit!=@get_flag("MISC_CONFIRM_QUIT",TRUE)
misc_confirm_send!=@get_flag("MISC_CONFIRM_SEND",FALSE)
misc_confirm_delete!=@get_flag("MISC_CONFIRM_DELETE",TRUE)
'
misc_zip_use_log!=@get_flag("MISC_ZIP_USE_LOG",FALSE)
kkcmd_logfile$=@find$("MISC_ZIP_LOGFILE")+c0$
IF LEN(kkcmd_logfile$)<4
kkcmd_logfile$=kkcmd_path$+kkcmd_log$
ENDIF
dummy$=@find$("MISC_ZIP_DEFLATE_LEVEL")
IF LEN(dummy$)=0
misc_zip_deflate_level&=2
ELSE
misc_zip_deflate_level&=MAX(0,MIN(VAL(dummy$),9))
ENDIF
'
split_current_size&=MAX(0,MIN(VAL(@find$("SPLIT_CURRENT_SIZE")),9999))
IF split_current_size&=0
split_current_size&=700
ENDIF
'
misc_set_all
'
@set_fields_pos(3,@find$("DISPLAY_LEFT_FIELDS_POS"))
@set_fields_pos(4,@find$("DISPLAY_RIGHT_FIELDS_POS"))
'
FOR i&=0 TO 9
shortcuts$(i&)=@find$("SHORTCUT_"+STR$(SUCC(i&)))
NEXT i&
'
shortcuts_set_all
ELSE
~@alert(1,4)
ENDIF
new_preferences_text
ELSE
'
FOR i&=3 TO 4
@set_win_pos(i&,"0,0,0,0")
NEXT i&
IF display_win_pos_type&>0
win_join
ENDIF
'
local_path_left$=""
local_path_right$=""
'
display_set_all
'
misc_set_all
'
@set_fields_pos(3,"")
@set_fields_pos(4,"")
'
shortcuts_set_all
ENDIF
'
IF LEFT$(ordre$)="'" AND RIGHT$(ordre$)="'"
ordre$=MID$(ordre$,2)
ordre$=LEFT$(ordre$,PRED(LEN(ordre$)))
ENDIF
'
dummy$=UPPER$(RIGHT$(ordre$,4))
'
IF dummy$=".ZIP" OR dummy$=".MSA" OR INSTR(dummy$,".ST")>0
local_path_left$=ordre$+">"
ENDIF
IF LEN(local_path_left$)=0
local_path_left$=kkcmd_path$
ENDIF
IF LEN(local_path_right$)=0
local_path_right$=kkcmd_path$
ENDIF
'
ordre$=""
'
@local_set_fonts(3)
@local_set_fonts(4)
@local_set_display(3)
@local_set_display(4)
'
RETURN
> PROCEDURE save_options
LOCAL pos_x&
'
mouse_busy
'
mem_pos%=@mem_init
'
mem_pos%=@mem_put_property("KKCMD_CFG","1.0",mem_pos%)
mem_pos%=@mem_put_property("#","",mem_pos%)
mem_pos%=@mem_put_property("#","DO NOT MODIFY THESE VALUES! USE KKCMD",mem_pos%)
mem_pos%=@mem_put_property("#","",mem_pos%)
'
FOR i&=3 TO 4
IF display_win_pos_save!
win_pos$(i&)=STR$(pwx&(i&))+","+STR$(pwy&(i&))+","+STR$(pwl&(i&))+","+STR$(pwh&(i&))
ELSE
win_pos$(i&)="0,0,0,0"
ENDIF
win_pos$(i&)=win_pos$(i&)+";OPENED"
NEXT i&
'
mem_pos%=@mem_put_property("WIN_POS_LEFT",win_pos$(3),mem_pos%)
mem_pos%=@mem_put_property("WIN_POS_RIGHT",win_pos$(4),mem_pos%)
'
mem_pos%=@mem_put_property("WIN_PATH_LEFT",CHAR{local_path0%},mem_pos%)
mem_pos%=@mem_put_property("WIN_PATH_RIGHT",CHAR{local_path1%},mem_pos%)
'
mem_pos%=@mem_put_property("DISPLAY_WIN_POS_TYPE",STR$(display_win_pos_type&),mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_WIN_POS_SAVE",display_win_pos_save!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_DISK_B",display_disk_b!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_SORT_DISCARD_HIDDEN_FILES",display_sort_discard_hidden_files!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_FILES_WITH_DATE",display_files_with_date!,mem_pos%)
mem_pos%=@mem_put_property("DISPLAY_FILES_DATE_FORMAT",STR$(display_files_date_format&),mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_FILES_WITH_LENGTH",display_files_with_length!,mem_pos%)
mem_pos%=@mem_put_property("DISPLAY_FILES_LENGTH_FORMAT",STR$(display_files_length_format&),mem_pos%)
mem_pos%=@mem_put_property("DISPLAY_FILES_FONT_SIZE",STR$(display_files_font_size&),mem_pos%)
'
mem_pos%=@mem_put_property("DISPLAY_SORT_TYPE",STR$(display_sort_type&),mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_SORT_DISTINGUISH_CASE",display_sort_distinguish_case!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_SORT_FOLDER_AT_TOP",display_sort_folder_at_top!,mem_pos%)
mem_pos%=@mem_put_flag("DISPLAY_SORT_REVERSE",display_sort_reverse!,mem_pos%)
'
mem_pos%=@mem_put_flag("MISC_KEEP_DATE",misc_keep_date!,mem_pos%)
mem_pos%=@mem_put_flag("MISC_VERIFY_FREE_SPACE",misc_verify_free_space!,mem_pos%)
'
mem_pos%=@mem_put_property("MISC_CHOICE_EXISTING_FILE_TYPE",STR$(misc_choice_existing_file_type&),mem_pos%)
'
mem_pos%=@mem_put_flag("MISC_CONFIRM_QUIT",misc_confirm_quit!,mem_pos%)
mem_pos%=@mem_put_flag("MISC_CONFIRM_SEND",misc_confirm_send!,mem_pos%)
mem_pos%=@mem_put_flag("MISC_CONFIRM_DELETE",misc_confirm_delete!,mem_pos%)
'
mem_pos%=@mem_put_flag("MISC_ZIP_USE_LOG",misc_zip_use_log!,mem_pos%)
mem_pos%=@mem_put_property("MISC_ZIP_LOGFILE",@remove_c0$(kkcmd_logfile$),mem_pos%)
mem_pos%=@mem_put_property("MISC_ZIP_DEFLATE_LEVEL",STR$(misc_zip_deflate_level&),mem_pos%)
'
mem_pos%=@mem_put_property("SPLIT_CURRENT_SIZE",STR$(split_current_size&),mem_pos%)
'
FOR i&=3 TO 4
str$=""
FOR j&=SUCC(field_bar_obj&(i&)) TO OB_TAIL(adtree%(i&),field_bar_obj&(i&))
IF OB_X(adtree%(i&),j&)>0
pos_x&=ADD(OB_X(adtree%(i&),j&),5)
ELSE
pos_x&=0
ENDIF
IF LEN(str$)>0
str$=str$+","
ENDIF
str$=str$+STR$(pos_x&)
NEXT j&
IF i&=3
mem_pos%=@mem_put_property("DISPLAY_LEFT_FIELDS_POS",str$,mem_pos%)
ELSE IF i&=4
mem_pos%=@mem_put_property("DISPLAY_RIGHT_FIELDS_POS",str$,mem_pos%)
ENDIF
NEXT i&
'
FOR i&=0 TO 9
mem_pos%=@mem_put_property("SHORTCUT_"+STR$(SUCC(i&)),shortcuts$(i&),mem_pos%)
NEXT i&
'
IF mem_pos%>0
~GEMDOS(65,L:V:kkcmd_configuration_file$)
'
file_handle&=GEMDOS(60,L:V:kkcmd_configuration_file$,W:0)
IF file_handle&>0
IF GEMDOS(64,W:file_handle&,L:SUB(mem_pos%,text_mem%),L:text_mem%)<>SUB(mem_pos%,text_mem%)
~@alert(1,5)
ENDIF
~GEMDOS(62,W:file_handle&)
ELSE
~@alert(1,6)
ENDIF
ENDIF
'
~@mem_close
'
mouse_free
'
RETURN
> FUNCTION find$(find_key$)
LOCAL find_len&,find_i&,find_exit!,find_end&
'
~FRE()
~FRE(0)
'
find_value$=""
find_exit!=FALSE
find_key$=find_key$+"="
find_len&=LEN(find_key$)
find_end&=MIN(preferences_text_size&,SUCC(nb_line%))
IF find_ptr&>PRED(find_end&)
find_cursor&=0
ENDIF
old_find_cursor&=find_cursor&
'
FOR find_i&=find_cursor& TO find_end&
IF LEFT$(preferences_text$(find_i&),find_len&)=find_key$
find_value$=MID$(preferences_text$(find_i&),SUCC(find_len&))
find_exit!=TRUE
ENDIF
EXIT IF find_exit!
NEXT find_i&
'
IF NOT find_exit!
find_cursor&=0
IF old_find_cursor&>PRED(find_end&)
FOR find_i&=0 TO old_find_cursor&
IF LEFT$(preferences_text$(find_i&),find_len&)=find_key$
find_value$=MID$(preferences_text$(find_i&),SUCC(find_len&))
find_exit!=TRUE
old_find_cursor&=find_i&
ENDIF
EXIT IF find_exit!
NEXT find_i&
find_cursor&=old_find_cursor&
ENDIF
ENDIF
'
RETURN find_value$
ENDFUNC
> FUNCTION get_flag(find_str$,default_flag!)
'
IF @find$(find_str$)=boolean$(ABS(NOT default_flag!))
RETURN NOT default_flag!
ELSE
RETURN default_flag!
ENDIF
'
ENDFUNC
> PROCEDURE set_win_pos(dial&,str$)
LOCAL str1$
'
IF str$=""
str$="0,0,0,0"
ENDIF
'
IF INSTR(str$,";OPENED")>0
ope!(dial&)=TRUE
str$=LEFT$(str$,PRED(RINSTR(str$,";OPENED")))
ELSE
ope!(dial&)=FALSE
ENDIF
'
str1$=MID$(str$,SUCC(RINSTR(str$,",")))
pwh&(dial&)=VAL(str1$)
str$=LEFT$(str$,PRED(RINSTR(str$,",")))
str1$=MID$(str$,SUCC(RINSTR(str$,",")))
pwl&(dial&)=VAL(str1$)
str$=LEFT$(str$,PRED(RINSTR(str$,",")))
str1$=MID$(str$,SUCC(RINSTR(str$,",")))
pwy&(dial&)=VAL(str1$)
str$=LEFT$(str$,PRED(RINSTR(str$,",")))
pwx&(dial&)=VAL(str$)
'
RETURN
> PROCEDURE set_fields_pos(dial&,str$)
LOCAL str1$,obj_field&,obj_default_pos&
exit!=FALSE
'
IF dial&<6
IF field_bar_obj&(dial&)>0
obj_field&=SUCC(field_bar_obj&(dial&))
obj_default_pos&=0
'
FOR j&=1 TO 6
field_x&(dial&,j&)=32000
NEXT j&
'
WHILE NOT exit!
IF INSTR(str$,",")>0
str1$=LEFT$(str$,MAX(0,PRED(INSTR(str$,","))))
str$=MID$(str$,SUCC(INSTR(str$,",")))
ELSE
str1$=str$
ENDIF
IF obj_field&>OB_TAIL(adtree%(dial&),field_bar_obj&(dial&))
exit!=TRUE
ELSE
dummy&=VAL(str1$)
IF dummy&<1
dummy&=obj_default_pos&
ENDIF
OB_X(adtree%(dial&),obj_field&)=MAX(0,SUB(dummy&,5))
field_x&(dial&,SUB(obj_field&,field_bar_obj&(dial&)))=dummy&
ENDIF
INC obj_field&
ADD obj_default_pos&,120
WEND
ENDIF
ENDIF
'
' set_fields_order(dial&)
'
RETURN
> PROCEDURE set_fields_order(dial&)
LOCAL obj_idx&
'
DIM field_x_temp&(7)
'
FOR j&=1 TO 6
field_x_temp&(j&)=field_x&(dial&,j&)
NEXT j&
'
FOR j&=1 TO 6
obj_idx&=1
FOR k&=1 TO 6
IF field_x_temp&(k&)=<field_x_temp&(obj_idx&)
obj_idx&=k&
ENDIF
NEXT k&
field_x_temp&(obj_idx&)=32000
field_order&(dial&,j&)=obj_idx&
NEXT j&
'
ERASE field_x_temp&()
'
RETURN
> PROCEDURE set_fields_width(dial&)
LOCAL field_x_min&
'
FOR j&=1 TO 6
field_x_min&=32000 ! field_x&(dial&,j&)
FOR k&=1 TO 6
IF field_x&(dial&,j&)<field_x&(dial&,k&) AND field_display!(dial&,k&)=TRUE
field_x_min&=MIN(field_x_min&,field_x&(dial&,k&))
ENDIF
NEXT k&
field_w&(dial&,j&)=MAX(0,SUB(field_x_min&,field_x&(dial&,j&)))
NEXT j&
'
RETURN
> PROCEDURE set_fields_display(dial&)
IF dial&>2 AND dial&<5
IF field_bar_obj&(dial&)>0
FOR k&=1 TO 6
field_display!(dial&,k&)=FALSE
IF field_x&(dial&,k&)<32000
IF NOT BTST(OB_FLAGS(adtree%(dial&),ADD(field_bar_obj&(dial&),k&)),7)
field_display!(dial&,k&)=TRUE
ENDIF
ENDIF
NEXT k&
set_fields_width(dial&)
ENDIF
ENDIF
RETURN
> PROCEDURE new_preferences_text
FOR i&=0 TO preferences_text_size&
preferences_text$(i&)=""
NEXT i&
find_ptr&=0
~FRE()
~FRE(0)
RETURN
'
> PROCEDURE call_documentation
IF @s_exist(kkcmd_guide$)=TRUE
guide_acc&=APPL_FIND("ST-GUIDE")
IF (@s_exist(stguide_app$)=TRUE AND stguide_app$<>c0$) OR guide_acc&>0
IF guide_acc&<0
close_multi
num_drive&=SUB(ASC(stguide_app$),65)
~GEMDOS(14,W:num_drive&)
CHDIR LEFT$(stguide_app$,RINSTR(stguide_app$,"\"))+c0$
ENDIF
IF multi!
IF guide_acc&<0
dummy$=@remove_c0$(kkcmd_guide$)
dummy$=CHR$(LEN(dummy$))+dummy$
shl_write(1,1,100,dummy$,stguide_app$)
FOR i&=0 TO 7
delay
NEXT i&
ELSE
call_accessory(guide_acc&)
ENDIF
ELSE
IF guide_acc&<0
dummy$=@remove_c0$(kkcmd_guide$)
dummy$=CHR$(LEN(dummy$))+dummy$
~EXEC(0,stguide_app$,dummy$,c0$+c0$)
mouse_free
ELSE
call_accessory(guide_acc&)
ENDIF
ENDIF
open_multi
ELSE
~@alert(1,30)
ENDIF
ELSE
~@alert(1,31)
ENDIF
RETURN
> PROCEDURE call_accessory(guide_id&)
IF shell_buf%>0 AND kkcmd_guide$<>c0$
CHAR{shell_buf%}=LEFT$(kkcmd_guide$,254)+c0$
clear_s
INT{s_adr%}=18193
INT{ADD(s_adr%,2)}=ap_id&
LONG{ADD(s_adr%,6)}=shell_buf%
~APPL_WRITE(guide_id&,16,s_adr%)
ENDIF
RETURN
'
> PROCEDURE ldg_init
'
ldgm_cookie!=@test_cookie("LDGM",ldg_cookie_ptr%)
IF ldgm_cookie!=TRUE AND ldg_cookie_ptr%>0
'
ldg_version&=INT{ldg_cookie_ptr%}
ldg_path$=CHAR{ADD(ldg_cookie_ptr%,2)}
ldg_gc_time&=INT{ADD(ldg_cookie_ptr%,130)}
'
ldg_open%=LONG{ADD(ldg_cookie_ptr%,134)}
ldg_close%=LONG{ADD(ldg_cookie_ptr%,138)}
ldg_find%=LONG{ADD(ldg_cookie_ptr%,142)}
ldg_error%=ADD(ldg_cookie_ptr%,150)
'
ELSE
ldg_version&=0
ldg_path$=""
ldg_gc_time&=0
'
ldg_open%=0
ldg_close%=0
ldg_find%=0
ldg_error%=0
ENDIF
'
RETURN
> PROCEDURE ldg_exit
'
ldgm_cookie!=FALSE
'
ldg_version&=0
ldg_path$=""
ldg_gc_time&=0
'
ldg_open%=0
ldg_close%=0
ldg_find%=0
ldg_error%=0
'
RETURN
'
> FUNCTION zlib_open
$F%
'
zlib_get_version%=0
zlib_get_info%=0
zlib_get_error%=0
zlib_get_compil_flags%=0
zlib_get_sizeof_stream_struct%=0
'
zlib_deflate_init%=0
zlib_deflate%=0
zlib_deflate_end%=0
'
zlib_inflate_init%=0
zlib_inflate%=0
zlib_inflate_end%=0
'
zlib_crc%=0
'
ldg_mem_malloc_ptr%=0
ldg_mem_free_ptr%=0
'
IF shell_buf%>0 AND ldg_open%>0
zlib_error&=0
'
CHAR{shell_buf%}="deflate.ldg"
'
zlib_ldg_ptr%=C:ldg_open%(L:shell_buf%,L:global%)
IF zlib_ldg_ptr%<1
zlib_error&=INT{ldg_error%}
'
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="get_version"
'
zlib_get_version%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_get_version%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="get_info"
'
zlib_get_info%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_get_info%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="get_error"
'
zlib_get_error%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_get_error%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="get_compil_flags"
'
zlib_get_compil_flags%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_get_compil_flags%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="get_sizeof_stream_struct"
'
zlib_get_sizeof_stream_struct%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_get_sizeof_stream_struct%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="raw_deflate_init"
'
zlib_deflate_init%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_deflate_init%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="raw_deflate"
'
zlib_deflate%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_deflate%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="raw_deflate_end"
'
zlib_deflate_end%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_deflate_end%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="raw_inflate_init"
'
zlib_inflate_init%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_inflate_init%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="raw_inflate"
'
zlib_inflate%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_inflate%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="raw_inflate_end"
'
zlib_inflate_end%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_inflate_end%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="update_crc32"
'
zlib_crc%=C:ldg_find%(L:shell_buf%,L:zlib_ldg_ptr%)
IF zlib_crc%=0
RETURN FALSE
ENDIF
'
CHAR{shell_buf%}="mem.ldg"
'
ldg_mem_ptr%=C:ldg_open%(L:shell_buf%,L:global%)
IF ldg_mem_ptr%>0
'
CHAR{shell_buf%}="ldg_malloc"
'
ldg_mem_malloc_ptr%=C:ldg_find%(L:shell_buf%,L:ldg_mem_ptr%)
'
CHAR{shell_buf%}="ldg_free"
'
ldg_mem_free_ptr%=C:ldg_find%(L:shell_buf%,L:ldg_mem_ptr%)
'
ENDIF
'
RETURN TRUE
ENDIF
RETURN FALSE
ENDFUNC
> PROCEDURE zlib_clear_stream
'
IF zlib_stream%<1
IF zlib_stream_size&<1
zlib_stream_size&=SHL&(SHR&(ADD(@zlib_get_sizeof_stream_struct,15),4),4)
ENDIF
zlib_stream%=@mxalloc_global(zlib_stream_size&,3)
ENDIF
'
IF zlib_stream%>0
FOR zlib_i&=0 TO PRED(zlib_stream_size&)
BYTE{ADD(zlib_stream%,zlib_i&)}=0
NEXT zlib_i&
'
LONG{ADD(zlib_stream%,32)}=ldg_mem_malloc_ptr%
LONG{ADD(zlib_stream%,36)}=ldg_mem_free_ptr%
'
ENDIF
'
RETURN
> PROCEDURE zlib_close(ldg_forced!)
'
IF ldg_forced!
proceed!=(zlib_ldg_ptr%=LONG{ADD(m_adr%,6)})
ELSE
proceed!=TRUE
ENDIF
'
IF proceed!
zlib_opened!=FALSE
'
zlib_get_version%=0
zlib_get_info%=0
zlib_get_error%=0
zlib_get_compil_flags%=0
zlib_get_sizeof_stream_struct%=0
'
zlib_deflate_init%=0
zlib_deflate%=0
zlib_deflate_end%=0
'
zlib_inflate_init%=0
zlib_inflate%=0
zlib_inflate_end%=0
'
zlib_crc%=0
'
ldg_mem_malloc_ptr%=0
ldg_mem_free_ptr%=0
'
IF ldg_close%>0 AND zlib_ldg_ptr%>0
~C:ldg_close%(L:zlib_ldg_ptr%,L:global%)
ENDIF
zlib_ldg_ptr%=0
'
IF ldg_close%>0 AND ldg_mem_ptr%>0
~C:ldg_close%(L:ldg_mem_ptr%,L:global%)
ENDIF
ldg_mem_ptr%=0
'
IF zlib_stream%>0
@mxfree(zlib_stream%)
zlib_stream%=0
ENDIF
ENDIF
RETURN
> FUNCTION zlib_get_version$
zlib_s$=""
'
IF zlib_get_version%>0
$C+
zlib_s$=CHAR{C:zlib_get_version%()}
$C-
ENDIF
RETURN zlib_s$
ENDFUNC
> FUNCTION zlib_get_info$
zlib_s$=""
'
IF zlib_get_info%>0
$C+
zlib_s$=CHAR{C:zlib_get_info%()}
$C-
ENDIF
RETURN zlib_s$
ENDFUNC
> FUNCTION zlib_get_error$(zlib_error%)
zlib_s$=""
'
IF zlib_get_error%>0
$C+
zlib_s$=CHAR{C:zlib_get_error%(L:zlib_error%)}
$C-
ELSE IF zlib_stream%>0
zlib_s$=CHAR{ADD(zlib_stream%,20)}
ENDIF
RETURN zlib_s$
ENDFUNC
> FUNCTION zlib_deflate_init(zlib_level%)
$F%
zlib_r%=0
'
IF zlib_deflate_init%>0 AND zlib_stream%>0
$C+
zlib_r%=C:zlib_deflate_init%(L:zlib_stream%,L:zlib_level%)
$C-
ENDIF
RETURN zlib_r%
ENDFUNC
> FUNCTION zlib_deflate(zlib_flush%)
$F%
zlib_r%=0
'
IF zlib_deflate%>0 AND zlib_stream%>0
$C+
zlib_r%=C:zlib_deflate%(L:zlib_stream%,L:zlib_flush%)
$C-
ENDIF
RETURN zlib_r%
ENDFUNC
> FUNCTION zlib_deflate_end
$F%
zlib_r%=0
'
IF zlib_deflate_end%>0 AND zlib_stream%>0
$C+
zlib_r%=C:zlib_deflate_end%(L:zlib_stream%)
$C-
ENDIF
RETURN zlib_r%
ENDFUNC
> FUNCTION zlib_inflate_init
$F%
zlib_r%=0
'
IF zlib_inflate_init%>0 AND zlib_stream%>0
$C+
zlib_r%=C:zlib_inflate_init%(L:zlib_stream%)
$C-
ENDIF
RETURN zlib_r%
ENDFUNC
> FUNCTION zlib_inflate(zlib_flush%)
$F%
zlib_r%=0
'
IF zlib_inflate%>0 AND zlib_stream%>0
$C+
zlib_r%=C:zlib_inflate%(L:zlib_stream%,L:zlib_flush%)
$C-
ENDIF
RETURN zlib_r%
ENDFUNC
> FUNCTION zlib_inflate_end
$F%
zlib_r%=0
'
IF zlib_inflate_end%>0 AND zlib_stream%>0
$C+
zlib_r%=C:zlib_inflate_end%(L:zlib_stream%)
$C-
ENDIF
RETURN zlib_r%
ENDFUNC
> FUNCTION zlib_crc(zlib_crc_val%,zlib_src_ptr%,zlib_src_len%)
$F%
zlib_r%=0
'
IF zlib_crc%>0
$C+
zlib_r%=C:zlib_crc%(L:zlib_crc_val%,L:zlib_src_ptr%,L:zlib_src_len%)
$C-
ENDIF
RETURN zlib_r%
ENDFUNC
> FUNCTION zlib_get_compil_flags
$F%
zlib_s%=0
'
IF zlib_get_compil_flags%>0
$C+
zlib_s%=C:zlib_get_compil_flags%()
$C-
ENDIF
RETURN zlib_s%
ENDFUNC
> FUNCTION zlib_get_sizeof_stream_struct
$F%
zlib_r%=0
'
IF zlib_get_sizeof_stream_struct%>0
$C+
zlib_r%=C:zlib_get_sizeof_stream_struct%()
$C-
ENDIF
RETURN zlib_r%
ENDFUNC
'
> PROCEDURE log_open
IF @s_exist(kkcmd_logfile$)
av_start_program(kkcmd_logfile$)
ENDIF
RETURN
> PROCEDURE log_clear
IF @alert(1,45)=1
IF @s_exist(kkcmd_logfile$)
~GEMDOS(65,L:V:kkcmd_logfile$)
ENDIF
ENDIF
RETURN
> PROCEDURE log_write
LOCAL log_handle&
'
IF log_mem%>0 AND SUB(log_pos%,log_mem%)>0 AND misc_zip_use_log!=TRUE
IF @s_exist(kkcmd_logfile$)
log_handle&=GEMDOS(61,L:V:kkcmd_logfile$,W:1)
IF log_handle&>0
~GEMDOS(66,L:0,W:log_handle&,W:2)
~GEMDOS(64,W:log_handle&,L:SUB(log_pos%,log_mem%),L:log_mem%)
~GEMDOS(62,W:log_handle&)
ENDIF
ELSE
log_handle&=GEMDOS(60,L:V:kkcmd_logfile$,W:0)
IF log_handle&>0
~GEMDOS(64,W:log_handle&,L:SUB(log_pos%,log_mem%),L:log_mem%)
~GEMDOS(62,W:log_handle&)
ENDIF
ENDIF
'
log_pos%=log_mem%
ENDIF
'
RETURN
> FUNCTION log_init
~@log_exit
log_len%=4096
log_mem%=@mxalloc(log_len%,3)
IF log_mem%<0
log_mem%=0
ENDIF
RETURN log_mem%
ENDFUNC
> FUNCTION log_exit
IF log_mem%>0
@mxfree(log_mem%)
log_mem%=0
log_len%=0
ENDIF
RETURN TRUE
ENDFUNC
> FUNCTION log_add(str$,ptr%)
LOCAL len_str%
'
IF misc_zip_use_log!
len_str%=LEN(str$)
IF @log_mem(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
ENDIF
'
RETURN ptr%
ENDFUNC
> FUNCTION log_append(adr%,len%,ptr%)
'
IF misc_zip_use_log!
IF @log_mem(ptr%,len%,ptr%)
IF ptr%>0
BMOVE adr%,ptr%,len%
ADD ptr%,len%
ENDIF
ENDIF
ENDIF
'
RETURN ptr%
ENDFUNC
> FUNCTION log_mem(old_pos%,add%,VAR new_pos%)
LOCAL new_log_mem%,new_log_len%
'
IF old_pos%=0
RETURN FALSE
ENDIF
'
IF ADD(SUB(old_pos%,log_mem%),add%)>log_len%
new_log_len%=SHL(SHR(ADD(ADD(log_len%,MAX(ADD(add%,16),4096)),32),4),4)
new_log_mem%=@mxalloc(new_log_len%,3)
IF new_log_mem%>0
BMOVE log_mem%,new_log_mem%,log_len%
@mxfree(log_mem%)
new_pos%=ADD(old_pos%,SUB(new_log_mem%,log_mem%))
log_mem%=new_log_mem%
log_len%=new_log_len%
RETURN TRUE
ELSE
new_pos%=0
RETURN FALSE
ENDIF
ENDIF
new_pos%=old_pos%
RETURN TRUE
ENDFUNC
'
> FUNCTION mem_init
~@mem_close
text_len%=16000
text_mem%=@mxalloc(text_len%,3)
IF text_mem%<0
~@alert(1,1)
text_mem%=0
ENDIF
RETURN text_mem%
ENDFUNC
> FUNCTION mem_close
IF text_mem%>0
@mxfree(text_mem%)
text_mem%=0
text_len%=0
ENDIF
RETURN TRUE
ENDFUNC
> FUNCTION mem_put_string(str$,ptr%)
LOCAL len_str%
'
len_str%=LEN(str$)
IF @mem_add(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
'
RETURN ptr%
ENDFUNC
> FUNCTION mem_put_line(str$,ptr%)
LOCAL len_str%
'
str$=str$+CHR$(13)+CHR$(10)
len_str%=LEN(str$)
IF @mem_add(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
'
RETURN ptr%
ENDFUNC
> FUNCTION mem_put_property(str1$,str2$,ptr%)
LOCAL len_str%
'
str$=str1$+"="+str2$+CHR$(13)+CHR$(10)
'
len_str%=LEN(str$)
IF @mem_add(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
'
RETURN ptr%
ENDFUNC
> FUNCTION mem_put_flag(str1$,flag!,ptr%)
LOCAL len_str%
'
str$=str1$+"="+boolean$(ABS(flag!))+CHR$(13)+CHR$(10)
'
len_str%=LEN(str$)
IF @mem_add(ptr%,len_str%,ptr%)
IF ptr%>0
CHAR{ptr%}=str$
ADD ptr%,len_str%
ENDIF
ENDIF
str$=""
'
RETURN ptr%
ENDFUNC
> FUNCTION mem_add(old_pos%,add%,VAR new_pos%)
LOCAL new_text_mem%,new_text_len%
'
IF old_pos%=0
RETURN FALSE
ENDIF
'
IF ADD(SUB(old_pos%,text_mem%),add%)>text_len%
new_text_len%=SHL(SHR(ADD(ADD(text_len%,MAX(ADD(add%,16),16000)),32),4),4)
new_text_mem%=@mxalloc(new_text_len%,3)
IF new_text_mem%>0
BMOVE text_mem%,new_text_mem%,text_len%
@mxfree(text_mem%)
new_pos%=ADD(new_text_mem%,SUB(old_pos%,text_mem%))
text_mem%=new_text_mem%
text_len%=new_text_len%
RETURN TRUE
ELSE
~@alert(1,1)
new_pos%=0
RETURN FALSE
ENDIF
ENDIF
new_pos%=old_pos%
RETURN TRUE
ENDFUNC
'
> PROCEDURE crc32_init
'
INLINE crc32table%,1024
'
RETURN
> FUNCTION crc32(crc_v%,crc_ptr%,crc_length%)
$F%
LOCAL crc_b|,crc_end%
'
crc_end%=ADD(crc_ptr%,crc_length%)
'
crc_v%=crc_v% XOR &HFFFFFFFF
'
WHILE crc_ptr%<crc_end%
crc_b|=BYTE{crc_ptr%}
crc_v%=LONG{ADD(crc32table%,MUL(AND(crc_v%,&HFF) XOR crc_b|,4))} XOR SHR(crc_v%,8)
INC crc_ptr%
WEND
'
RETURN crc_v% XOR &HFFFFFFFF
ENDFUNC
'
> PROCEDURE av_init
LOCAL av_str$,av_ptr%
'
av_id&=-1
'
IF magic!=TRUE OR magic!=FALSE
~SHEL_ENVRN(av_ptr%,"AVSERVER")
IF av_ptr%>0
av_str$=CHAR{SUCC(av_ptr%)}
av_id&=APPL_FIND(av_str$+STRING$(MAX(0,SUB(8,LEN(av_str$)))," "))
ENDIF
'
IF av_id&=-1 AND magic!=TRUE
av_id&=0
ENDIF
ENDIF
'
av_server!=(av_id&>=0)
'
IF av_server!
CHAR{shell_buf%}="KKCMD   "
clear_s
'
INT{s_adr%}=&H4700 ! AV-PROTKOLL
INT{ADD(s_adr%,2)}=ap_id&
INT{ADD(s_adr%,6)}=&X111111
LONG{ADD(s_adr%,12)}=shell_buf%
~APPL_WRITE(av_id&,16,s_adr%)
ENDIF
RETURN
> PROCEDURE av_exit
IF av_server!
clear_s
'
INT{s_adr%}=&H4736
INT{ADD(s_adr%,6)}=ap_id&
~APPL_WRITE(av_id&,16,s_adr%)
ENDIF
RETURN
> PROCEDURE av_start_program(av_path$)
IF av_server!
CHAR{shell_buf%}=LEFT$(av_path$,255)
clear_s
'
INT{s_adr%}=&H4722
INT{ADD(s_adr%,2)}=ap_id&
LONG{ADD(s_adr%,6)}=shell_buf%
~APPL_WRITE(av_id&,16,s_adr%)
ENDIF
RETURN
'
> FUNCTION pop_up(pop_tree&,pop_obj&,pop_dial&,pop_mouse_pos!)
$F%
'
~OBJC_OFFSET(adtree%(pop_tree&),pop_obj&,pop_obj_x&,pop_obj_y&)
'
IF pop_mouse_pos!
OB_X(adtree%(pop_dial&),0)=MAX(ADD(screenx&,4),MIN(mo_x&,SUB(ADD(screenx&,screenl&),ADD(OB_W(adtree%(pop_dial&),0),20))))
OB_Y(adtree%(pop_dial&),0)=MAX(ADD(screeny&,4),MIN(mo_y&,SUB(ADD(screeny&,screenh&),ADD(OB_H(adtree%(pop_dial&),0),20))))
ELSE IF pop_dial&=27
OB_X(adtree%(pop_dial&),0)=pop_obj_x&
ADD pop_obj_y&,2
OB_Y(adtree%(pop_dial&),0)=SUB(pop_obj_y&,MUL(OB_H(adtree%(pop_tree&),pop_obj&),4))
ELSE
OB_X(adtree%(pop_dial&),0)=pop_obj_x&
ADD pop_obj_y&,2
OB_Y(adtree%(pop_dial&),0)=ADD(pop_obj_y&,OB_H(adtree%(pop_tree&),pop_obj&))
ENDIF
control
get_photo(pop_dial&,0)
v_show_c
pop_result_old&=0
pop_result&=0
DO
IF NOT magic!
evnt&=@ev_multi(&X100010,258,3,0,30,mo_x&,mo_y&,mo_k&,dummy&,dummy&,mo_c&)
ELSE
evnt&=@ev_multi(&X10,2,0,1,30,mo_x&,mo_y&,mo_k&,dummy&,dummy&,mo_c&)
ENDIF
pop_result&=OBJC_FIND(adtree%(pop_dial&),0,OB_TAIL(adtree%(pop_dial&),0),mo_x&,mo_y&)
IF pop_result_old&<>pop_result&
IF pop_result&>0 AND @tst_enabled(pop_dial&,pop_result&)=TRUE
IF pop_result_old&>0
black_white(pop_dial&,pop_result_old&,0)
ENDIF
pop_result_old&=pop_result&
black_white(pop_dial&,pop_result_old&,1)
ELSE
IF pop_result_old&>0
black_white(pop_dial&,pop_result_old&,0)
ENDIF
pop_result_old&=0
ENDIF
ENDIF
LOOP UNTIL mo_c&=1 AND mo_k&=1
v_hide_c
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WHILE mo_c&>0
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WEND
put_photo(pop_dial&)
uncontrol
'
IF pop_result_old&>0
OB_STATE(adtree%(pop_dial&),pop_result_old&)=0
RETURN PRED(pop_result_old&)
ELSE
RETURN -1
ENDIF
ENDFUNC
> FUNCTION pop_up_with_slider(pop_tree&,pop_obj&,dial&,code!,can_be_null!,pop_length&)
$F%
LOCAL item_height&
'
IF head_nbline&(dial&)<1 AND dial&<>6
RETURN -1
ENDIF
'
FOR it&=2 TO 21
obj_unhide(15,it&)
NEXT it&
'
~OBJC_OFFSET(adtree%(pop_tree&),pop_obj&,pop_obj_x&,pop_obj_y&)
'
OB_X(adtree%(15),0)=pop_obj_x&
OB_Y(adtree%(15),0)=ADD(ADD(pop_obj_y&,2),OB_H(adtree%(pop_tree&),pop_obj&))
'
item_height&=OB_H(adtree%(15),2)
pop_up_start&=0
IF dial&=6
pop_up_max&=MAX(1,loc_drive_nb&)
ELSE
pop_up_max&=MAX(1,ADD(head_nbline&(dial&),ABS(can_be_null!)))
ENDIF
'
dummy&=SUB(ADD(screenh&,screeny&),ADD(pop_obj_y&,OB_H(adtree%(pop_tree&),pop_obj&)))
dummy&=DIV(dummy&,item_height&)
IF dummy&<5
dummy&=SUB(pop_obj_y&,screeny&)
pop_up_height&=MIN(20,DIV(dummy&,item_height&),pop_up_max&)
OB_Y(adtree%(15),0)=MAX(SUCC(screeny&),SUB(SUB(pop_obj_y&,4),MUL(pop_up_height&,item_height&)))
ELSE
pop_up_height&=dummy&
ENDIF
'
pop_up_height&=MIN(20,pop_up_height&,pop_up_max&)
'
OB_H(adtree%(15),0)=MUL(pop_up_height&,item_height&)
OB_H(adtree%(15),1)=MUL(pop_up_height&,item_height&)
'
IF pop_up_height&>3
OB_H(adtree%(15),23)=SUB(OB_H(adtree%(15),0),MUL(OB_H(adtree%(15),22),2))
OB_Y(adtree%(15),24)=1
OB_H(adtree%(15),24)=MIN(ADD(OB_H(adtree%(15),23),EVEN(pop_up_max&))*MIN(1,pop_up_height&/MAX(1,pop_up_max&)),SUB(OB_H(adtree%(15),23),2))
OB_Y(adtree%(15),25)=ADD(OB_H(adtree%(15),23),OB_H(adtree%(15),22))
obj_unhide(15,22)
obj_unhide(15,23)
obj_unhide(15,25)
ELSE
obj_hide(15,22)
obj_hide(15,23)
obj_hide(15,25)
ENDIF
'
IF pop_up_height&<20
FOR it&=ADD(pop_up_height&,2) TO 21
obj_hide(15,it&)
NEXT it&
ENDIF
'
OB_W(adtree%(15),1)=MUL(8,pop_length&)
FOR it&=2 TO 21
OB_W(adtree%(15),it&)=MUL(8,pop_length&)
NEXT it&
OB_W(adtree%(15),0)=SUCC(ADD(OB_W(adtree%(15),1),OB_W(adtree%(15),22)))
OB_X(adtree%(15),22)=SUCC(OB_W(adtree%(15),1))
OB_X(adtree%(15),23)=SUCC(OB_W(adtree%(15),1))
OB_X(adtree%(15),25)=SUCC(OB_W(adtree%(15),1))
'
control
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
get_photo(15,0)
win!(15)=TRUE
v_show_c
pop_result_old&=0
pop_result&=0
DO
IF NOT magic!
evnt&=@ev_multi(&X100010,258,3,0,30,mo_x&,mo_y&,mo_k&,dummy&,dummy&,mo_c&)
ELSE
evnt&=@ev_multi(&X10,2,0,1,30,mo_x&,mo_y&,mo_k&,dummy&,dummy&,mo_c&)
ENDIF
pop_result&=OBJC_FIND(adtree%(15),0,OB_TAIL(adtree%(15),0),mo_x&,mo_y&)
IF mo_k&=1
SELECT pop_result&
CASE 22
pop_up_slide_up(dial&,code!,can_be_null!,pop_length&)
mo_k&=0
CASE 23
pop_up_slide_page(dial&,code!,can_be_null!,pop_length&)
mo_k&=0
CASE 24
pop_up_slide_drag(dial&,code!,can_be_null!,pop_length&)
mo_k&=0
CASE 25
pop_up_slide_down(dial&,code!,can_be_null!,pop_length&)
mo_k&=0
ENDSELECT
ENDIF
IF pop_result_old&<>pop_result&
IF pop_result&>0 AND pop_result&<ADD(pop_up_height&,2) AND @tst_enabled(15,pop_result&)
IF pop_result_old&>0
black_white(15,pop_result_old&,0)
ENDIF
pop_result_old&=pop_result&
black_white(15,pop_result_old&,1)
ELSE
IF pop_result_old&>0
black_white(15,pop_result_old&,0)
ENDIF
pop_result_old&=0
ENDIF
ENDIF
LOOP UNTIL mo_c&=1 AND mo_k&=1
v_hide_c
win!(15)=FALSE
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WHILE mo_c&>0
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WEND
put_photo(15)
uncontrol
'
IF pop_result_old&>1 AND pop_result_old&<22
OB_STATE(adtree%(15),pop_result_old&)=0
RETURN pop_up_value&(pop_result_old&)
ELSE
RETURN -1
ENDIF
ENDFUNC
> PROCEDURE pop_up_slide_drag(dial&,code!,can_be_null!,pop_length&)
IF pop_up_max&>pop_up_height&
WHILE mo_c&=1
pop_up_position&=GRAF_SLIDEBOX(adtree%(15),23,24,1)
~GRAF_MKSTATE(dummy&,dummy&,mo_c&,dummy&)
WEND
pop_up_start&=(pop_up_position&/1000)*MAX(1,SUB(pop_up_max&,pop_up_height&))
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
ENDIF
RETURN
> PROCEDURE pop_up_slide_up(dial&,code!,can_be_null!,pop_length&)
IF pop_up_start&>0
black_white(15,22,1)
DEC pop_up_start&
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
black_white(15,22,0)
ENDIF
RETURN
> PROCEDURE pop_up_slide_down(dial&,code!,can_be_null!,pop_length&)
IF pop_up_start&<SUB(pop_up_max&,pop_up_height&)
black_white(15,25,1)
INC pop_up_start&
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
black_white(15,25,0)
ENDIF
RETURN
> PROCEDURE pop_up_slide_page(dial&,code!,can_be_null!,pop_length&)
~OBJC_OFFSET(adtree%(15),24,x_ob&,y_ob&)
IF mo_y&<y_ob&
SUB pop_up_start&,pop_up_height&
ENDIF
IF mo_y&>ADD(y_ob&,OB_H(adtree%(22),24))
ADD pop_up_start&,pop_up_height&
ENDIF
pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
RETURN
> PROCEDURE pop_up_slide_redraw(dial&,code!,can_be_null!,pop_length&)
pop_up_start&=MAX(0,MIN(pop_up_start&,SUB(pop_up_max&,pop_up_height&)))
'
FOR ip&=0 TO 22
pop_up_value&(ip&)=0
NEXT ip&
'
SELECT dial&
CASE 6
FOR ip&=0 TO MIN(PRED(pop_up_height&),PRED(pop_up_max&))
idx&=ADD(pop_up_start&,ip&)
@obj_enable(15,ADD(2,ip&))
txt$=" "+loc_drive$(idx&)+": "
num&=loc_drive&(idx&)
'
CHAR{{OB_SPEC(adtree%(15),ADD(2,ip&))}}=txt$
pop_up_value&(ADD(2,ip&))=num&
NEXT ip&
ENDSELECT
'
OB_Y(adtree%(15),24)=MAX(1,(pop_up_start&*OB_H(adtree%(15),23))/MAX(1,pop_up_max&))
'
IF win!(15)
black_white(15,1,0)
black_white(15,23,1)
ENDIF
RETURN
'
> FUNCTION pop_up_next_value(dial&,value&,can_be_null!)
$F%
LOCAL head_line&
'
IF head_nbline&(dial&)<1 AND dial&<>6
RETURN -1
ENDIF
'
SELECT dial&
CASE 2
head_line&=SUCC(value&)
IF head_line&>=loc_drive_nb&
head_line&=0
ENDIF
RETURN head_line&
ENDSELECT
'
RETURN 0
ENDFUNC
'
> PROCEDURE get_photo(photo_tree&,photo_obj&)
IF photo_buf%>0
~OBJC_OFFSET(adtree%(photo_tree&),photo_obj&,x9&,y9&)
SUB x9&,3
SUB y9&,3
l9&=ADD(OB_W(adtree%(photo_tree&),photo_obj&),9)
h9&=ADD(OB_H(adtree%(photo_tree&),photo_obj&),9)
IF ADD(x9&,l9&)>ADD(screenx&,screenl&)
l9&=SUB(ADD(screenx&,screenl&),x9&)
ENDIF
IF ADD(y9&,h9&)>ADD(screeny&,screenh&)
h9&=SUB(ADD(screeny&,screenh&),y9&)
ENDIF
'
IF x9&<ADD(screenx&,screenl&) AND y9&<ADD(screeny&,screenh&)
l9_temp&=SHR&(ADD(ADD(l9&,16),15),4)
photo_buf_new_length%=ADD(MUL(MUL(l9_temp&,ADD(h9&,16)),SHL(nb_plan&,1)),16)
IF photo_buf_new_length%>photo_buf_old_length%
mxfree(photo_buf%)
photo_buf%=@mxalloc(photo_buf_new_length%,3)
IF photo_buf%>0
photo_buf_old_length%=photo_buf_new_length%
ELSE
photo_buf%=0
photo_buf_old_length%=0
ENDIF
ENDIF
'
IF photo_buf%>0
LONG{pdesmfdb%}=photo_buf%
WORD{ADD(pdesmfdb%,4)}=ADD(l9&,16)
WORD{ADD(pdesmfdb%,6)}=ADD(h9&,16)
WORD{ADD(pdesmfdb%,8)}=l9_temp&
make_xyarray(x9&,y9&,ADD(x9&,l9&),ADD(y9&,h9&),1,1,l9&,h9&)
vro_cpyfm(pscrmfdb%,pdesmfdb%)
ENDIF
ENDIF
'
ENDIF
black_white(photo_tree&,photo_obj&,0)
RETURN
> PROCEDURE put_photo(photo_tree&)
IF photo_buf%>0
IF x9&<ADD(screenx&,screenl&) AND y9&<ADD(screeny&,screenh&)
make_xyarray(1,1,l9&,h9&,x9&,y9&,ADD(x9&,PRED(l9&)),ADD(y9&,PRED(h9&)))
vro_cpyfm(pdesmfdb%,pscrmfdb%)
ENDIF
ELSE
~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
ENDIF
RETURN
'
> PROCEDURE control
~WIND_UPDATE(1)
~WIND_UPDATE(3)
v_hide_c
RETURN
> PROCEDURE uncontrol
v_show_c
~WIND_UPDATE(2)
~WIND_UPDATE(0)
RETURN
> PROCEDURE control_form(form&)
~WIND_UPDATE(1)
~WIND_UPDATE(3)
~FORM_CENTER(adtree%(form&),xd&(form&),yd&(form&),ld&(form&),hd&(form&))
DEC xd&(form&)
DEC yd&(form&)
ADD ld&(form&),5
ADD hd&(form&),5
~FORM_DIAL(0,0,0,0,0,xd&(form&),yd&(form&),ld&(form&),hd&(form&))
~OBJC_DRAW(adtree%(form&),0,3,screenx&,screeny&,screenl&,screenh&)
RETURN
> PROCEDURE uncontrol_form(form&)
~WIND_UPDATE(2)
~WIND_UPDATE(0)
~FORM_DIAL(3,0,0,0,0,xd&(form&),yd&(form&),ld&(form&),hd&(form&))
RETURN
> PROCEDURE delay
~EVNT_TIMER(75)
RETURN
> PROCEDURE show_error
~FORM_ALERT(1,ERR$(ERR))
RETURN
> PROCEDURE shl_write(mode&,wisgr&,wiscr&,cmd$,tail$)
'
GCONTRL(0)=121
GCONTRL(1)=3
GCONTRL(2)=1
GCONTRL(3)=2
GCONTRL(4)=0
'
GINTIN(0)=mode&
GINTIN(1)=wisgr&
GINTIN(2)=wiscr&
'
ADDRIN(0)=V:tail$
ADDRIN(1)=V:cmd$
'
GEMSYS
'
RETURN
> PROCEDURE close_multi
IF multi!=FALSE
close_all_windows
~MENU_BAR(adtree%(0),0)
ENDIF
RETURN
> PROCEDURE open_multi
@local_set_drive(4)
@local_set_path(4)
@local_set_drive(3)
@local_set_drive(3)
IF multi!=FALSE
~MENU_BAR(adtree%(0),1)
~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
ENDIF
RETURN
'
> PROCEDURE mouse_busy
~GRAF_MOUSE(2,0)
RETURN
> PROCEDURE mouse_free
~GRAF_MOUSE(0,0)
RETURN
> PROCEDURE mouse_pointing
~GRAF_MOUSE(3,0)
RETURN
> PROCEDURE mouse_prompt
~GRAF_MOUSE(1,0)
RETURN
> FUNCTION alert(typ&,id_msg&)
$F%
RETURN FORM_ALERT(typ&,CHAR{OB_SPEC(adtree%(alert_tree&),id_msg&)})
ENDFUNC
> FUNCTION remove_c0$(remove_str1$)
IF RIGHT$(remove_str1$)=c0$
RETURN LEFT$(remove_str1$,MAX(0,PRED(LEN(remove_str1$))))
ELSE
RETURN remove_str1$
ENDIF
ENDFUNC
> FUNCTION remove_slash$(remove_str2$)
LET remove_str2$=@remove_c0$(remove_str2$)
IF RIGHT$(remove_str2$)="\"
RETURN LEFT$(remove_str2$,MAX(0,PRED(LEN(remove_str2$))))
ELSE
RETURN remove_str2$
ENDIF
ENDFUNC
'
> FUNCTION fsl_get_path$(fsl_title&,path$,name$)
LOCAL fsl_path$,fsl_name$,fsl_choice&,fsl_return&
fsl_path$=path$
fsl_name$=name$
fsl_return&=@fsl_input(fsl_title&,fsl_path$,fsl_name$,fsl_choice&)
IF fsl_return&=0 OR fsl_choice&=0
RETURN kkcmd_path$+c0$
ELSE
RETURN LEFT$(fsl_path$,RINSTR(fsl_path$,"\"))+c0$
ENDIF
ENDFUNC
> FUNCTION fsl_get_file$(fsl_title&,path$,name$)
LOCAL fsl_path$,fsl_name$,fsl_choice&,fsl_return&
fsl_path$=path$
fsl_name$=name$
fsl_return&=@fsl_input(fsl_title&,fsl_path$,fsl_name$,fsl_choice&)
IF fsl_return&=0 OR fsl_choice&=0
RETURN c0$
ELSE
RETURN LEFT$(fsl_path$,RINSTR(fsl_path$,"\"))+fsl_name$
ENDIF
ENDFUNC
> FUNCTION fsl_input(fi_title&,VAR fi_path$,fi_name$,fi_choice&)
$F%
'
IF fslx_mem%>0
fi_choice&=0
IF GEMDOS(48)>=&H1500
type_msg&=0
ENDIF
'
IF type_msg&>0
GCONTRL(0)=91
ELSE
GCONTRL(0)=90
ENDIF
GCONTRL(1)=0
GCONTRL(2)=2
IF type_msg&>0
GCONTRL(3)=3
ELSE
GCONTRL(3)=2
ENDIF
GCONTRL(4)=0
'
CHAR{fslx_path%}=LEFT$(fi_path$,257)
CHAR{fslx_name%}=LEFT$(fi_name$,127)
'
LONG{ADDRIN}=fslx_path%
LONG{ADD(ADDRIN,4)}=fslx_name%
IF type_msg&>0
LONG{ADD(ADDRIN,8)}=OB_SPEC(adtree%(alert_tree&),fi_title&)
ENDIF
'
GEMSYS
'
fi_path$=CHAR{fslx_path%}+c0$
fi_name$=CHAR{fslx_name%}+c0$
fi_choice&=INT{ADD(GINTOUT,2)}
'
RETURN INT{GINTOUT}
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE fslx_init
LOCAL fslx_str$
'
fslx_mem%=@mxalloc(256,3)
IF fslx_mem%>0
fslx_path%=fslx_mem%
fslx_name%=ADD(fslx_path%,128)
ENDIF
'
RETURN
'
> FUNCTION test_cookie(cookie_name$,VAR cookie_valeur%)
$F%
LOCAL read_cook%,nom_cook%,cookie%
'
nom_cook%=CVL(cookie_name$)
cookie%=LPEEK(&H5A0)
cookie_valeur%=0
'
IF cookie%<>0
REPEAT
read_cook%=LPEEK(cookie%)
cookie_valeur%=LPEEK(cookie%+4)
ADD cookie%,8
UNTIL read_cook%=0 OR read_cook%=nom_cook%
IF read_cook%=nom_cook%
RETURN TRUE
ELSE
RETURN FALSE
ENDIF
ELSE
RETURN FALSE
ENDIF
ENDFUNC
> FUNCTION s_exist(exist_name$)
$F%
'
exist_name$=exist_name$+c0$
LOCAL existe&
IF LEN(exist_name$)=0 OR LEFT$(exist_name$)=c0$
RETURN FALSE
ELSE
existe&=GEMDOS(61,L:V:exist_name$,W:0)
IF existe&>0
~GEMDOS(62,W:existe&)
RETURN TRUE
ELSE
RETURN FALSE
ENDIF
ENDIF
ENDFUNC
> FUNCTION d_exist(exist_name$)
$F%
LOCAL d_handle%,d_exist!,d_name$,d_path$,d_err%,d_dta%
'
IF shell_buf%>0
IF FRE()<4000
~FRE(0)
ENDIF
'
d_exist!=FALSE
d_path$=LEFT$(exist_name$,PRED(RINSTR(exist_name$,"\")))+c0$
d_test$=""
d_name$=@remove_c0$(MID$(exist_name$,SUCC(RINSTR(exist_name$,"\"))))
'
IF (magic!=TRUE OR mint!=TRUE) AND xattr_buf%>0
IF LEN(d_path$)=3
d_path$=@remove_c0$(d_path$)+"\"+c0$
ENDIF
'
d_handle%=GEMDOS(296,L:V:d_path$,0)
IF d_handle%>0
'
WHILE GEMDOS(297,W:250,L:d_handle%,L:shell_buf%)=0 AND d_exist!=FALSE
IF UPPER$(CHAR{ADD(shell_buf%,4)})=UPPER$(d_name$)
d_test$=@remove_c0$(d_path$)+"\"+d_name$+c0$
IF GEMDOS(300,W:0,L:V:d_test$,L:xattr_buf%)=0
IF (INT{xattr_buf%} AND &HF000)=&H4000
d_exist!=TRUE
ENDIF
ENDIF
ENDIF
WEND
'
~GEMDOS(299,L:d_handle%)
ENDIF
'
RETURN d_exist!
ELSE
d_path$=@remove_c0$(d_path$)+"\*"+c0$
d_err%=GEMDOS(78,L:V:d_path$,W:16)
DO
EXIT IF d_err%<>0 OR d_exist!=TRUE
d_dta%=FGETDTA()
IF CHAR{ADD(d_dta%,30)}=UPPER$(d_name$) AND BTST(BYTE{ADD(d_dta%,21)},4)=TRUE
d_exist!=TRUE
ENDIF
d_err%=GEMDOS(79)
LOOP
RETURN d_exist!
ENDIF
ENDIF
'
RETURN FALSE
ENDFUNC
> FUNCTION get_env$(get_env$)
LOCAL get_env%
'
get_env%=@mxalloc(512,3)
ret_env$=""
IF RIGHT$(get_env$)<>"="
get_env$=get_env$+"="
ENDIF
'
IF get_env%>0
CHAR{get_env%}=LEFT$(get_env$,31)
'
INT{ADD(GCONTRL,2)}=0
INT{ADD(GCONTRL,4)}=1
INT{ADD(GCONTRL,6)}=2
INT{ADD(GCONTRL,8)}=0
'
LONG{ADDRIN}=ADD(32,get_env%)
LONG{ADD(ADDRIN,4)}=get_env%
'
GEMSYS 125
'
IF LONG{ADD(32,get_env%)}>0
ret_env$=CHAR{{ADD(32,get_env%)}}
IF RIGHT$(ret_env$)<>"\"
ret_env$=ret_env$+"\"
ENDIF
ENDIF
mxfree(get_env%)
ENDIF
RETURN ret_env$
ENDFUNC
> FUNCTION get_app_name$(app_name$)
app_name$=MID$(app_name$,SUCC(RINSTR(app_name$,"\")))
app_name$=LEFT$(app_name$,MAX(0,PRED(RINSTR(app_name$,"."))))
app_name$=app_name$+SPACE$(MAX(0,SUB(8,LEN(app_name$))))
RETURN app_name$
ENDFUNC
'
> FUNCTION ev_multi(em_flags&,em_cl&,em_ma&,em_st&,em_ct%,VAR em_mx&,em_my&,em_mk&,em_kbd&,em_key&,em_click&)
$F%
'
INT{ADD(GCONTRL,2)}=16
INT{ADD(GCONTRL,4)}=7
INT{ADD(GCONTRL,6)}=1
INT{ADD(GCONTRL,8)}=0
'
INT{GINTIN}=em_flags&
INT{ADD(GINTIN,2)}=em_cl&
INT{ADD(GINTIN,4)}=em_ma&
INT{ADD(GINTIN,6)}=em_st&
INT{ADD(GINTIN,8)}=0
INT{ADD(GINTIN,10)}=0
INT{ADD(GINTIN,12)}=0
INT{ADD(GINTIN,14)}=0
INT{ADD(GINTIN,16)}=0
INT{ADD(GINTIN,18)}=0
INT{ADD(GINTIN,20)}=0
INT{ADD(GINTIN,22)}=0
INT{ADD(GINTIN,24)}=0
INT{ADD(GINTIN,26)}=0
INT{ADD(GINTIN,28)}=WORD(em_ct%)
INT{ADD(GINTIN,30)}=WORD(SWAP(em_ct%))
'
LONG{ADDRIN}=m_adr%
'
GEMSYS 25
'
em_mx&=INT{ADD(GINTOUT,2)}
em_my&=INT{ADD(GINTOUT,4)}
em_mk&=INT{ADD(GINTOUT,6)}
em_kbd&=INT{ADD(GINTOUT,8)}
em_key&=INT{ADD(GINTOUT,10)}
em_click&=INT{ADD(GINTOUT,12)}
'
RETURN INT{GINTOUT}
ENDFUNC
> FUNCTION window_create(cp_win_recu&)
$F%
'
INT{ADD(GCONTRL,2)}=5
INT{ADD(GCONTRL,4)}=1
INT{ADD(GCONTRL,6)}=0
INT{ADD(GCONTRL,8)}=0
'
INT{GINTIN}=cp_win_recu&
INT{ADD(GINTIN,2)}=100
INT{ADD(GINTIN,4)}=100
INT{ADD(GINTIN,6)}=100
INT{ADD(GINTIN,8)}=100
'
GEMSYS 100
'
RETURN INT{GINTOUT}
ENDFUNC
> FUNCTION get_top_window
$F%
'
INT{ADD(GCONTRL,2)}=2
INT{ADD(GCONTRL,4)}=5
LONG{ADD(GCONTRL,6)}=0
'
INT{GINTIN}=0
INT{ADD(GINTIN,2)}=10
'
GEMSYS 104
'
RETURN INT{ADD(GINTOUT,2)}
ENDFUNC
'
> FUNCTION v_opnvwk
$F%
'
INT{ADD(CONTRL,2)}=0
INT{ADD(CONTRL,6)}=11
INT{ADD(CONTRL,12)}=@graf_handle
'
INT{INTIN}=1           ! Numro ID du priphrique physique (cran)
INT{ADD(INTIN,2)}=1    ! Type de ligne
INT{ADD(INTIN,4)}=1    ! Index de couleur Polyline
INT{ADD(INTIN,6)}=1    ! Type de marqueur
INT{ADD(INTIN,8)}=1    ! Index de couleur Polymarker
INT{ADD(INTIN,10)}=1   ! Fonte de caractres
INT{ADD(INTIN,12)}=1   ! Index couleur texte
INT{ADD(INTIN,14)}=1   ! Fill interior Style
INT{ADD(INTIN,16)}=1   ! Fill style index
INT{ADD(INTIN,18)}=1   ! Fill index couleur
INT{ADD(INTIN,20)}=2   ! Flag coordonnes NDC ou RC
'
VDISYS 100
'
RETURN INT{ADD(CONTRL,12)}
ENDFUNC
> PROCEDURE v_clsvwk(handle&)
INT{ADD(CONTRL,12)}=handle&
VDISYS 101,0,0
RETURN
> FUNCTION graf_handle
$F%
'
INT{ADD(GCONTRL,2)}=0
INT{ADD(GCONTRL,4)}=5
LONG{ADD(GCONTRL,6)}=0
'
GEMSYS 77
'
RETURN INT{GINTOUT}
ENDFUNC
'
> PROCEDURE make_zero_mfdb(pmfdb%)
LONG{pmfdb%}=0
LONG{ADD(pmfdb%,4)}=0
LONG{ADD(pmfdb%,8)}=0
LONG{ADD(pmfdb%,12)}=0
LONG{ADD(pmfdb%,16)}=0
RETURN
> PROCEDURE make_xywh(x0&,y0&,w0&,h0&,x1&,y1&,w1&,h1&)
WORD{pxyarray%}=x0&
WORD{ADD(pxyarray%,2)}=y0&
WORD{ADD(pxyarray%,4)}=ADD(x0&,PRED(w0&))
WORD{ADD(pxyarray%,6)}=ADD(y0&,PRED(h0&))
WORD{ADD(pxyarray%,8)}=x1&
WORD{ADD(pxyarray%,10)}=y1&
WORD{ADD(pxyarray%,12)}=ADD(x1&,PRED(w1&))
WORD{ADD(pxyarray%,14)}=ADD(y1&,PRED(h1&))
RETURN
> PROCEDURE make_xyarray(xq0&,yq0&,xq1&,yq1&,xz0&,yz0&,xz1&,yz1&)
WORD{pxyarray%}=xq0&
WORD{ADD(pxyarray%,2)}=yq0&
WORD{ADD(pxyarray%,4)}=xq1&
WORD{ADD(pxyarray%,6)}=yq1&
WORD{ADD(pxyarray%,8)}=xz0&
WORD{ADD(pxyarray%,10)}=yz0&
WORD{ADD(pxyarray%,12)}=xz1&
WORD{ADD(pxyarray%,14)}=yz1&
RETURN
> PROCEDURE vro_cpyfm(pscr_mfdb%,pdes_mfdb%)
'
v_hide_c
'
INT{ADD(CONTRL,2)}=4
INT{ADD(CONTRL,6)}=1
INT{ADD(CONTRL,12)}=vdi_handle&
LONG{ADD(CONTRL,14)}=pscr_mfdb%
LONG{ADD(CONTRL,18)}=pdes_mfdb%
INT{INTIN}=3
BMOVE pxyarray%,PTSIN,16
'
VDISYS 109
'
v_show_c
'
RETURN
'
> PROCEDURE v_hide_c
INT{ADD(CONTRL,2)}=0
INT{ADD(CONTRL,6)}=0
INT{ADD(CONTRL,12)}=vdi_handle&
VDISYS 123
RETURN
> PROCEDURE v_show_c
INT{ADD(CONTRL,2)}=0
INT{ADD(CONTRL,6)}=1
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=1
VDISYS 122
RETURN
'
> PROCEDURE v_text(x&,y&,str$,is_vector!)
'
CHAR{vtxt_buf%}=LEFT$(str$,255)
vtext_len&=MIN(LEN(str$),255)
str$=""
'
IF vtext_len&>0
INT{ADD(CONTRL,12)}=vdi_handle&
INT{ADD(PTSIN,2)}=y&
itext_len&=MIN(vtext_len&,255)
INT{PTSIN}=x&
DEC itext_len&
FOR it&=0 TO itext_len&
INT{ADD(INTIN,SHL&(it&,1))}=BYTE{ADD(vtxt_buf%,it&)}
NEXT it&
INC itext_len&
IF is_vector!
VDISYS 241,itext_len&,1,0
ELSE
VDISYS 8,itext_len&,1,0
ENDIF
ENDIF
RETURN
> FUNCTION vqt_extent(str$,is_vector!)
$F%
'
CHAR{vtxt_buf%}=LEFT$(str$,255)
vtext_len&=MIN(LEN(str$),255)
str$=""
'
IF vtext_len&>0
INT{ADD(CONTRL,12)}=vdi_handle&
itext_len&=MIN(vtext_len&,255)
DEC itext_len&
FOR it&=0 TO itext_len&
INT{ADD(INTIN,SHL&(it&,1))}=BYTE{ADD(vtxt_buf%,it&)}
NEXT it&
INC itext_len&
IF is_vector!
VDISYS 240,itext_len&,0,0
ELSE
VDISYS 116,itext_len&,0,0
ENDIF
RETURN SUB(INT{ADD(PTSOUT,4)},INT{PTSOUT})
ELSE
RETURN 0
ENDIF
ENDFUNC
> PROCEDURE vst_alignment(halign&,valign&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=halign&
INT{ADD(INTIN,2)}=valign&
VDISYS 39,2,0
RETURN
> PROCEDURE vst_color(color&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=color&
VDISYS 22,1,0
RETURN
> PROCEDURE vst_effect(effect&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=effect&
VDISYS 106,1,0
RETURN
> FUNCTION vst_point(point&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=point&
VDISYS 107,1,0
RETURN SUCC(INT{ADD(PTSOUT,6)})
ENDFUNC
'
> PROCEDURE clip(flag&,cx&,cy&,cw&,ch&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=flag&
INT{PTSIN}=cx&
INT{ADD(PTSIN,2)}=cy&
INT{ADD(PTSIN,4)}=ADD(cx&,PRED(cw&))
INT{ADD(PTSIN,6)}=ADD(cy&,PRED(ch&))
VDISYS 129,1,2
RETURN
> PROCEDURE vswr_mode(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 32,1,0
RETURN
'
> PROCEDURE v_bar(cx&,cy&,cw&,ch&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{PTSIN}=cx&
INT{ADD(PTSIN,2)}=cy&
INT{ADD(PTSIN,4)}=ADD(cx&,PRED(cw&))
INT{ADD(PTSIN,6)}=ADD(cy&,PRED(ch&))
VDISYS 11,0,2,1
RETURN
> PROCEDURE vsf_interior(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 23,1,0
RETURN
> PROCEDURE vsf_style(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 24,1,0
RETURN
> PROCEDURE vsf_color(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 25,1,0
RETURN
> PROCEDURE vsf_perimeter(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 104,1,0
RETURN
> PROCEDURE vsl_type(mode&)
INT{ADD(CONTRL,12)}=vdi_handle&
INT{INTIN}=mode&
VDISYS 15,1,0
RETURN
'
> PROCEDURE record_general_init(n&)
'
record_number_max&=32000
'
DIM head%(n&),head_maxline&(n&),head_nbline&(n&)
'
DIM record_buffer%(n&),record_len&(n&)
DIM record_start&(n&),record_height&(n&),record_selected&(n&)
DIM record_changed!(n&),record_nb_selected&(n&),record_cursor&(n&)
'
DIM field_bar_h&(n&),field_bar_obj&(n&)
DIM field_x&(n&,16),field_w&(n&,16),field_order&(n&,16),field_display!(n&,16)
DIM field_start&(n&)
'
RETURN
> PROCEDURE record_general_exit
'
ERASE head%(),head_maxline&(),head_nbline&()
'
ERASE record_buffer%(),record_len&()
ERASE record_start&(),record_height&(),record_selected&()
ERASE record_changed!(),record_nb_selected&(),record_cursor&()
'
ERASE field_bar_h&(),field_bar_obj&()
ERASE field_x&(),field_w&(),field_order&(),field_display!()
ERASE field_start&()
'
RETURN
> PROCEDURE record_open(tid&,h_size&,r_len&,obj&)
'
~FRE(0)
~@head_init(tid&,h_size&)
~@gxalloc_init(tid&)
'
record_len&(tid&)=MAX(2,r_len&)
record_buffer%(tid&)=@gxalloc(tid&,record_len&(tid&))
record_start&(tid&)=0
record_height&(tid&)=0
record_selected&(tid&)=-1
record_nb_selected&(tid&)=0
record_changed!(tid&)=FALSE
record_cursor&(tid&)=-1
'
field_bar_obj&(tid&)=obj&
'
SELECT tid&
CASE 1
~@gxalloc_init(7)
CASE 5
~@gxalloc_init(11)
CASE 6
~@gxalloc_init(12)
ENDSELECT
'
RETURN
> PROCEDURE record_close(tid&)
'
IF head%(tid&)>0
~@gxalloc_exit(tid&)
~@head_close(tid&)
ENDIF
'
SELECT tid&
CASE 1
~@gxalloc_exit(7)
CASE 5
~@gxalloc_exit(11)
CASE 6
~@gxalloc_exit(12)
ENDSELECT
'
RETURN
'
> PROCEDURE record_set(tid&,line&)
'
BMOVE record_buffer%(tid&),@px(tid&,line&),record_len&(tid&)
'
record_changed!(tid&)=TRUE
'
RETURN
> PROCEDURE record_insert(tid&,line&)
'
IF @head_resize(tid&,SUCC(head_nbline&(tid&)))
IF line&<head_nbline&(tid&)
temp_len%=MUL(SUB(head_nbline&(tid&),line&),6)
temp_ptr%=ADD(head%(tid&),MUL(line&,6))
IF temp_len%>0
BMOVE temp_ptr%,ADD(temp_ptr%,6),temp_len%
ENDIF
ENDIF
'
temp_ptr%=@gxalloc(tid&,record_len&(tid&))
IF temp_ptr%>0
BMOVE record_buffer%(tid&),temp_ptr%,record_len&(tid&)
LONG{ADD(head%(tid&),MUL(line&,6))}=temp_ptr%
INT{ADD(head%(tid&),ADD(MUL(line&,6),4))}=0
INC head_nbline&(tid&)
record_changed!(tid&)=TRUE
ELSE
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: can't insert a record.|][ Ok ]")
ENDIF
'
ENDIF
RETURN
> PROCEDURE record_delete(tid&,line&)
'
temp_len%=MUL(SUB(head_maxline&(tid&),line&),6)
temp_ptr%=ADD(head%(tid&),MUL(line&,6))
IF temp_len%>0
BMOVE ADD(temp_ptr%,6),temp_ptr%,temp_len%
ENDIF
'
head_nbline&(tid&)=MAX(0,PRED(head_nbline&(tid&)))
record_changed!(tid&)=TRUE
'
RETURN
> PROCEDURE record_select(tid&,line&)
IF line&>-1
BYTE{ADD(head%(tid&),ADD(MUL(line&,6),4))}=1
ENDIF
RETURN
> PROCEDURE record_deselect(tid&,line&)
IF line&>-1
BYTE{ADD(head%(tid&),ADD(MUL(line&,6),4))}=0
ENDIF
RETURN
> PROCEDURE record_select_all(tid&)
LOCAL sa_i&,sa_end&,sa_ptr%
'
sa_end&=PRED(head_nbline&(tid&))
IF sa_end&>-1
sa_ptr%=ADD(head%(tid&),4)
FOR sa_i&=0 TO sa_end&
BYTE{sa_ptr%}=1
ADD sa_ptr%,6
NEXT sa_i&
ENDIF
'
RETURN
> PROCEDURE record_deselect_all(tid&)
LOCAL da_i&,da_end&,da_ptr%
'
da_end&=PRED(head_nbline&(tid&))
IF da_end&>-1
da_ptr%=ADD(head%(tid&),4)
FOR da_i&=0 TO da_end&
BYTE{da_ptr%}=0
ADD da_ptr%,6
NEXT da_i&
ENDIF
'
RETURN
> FUNCTION is_record_selected(tid&,line&)
$F%
IF BYTE{ADD(head%(tid&),ADD(MUL(line&,6),4))}=1
RETURN TRUE
ELSE
RETURN FALSE
ENDIF
ENDFUNC
> FUNCTION record_get_next_selected(tid&,line&)
$F%
LOCAL gfs_end&,gfs_i&,gfs_ptr%
'
gfs_end&=PRED(head_nbline&(tid&))
INC line&
'
IF line&=<gfs_end& AND gfs_end&>-1
'
gfs_ptr%=ADD(head%(tid&),ADD(MUL(line&,6),4))
'
FOR gfs_i&=line& TO gfs_end&
IF BYTE{gfs_ptr%}>0
RETURN gfs_i&
ENDIF
ADD gfs_ptr%,6
NEXT gfs_i&
ENDIF
'
RETURN -1
ENDFUNC
> FUNCTION record_get_previous_selected(tid&,line&)
$F%
LOCAL gfs_i&,gfs_ptr%
'
DEC line&
'
IF line&>-1
'
gfs_ptr%=ADD(head%(tid&),ADD(MUL(line&,6),4))
'
FOR gfs_i&=line& TO 0 STEP -1
IF BYTE{gfs_ptr%}>0
RETURN gfs_i&
ENDIF
SUB gfs_ptr%,6
NEXT gfs_i&
ENDIF
'
RETURN -1
ENDFUNC
> FUNCTION record_move_to_top(tid&)
$F%
LOCAL current_ptr%,changed_ptr%
'
IF record_selected&(tid&)>0
current_ptr%=@px(tid&,record_selected&(tid&))
changed_ptr%=@px(tid&,PRED(record_selected&(tid&)))
'
head_fix(tid&,PRED(record_selected&(tid&)),current_ptr%)
head_fix(tid&,record_selected&(tid&),changed_ptr%)
'
DEC record_selected&(tid&)
'
get_record_xywh(tid&,record_selected&(tid&),sx&,sy&,sl&,sh&)
force_update_xywh(tid&,record_selected&(tid&),sx&,sy&,sl&,sh&)
get_record_xywh(tid&,SUCC(record_selected&(tid&)),sx&,sy&,sl&,sh&)
force_update_xywh(tid&,SUCC(record_selected&(tid&)),sx&,sy&,sl&,sh&)
'
record_changed!(tid&)=TRUE
RETURN TRUE
ENDIF
RETURN FALSE
ENDFUNC
> FUNCTION record_move_to_bottom(tid&)
$F%
LOCAL current_ptr%,changed_ptr%
'
IF record_selected&(tid&)<PRED(head_nbline&(tid&))
current_ptr%=@px(tid&,record_selected&(tid&))
changed_ptr%=@px(tid&,SUCC(record_selected&(tid&)))
'
head_fix(tid&,SUCC(record_selected&(tid&)),current_ptr%)
head_fix(tid&,record_selected&(tid&),changed_ptr%)
'
INC record_selected&(tid&)
'
get_record_xywh(tid&,record_selected&(tid&),sx&,sy&,sl&,sh&)
force_update_xywh(tid&,record_selected&(tid&),sx&,sy&,sl&,sh&)
get_record_xywh(tid&,PRED(record_selected&(tid&)),sx&,sy&,sl&,sh&)
force_update_xywh(tid&,PRED(record_selected&(tid&)),sx&,sy&,sl&,sh&)
'
record_changed!(tid&)=TRUE
RETURN TRUE
ENDIF
RETURN FALSE
ENDFUNC
> PROCEDURE record_clear_buffer(tid&)
LOCAL ips%,ipe%
'
ips%=record_buffer%(tid&)
ipe%=ADD(ips%,record_len&(tid&))
'
WHILE ips%<ipe%
INT{ips%}=0
ADD ips%,2
WEND
'
RETURN
'
> FUNCTION head_init(tid&,n&)
$F%
~@head_close(tid&)
head%(tid&)=@mxalloc(MUL(ADD(n&,100),6),3)
IF head%(tid&)>0
head_maxline&(tid&)=ADD(n&,100)
head_nbline&(tid&)=0
RETURN TRUE
ELSE
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: failed in creating|memory block for line pointers.][ Ok ]")
head_nbline&(tid&)=0
head_maxline&(tid&)=0
RETURN FALSE
ENDIF
ENDFUNC
> FUNCTION head_close(tid&)
$F%
head_nbline&(tid&)=0
IF head%(tid&)>0
@mxfree(head%(tid&))
head%(tid&)=0
RETURN TRUE
ELSE
head%(tid&)=0
RETURN FALSE
ENDIF
ENDFUNC
> FUNCTION head_resize(tid&,n&)
$F%
IF n&>head_maxline&(tid&)
new_head%=@mxalloc(ADD(MUL(ADD(n&,200),6),6),3)
IF new_head%>0
BMOVE head%(tid&),new_head%,MUL(head_nbline&(tid&),6)
@mxfree(head%(tid&))
head%(tid&)=new_head%
head_maxline&(tid&)=ADD(n&,200)
RETURN TRUE
ELSE
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: can't resize the|memory block for lines.|][ Ok ]")
ENDIF
RETURN FALSE
ENDIF
RETURN TRUE
ENDFUNC
> PROCEDURE head_fix(tid&,line&,head_ptr%)
IF line&>-1 AND line&<head_nbline&(tid&)
LONG{ADD(head%(tid&),MUL(line&,6))}=head_ptr%
ENDIF
RETURN
> FUNCTION px(tid&,line&)
$F%
RETURN LONG{ADD(head%(tid&),MUL(line&,6))}
ENDFUNC
> FUNCTION lx(tid&,line&)
$F%
RETURN INT{ADD(head%(tid&),ADD(MUL(line&,6),4))} AND &X111111111111
ENDFUNC
> FUNCTION cx(tid&,line&)
$F%
RETURN SHR|(BYTE{ADD(head%(tid&),ADD(MUL(line&,6),4))},4) AND &X1111
ENDFUNC
'
> PROCEDURE gxalloc_main_init(n&)
'
gxblk_max&=128
'
DIM gxblk%(n&,PRED(gxblk_max&)),sgxblk%(n&,PRED(gxblk_max&))
DIM gxblk_count&(n&),sgxblk_count&(n&)
DIM last_gxadr%(n&),last_gxlen%(n&)
DIM gxalloc!(n&),gxblk_size%(n&)
'
FOR i&=1 TO n&
gxblk_size%(i&)=12800
NEXT i&
'
RETURN
> PROCEDURE gxalloc_main_exit(n&)
LOCAL in&
'
FOR in&=0 TO n&
~@gxalloc_exit(in&)
NEXT in&
ERASE gxblk%(),sgxblk%()
ERASE gxblk_count&(),sgxblk_count&()
ERASE last_gxadr%(),last_gxlen%()
ERASE gxalloc!(),gxblk_size%()
'
RETURN
> FUNCTION gxalloc_init(tid&)
$F%
LOCAL ig&
'
IF NOT gxalloc!(tid&)
FOR ig&=0 TO PRED(gxblk_max&)
gxblk%(tid&,ig&)=0
sgxblk%(tid&,ig&)=0
NEXT ig&
'
gxblk_count&(tid&)=0
sgxblk_count&(tid&)=0
last_gxadr%(tid&)=0
last_gxlen%(tid&)=0
'
gxblk%(tid&,0)=@mxalloc(ADD(gxblk_size%(tid&),4),3)
IF gxblk%(tid&,0)>0
last_gxadr%(tid&)=gxblk%(tid&,0)
last_gxlen%(tid&)=gxblk_size%(tid&)
gxalloc!(tid&)=TRUE
RETURN 0
ENDIF
RETURN gxblk%(tid&,0)
ENDIF
RETURN -1
'
ENDFUNC
> FUNCTION gxalloc_exit(tid&)
$F%
LOCAL ig&
'
IF gxalloc!(tid&)
gxalloc!(tid&)=FALSE
FOR ig&=0 TO gxblk_count&(tid&)
IF gxblk%(tid&,ig&)>0
IF GEMDOS(73,L:gxblk%(tid&,ig&))=0
gxblk%(tid&,ig&)=0
ELSE
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: can't free|block memory.|][ Ok ]")
ENDIF
ENDIF
NEXT ig&
FOR ig&=0 TO sgxblk_count&(tid&)
IF sgxblk%(tid&,ig&)>0
IF GEMDOS(73,L:sgxblk%(tid&,ig&))=0
sgxblk%(tid&,ig&)=0
ELSE
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: can't free|(big) block memory.|][ Ok ]")
ENDIF
ENDIF
NEXT ig&
RETURN 0
ENDIF
RETURN -1
ENDFUNC
> FUNCTION gxalloc(tid&,wanted_size%)
$F%
'
wanted_size%=ADD(wanted_size%,3) AND -4
wanted_size%=MAX(4,wanted_size%)
'
IF wanted_size%>0
IF wanted_size%<gxblk_size%(tid&)
IF SUB(last_gxlen%(tid&),wanted_size%)<0
temp_adr%=@new_gxblk(tid&)
IF temp_adr%=0
new_gxadr%=last_gxadr%(tid&)
ADD last_gxadr%(tid&),wanted_size%
SUB last_gxlen%(tid&),wanted_size%
RETURN new_gxadr%
ENDIF
RETURN temp_adr%
ELSE
new_gxadr%=last_gxadr%(tid&)
ADD last_gxadr%(tid&),wanted_size%
SUB last_gxlen%(tid&),wanted_size%
RETURN new_gxadr%
ENDIF
ELSE IF wanted_size%=>gxblk_size%(tid&)
IF sgxblk_count&(tid&)<gxblk_max&
temp_adr%=@mxalloc(ADD(wanted_size%,32),3)
IF temp_adr%>0
sgxblk%(tid&,sgxblk_count&(tid&))=temp_adr%
INC sgxblk_count&(tid&)
RETURN temp_adr%
ENDIF
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: can't allocate|a large memory block.][ Ok ]")
RETURN temp_adr%
ENDIF
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: all available (big) memory|blocks are allocated.][ Ok ]")
RETURN -74
ENDIF
ENDIF
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: negative|length for a memory block.][ Ok ]")
RETURN -70
ENDFUNC
> FUNCTION new_gxblk(tid&)
$F%
INC gxblk_count&(tid&)
IF gxblk_count&(tid&)<gxblk_max&
gxblk%(tid&,gxblk_count&(tid&))=@mxalloc(ADD(gxblk_size%(tid&),32),3)
IF gxblk%(tid&,gxblk_count&(tid&))>0
last_gxadr%(tid&)=gxblk%(tid&,gxblk_count&(tid&))
last_gxlen%(tid&)=gxblk_size%(tid&)
RETURN 0
ELSE
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: can't |allocate a block.][ Ok ]")
ENDIF
DEC gxblk_count&(tid&)
RETURN gxblk%(tid&,SUCC(gxblk_count&(tid&)))
ENDIF
~FORM_ALERT(1,"[1][|KKcmd's GXALLOC: all available|blocks are allocated.][ Ok ]")
RETURN -74
ENDFUNC
'
> FUNCTION mx_mask
$F%
IF GEMDOS(68,L:-1,0)=-32
RETURN 0
ELSE IF GEMDOS(290,-1)=-32
RETURN 3
ELSE
RETURN -1
ENDIF
ENDFUNC
> FUNCTION mxalloc(mx_len%,mx_mode&)
$F%
IF mx_alloc!
RETURN GEMDOS(68,L:mx_len%,W:mx_mode&)
ENDIF
RETURN GEMDOS(72,L:mx_len%)
ENDFUNC
> FUNCTION mxalloc_global(mx_len%,mx_mode&)
$F%
'
IF mx_mask%<>0
mx_mode&=OR(mx_mode&,&X101000)
RETURN GEMDOS(68,L:mx_len%,W:mx_mode& AND mx_mask%)
ELSE
RETURN GEMDOS(72,L:mx_len%)
ENDIF
ENDFUNC
> PROCEDURE mxfree(mx_adr%)
IF mx_adr%>0
~GEMDOS(73,L:mx_adr%)
ENDIF
RETURN
> PROCEDURE pdomain(domain&)
IF magic! OR mint!
~GEMDOS(281,W:domain&)
ENDIF
RETURN
'
