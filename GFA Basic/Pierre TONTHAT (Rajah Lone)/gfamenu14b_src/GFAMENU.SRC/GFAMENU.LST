$m204800
IF FRE()<128000
  ~FORM_ALERT(1,"[1][| MÇmoire insuffisante | ou fragmentÇe. |][Quitter]")
  QUIT
ELSE
  ON ERROR GOSUB gest_err
  init_1
  init_2
  boucle_generale
ENDIF
'
> PROCEDURE sortir
  FOR i%=1 TO 3
    ferme_win(i%)
  NEXT i%
  IF aa_start%
    ~GEMDOS(73,L:aa_start%)
  ENDIF
  IF shel_w%
    ~GEMDOS(73,L:shel_w%)
  ENDIF
  IF m_adr%
    ~GEMDOS(73,L:m_adr%)
  ENDIF
  IF long_photo%=0
    ~GEMDOS(73,L:buf_photo%)
  ENDIF
  IF raster_image%
    ~GEMDOS(73,L:raster_image%)
  ENDIF
  ~RSRC_FREE()
  @v_clsvwk(vdi_handle&)
  ~APPL_EXIT()
  QUIT 0 ! en compilÇ
  ' EDIT     ! en interprÇtÇ
RETURN
'
> PROCEDURE init_1
  ap_id&=APPL_INIT()
  vdi_handle&=@v_opnvwk
  '
  ~GRAF_MOUSE(0,0)
  '
  ~WIND_UPDATE(3)
  ~WIND_UPDATE(1)
  '
  ~WIND_GET(0,4,screenx&,screeny&,screenl&,screenh&)
  '
  IF VAL(HEX$(INT{{ADD(GB,4)}}))>=399
    naes!=TRUE
  ELSE
    naes!=FALSE
  ENDIF
  '
  magic!=@test_cookie("MagX",dummy%)
  mint!=@test_cookie("MiNT",dummy%)
  IF magic! OR mint!
    multi!=TRUE
  ELSE
    multi!=FALSE
  ENDIF
  IF ap_id&=0
    multi!=FALSE
  ENDIF
  '
  @declare_1
  @declare_2
  @declare_3
  @declare_4
  IF mint!=FALSE
    RESERVE 15360
  ENDIF
  @declare_5
  '
  ~RSRC_FREE()
  IF @s_exist(chemin$+gfamenu_rsc$)=TRUE
    result%=RSRC_LOAD(chemin$+gfamenu_rsc$)
    IF result%=0
      ~FORM_ALERT(1,"[1][ GFAMENU.RSC n'a pas pu| àtre chargÇ comme fichier| ressource.][ Ok ]")
      sortir
    ELSE
      FOR i%=0 TO nb_tree%
        ~RSRC_GADDR(0,i%,adtree%(i%))
      NEXT i%
      FOR i%=1 TO 4
        ~FORM_CENTER(adtree%(i%),xd&(i%),yd&(i%),ld&(i%),hd&(i%))
      NEXT i%
    ENDIF
  ELSE
    ~FORM_ALERT(1,"[1][ GFAMENU.RSC non trouvÇ.][ Ok ]")
    sortir
  ENDIF
  '
RETURN
> PROCEDURE declare_1
  lect%=GEMDOS(25)
  chemin$=CHR$(lect%+65)+":"+DIR$(lect%+1)+"\"
  c0$=CHR$(0)
  masque$="*.*"+c0$
  gfamenu_rsc$="GFAMENU.RSC"+c0$
  gfamenu_inf$="GFAMENU.INF"+c0$
  '
  nb_tree%=5
  '
  DIM adtree%(nb_tree%),xd&(nb_tree%),yd&(nb_tree%),ld&(nb_tree%),hd&(nb_tree%)
  DIM hand_win%(nb_tree%),wx&(nb_tree%),wy&(nb_tree%),wl&(nb_tree%),wh&(nb_tree%)
  '
RETURN
> PROCEDURE declare_2
  '
  DIM win!(nb_tree%),aff!(nb_tree%)
  FOR i%=0 TO 3
    win!(i%)=FALSE
    aff!(i%)=FALSE
  NEXT i%
  '
  DIM cp_win%(nb_tree%)
  cp_win%(1)=&X1011
  cp_win%(2)=&X1011
  cp_win%(3)=&X1011
  '
RETURN
> PROCEDURE declare_3
  DIM info_chemin$(7),nom_chemin$(7),conv&(7),co!(17)
  '
  co!(6)=FALSE     ! DÇsactiver les routines d'interruptions
  co!(7)=TRUE      ! Optimiser SELECT...CASE
  co!(8)=TRUE      ! Ne pas gÇnÇrer ENDFUNC
  co!(9)=FALSE     ! pas P>, compiler en sous-routine 68000
  co!(10)=FALSE    ! pas de %3, convertir les divisions entiäres en virg. flottante
  co!(11)=FALSE    ! pas *&, MUL (MC68000) n'est pas utilisÇ
  co!(12)=TRUE     ! E$, pas de messages d'erreurs sous forme de texte
  co!(17)=FALSE    ! Aucun symbìle de Debogage
  '
  FOR i%=1 TO 6
    info_chemin$(i%)=SPACE$(256)
  NEXT i%
  '
  nom_chemin$(1)="GFABASIC.PRG"+c0$
  nom_chemin$(2)="RSC2.PRG"+c0$
  nom_chemin$(3)="GFA_BCOM.PRG"+c0$
  nom_chemin$(4)="GL.PRG"+c0$
  nom_chemin$(5)=c0$
  nom_chemin$(6)="GFA3BLIB"+c0$
  '
  FOR i%=1 TO 6
    conv&(i%)=11+i%
  NEXT i%
  '
RETURN
> PROCEDURE declare_4
  '
  choix_ext&=1
  edit_cur%=0
  ext$=SPACE$(4)
  prg$="GFAMTEST.PRG"+c0$
  ext_gfa$=".GFA"+c0$
  '
  m$=SPACE$(10)
  '
  gfamenu_o$="GFAMENU.O"
  tobj$=SPACE$(64)         ! DêBUT FICHIER OBJET
  tprg$=SPACE$(64)         ! dÇbut PRG
  tlib$=SPACE$(64)         ! La librairie GFA-BASIC
  t_wait$="G3WAIT=OFF"     ! Attendre entre chaque opÇration ?
  t_move$="G3MOVE=ON"     ! Optimisation de la RAM ?
  ' cobj$=SPACE$(64)       ! Les fichiers pour le linker
  '
RETURN
> PROCEDURE declare_5
  '
  m_adr%=GEMDOS(72,L:16)
  IF m_adr%<1
    sortir
  ENDIF
  '
  IF magic! OR mint!
    shel_w%=GEMDOS(68,L:20,W:32)
    aa_start%=GEMDOS(68,L:512,W:32)
  ELSE
    shel_w%=GEMDOS(72,L:20)
    aa_start%=GEMDOS(72,L:512)
  ENDIF
  '
  buf_photo%=0
  '
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=vdi_handle&
  INTIN(0)=1
  VDISYS 102
  nb_plan&=INTOUT(4)
  IF nb_plan&=0
    nb_plan&=ROUND(LOG(WORK_OUT(13))/LOG(2))
  ENDIF
  '
  raster_image%=GEMDOS(72,L:64)
  IF raster_image%
    '
    long_photo%=1
    IF WIND_GET(0,17)
      buf_photo%={GINTOUT+2}
      long_photo%={GINTOUT+6}
    ENDIF
    IF nb_plan&>8 OR long_photo%<2000*nb_plan& OR buf_photo%<1
      long_photo%=0
      IF magic! OR mint!
        buf_photo%=GEMDOS(68,L:2000*nb_plan&,W:0)
      ELSE
        buf_photo%=GEMDOS(72,L:2000*nb_plan&)
      ENDIF
    ENDIF
    '
    pscrmfdb%=raster_image%
    pdesmfdb%=pscrmfdb%+20
    pxyarray%=pdesmfdb%+20
    '
    make_zero_mfdb(pscrmfdb%)
    make_zero_mfdb(pdesmfdb%)
    '
    LONG{pdesmfdb%}=buf_photo%
    WORD{ADD(pdesmfdb%,4)}=160
    WORD{ADD(pdesmfdb%,6)}=100
    WORD{ADD(pdesmfdb%,8)}=10
    WORD{ADD(pdesmfdb%,12)}=nb_plan&
    '
  ENDIF
  '
RETURN
> PROCEDURE init_2
  '
  FOR i%=1 TO 4
    FOR j%=0 TO OB_TAIL(adtree%(i%),0)
      dummy%=OB_TYPE(adtree%(i%),j%)
      SELECT dummy%
      CASE 20,22,26,27
        OB_FLAGS(adtree%(i%),j%)=BSET(OB_FLAGS(adtree%(i%),j%),9)
        IF dummy%=20 AND j%=0 AND nb_plan&<3 AND i%<>4
          OB_SPEC(adtree%(i%),0)=OB_SPEC(adtree%(i%),0) AND &X1111111111111111
        ENDIF
      DEFAULT
        OB_FLAGS(adtree%(i%),j%)=OB_FLAGS(adtree%(i%),j%) AND &X111111111
      ENDSELECT
    NEXT j%
  NEXT i%
  '
  FOR i%=1 TO 6
    info_chemin$(i%)=chemin$+c0$
  NEXT i%
  IF @s_exist(chemin$+gfamenu_inf$)=TRUE
    OPEN "i",#1,chemin$+gfamenu_inf$
    INPUT #1,dummy$
    IF dummy$="GFAM#1.4"
      FOR i%=1 TO 6
        INPUT #1,info_chemin$(i%)
      NEXT i%
      FOR i%=6 TO 12
        INPUT #1,co!(i%)
      NEXT i%
      INPUT #1,co!(17)
      INPUT #1,choix_ext&
    ENDIF
    CLOSE #1
  ENDIF
  '
  CHAR{{OB_SPEC(adtree%(3),4)}}=LEFT$(TRIM$(CHAR{{OB_SPEC(adtree%(4),MAX(1,MIN(choix_ext&,6)))}}),5)
  ext$=MID$(TRIM$(CHAR{{OB_SPEC(adtree%(3),4)}}),2)+c0$
  FOR i%=1 TO 6
    CHAR{{OB_SPEC(adtree%(2),i%*2)}}=RIGHT$(info_chemin$(i%),30)+c0$
  NEXT i%
  CHAR{{OB_SPEC(adtree%(3),15)}}=""
  FOR i%=6 TO 12
    IF co!(i%)
      OB_STATE(adtree%(3),i%)=BSET(OB_STATE(adtree%(3),i%),2)
    ELSE
      OB_STATE(adtree%(3),i%)=BCLR(OB_STATE(adtree%(3),i%),2)
    ENDIF
  NEXT i%
  IF co!(17)
    OB_STATE(adtree%(3),i%)=BSET(OB_STATE(adtree%(3),17),2)
  ELSE
    OB_STATE(adtree%(3),i%)=BCLR(OB_STATE(adtree%(3),17),2)
  ENDIF
  '
  ~WIND_UPDATE(2)
  ~WIND_UPDATE(0)
  '
  ~MENU_BAR(adtree%(0),1)
  IF multi!=FALSE
    ~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
  ENDIF
  win(3)
  '
RETURN
'
> PROCEDURE boucle_generale
  DO
    evnt&=@ev_multi(&X10011,2,1,1,100,mo_x&,mo_y&,mo_k&,m_touche%,m_clavier%,mo_c&)
    IF BTST(evnt&,0)
      m_clavier&=BYTE(m_clavier%)
      IF m_clavier&=17
        IF FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),4)})=1
          sortir
        ENDIF
      ENDIF
      SELECT m_clavier%
      CASE 25088 ! Help
      CASE 15104 ! f1
        win(1)
      CASE 15360 ! f2
        win(2)
      CASE 15616 ! f3
        call(1)
      CASE 15872 ! f4
        call(2)
      CASE 16128 ! f5
        compiler_prg
      CASE 16384 ! f6
        tester
      CASE 16896 ! f8
      CASE 17152 ! f9
      CASE 17408 ! f10
      ENDSELECT
      ~WIND_GET(0,10,top_win%,dummy%,dummy%,dummy%)
      IF top_win%=hand_win%(3) AND win!(3)
        SELECT m_clavier&
        CASE 8,27,48 TO 57
          ~OBJC_EDIT(adtree%(3),15,m_clavier%,edit_cur%,2,edit_cur%)
        ENDSELECT
      ENDIF
    ENDIF
    IF BTST(evnt&,1)
      IF mo_c&=1 AND mo_k&=1
        ~WIND_GET(0,10,top_win%,dummy%,dummy%,dummy%)
        clic_win%=WIND_FIND(mo_x&,mo_y&)
        delai
        boucle_generale_suite
      ENDIF
    ENDIF
    IF BTST(evnt&,4)
      SELECT m_type&
      CASE 10
        boucle_menu
      CASE 20
        redraw
      CASE 21
        win_topped
      CASE 22
        win_closed
      CASE 28
        win_moved
      CASE 29,31
        win_top
      CASE 50
        QUIT
      CASE 22360
        FOR i%=1 TO 3
          IF m_fenetre&=hand_win%(i%) AND win!(i%)=TRUE
            aff!(i%)=FALSE
          ENDIF
        NEXT i%
      CASE 22361
        FOR i%=1 TO 3
          IF m_fenetre&=hand_win%(i%) AND win!(i%)=TRUE
            aff!(i%)=TRUE
          ENDIF
        NEXT i%
      ENDSELECT
    ENDIF
    clavier_win%=0
    m_entree&=0
    m_titre&=0
    m_type&=0
    m_fenetre&=0
    m_x&=0
    m_y&=0
    m_l&=0
    m_h&=0
  LOOP
RETURN
> PROCEDURE boucle_generale_suite
  IF clic_win%=hand_win%(2) AND win!(2)
    gere_chemins
  ENDIF
  IF clic_win%=hand_win%(3) AND win!(3)
    gere_dialog
  ENDIF
RETURN
> PROCEDURE boucle_menu
  ~MENU_TNORMAL(adtree%(0),m_titre&,1)
  SELECT m_entree&
  CASE 7 ! about
    win(1)
  CASE 16 ! chemins
    win(2)
  CASE 18 ! editeur GFA
    call(1)
  CASE 19 ! editeur RSC
    call(2)
  CASE 21 ! quitter
    sortir
  ENDSELECT
RETURN
'
> PROCEDURE win(dial&)
  IF win!(dial&)
    force_top(dial&)
  ELSE
    IF win!(dial&)=FALSE
      create_win(dial&)
    ENDIF
  ENDIF
RETURN
> PROCEDURE create_win(dial&)
  hand_win%(dial&)=@window_create(cp_win%(dial&))
  IF hand_win%(dial&)>0
    win!(dial&)=TRUE
    ~FORM_CENTER(adtree%(dial&),xd&(dial&),yd&(dial&),ld&(dial&),hd&(dial&))
    ~WIND_SET(hand_win%(dial&),2,CARD(SWAP(OB_SPEC(adtree%(nb_tree%),dial&))),CARD(OB_SPEC(adtree%(nb_tree%),dial&)),0,0)
    ~WIND_CALC(0,cp_win%(dial&),xd&(dial&),yd&(dial&),ld&(dial&),hd&(dial&),wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))
    wx&(dial&)=MAX(screenx&+1,wx&(dial&))
    wy&(dial&)=MAX(screeny&+1,wy&(dial&))
    move_win(dial&,wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))
    ~WIND_SET(hand_win%(dial&),24,&X1,0,0,0)
    dummy%=WIND_OPEN(hand_win%(dial&),wx&(dial&),wy&(dial&),wl&(dial&),wh&(dial&))
    IF dummy%=0
      win!(dial&)=FALSE
    ENDIF
  ELSE
    no_more_win
  ENDIF
  aff!(dial&)=win!(dial&)
RETURN
> PROCEDURE no_more_win
  ~FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),5)})
  win!(dial&)=FALSE
RETURN
> PROCEDURE ferme_win(dial&)
  IF win!(dial&)
    ~WIND_CLOSE(hand_win%(dial&))
    ~WIND_DELETE(hand_win%(dial&))
    win!(dial&)=FALSE
  ENDIF
RETURN
> PROCEDURE move_win(dial&,x0&,y0&,l0&,h0&)
  IF win!(dial&)
    ~WIND_CALC(1,cp_win%(dial&),x0&,y0&,l0&,h0&,xd&(dial&),yd&(dial&),dummy%,dummy%)
    OB_X(adtree%(dial&),0)=xd&(dial&)
    OB_Y(adtree%(dial&),0)=yd&(dial&)
  ENDIF
RETURN
'
> PROCEDURE force_top(bar%)
  ~WIND_GET(0,10,top_win%,dummy%,dummy%,dummy%)
  IF top_win%<>hand_win%(bar%) AND win!(bar%)=TRUE
    INT{m_adr%}=21
    INT{m_adr%+2}=ap_id&
    INT{m_adr%+4}=0
    INT{m_adr%+6}=hand_win%(bar%)
    INT{m_adr%+8}=0
    INT{m_adr%+10}=0
    INT{m_adr%+12}=0
    INT{m_adr%+14}=0
    ~APPL_WRITE(ap_id&,16,m_adr%)
  ENDIF
RETURN
'
> PROCEDURE win_closed
  IF m_fenetre&=hand_win%(3)
    sortir
  ELSE
    FOR i%=1 TO 2
      IF m_fenetre&=hand_win%(i%) AND win!(i%)
        ferme_win(i%)
      ENDIF
    NEXT i%
  ENDIF
RETURN
> PROCEDURE win_moved
  m_x&=MAX(screenx&+1,m_x&)
  m_y&=MAX(screeny&+1,m_y&)
  ~WIND_SET(m_fenetre&,5,m_x&,m_y&,m_l&,m_h&)
  FOR i%=1 TO 3
    IF m_fenetre&=hand_win%(i%) AND win!(i%)
      move_win(i%,m_x&,m_y&,m_l&,m_h&)
    ENDIF
  NEXT i%
RETURN
> PROCEDURE win_topped
  ~WIND_GET(0,10,top_win%,dummy%,dummy%,dummy%)
  IF aff!(3)=TRUE
    ~OBJC_EDIT(adtree%(3),15,0,edit_cur%,3,edit_cur%)
  ENDIF
  FOR i%=1 TO 3
    IF m_fenetre&=hand_win%(i%) AND win!(i%)=TRUE
      ~WIND_SET(hand_win%(i%),10,0,0,0,0)
      ~WIND_GET(0,10,top_win%,dummy%,dummy%,dummy%)
      IF i%=3 AND top_win%=hand_win%(3) AND aff!(3)=TRUE
        ~OBJC_EDIT(adtree%(3),15,0,0,1,edit_cur%)
      ENDIF
    ENDIF
  NEXT i%
RETURN
> PROCEDURE win_top
  IF m_fenetre&=hand_win%(3) AND win!(3)=TRUE AND aff!(3)=TRUE
    ~OBJC_EDIT(adtree%(3),15,0,0,1,edit_cur%)
  ENDIF
RETURN
> PROCEDURE redraw
  control
  ~WIND_GET(m_fenetre&,11,rx&,ry&,rl&,rh&)
  WHILE rl&<>0 AND rh&<>0
    IF RC_INTERSECT(m_x&,m_y&,m_l&,m_h&,rx&,ry&,rl&,rh&)
      FOR i%=1 TO 3
        IF m_fenetre&=hand_win%(i%) AND win!(i%)
          ~OBJC_DRAW(adtree%(i%),0,3,rx&,ry&,rl&,rh&)
          ~WIND_GET(0,10,top_win%,dummy%,dumy%,dummy%)
          IF i%=3 AND top_win%=hand_win%(3) AND aff!(3)=TRUE
            ~OBJC_EDIT(adtree%(3),15,0,0,1,edit_cur%)
          ENDIF
        ENDIF
      NEXT i%
    ENDIF
    ~WIND_GET(m_fenetre&,12,rx&,ry&,rl&,rh&)
  WEND
  uncontrol
RETURN
'
> PROCEDURE call(nom_chemin%)
  IF @s_exist(info_chemin$(nom_chemin%))=TRUE
    IF multi!=FALSE
      ~MENU_BAR(adtree%(0),0)
      FOR i%=1 TO 3
        ferme_win(i%)
      NEXT i%
      IF nom_chemin%=1
        num_drive&=ASC(info_chemin$(5))-65
        ~GEMDOS(14,W:num_drive&)
        CHDIR LEFT$(info_chemin$(5),RINSTR(info_chemin$(5),"\"))+c0$
      ELSE
        num_drive&=ASC(info_chemin$(nom_chemin%))-65
        ~GEMDOS(14,W:num_drive&)
        CHDIR LEFT$(info_chemin$(nom_chemin%),RINSTR(info_chemin$(nom_chemin%),"\"))+c0$
      ENDIF
    ENDIF
    IF multi!
      IF nom_chemin%=1
        IF magic!=TRUE
          CHAR{aa_start%}=LEFT$(info_chemin$(1),255)
          CHAR{aa_start%+256}=c0$
          '
          INT{m_adr%}=&H4722
          INT{m_adr%+2}=ap_id&
          INT{m_adr%+4}=0
          LONG{m_adr%+6}=aa_start%
          LONG{m_adr%+10}=aa_start%+256
          INT{m_adr%+14}=0
          ~APPL_WRITE(0,16,m_adr%)
        ELSE
          shl_write(1,1,100,info_chemin$(1),c0$)
        ENDIF
      ELSE
        SELECT LEFT$(RIGHT$(info_chemin$(nom_chemin%),4),3)
        CASE "ACC","ACX"
          shl_write(3,1,100,info_chemin$(nom_chemin%),c0$)
        CASE "GTP","PRG","PRX","APP"
          shl_write(1,1,100,info_chemin$(nom_chemin%),c0$)
        CASE "TTP","TOS","TTX"
          shl_write(1,0,100,info_chemin$(nom_chemin%),c0$)
        ENDSELECT
      ENDIF
    ELSE
      ~EXEC(0,info_chemin$(nom_chemin%),c0$,c0$+c0$)
    ENDIF
    IF multi!=FALSE
      num_drive&=ASC(chemin$)-65
      ~GEMDOS(14,W:num_drive&)
      CHDIR chemin$+c0$
      ~MENU_BAR(adtree%(0),1)
      win(3)
      ~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
    ENDIF
  ELSE
    ~FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),6)})
  ENDIF
RETURN
'
> PROCEDURE gere_chemins
  result%=OBJC_FIND(adtree%(2),0,2,mo_x&,mo_y&)
  IF result%>0
    SELECT result%
    CASE 2,4,6,8,10,12
      e%=result%/2
      black_white(2,result%,1)
      info_chemin$(e%)=@fileselector2$(conv&(e%),LEFT$(info_chemin$(e%),RINSTR(info_chemin$(e%),"\"))+c0$,nom_chemin$(e%))
      CHAR{{OB_SPEC(adtree%(2),result%)}}=RIGHT$(info_chemin$(e%),30)+c0$
      black_white(2,result%,0)
    CASE 13
      black_white(2,13,1)
      IF FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),7)})=1
        OPEN "o",#1,chemin$+gfamenu_inf$
        PRINT #1,"GFAM#1.4"
        FOR i%=1 TO 6
          PRINT #1,info_chemin$(i%)
        NEXT i%
        FOR i%=6 TO 12
          PRINT #1,co!(i%)
        NEXT i%
        PRINT #1,co!(17)
        PRINT #1,choix_ext&
        CLOSE #1
        force_drive(LEFT$(chemin$))
      ENDIF
      black_white(2,13,0)
    ENDSELECT
  ENDIF
RETURN
'
> PROCEDURE gere_dialog
  result%=OBJC_FIND(adtree%(3),0,2,mo_x&,mo_y&)
  SELECT result%
  CASE 1
    compiler_prg
  CASE 2
    tester
  CASE 4
    pop_up(3,4,4)
  CASE 6 TO 12,17
    IF OB_STATE(adtree%(3),result%)
      OB_STATE(adtree%(3),result%)=BCLR(OB_STATE(adtree%(3),result%),2)
      black_white(3,result%,0)
      co!(result%)=FALSE
    ELSE
      black_white(3,result%,4)
      co!(result%)=TRUE
    ENDIF
  CASE 18
    black_white(3,18,1)
    IF FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),8)})=1
      do_reset
    ENDIF
    black_white(3,18,0)
  DEFAULT
    win(3)
  ENDSELECT
RETURN
> PROCEDURE compiler_prg
  black_white(3,1,1)
  ext$=MID$(TRIM$(CHAR{{OB_SPEC(adtree%(3),4)}}),2)+c0$
  compiler_0
  black_white(3,1,0)
RETURN
> PROCEDURE tester
  black_white(3,2,1)
  ext$=MID$(TRIM$(CHAR{{OB_SPEC(adtree%(3),4)}}),2)+c0$
  info_chemin$(7)=@fileselector2$(18,LEFT$(info_chemin$(5),RINSTR(info_chemin$(5),"\"))+"*"+ext$,c0$)
  IF @s_exist(info_chemin$(7))=TRUE
    call(7)
  ENDIF
  black_white(3,2,0)
RETURN
> PROCEDURE do_reset
  ~XBIOS(38,L:LPEEK(4))
RETURN
> PROCEDURE pop_up(pop_abr%,pop_ob%,pop_menu%)
  OB_X(adtree%(pop_menu%),0)=OB_X(adtree%(pop_abr%),0)+OB_X(adtree%(pop_abr%),pop_ob%)
  OB_Y(adtree%(pop_menu%),0)=OB_Y(adtree%(pop_abr%),0)+OB_Y(adtree%(pop_abr%),pop_ob%)
  control
  get_photo(pop_menu%,0)
  v_show_c
  old_result%=0
  DO
    evnt&=@ev_multi(&X10011,2,0,1,100,mo_x&,mo_y&,mo_k&,dummy%,dummy%,mo_c&)
    menu_result%=OBJC_FIND(adtree%(pop_menu%),0,OB_TAIL(adtree%(pop_menu%),0),mo_x&,mo_y&)
    IF old_result%<>menu_result%
      IF menu_result%>0 AND menu_result%<15
        IF old_result%>0
          noir_blanc(pop_menu%,old_result%,0)
        ENDIF
        old_result%=menu_result%
        noir_blanc(pop_menu%,old_result%,1)
      ELSE
        IF old_result%>0
          noir_blanc(pop_menu%,old_result%,0)
        ENDIF
        old_result%=0
      ENDIF
    ENDIF
  LOOP UNTIL evnt&<>2 OR (mo_c&=1 AND mo_k&=1)
  delai
  put_photo(pop_abr%)
  uncontrol
  IF old_result%>0
    OB_STATE(adtree%(pop_menu%),old_result%)=0
    choix_ext&=old_result%
    CHAR{{OB_SPEC(adtree%(pop_abr%),pop_ob%)}}=CHAR{{OB_SPEC(adtree%(pop_menu%),old_result%)}}
    black_white(pop_abr%,pop_ob%,0)
  ENDIF
  delai
RETURN
'
> PROCEDURE compiler_0 ! nom des fichiers
  gfa_name$=@fileselector2$(19,LEFT$(info_chemin$(5),RINSTR(info_chemin$(5),"\"))+LEFT$(masque$)+ext_gfa$,c0$)
  IF @s_exist(gfa_name$)=TRUE AND RIGHT$(gfa_name$,5)=ext_gfa$
    dummy%=RINSTR(gfa_name$,ext_gfa$)
    IF dummy%>0
      gfa_name$=LEFT$(gfa_name$,dummy%-1)
    ENDIF
    prg$=MID$(gfa_name$,RINSTR(gfa_name$,"\")+1)+ext$
    prg_name$=@fileselector2$(20,LEFT$(info_chemin$(5),RINSTR(info_chemin$(5),"\"))+LEFT$(masque$)+ext$,prg$)
    IF LEN(prg_name$)>1 AND prg_name$<>c0$
      IF multi!=FALSE
        ~MENU_BAR(adtree%(0),0)
        FOR i%=1 TO 3
          ferme_win(i%)
        NEXT i%
        v_hide_c
        CLS
        ~WIND_UPDATE(1)
        ~WIND_UPDATE(3)
      ENDIF
      compiler_1
      compiler_2
      compiler_3
      compiler_4
      IF multi!=FALSE
        ~WIND_UPDATE(0)
        ~WIND_UPDATE(2)
        ~MENU_BAR(adtree%(0),1)
        win(3)
        ~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
        v_show_c
      ENDIF
    ENDIF
  ENDIF
RETURN
> PROCEDURE compiler_1 ! options de compil
  co$=""
  IF co!(6) ! interruptions
    co$=co$+" I+"
  ENDIF
  IF co!(7) ! Select
    co$=co$+" S<"
  ENDIF
  IF co!(8) ! Fonctions
    co$=co$+" F>"
  ELSE
    co$=co$+" F<"
  ENDIF
  IF co!(9) ! ProcÇdures
    co$=co$+" P>"
  ELSE
    co$=co$+" P<"
  ENDIF
  IF co!(10) ! IntDiv
    co$=co$+" %3"
  ELSE
    co$=co$+" %0"
  ENDIF
  IF co!(11) ! IntMul
    co$=co$+" *&"
  ELSE
    co$=co$+" *%"
  ENDIF
  IF co!(12) ! Erreurs
    co$=co$+" E$"
  ELSE
    co$=co$+" E-"
  ENDIF
  IF co!(17) ! Debug Symbol
    s$="-s"
  ELSE
    s$=""
  ENDIF
  m$=CHAR{{OB_SPEC(adtree%(3),15)}}
  memory%=VAL(m$)
  IF memory%>0 AND memory%<100000000
    co$=co$+" $m"+STR$(memory%)
  ENDIF
RETURN
> PROCEDURE compiler_2 ! env$
  tobj$="G3OBJ="+chemin$+gfamenu_o$
  tprg$="G3PRG="+prg_name$
  tlib$="G3LIB="+info_chemin$(6)
  IF RIGHT$(tprg$)=c0$
    tprg$=LEFT$(tprg$,LEN(tprg$)-1)
  ENDIF
  IF RIGHT$(tlib$)=c0$
    tlib$=LEFT$(tlib$,LEN(tlib$)-1)
  ENDIF
  env$=tobj$+c0$+tprg$+c0$+tlib$+c0$+twait$+c0$+tmove$+c0$+c0$
RETURN
> PROCEDURE compiler_3 ! Appelle le compilateur avec les paramätres choisis
  IF @s_exist(info_chemin$(3))=TRUE
    IF @s_exist(chemin$+gfamenu_o$+c0$)
      KILL chemin$+gfamenu_o$+c0$
    ENDIF
    len_tail|=MIN(255,LEN(gfa_name$+co$+c0$))
    IF multi!=TRUE
      CHAR{aa_start%}=LEFT$(info_chemin$(3),255)
      CHAR{aa_start%+384}=LEFT$(env$,127)
      LONG{shel_w%}=aa_start%
      LONG{shel_w%+16}=aa_start%+384
      shl_write(1+8*256,0,100,info_chemin$(3),CHR$(len_tail|)+gfa_name$+co$+c0$)
      FOR i%=0 TO 50
        delai
      NEXT i%
      survey_prg$=@survey_name$(info_chemin$(3))
      DO
      LOOP UNTIL APPL_FIND(survey_prg$)<0
    ELSE
      e%=EXEC(0,info_chemin$(3),CHR$(len_tail|)+gfa_name$+co$+c0$,env$)
    ENDIF
  ELSE
    ~FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),9)})
  ENDIF
RETURN
> PROCEDURE compiler_4 ! Appelle le linker avec les paramätres choisis
  IF @s_exist(info_chemin$(4))=TRUE
    IF @s_exist(chemin$+gfamenu_o$+c0$)=TRUE
      len_tail|=MIN(255,LEN(s$+c0$))
      IF multi!=TRUE
        CHAR{aa_start%}=LEFT$(info_chemin$(4),255)
        CHAR{aa_start%+384}=LEFT$(env$,127)
        LONG{shel_w%}=aa_start%
        LONG{shel_w%+16}=aa_start%+384
        shl_write(1+8*256,0,100,info_chemin$(4),CHR$(len_tail|)+s$+c0$)
        FOR i%=0 TO 20
          delai
        NEXT i%
        survey_prg$=@survey_name$(info_chemin$(4))
        DO
        LOOP UNTIL APPL_FIND(survey_prg$)<0
      ELSE
        e%=EXEC(0,info_chemin$(4),CHR$(len_tail|)+s$+c0$,env$)
        PRINT "End : clic mouse"
        DO
          ~GRAF_MKSTATE(dummy%,dummy%,mo_k&,mo_c&)
        LOOP UNTIL mo_k& OR mo_c&
      ENDIF
    ELSE
      ~FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),21)})
    ENDIF
  ELSE
    ~FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),10)})
  ENDIF
RETURN
'
> PROCEDURE control
  ~WIND_UPDATE(1)
  ~WIND_UPDATE(3)
  v_hide_c
RETURN
> PROCEDURE uncontrol
  ~WIND_UPDATE(2)
  ~WIND_UPDATE(0)
  v_show_c
RETURN
> PROCEDURE noir_blanc(arbre%,fils%,nb_etat%)
  IF BTST(OB_FLAGS(adtree%(arbre%),fils%),9)=0
    black_white(arbre%,fils%,OB_STATE(adtree%(arbre%),fils%))
  ENDIF
  black_white(arbre%,fils%,nb_etat%)
RETURN
> PROCEDURE black_white(arbre%,fils%,etat%)
  SELECT etat%
  CASE 0
    OB_STATE(adtree%(arbre%),fils%)=BCLR(OB_STATE(adtree%(arbre%),fils%),0)
  CASE 1
    OB_STATE(adtree%(arbre%),fils%)=BSET(OB_STATE(adtree%(arbre%),fils%),0)
  CASE 4
    OB_STATE(adtree%(arbre%),fils%)=BSET(OB_STATE(adtree%(arbre%),fils%),2)
  ENDSELECT
  IF (win!(arbre%)=TRUE AND aff!(arbre%)=TRUE) OR arbre%=4
    IF arbre%=4
      xf&=screenx&
      yf&=screeny&
      lf&=screenl&
      hf&=screenh&
      rx&=xf&
      ry&=yf&
      rl&=lf&
      rh&=hf&
    ELSE
      ~WIND_GET(hand_win%(arbre%),4,xf&,yf&,lf&,hf&)
      ~WIND_GET(hand_win%(arbre%),11,rx&,ry&,rl&,rh&)
    ENDIF
    control
    WHILE rl&<>0 AND rh&<>0
      IF RC_INTERSECT(xf&,yf&,lf&,hf&,rx&,ry&,rl&,rh&)
        ~OBJC_DRAW(adtree%(arbre%),fils%,1,rx&,ry&,rl&,rh&)
      ENDIF
      ~WIND_GET(hand_win%(arbre%),12,rx&,ry&,rl&,rh&)
    WEND
    uncontrol
  ENDIF
RETURN
> PROCEDURE delai
  ~EVNT_TIMER(75)
RETURN
> PROCEDURE shl_write(mode%,wisgr%,wiscr%,cmd$,tail$)
  '
  GCONTRL(0)=121
  GCONTRL(1)=3
  GCONTRL(2)=1
  GCONTRL(3)=2
  GCONTRL(4)=0
  '
  GINTIN(0)=mode%
  GINTIN(1)=wisgr%
  GINTIN(2)=wiscr%
  '
  IF mode%>256
    ADDRIN(0)=shel_w%
    ADDRIN(1)=V:tail$
  ELSE
    ADDRIN(0)=V:cmd$
    ADDRIN(1)=V:tail$
  ENDIF
  '
  GEMSYS
  '
  IF GINTOUT(0)=0
    ~FORM_ALERT(1,CHAR{OB_SPEC(adtree%(nb_tree%),11)})
  ENDIF
  '
RETURN
'
> PROCEDURE gest_err
  result%=FORM_ALERT(2,"[1][ erreur n¯"+STR$(ERR)+"][Quitter]")
  sortir
RETURN
'
> FUNCTION window_create(cp_win_recu%)
  '
  GCONTRL(0)=100
  GCONTRL(1)=5
  GCONTRL(2)=1
  GCONTRL(3)=0
  GCONTRL(4)=0
  '
  GINTIN(0)=cp_win_recu%
  GINTIN(1)=30
  GINTIN(2)=30
  GINTIN(3)=30
  GINTIN(4)=30
  '
  GEMSYS
  '
  RETURN GINTOUT(0)
ENDFUNC
> FUNCTION test_cookie(cookie_name$,VAR cookie_valeur%)
  LOCAL read_cook%,nom_cook%,cookie%
  '
  nom_cook%=CVL(cookie_name$)
  cookie%=LPEEK(&H5A0)
  cookie_valeur%=0
  '
  IF cookie%<>0
    REPEAT
      read_cook%=LPEEK(cookie%)
      cookie_valeur%=LPEEK(cookie%+4)
      ADD cookie%,8
    UNTIL read_cook%=0 OR read_cook%=nom_cook%
    IF read_cook%=nom_cook%
      RETURN TRUE
    ELSE
      RETURN FALSE
    ENDIF
  ELSE
    RETURN FALSE
  ENDIF
ENDFUNC
> FUNCTION ev_multi(em_flags&,em_cl&,em_ma&,em_st&,em_ct%,VAR em_mx&,em_my&,em_mk&,em_kbd%,em_key%,em_click&)
  '
  GCONTRL(0)=25
  GCONTRL(1)=16
  GCONTRL(2)=7
  GCONTRL(3)=1
  GCONTRL(4)=0
  '
  GINTIN(0)=em_flags&
  GINTIN(1)=em_cl&
  GINTIN(2)=em_ma&
  GINTIN(3)=em_st&
  GINTIN(4)=0
  GINTIN(5)=0
  GINTIN(6)=0
  GINTIN(7)=0
  GINTIN(8)=0
  GINTIN(9)=0
  GINTIN(10)=0
  GINTIN(11)=0
  GINTIN(12)=0
  GINTIN(13)=0
  INT{GINTIN+28}=WORD(em_ct%)
  INT{GINTIN+30}=WORD(SWAP(em_ct%))
  '
  ADDRIN(0)=m_adr%
  '
  GEMSYS
  '
  em_mx&=GINTOUT(1)
  em_my&=GINTOUT(2)
  em_mk&=GINTOUT(3)
  em_kbd%=GINTOUT(4)
  em_key%=GINTOUT(5)
  em_click&=GINTOUT(6)
  '
  m_type&=INT{m_adr%}
  m_ap_id&=INT{m_adr%+2}
  m_nothing&=INT{m_adr%+4}
  m_titre&=INT{m_adr%+6}
  m_fenetre&=INT{m_adr%+6}
  m_entree&=INT{m_adr%+8}
  m_x&=INT{m_adr%+8}
  m_y&=INT{m_adr%+10}
  m_l&=INT{m_adr%+12}
  m_h&=INT{m_adr%+14}
  '
  RETURN GINTOUT(0)
ENDFUNC
> FUNCTION fileselector2$(type_msg&,path$,name$)
  LOCAL path1$,name1$,choix_file&,retour_file&
  LET path1$=path$
  LET name1$=name$
  retour_file&=@fi_input(type_msg&,path1$,name1$,choix_file&)
  IF name1$=c0$ OR name1$=""
    choix_file&=0
  ENDIF
  IF retour_file&=0 OR choix_file&=0
    IF type_msg&=16
      RETURN LEFT$(path1$,RINSTR(path1$,"\"))+c0$
    ELSE
      RETURN c0$
    ENDIF
  ELSE
    RETURN LEFT$(path1$,RINSTR(path1$,"\"))+name1$
  ENDIF
ENDFUNC
> FUNCTION fi_input(type_msg&,VAR fi_path$,fi_name$,fi_choix&)
  '
  ~FRE()
  ~FRE(0)
  '
  IF GEMDOS(48)<&H1500
    type_msg&=0
  ENDIF
  '
  IF type_msg&>0
    GCONTRL(0)=91
  ELSE
    GCONTRL(0)=90
  ENDIF
  GCONTRL(1)=0
  GCONTRL(2)=2
  IF type_msg&>0
    GCONTRL(3)=3
  ELSE
    GCONTRL(3)=2
  ENDIF
  GCONTRL(4)=0
  '
  fi_path$=fi_path$+SPACE$(300)
  fi_name$=fi_name$+SPACE$(100)
  '
  ADDRIN(0)=V:fi_path$
  ADDRIN(1)=V:fi_name$
  IF type_msg&>0
    ADDRIN(2)=OB_SPEC(adtree%(nb_tree%),type_msg&)
  ENDIF
  '
  GEMSYS
  '
  fi_path$=CHAR{V:fi_path$}+c0$
  fi_name$=CHAR{V:fi_name$}+c0$
  fi_choix&=GINTOUT(1)
  '
  RETURN GINTOUT(0)
ENDFUNC
> FUNCTION s_exist(exist_name$)
  exist_name$=exist_name$+c0$
  LOCAL existe&
  IF LEN(exist_name$)=0 OR LEFT$(exist_name$)=c0$
    RETURN FALSE
  ELSE
    existe&=GEMDOS(61,L:V:exist_name$,W:0)
    IF existe&>0
      ~GEMDOS(62,W:existe&)
      RETURN TRUE
    ELSE
      RETURN FALSE
    ENDIF
  ENDIF
ENDFUNC
> PROCEDURE force_drive(force_drive$)
  INT{m_adr%}=72
  INT{m_adr%+2}=ap_id&
  INT{m_adr%+4}=0
  INT{m_adr%+6}=MAX(0,MIN(ASC(force_drive$)-65,25))
  INT{m_adr%+8}=0
  INT{m_adr%+10}=0
  INT{m_adr%+12}=0
  INT{m_adr%+14}=0
  ~APPL_WRITE(0,16,m_adr%)
RETURN
> FUNCTION survey_name$(survey_recu$)
  LOCAL survey$
  survey$=MID$(survey_recu$,RINSTR(survey_recu$,"\")+1)
  survey$=LEFT$(survey$,MAX(0,RINSTR(survey$,".")-1))
  survey$=survey$+SPACE$(MAX(0,8-LEN(survey$)))
  RETURN survey$
ENDFUNC
'
> PROCEDURE get_photo(abr%,menu_box%)
  IF buf_photo%
    ~OBJC_OFFSET(adtree%(abr%),menu_box%,x9&,y9&)
    SUB x9&,3
    SUB y9&,3
    l9&=OB_W(adtree%(abr%),menu_box%)+9
    h9&=OB_H(adtree%(abr%),menu_box%)+9
    IF x9&+l9&>screenx&+screenl&
      l9&=screenx&+screenl&-x9&
    ENDIF
    IF y9&+h9&>screeny&+screenh&
      h9&=screeny&+screenh&-y9&
    ENDIF
    '
    make_xyarray(x9&,y9&,x9&+l9&,y9&+h9&,1,1,l9&,h9&)
    '
    vro_cpyfm(pscrmfdb%,pdesmfdb%)
    '
  ENDIF
  black_white(abr%,menu_box%,0)
RETURN
> PROCEDURE put_photo(abr%)
  IF buf_photo%
    '
    make_xyarray(1,1,l9&,h9&,x9&,y9&,x9&+l9&-1,y9&+h9&-1)
    '
    vro_cpyfm(pdesmfdb%,pscrmfdb%)
    '
  ELSE
    ~FORM_DIAL(3,0,0,0,0,screenx&,screeny&,screenl&,screenh&)
  ENDIF
RETURN
> PROCEDURE make_zero_mfdb(pmfdb%)
  LONG{pmfdb%}=0
  LONG{ADD(pmfdb%,4)}=0
  LONG{ADD(pmfdb%,8)}=0
  LONG{ADD(pmfdb%,12)}=0
  LONG{ADD(pmfdb%,16)}=0
RETURN
> PROCEDURE make_xyarray(xq0,yq0,xq1,yq1,xz0,yz0,xz1,yz1)
  WORD{pxyarray%}=xq0
  WORD{ADD(pxyarray%,2)}=yq0
  WORD{ADD(pxyarray%,4)}=xq1
  WORD{ADD(pxyarray%,6)}=yq1
  WORD{ADD(pxyarray%,8)}=xz0
  WORD{ADD(pxyarray%,10)}=yz0
  WORD{ADD(pxyarray%,12)}=xz1
  WORD{ADD(pxyarray%,14)}=yz1
RETURN
> PROCEDURE vro_cpyfm(pscr_mfdb%,pdes_mfdb%)
  '
  v_hide_c
  '
  CONTRL(1)=4
  CONTRL(2)=0
  CONTRL(3)=1
  CONTRL(4)=0
  CONTRL(6)=vdi_handle&
  LONG{ADD(CONTRL,14)}=pscr_mfdb%
  LONG{ADD(CONTRL,18)}=pdes_mfdb%
  INTIN(0)=3
  BMOVE pxyarray%,PTSIN,16
  '
  VDISYS 109
  '
  v_show_c
  '
RETURN
'
> FUNCTION v_opnvwk
  $F&
  '
  INT{ADD(CONTRL,2)}=0
  INT{ADD(CONTRL,6)}=11
  INT{ADD(CONTRL,12)}=@graf_handle
  '
  INT{INTIN}=1           ! NumÇro ID du pÇriphÇrique physique (Çcran)
  INT{ADD(INTIN,2)}=1    ! Type de ligne
  INT{ADD(INTIN,4)}=1    ! Index de couleur Polyline
  INT{ADD(INTIN,6)}=1    ! Type de marqueur
  INT{ADD(INTIN,8)}=1    ! Index de couleur Polymarker
  INT{ADD(INTIN,10)}=1   ! Fonte de caractäres
  INT{ADD(INTIN,12)}=1   ! Index couleur texte
  INT{ADD(INTIN,14)}=1   ! Fill interior Style
  INT{ADD(INTIN,16)}=1   ! Fill style index
  INT{ADD(INTIN,18)}=1   ! Fill index couleur
  INT{ADD(INTIN,20)}=2   ! Flag coordonnÇes NDC ou RC
  '
  VDISYS 100
  '
  RETURN INT{ADD(CONTRL,12)}
  '
ENDFUNC
> PROCEDURE v_clsvwk(vdi_handle0&)
  INT{ADD(CONTRL,12)}=vdi_handle0&
  VDISYS 101,0,0
RETURN
> FUNCTION graf_handle
  $F&
  '
  INT{ADD(GCONTRL,2)}=0
  INT{ADD(GCONTRL,4)}=5
  LONG{ADD(GCONTRL,6)}=0
  '
  GEMSYS 77
  '
  RETURN INT{GINTOUT}
ENDFUNC
> PROCEDURE v_hide_c
  INT{ADD(CONTRL,12)}=vdi_handle&
  VDISYS 123,0,0
RETURN
> PROCEDURE v_show_c
  INT{ADD(CONTRL,12)}=vdi_handle&
  INT{INTIN}=1
  VDISYS 122,1,0
RETURN
