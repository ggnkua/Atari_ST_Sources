' *** SCRL_PRT.LST ***                                 2nd GFA-Manual, page 9-9
> PROCEDURE scroll_print(text$,column,line,n)
  '
  ' *** Scroll text$ at position (column&,line&) in box
  ' *** Width of box is n& characters
  ' *** Scrolling is rather jerky because PRINT is used
  ' *** Original screen is restored after pressing any key or mouse-button
  '
  ' *** Standard:  char.width&  char.height&
  '
  LOCAL box$,a$,i,x1,y1,x2,y2,in$,k
  x1=column*char.width-9
  y1=line*char.height-(char.height+2)
  x2=x1+n*char.width+1
  y2=y1+char.height+3
  GET x1,y1,x2,y2,box$                          ! save rectangle
  BOX x1,y1,x2,y2
  a$=SPACE$(PRED(n))+text$+" "
  WHILE MOUSEK
  WEND                                          ! mouse released?
  {ADD(XBIOS(14,1),6)}=0                        ! clear keyboard-buffer
  REPEAT
    FOR i=1 TO LEN(a$)
      k=MOUSEK
      in$=INKEY$
      PRINT AT(column,line);MID$(a$,i,n);
      PAUSE 7
      EXIT IF in$<>"" OR k>0
    NEXT i
  UNTIL in$<>"" OR k>0
  PUT x1,y1,box$                                ! restore screen
RETURN
' *********
'
