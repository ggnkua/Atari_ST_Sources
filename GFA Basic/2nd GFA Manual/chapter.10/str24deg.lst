' *** STR24DEG.LST ***                                2nd GFA-Manual, page 10-4
> PROCEDURE screendump_star24_degas
  '
  ' *** Screendump from logical screen to Star LC24-10 (or compatible printer)
  ' *** Procedure uses two INLINE-drivers
  ' *** Possible formats (in cm):  27x16  18x11  13.5x8.5  9x5.5
  '
  LOCAL buffer%,palet$,palet%,res,command,r%,m$,k,large!,stop!,i
  '
  ' <<< Load driver STAR24_L.INL in: INLINE star24.driver.large%,2000 >>>
  ' <<< Load driver STAR24_S.INL in: INLINE star24.driver.small%,2000 >>>
  '
  REPEAT                                ! printer ready?
    EXIT IF GEMDOS(&H11)
    SOUND 1,10,12,4,25
    SOUND 1,10,6,4,25
    SOUND 1,10,12,4,50
    SOUND 1,0
    ALERT 3," Printer| | not ready !!",1," OK | Stop ",k
    IF k=2
      LET stop!=TRUE
    ENDIF
  UNTIL stop!
  '
  IF NOT stop!
    INLINE buffer%,1280
    FOR i=0 TO 15
      palet$=palet$+MKI$(XBIOS(7,i,-1))
    NEXT i
    palet%=V:palet$                     ! address of current palette
    res=XBIOS(4)                        ! resolution
    command=0                           ! initialization-command
    m$="   *** Screendump ***| |1 - large format|2 - small format"
    ALERT 2,m$,0," 1 | 2 ",k
    IF k=1
      large!=TRUE
      m$="  *** Large screendump ***| |Standard format:  27x17 cm|"
      m$=m$+"with <Alternate>: 18x11 cm"
      ALERT 2,m$,1," OK ",k
      r%=C:star24.driver.large%(command,res,L:logbase%,L:palet%,L:buffer%)
    ELSE
      large!=FALSE
      m$="  *** Small screendump ***| |Standard format: 13.5x8.5 cm|"
      m$=m$+"with <Alternate>: 9  x5.5 cm"
      ALERT 2,m$,1," OK ",k
      r%=C:star24.driver.small%(command,res,L:logbase%,L:palet%,L:buffer%)
    ENDIF
    SELECT BYTE(r%)
    CASE 0
      m$="*** Error ***| |Initialisation of the |printer-driver failed"
      ALERT 3,m$,1,"EDIT",k
      EDIT
    CASE 1
      m$="Abort screendump | |with <Undo>"
      ALERT 1,m$,1," OK ",k
      command=1                                ! screendump-command
      HIDEM
      IF large!
        r%=C:star24.driver.large%(command,res,L:logbase%,L:palet%,L:buffer%)
      ELSE
        r%=C:star24.driver.small%(command,res,L:logbase%,L:palet%,L:buffer%)
      ENDIF
    DEFAULT
      m$="*** Error ***| |Printer-driver |doesn't work"
      ALERT 3,m$,1,"EDIT",k
      EDIT
    ENDSELECT
  ENDIF
RETURN
' *********
'
