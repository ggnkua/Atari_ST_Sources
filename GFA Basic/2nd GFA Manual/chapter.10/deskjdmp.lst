' *** DESKJDMP.LST ***                                2nd GFA-Manual, page 10-3
> PROCEDURE screendump_deskjet
  '
  ' *** Screendump from logical screen to HP Deskjet
  ' *** Works in High resolution only
  ' *** Uses file-handle #90
  ' *** Couldn't test this Procedure myself, as I don't have a HP Deskjet
  ' *** You could also use Procedure Screendump_hp_jet
  '
  ' *** Standard:  high.res!  logbase%
  '
  LOCAL m$,k,stop!,command$,adr%
  IF high.res!
    m$="Screendump will last |several minutes, |abort dump with <Esc>"
    ALERT 1,m$,1," OK | Stop ",k
    IF k=1
      REPEAT                                    ! printer ready?
        EXIT IF GEMDOS(&H11)
        SOUND 1,10,12,4,25
        SOUND 1,10,6,4,25
        SOUND 1,10,12,4,50
        SOUND 1,0
        ALERT 3," Printer| | not ready !!",1," OK | Stop ",k
        IF k=2
          LET stop!=TRUE
        ENDIF
      UNTIL stop!
      '
      IF NOT stop!
        HIDEM
        command$="*b80W"                       ! announce 80 bytes of data
        OPEN "O",#90,"LST:"                     ! send to parallel printer
        PRINT #1,"*t75R*rA";                  ! switch to 75 dpi graphics mode
        FOR adr%=logbase% TO logbase%+32000-80 STEP 80
          PRINT #1,command$;
          BPUT #1,adr%,80
          EXIT IF INKEY$=CHR$(27)               ! abort after <Esc>
        NEXT adr%
        PRINT #1,"*rB";                        ! turn graphics off
        CLOSE #90
      ENDIF
    ENDIF
  ELSE
    m$="Sorry, screendump |only possible in |High resolution"
    ALERT 3,m$,1," OK ",k
  ENDIF
RETURN
' *********
'
