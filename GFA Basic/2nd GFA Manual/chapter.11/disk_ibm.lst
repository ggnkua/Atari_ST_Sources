' *** DISK_IBM.LST ***                               2nd GFA-Manual, page 11-21
> PROCEDURE disk_ibm(drive$,VAR ok!)
  '
  ' *** Convert disk (in drive A or B) to IBM-format
  ' *** Procedure returns ok!=TRUE if successful
  ' *** MS-DOS expects 9 sectors/track, so it's probably not a good idea to
  ' ***                      convert a disk with 10 sectors/track to IBM-format
  '
  ' *** Procedure: Disk_ibm_error
  '
  LOCAL buffer%,drive,e,media,spf,spt,m$,k
  INLINE buffer%,512
  drive=ASC(UPPER$(drive$))-65           ! 0 = drive A, 1 = drive B
  ok!=TRUE
  e=BIOS(4,2,L:buffer%,1,0,drive)        ! load bootsector (ignore media change)
  IF e=0
    spt=BYTE{buffer%+24}                 ! check sectors/track
    IF spt<>9
      m$="    *** Warning ***|Disk has "+STR$(spt)+" sectors/track,|"
      m$=m$+"but MS-DOS expects 9. Do you|still want to continue?"
      ALERT 3,m$,2,"Yes|No",k
      IF k=2
        ok!=FALSE
      ENDIF
    ENDIF
    IF spt=9 OR (spt<>9 AND k=1)
      BYTE{buffer%}=&HEB
      BYTE{buffer%+1}=&H38                 ! other values possible ??
      BYTE{buffer%+2}=&H90
      BYTE{buffer%+3}=&H49                 ! I
      BYTE{buffer%+4}=&H42                 ! B
      BYTE{buffer%+5}=&H4D                 ! M
      BYTE{buffer%+6}=&H0
      BYTE{buffer%+7}=&H0
      BYTE{buffer%+8}=&H0
      media=BYTE{buffer%+21}               ! media-byte (&HF9 or &HF8)
      spf=BYTE{buffer%+22}                 ! sectors/FAT
      e=BIOS(4,3,L:buffer%,1,0,drive)      ! write IBM-bootsector
      IF e=0
        e=BIOS(4,2,L:buffer%,1,1,drive)    ! load 1st sector of FAT no. 1
        IF e=0
          BYTE{buffer%}=media              ! put correct media-byte there
          e=BIOS(4,3,L:buffer%,1,1,drive)  ! write back
          IF e=0
            e=BIOS(4,2,L:buffer%,1,1+spf,drive)   ! load 1st sector of FAT no. 2
            IF e=0
              BYTE{buffer%}=media                 ! put correct media-byte there
              e=BIOS(4,3,L:buffer%,1,1+spf,drive) ! write back
              IF e<>0
                @disk_ibm_error(1,1+spf,drive,ok!)
              ENDIF
            ELSE
              @disk_ibm_error(0,1+spf,drive,ok!)
            ENDIF
          ELSE
            @disk_ibm_error(1,1,drive,ok!)
          ENDIF
        ELSE
          @disk_ibm_error(0,1,drive,ok!)
        ENDIF
      ELSE
        @disk_ibm_error(1,0,drive,ok!)
      ENDIF
    ENDIF
  ELSE
    @disk_ibm_error(0,0,drive,ok!)
  ENDIF
  ~FSFIRST(drive$+":\@@$$%%&&",17)      ! force media-change
  ~DFREE(drive+1)
RETURN
' ***
> PROCEDURE disk_ibm_error(r,s,drive,VAR ok!)
  '
  LOCAL m$,k,r1$,r2$
  IF r=0
    r1$="load"
    r2$="from"
  ELSE
    r1$="save"
    r2$="to"
  ENDIF
  m$="    *** Error ***|Procedure Disk_ibm was |unable to "+r1$
  m$=m$+" sector "+STR$(s)+"|with BIOS 4 "+r2$+" drive "+CHR$(drive+65)+"."
  ALERT 2,m$,1,"Sorry",k
  ok!=FALSE
RETURN
' *******
'
