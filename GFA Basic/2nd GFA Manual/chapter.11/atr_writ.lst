' *** ATR_WRIT.LST ***                               2nd GFA-Manual, page 11-20
> PROCEDURE attr_write(file$,read.only!,hidden!,system!,archive!,VAR ok!)
  '
  ' *** Write file-attributes (if TRUE) to file$: ok!=TRUE if succesful
  '
  LOCAL path$,path%,attr,a%,m$,k
  ok!=TRUE
  path$=file$+CHR$(0)
  path%=V:path$
  attr=0
  IF read.only!
    attr=BSET(attr,0)
  ENDIF
  IF hidden!
    attr=BSET(attr,1)
  ENDIF
  IF system!
    attr=BSET(attr,2)
  ENDIF
  IF archive!
    attr=BSET(attr,5)
  ENDIF
  a%=GEMDOS(&H43,L:path%,1,attr)
  IF a%=-33
    m$=" *** File-error ***| |"+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found"
    ALERT 3,m$,1," OK ",k
    ok!=FALSE
  ELSE IF a%=-34
    m$=" *** Path-error ***| |"+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found"
    ALERT 3,m$,1," OK ",k
    ok!=FALSE
  ENDIF
RETURN
' *********
'
