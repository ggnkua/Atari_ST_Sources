' *** PARSFILE.LST ***                               2nd GFA-Manual, page 11-23
> PROCEDURE parse_filename(parse.name$,VAR drive$,path$,file$,ext$)
  '
  ' *** Split file parse.name$ in drive$, path$, file$ en ext$
  ' *** "A:\GAMES\CHESS.GFA" becomes  A  \GAMES\ CHESS    GFA
  ' *** "A:CHESS.GFA" becomes         A          CHESS    GFA
  ' *** No checking for correct syntax is done
  '
  LOCAL parse.file$,point,slsh
  CLR drive$,path$,file$,ext$
  drive$=CHR$(ADD(65,GEMDOS(25)))       ! current drive
  parse.name$=UPPER$(parse.name$)
  slsh=RINSTR(parse.name$,"\")
  path$=LEFT$(parse.name$,slsh)
  file$=MID$(parse.name$,SUCC(slsh))
  IF MID$(parse.name$,2,1)=":"
    drive$=LEFT$(parse.name$)
    IF slsh>0
      path$=MID$(path$,3)
    ELSE
      file$=MID$(parse.name$,3)
    ENDIF
  ENDIF
  point=INSTR(file$,".")
  IF point>0
    ext$=MID$(file$,SUCC(point))
    file$=LEFT$(file$,PRED(point))
  ENDIF
RETURN
' *********
'
