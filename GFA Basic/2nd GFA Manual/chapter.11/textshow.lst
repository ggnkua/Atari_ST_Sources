' *** TEXTSHOW.LST ***                               2nd GFA-Manual, page 11-23
> PROCEDURE text_show(title$,VAR array$())
  '
  ' *** Show text in array$() on screen (20 lines on screen)
  ' *** Title and page-number are shown at the top
  ' *** All available keys are shown at the bottom (string-search also possible)
  ' *** ETX (CHR$(3)) is recognized as the end of the text
  ' *** Empty lines at end of array are ignored (if no ETX encountered)
  ' *** Works in High and Medium resolution only
  '
  ' *** Standard:  x.max&  y.max&  char.height&  col.max&
  '
  LOCAL exit!,etx!,keypress!,show!,last,k,i,y,line,l1$,l2$,menu$
  LOCAL asci|,scan|,stat|,keyget.code%
  LOCAL s.col,s.line,f.col,f.line,search$,found!,search.page
  ABSOLUTE asci|,V:keyget.code%+3       ! ASCII-code of key
  ABSOLUTE scan|,V:keyget.code%+1       ! scan-code of key
  ABSOLUTE stat|,V:keyget.code%         ! status-byte (special keys)
  ' *** examinek array$()
  last=PRED(DIM?(array$()))
  FOR i=last DOWNTO 1
    EXIT IF array$(i)=CHR$(3)           ! ETX (End Of Text) found
    EXIT IF array$(i)<>""               ! last text-line found
  NEXT i
  IF array$(i)=CHR$(3)
    etx!=TRUE
    last=PRED(i)                        ! ignore everything after ETX
  ELSE
    last=i                              ! ignore empty lines from i+1
  ENDIF
  page=0                                ! start at first page
  last.page=DIV(last,20)                ! the last page
  show!=TRUE                            ! show page
  ' *** a few preparations...
  CLS
  DEFFILL ,2,4
  DEFLINE ,3
  y=char.height*1.5
  LINE 0,y,x.max,y
  PBOX -1,-1,x.max,y
  PRINT AT(DIV(SUB(col.max,LEN(title$)),2),1);title$
  y=MUL(22,char.height)+char.height/2
  LINE 0,y,x.max,y
  DEFLINE ,1
  PBOX 0,y,x.max,y.max
  l1$="  < >   < >   <ClrHome>   <Shift><ClrHome>   <Contr><F>   <Contr><G>   <Esc>  "
  l2$="page down/up   1st page      last page      find string  search again   exit  "
  PRINT AT(2,24);l1$;
  PRINT AT(2,25);l2$;
  LOCATE 5,24
  OUT 5,2
  LOCATE 11,24
  OUT 5,1
  SHOWM
  {ADD(XBIOS(14,1),6)}=0                        ! clear keyboard-buffer
  ' *** laat de tekst zien
  DO
    IF show!                                    ! put page-number on screen
      PRINT AT(2,1);USING " Page #### ",SUCC(page)      ! 1st page = 1
      FOR line=MUL(20,page) TO ADD(19,MUL(20,page))
        PRINT AT(1,ADD(line MOD 20,3));
        IF line>last
          PRINT SPACE$(col.max)
        ELSE
          IF LEN(array$(line))>col.max
            PRINT LEFT$(array$(line),PRED(col.max));
            OUT 5,3
          ELSE
            PRINT array$(line);SPACE$(SUB(col.max,LEN(array$(line))))
          ENDIF
        ENDIF
      NEXT line
      IF found!              ! search-string found on new page
        PRINT AT(f.col,ADD(f.line MOD 20,3));CHR$(27);"p";search$;CHR$(27);"q";
        CLR found!
      ENDIF
    ENDIF
    IF found!                ! search-string found on current page
      PRINT AT(f.col,ADD(f.line MOD 20,3));CHR$(27);"p";search$;CHR$(27);"q";
      CLR found!
    ENDIF
    REPEAT
      KEYGET keyget.code%
      keypress!=TRUE
      show!=TRUE
      SELECT scan|
      CASE &H47
        IF BTST(stat|,0) OR BTST(stat|,1)       ! <Shift> <ClrHome> = last page
          IF page<last.page
            page=last.page
          ELSE
            OUT 2,7
            show!=FALSE
            PAUSE 5
          ENDIF
        ELSE IF stat|=0                         ! <ClrHome> = first page
          IF page>0
            page=0
          ELSE
            OUT 2,7
            show!=FALSE
            PAUSE 5
          ENDIF
          page=0
        ELSE
          keypress!=FALSE
        ENDIF
      CASE &H50                                 ! <> = next page
        IF page<last.page
          page=SUCC(page)
        ELSE
          OUT 2,7
          show!=FALSE
          PAUSE 5
        ENDIF
      CASE &H48                                 ! <> = previous page
        IF page>0
          page=PRED(page)
        ELSE
          OUT 2,7
          show!=FALSE
          PAUSE 5
        ENDIF
      CASE &H1                                  ! <Esc> = exit
        exit!=TRUE
        PAUSE 5
      DEFAULT
        keypress!=FALSE
      ENDSELECT
      SELECT asci|
      CASE &H11                                 ! <Control> <Q> = exit
        keypress!=TRUE
        exit!=TRUE
        PAUSE 5
      CASE &H6                                  ! <Control> <F> = search string
        GET 0,y.max-2*char.height,x.max,y.max,menu$
        PRINT AT(2,24);SPACE$(78);
        PRINT AT(2,25);SPACE$(78);
        PRINT AT(2,24);" Searchstring: ";
        FORM INPUT 35 AS search$
        PRINT AT(60,24);" From page: ";
        INPUT "",search.pag
        search.pag=MAX(MIN(search.pag,SUCC(last.page)),1)
        s.line=MUL(PRED(search.pag),20)
        f.col=INSTR(array$(s.line),search$)     ! in 1st line?
        IF f.col=0
          FOR f.line=SUCC(s.line) TO last
            f.col=INSTR(array$(f.line),search$)
            EXIT IF f.col
          NEXT f.line
          IF f.line>last
            CLR f.line
          ENDIF
        ELSE
          f.line=s.line
        ENDIF
        IF f.col
          search.page=DIV(f.line,20)
          IF page=search.page
            show!=FALSE
          ELSE
            page=search.page
          ENDIF
          found!=TRUE
        ELSE
          OUT 2,7
          PAUSE 5
        ENDIF
        PUT 0,y.max-2*char.height,menu$
      CASE &H7                          ! <Control> <G> = continue search
        IF f.col>0
          PRINT AT(f.col,ADD(f.line MOD 20,3));search$;    ! clear previous
          s.col=SUCC(f.col)             ! search after last position
          s.line=f.line
          f.col=INSTR(array$(s.line),search$,s.col)        ! in current line?
          IF f.col=0
            FOR f.line=SUCC(s.line) TO last
              f.col=INSTR(array$(f.line),search$)
              EXIT IF f.col
            NEXT f.line
            IF f.line>last
              CLR f.line
            ENDIF
          ELSE
            f.line=s.line
          ENDIF
          IF f.col
            search.page=DIV(f.line,20)
            IF page=search.page
              show!=FALSE
            ELSE
              page=search.page
            ENDIF
            found!=TRUE
          ELSE
            OUT 2,7
            PAUSE 5
          ENDIF
        ELSE
          OUT 2,7
          PAUSE 5
        ENDIF
      ENDSELECT
    UNTIL keypress! OR found!
  LOOP UNTIL exit!
RETURN
' *********
'
