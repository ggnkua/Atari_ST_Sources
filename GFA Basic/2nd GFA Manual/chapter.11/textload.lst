' *** TEXTLOAD.LST ***                               2nd GFA-Manual, page 11-23
> PROCEDURE text_load(file$,lines,VAR array$(),n.lines%)
  '
  ' *** Load text-file file$ (ASCII-file) in array$()
  ' *** Maximum number of lines is lines&, actual number is n.lines%
  ' *** This Procedure uses channel #90 for loading
  '
  ' *** Procedure:  Program_exit
  '
  LOCAL m$,k
  IF EXIST(file$)
    IF DIM?(array$())>0
      ERASE array$()
    ENDIF
    DIM array$(PRED(lines))
    OPEN "I",#90,file$
    RECALL #90,array$(),-1,n.lines%
    CLOSE #90
  ELSE
    m$="    *** Error ***|Procedure Text_load |can't find the file|"
    m$=m$+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    ALERT 3,m$,1,"Continue|Quit",k
    IF k=2
      @program_exit
    ENDIF
    n.lines%=0
  ENDIF
RETURN
' *********
'
