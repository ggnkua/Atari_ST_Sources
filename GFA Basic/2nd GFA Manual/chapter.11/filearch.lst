' *** FILEARCH.LST ***                               2nd GFA-Manual, page 11-21
> PROCEDURE file_arch(file$,flag!,VAR ok!)
  '
  ' *** Set archive-bit of file (flag!=TRUE) or clear archive-bit (flag!=FALSE)
  ' *** ok!=TRUE if succesful
  '
  LOCAL path$,path%,attr%,a%,m$,k
  path$=file$+CHR$(0)
  path%=V:path$
  attr%=GEMDOS(&H43,L:path%,0,0)
  ok!=FALSE
  IF attr%=-33
    m$=" *** File-error ***| |"+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found"
    ALERT 3,m$,1," OK ",k
  ELSE IF attr%=-34
    m$=" *** Path-error ***| |"+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found"
    ALERT 3,m$,1," OK ",k
  ENDIF
  IF attr%<>-33 AND attr%<>-34
    ok!=TRUE
    IF BTST(attr%,5)                            ! archive-bit set:
      IF NOT flag!                              ! clear it
        attr%=BCLR(attr%,5)
        a%=GEMDOS(&H43,L:path%,1,attr%)         ! do it
      ENDIF
    ELSE                                        ! archive-bit not set:
      IF flag!                                  ! set it
        attr%=BSET(attr%,5)
        a%=GEMDOS(&H43,L:path%,1,attr%)         ! do it
      ENDIF
    ENDIF
  ENDIF
RETURN
' *********
'
