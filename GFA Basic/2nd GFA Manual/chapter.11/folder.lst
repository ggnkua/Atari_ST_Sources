' *** FOLDER.LST ***                                 2nd GFA-Manual, page 11-22
> PROCEDURE folder(path$,open!)
  '
  ' *** Create a folder in path$ (path may include drive)
  ' *** User enters name through Fileselector
  ' *** After <Cancel> (or choosing <OK> without an entry) no folder is created
  ' *** If open!=TRUE, the new folder becomes the default folder
  '
  LOCAL folder$,folder!
  IF INSTR(path$,":")=2
    CHDRIVE LEFT$(path$)
  ENDIF
  IF RIGHT$(path$)<>"\"
    path$=path$+"\"
  ENDIF
  REPEAT
    FILESELECT path$+"*.*","",folder$
    EXIT IF folder$="" OR RIGHT$(folder$)="\"
    e%=FSFIRST(folder$,16)           ! EXIST(folder$) will miss an empty folder
    IF e%=-33
      MKDIR folder$
      IF open!
        CHDIR folder$
      ENDIF
      folder!=TRUE
    ENDIF
  UNTIL folder!
RETURN
' *********
'
