' *** FILE_SEM.LST ***                               2nd GFA-Manual, page 11-22
> PROCEDURE fileselect_margins(save!,text$,path$,default$,left$,right$,VAR file$)
  '
  ' *** Use Fileselector with comment-line (especially for TOS 1.0)
  ' *** If save!=TRUE original screen will be restored
  ' *** A title in the left and right margin of the screen is optional
  ' *** Works with TOS-selector and UIS III
  '
  ' *** Standard:  high.res!  col.max&  char.width&  char.height&
  '
  LOCAL screen$,y.fac
  IF save!
    SGET screen$
  ENDIF
  CLS
  IF high.res!
    y.fac=1
  ELSE
    y.fac=2
  ENDIF
  DEFTEXT 1,2,900,32
  TEXT 100,350/y.fac,300/y.fac,left$
  DEFTEXT ,,2700
  TEXT 540,50/y.fac,300/y.fac,right$
  IF high.res!
    DEFTEXT ,0,0,13
  ELSE
    DEFTEXT ,0,0,6
  ENDIF
  TEXT MUL(DIV(SUB(col.max,LEN(text$)),2),char.width),SUB(MUL(3,char.height),7),text$
  GRAPHMODE 3
  DEFFILL 1,1
  BOUNDARY 0
  IF high.res!
    BOX 157,20,482,54
    PLOT 157,20
    PBOX 159,22,480,52
  ELSE
    BOX 157,6,482,27
    PLOT 157,6
    PBOX 160,8,479,24
  ENDIF
  BOUNDARY 1
  GRAPHMODE 1
  FILESELECT path$,default$,file$
  IF save!
    SPUT screen$
  ENDIF
RETURN
' *********
'
