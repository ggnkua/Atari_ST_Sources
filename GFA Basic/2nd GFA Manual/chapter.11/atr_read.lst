' *** ATR_READ.LST ***                               2nd GFA-Manual, page 11-20
> PROCEDURE attr_read(file$,VAR ok!,read!,hid!,sys!,lab!,fold!,arch!)
  '
  ' *** Returns the following file-attributes if ok!=TRUE:
  ' ***      read-only, hidden, system, label(=diskname), folder and/or archive
  '
  LOCAL path$,path%,attr%,m$,k
  path$=file$+CHR$(0)
  path%=V:path$
  attr%=GEMDOS(&H43,L:path%,0,0)
  ok!=FALSE
  IF attr%=-33
    m$=" *** File-error ***| |"+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found"
    ALERT 3,m$,1," OK ",k
  ELSE IF attr%=-34
    m$=" *** Path-error ***| |"+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found"
    ALERT 3,m$,1," OK ",k
  ENDIF
  CLR read!,hid!,sys!,lab!,fold!,arch!
  IF attr%<>-33 AND attr%<>-34
    ok!=TRUE
    IF BTST(attr%,0)
      read!=TRUE                ! read-only
    ENDIF
    IF BTST(attr%,1)
      hid!=TRUE                 ! hidden
    ENDIF
    IF BTST(attr%,2)
      sys!=TRUE                 ! system
    ENDIF
    IF BTST(attr%,3)
      lab!=TRUE                 ! label (diskname)
    ENDIF
    IF BTST(attr%,4)
      fold!=TRUE                ! folder
    ENDIF
    IF BTST(attr%,5)
      arch!=TRUE                ! archive-bit set
    ENDIF
  ENDIF
RETURN
' *********
'
