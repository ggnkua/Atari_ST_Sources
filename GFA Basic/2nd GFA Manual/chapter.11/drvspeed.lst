' *** DRVSPEED.LST ***                               2nd GFA-Manual, page 11-21
> PROCEDURE drive_speed(drive$)
  '
  ' *** Test drive-speed (A or B): should be 300 rpm
  ' *** Result is shown in Alert-box
  '
  LOCAL m$,k,drive%,buffer%,tries%,t1%,t2%,rpm#
  '
  drive%=SUB(ASC(UPPER$(drive$)),65)                    ! A=0 B=1
  '
  IF drive%>=0 AND drive%<=1
    m$="Let drive "+UPPER$(drive$)+" spin |for several seconds,"
    m$=m$+"|then press any key"
    ALERT 1,m$,1," OK ",k
    INLINE buffer%,512
    {ADD(XBIOS(14,1),6)}=0                              ! clear keyboard-buffer
    ~XBIOS(8,L:buffer%,L:0,drive%,1,0,0,1)              ! read one sector
    tries%=0
    t1%=TIMER
    '
    ' *** Read sector and calculate average speed
    REPEAT
      INC tries%
      ~XBIOS(8,L:buffer%,L:0,drive%,1,0,0,1)
      t2%=TIMER
      rpm#=INT(tries%*1200000/(t2%-t1%))/100
    UNTIL LEN(INKEY$)
    '
    m$="|Drive "+UPPER$(drive$)+" rotates at "+STR$(ROUND(rpm#,1))
    m$=m$+" rpm | |(300 rpm is perfect)"
    ALERT 1,m$,1," OK ",k
    '
  ELSE
    m$="     *** Error ***| |Procedure Drive_speed |checks drive A or B only"
    ALERT 3,m$,1," OK ",k
  ENDIF
  '
RETURN
' *********
'
