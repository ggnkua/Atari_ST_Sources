' *** FILEREAD.LST ***                               2nd GFA-Manual, page 11-22
> PROCEDURE file_read(file$,flag!,VAR ok!)
  '
  ' *** Make file read-only (flag!=TRUE) or make it read/write (flag!=FALSE)
  ' *** ok!=TRUE if succesful
  '
  LOCAL path$,path%,attr%,a%,m$,k
  path$=file$+CHR$(0)
  path%=V:path$
  attr%=GEMDOS(&H43,L:path%,0,0)
  ok!=FALSE
  IF attr%=-33
    m$=" *** File-error ***| |"+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found"
    ALERT 3,m$,1," OK ",k
  ELSE IF attr%=-34
    m$=" *** Path-error ***| |"+RIGHT$(file$,LEN(file$)-RINSTR(file$,"\"))
    m$=m$+"|not found"
    ALERT 3,m$,1," OK ",k
  ENDIF
  IF attr%<>-33 AND attr%<>-34
    ok!=TRUE
    IF BTST(attr%,0)                            ! read-only file:
      IF NOT flag!                              ! make it read/write
        attr%=BCLR(attr%,0)
        a%=GEMDOS(&H43,L:path%,1,attr%)         ! do it
      ENDIF
    ELSE                                        ! read/write file:
      IF flag!                                  ! make it read-only
        attr%=BSET(attr%,0)
        a%=GEMDOS(&H43,L:path%,1,attr%)         ! do it
      ENDIF
    ENDIF
  ENDIF
RETURN
' *********
'
