' *** MIDI_MON.LST ***                                2nd GFA-Manual, page 12-2
> PROCEDURE midi_monitor
  '
  ' *** Simple Midi-monitor
  '
  LOCAL m$,k,byte|,byte$,hex$,bin$,buffer$,key$,channel
  byte$=SPACE$(3)
  hex$=SPACE$(2)
  bin$=SPACE$(8)
  m$="All incoming Midi-bytes |(except 254) will be |printed on the screen|"
  m$=m$+"(press <Space> to stop)"
  ALERT 1,m$,1,"Start",k
  m$="|Clear screen with <Return>"
  ALERT 1,m$,1," OK ",k
  {ADD(XBIOS(14,1),6)}=0                        ! clear keyboard-buffer
  buffer$=INPMID$                               ! clear MIDI-buffer
  CLS
  PRINT TAB(10);"dec";TAB(20);"hex";TAB(30);"bin"
  REPEAT
    REPEAT
      key$=INKEY$
      IF INP?(3)
        byte|=INP(3)
        IF byte|<>254
          IF TIMER-t%>200
            PRINT           ! if more than 1 second between incoming bytes
          ENDIF
          t%=TIMER
          RSET byte$=STR$(byte|)
          RSET hex$=HEX$(byte|)
          RSET bin$=BIN$(byte|)
          PRINT TAB(10);byte$;TAB(20);hex$;TAB(30);bin$
        ENDIF
      ENDIF
    UNTIL key$=" " OR key$=CHR$(13)
    IF key$=CHR$(13)
      CLS
    ENDIF
  UNTIL key$=" "
  FOR channel=0 TO 15                           ! everything off
    OUT 3,&HB0+channel,123,0
    OUT 3,&HB0+channel,64,0
    OUT 3,&HB0+channel,1,0
    OUT 3,&HE0+channel,0,0
  NEXT channel
  {ADD(XBIOS(14,1),6)}=0                        ! clear keyboard-buffer
  PRINT " (Press any key)"
  ~INP(2)
RETURN
' *********
'
