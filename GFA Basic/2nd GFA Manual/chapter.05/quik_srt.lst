' *** QUIK_SRT.LST ***                                 2nd GFA-Manual, page 5-7
> PROCEDURE quick_sort(VAR proc())
  '
  ' *** Sort word-array with recursive 'Quick Sort'
  ' *** Uses more memory than iterative 'Quick Sort'
  ' *** Same algorithm as used by QSORT
  '
  LOCAL last,dummy
  last=PRED(DIM?(proc()))
  @quick(0,last)
RETURN
> PROCEDURE quick(l,r)
  LOCAL ll,rr,i,x,j
  ll=l
  rr=r
  dummy=proc(DIV(ADD(l,r),2))
  REPEAT
    WHILE proc(l)<dummy
      INC l
    WEND
    WHILE proc(r)>dummy
      DEC r
    WEND
    IF l<=r
      SWAP proc(l),proc(r)
      INC l
      DEC r
    ENDIF
  UNTIL l>r
  IF ll<r
    @quick(ll,r)
  ENDIF
  IF l<rr
    @quick(l,rr)
  ENDIF
RETURN
' *********
'
