' *** STR_INDX.LST ***                                 2nd GFA-Manual, page 5-7
> PROCEDURE string_index_qsort(flag!,VAR text$(),index%())
  '
  ' *** Fill index-array with sorted index-numbers of string-array text$()
  ' *** String-array text$() is not changed!
  ' *** If flag!=TRUE the array ascii|() is used (see Procedure Ascii_qsort)
  '
  ' *** Procedure:  Init_ascii_array
  '
  LOCAL last,i
  last=PRED(DIM?(text$()))            ! index of last element
  DIM temp$(last)                     ! temporary array
  IF PRED(DIM?(index%()))<>last       ! exists index-array already?
    IF DIM?(index%())=0
      DIM index%(last)
    ELSE
      ERASE index%()
      DIM index%(last)
    ENDIF
  ENDIF
  FOR i=0 TO last                     ! copy strings to temporary array
    temp$(i)=text$(i)
  NEXT i
  FOR i=0 TO last                     ! fill index-array with index-numbers
    index%(i)=i
  NEXT i
  IF flag!
    @init_ascii_array
    QSORT temp$() WITH ascii|(),SUCC(last),index%()
  ELSE
    QSORT temp$(),SUCC(last),index%()
  ENDIF
  ERASE temp$()                       ! remove temporary array
RETURN
' *********
'
