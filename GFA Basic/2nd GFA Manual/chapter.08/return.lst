' *** RETURN.LST ***                                  2nd GFA-Manual, page 8-12
> PROCEDURE return_key(blink!,bel!)
  '
  ' *** Wait until user presses <Return> while showing (blinking) <Return>
  ' *** If blink!=TRUE <Return> will blink, if bel!=TRUE you'll hear the bell
  ' *** If the user presses another key than <Return>, the blinking stops
  '
  ' *** Standard:  x.max&  y.max&  char.height&  col.max&  line.max&
  '
  LOCAL w1$,w2$,p,temp$,in$
  {ADD(XBIOS(14,1),6)}=0                             ! clear keyboard-buffer
  GET 0,SUB(y.max,char.height),x.max,y.max,temp$     ! save part of screen
  IF blink!
    w1$="<Return>"
    w2$=SPACE$(LEN(w1$))
    p=PRED(DIV(LEN(w1$),2))
    IF bel!
      w1$=w1$+CHR$(7)
    ENDIF
    PRINT AT(SUB(DIV(col.max,2),p),line.max);w1$;
    WHILE in$=""                                          ! wait for any key
      PAUSE 30
      SWAP w1$,w2$
      PRINT AT(SUB(DIV(col.max,2),p),line.max);w1$;
      in$=INKEY$
    WEND
  ENDIF
  PRINT AT(SUB(DIV(col.max,2),p),line.max);"<Return>";
  WHILE in$<>CHR$(13)
    in$=INKEY$                                            ! wait for <Return>
  WEND
  PUT 0,SUB(y.max,char.height),temp$,3                    ! restore screen
RETURN
' *********
'
