' *** CHOICE_2.LST ***                                2nd GFA-Manual, page 8-10
> PROCEDURE choice_2(line,text$,choice1$,choice2$,VAR choice)
  '
  ' *** Click with left mouse-button on choice1$ or choice2$ (choice& = 1 or 2)
  ' *** If you click the right mouse-button choice& = 0 (no choice made)
  ' *** Maximum length of text$ is 37 characters, each choice-string 12
  ' *** In Low resolution this becomes 19 and 6 characters
  '
  ' *** Standard:  char.height&  char.width&  col.max&
  '
  LOCAL choice1.col,choice2.col,len.sp,len.tk,len1,len2
  LOCAL x,x1,x2,x3,x4,y,y1,y2,k,box1$,box2$
  SELECT col.max
  CASE  TO 40           ! Low resolution
    choice1.col=22
    choice2.col=32
    len.sp=8
    len.tk=19
  CASE 41 TO            ! Medium or High resolution
    choice1.col=46
    choice2.col=61
    len.sp=14
    len.tk=37
  ENDSELECT
  text$=LEFT$(text$+SPACE$(len.tk),len.tk)
  choice1$=LEFT$(choice1$,len.sp)
  len1=DIV(SUB(len.sp,LEN(choice1$)),2)
  choice1$=LEFT$(SPACE$(len1)+choice1$+SPACE$(SUCC(len1)),len.sp)
  choice2$=LEFT$(choice2$,len.sp)
  len2=DIV(SUB(len.sp,LEN(choice2$)),2)
  choice2$=LEFT$(SPACE$(len2)+choice2$+SPACE$(SUCC(len2)),len.sp)
  PRINT AT(1,line);text$;
  PRINT AT(choice1.col,line);choice1$;
  PRINT AT(choice2.col,line);choice2$;
  x1=MUL(PRED(choice1.col),char.width)
  x2=MUL(ADD(choice1.col,PRED(LEN(choice1$))),char.width)
  x3=MUL(PRED(choice2.col),char.width)
  x4=MUL(ADD(choice2.col,PRED(LEN(choice2$))),char.width)
  y1=MUL(PRED(line),char.height)
  y2=MUL(line,char.height)
  BOX SUB(x1,2),SUB(y1,2),ADD(x2,2),ADD(y2,2)           ! 2 pixels larger
  BOX SUB(x3,2),SUB(y1,2),ADD(x4,2),ADD(y2,2)           ! than the text
  GET PRED(x1),PRED(y1),SUCC(x2),SUCC(y2),box1$
  GET PRED(x3),PRED(y1),SUCC(x4),SUCC(y2),box2$
  CLR choice
  SHOWM
  REPEAT
    MOUSE x,y,k
    IF k=1
      IF y>y1 AND y<y2
        IF x>x1 AND x<x2
          PUT PRED(x1),PRED(y1),box1$,10
          choice=1
        ELSE IF x>x3 AND x<x4
          PUT PRED(x3),PRED(y1),box1$,10
          choice=2
        ENDIF
      ENDIF
      PAUSE 10
    ELSE IF k=2
      choice=0
    ENDIF
  UNTIL choice OR k=2
RETURN
' *********
'
