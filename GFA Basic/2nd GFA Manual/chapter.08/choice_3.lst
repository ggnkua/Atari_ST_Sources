' *** CHOICE_3.LST ***                                2nd GFA-Manual, page 8-10
> PROCEDURE choice_3(line,text$,choice1$,choice2$,choice3$,VAR choice)
  '
  ' *** Click left mouse-button on choice1$/choice2$/choice3$ (choice&=1/2/3)
  ' *** If you click the right mouse-button choice& = 0 (no choice made)
  ' *** Maximum length of text$ is 35 characters, each choice-string 12
  ' *** Works in High or Medium resolution only!
  '
  ' *** Standard:  char.height&  char.width&  col.max&
  '
  LOCAL choice1.col,choice2.col,choice3.col
  LOCAL k,x,x1,x2,x3,x4,x5,x6,y,y1,y2
  LOCAL len.sp,len.tk,len1,len2,len3,box1$,box2$,box3$
  choice1.col=39
  choice2.col=53
  choice3.col=67
  len.sp=12
  len.tk=35
  text$=LEFT$(text$+SPACE$(len.tk),len.tk)
  choice1$=LEFT$(choice1$,len.sp)
  len1=DIV(SUB(len.sp,LEN(choice1$)),2)
  choice1$=LEFT$(SPACE$(len1)+choice1$+SPACE$(SUCC(len1)),len.sp)
  choice2$=LEFT$(choice2$,len.sp)
  len2=DIV(SUB(len.sp,LEN(choice2$)),2)
  choice2$=LEFT$(SPACE$(len2)+choice2$+SPACE$(SUCC(len2)),len.sp)
  choice3$=LEFT$(choice3$,len.sp)
  len3=DIV(SUB(len.sp,LEN(choice3$)),2)
  choice3$=LEFT$(SPACE$(len3)+choice3$+SPACE$(SUCC(len3)),len.sp)
  PRINT AT(1,line);text$;
  PRINT AT(choice1.col,line);choice1$;
  PRINT AT(choice2.col,line);choice2$;
  PRINT AT(choice3.col,line);choice3$;
  x1=MUL(PRED(choice1.col),char.width)
  x2=MUL(ADD(choice1.col,PRED(LEN(choice1$))),char.width)
  x3=MUL(PRED(choice2.col),char.width)
  x4=MUL(ADD(choice2.col,PRED(LEN(choice2$))),char.width)
  x5=MUL(PRED(choice3.col),char.width)
  x6=MUL(ADD(choice3.col,PRED(LEN(choice3$))),char.width)
  y1=MUL(PRED(line),char.height)
  y2=MUL(line,char.height)
  BOX SUB(x1,2),SUB(y1,2),ADD(x2,2),ADD(y2,2)           ! 2 pixels larger
  BOX SUB(x3,2),SUB(y1,2),ADD(x4,2),ADD(y2,2)           ! than the text
  BOX SUB(x5,2),SUB(y1,2),ADD(x6,2),ADD(y2,2)
  GET PRED(x1),PRED(y1),SUCC(x2),SUCC(y2),box1$
  GET PRED(x3),PRED(y1),SUCC(x4),SUCC(y2),box2$
  GET PRED(x5),PRED(y1),SUCC(x6),SUCC(y2),box3$
  CLR choice
  SHOWM
  REPEAT
    MOUSE x,y,k
    IF k=1
      IF y>y1 AND y<y2
        IF x>x1 AND x<x2
          PUT PRED(x1),PRED(y1),box1$,8
          choice=1
        ELSE IF x>x3 AND x<x4
          PUT PRED(x3),PRED(y1),box2$,8
          choice=2
        ELSE IF x>x5 AND x<x6
          PUT PRED(x5),PRED(y1),box3$,8
          choice=3
        ENDIF
      ENDIF
      PAUSE 10
    ELSE IF k=2
      choice=0
    ENDIF
  UNTIL choice OR k=2
RETURN
' *********
'
