' The Voice mag nr 2 by Team From The East
'
' Program odczytu i wyswietlania fontow w formacie GEM'u
' format FNT
'
' Dziekujemy redakcji ST(e)FAN'a za udostepnienie tego programu
'
RESERVE 50000
'
old_handle&=CONTRL(6)
v_opnvwk
v_clrwk
font_load                        ! Ladowanie fontow
vst_load_fonts
demo
v_clrwk
vst_unload_fonts
font_unload
v_clsvwk
CONTRL(6)=old_handle&
RESERVE
END
'
> PROCEDURE v_opnvwk
  LOCAL aes_h&,d&
  aes_h&=GRAF_HANDLE(d&,d&,d&,d&)
  INTIN(0)=1        !Ger„te-ID          1   (aktueller Bildschirmtreiber)
  INTIN(1)=1        !Linientyp          1   (durchgezogen)
  INTIN(2)=1        !Linienfarbe        1   (schwarz)
  INTIN(3)=1        !Markertyp          1   (Punkt)
  INTIN(4)=1        !Markerfarbe        1   (schwarz)
  INTIN(5)=1        !Zeichensatznummer  1   (Systemfont)
  INTIN(6)=1        !Textfarbe          1   (schwarz)
  INTIN(7)=1        !Flltyp            1
  INTIN(8)=1        !Fllmusterindex    1
  INTIN(9)=1        !Fllmusterfarbe    1   (schwarz)
  INTIN(10)=2       !Koordinatenflag    2   (RC)
  CONTRL(0)=100     !Funktionsnummer    100
  CONTRL(1)=0       !Parameter in ptsin 0
  CONTRL(3)=11      !Parameter in intin 11
  CONTRL(6)=aes_h&  !Handle des AES
  VDISYS            !VDI-Aufruf
  handle&=CONTRL(6) !Handle der virtuellen Workstation
RETURN
> PROCEDURE v_clsvwk
  CONTRL(0)=101
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=handle&
  VDISYS
  IF config%
    CARD{config%}=conf&                     !Konfigurationswort zurcksetzen
  ENDIF
RETURN
> PROCEDURE v_clrwk
  CONTRL(0)=3
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=handle&
  VDISYS
RETURN
> PROCEDURE font_load
  vst_font(1)                 !Systemfont
  vst_effects(0)              !0 oznacza brak efektow
  vst_rotation(0)             !brak rotacji
  vst_height(13,x&,x&,x&,x&)  !wielkosc fontow 13 Pixeli
  vst_alignment(1,0,x&,x&)    !Text mittenzentrieren
  FILESELECT "\*.FNT","",file$ ! wybor fontow
  IF EXIST(file$)=0           !Font vorhanden ?
    v_clsvwk
    RESERVE
    END
  ENDIF
  OPEN "I",#1,file$
  font_size%=LOF(#1)          !L„nge des Fonts
  font_hdr%=MALLOC(font_size%)!Speicher allozieren
  font_buffer$=STRING$(8192,0)!Fontbuffer
  IF font_hdr%=0              !Fehler beim Malloc ?
    CLOSE #1
    RESERVE
    END
  ENDIF
  '
  lighten%=font_hdr%+&H3E     !Adresse von lighten
  skew%=font_hdr%+&H40        !Adresse von skew
  flags%=font_hdr%+&H42       !Adresse der Flags
  hor_table%=font_hdr%+&H44
  off_table%=font_hdr%+&H48
  dat_table%=font_hdr%+&H4C
  next_font%=font_hdr%+&H54
  '
  BGET #1,font_hdr%,font_size% !ladowanie fontow
  CLOSE #1
  '
  IF BYTE{flags%+1}=0          !Intel-Format ?
    laenge%=BYTE{dat_table%}+256*BYTE{dat_table%+1}
    FOR a%=font_hdr% TO font_hdr%+laenge%-1 STEP 2
      CARD{a%}=BYTE{a%}+256*BYTE{a%+1}           !Bytes vertauschen
    NEXT a%
    LONG{hor_table%}=font_hdr%+CARD{hor_table%}
    LONG{off_table%}=font_hdr%+CARD{off_table%}  !Relozierung (Intel)
    LONG{dat_table%}=font_hdr%+CARD{dat_table%}
    CARD{flags%}=CARD{flags%} XOR 4              !Intel-Flag l”schen
  ELSE
    LONG{hor_table%}=font_hdr%+LONG{hor_table%}
    LONG{off_table%}=font_hdr%+LONG{off_table%}  !Relozierung(Motorola)
    LONG{dat_table%}=font_hdr%+LONG{dat_table%}
  ENDIF
  CARD{font_hdr%}=2                              !Zeichensatznummer
  LONG{next_font%}=0                             !kein weiterer Font
  height&=CARD{font_hdr%+&H28}                   !H”he des Font
  height2&=CARD{font_hdr%+&H52}                  !H”he des Fontimage
RETURN
> PROCEDURE font_unload
  ~MFREE(font_hdr%)
RETURN
> PROCEDURE vst_load_fonts
  LONG{CONTRL+14}=V:font_buffer$  !Adresse des neuen Effektbuffers
  CONTRL(9)=4096                  !Bufferl„nge in Worten
  LONG{CONTRL+20}=font_hdr%       !Adresse des Fontheaders
  CONTRL(0)=119
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=handle&
  VDISYS
RETURN
> PROCEDURE vst_unload_fonts
  INTIN(0)=0
  CONTRL(0)=120
  CONTRL(1)=0
  CONTRL(3)=0
  CONTRL(6)=handle&
  VDISYS 120
RETURN
> PROCEDURE v_gtext(x&,y&,string$)
  LOCAL i&,l&,adr%
  l&=LEN(string$)
  PTSIN(0)=x&
  PTSIN(1)=y&
  CONTRL(0)=8
  CONTRL(1)=1
  CONTRL(3)=l&
  CONTRL(6)=handle&
  DEC l&
  adr%=V:string$
  FOR i&=0 TO l&
    INTIN(i&)=BYTE{adr%+i&}
  NEXT i&
  VDISYS
RETURN
> PROCEDURE vst_height(height&,VAR char_width&,char_height&,cell_width&,cell_height&)
  PTSIN(1)=height&
  CONTRL(0)=12
  CONTRL(1)=1
  CONTRL(3)=0
  CONTRL(6)=handle&
  VDISYS
  char_width&=PTSOUT(0)
  char_height&=PTSOUT(1)
  cell_width&=PTSOUT(2)
  cell_height&=PTSOUT(3)
RETURN
> PROCEDURE vst_rotation(angle&)
  INTIN(0)=angle&
  CONTRL(0)=13
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=handle&
  VDISYS
RETURN
> PROCEDURE vst_font(font&)
  INTIN(0)=font&
  CONTRL(0)=21
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=handle&
  VDISYS
RETURN
> PROCEDURE vst_color(color_index&)
  INTIN(0)=color_index&
  CONTRL(0)=22
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=handle&
  VDISYS
RETURN
> PROCEDURE vst_effects(effect&)
  INTIN(0)=effect&
  CONTRL(0)=106
  CONTRL(1)=0
  CONTRL(3)=1
  CONTRL(6)=handle&
  VDISYS
RETURN
> PROCEDURE vst_alignment(hor_in&,vert_in&,VAR hot_out&,vert_out&)
  INTIN(0)=hor_in&
  INTIN(1)=vert_in&
  CONTRL(0)=39
  CONTRL(1)=0
  CONTRL(3)=2
  CONTRL(6)=handle&
  VDISYS
  hor_out&=INTOUT(0)
  vert_out&=INTOUT(1)
RETURN
> PROCEDURE demo ! ************** tu rozwin ************
  vst_font(2) ! sprawdz dla 1
  vst_effects(0) ! 0-brak efektow, sprawdz 100
  vst_rotation(0)
  vst_height(93,x&,x&,x&,x&) ! 93 to wielkosc fontow
  vst_alignment(1,0,x&,x&)
  v_gtext(67,116,"The Voice") ! tu wpisz text
  REPEAT  ! czeka na mysz
  UNTIL MOUSEK
RETURN
