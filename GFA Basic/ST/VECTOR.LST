' Mini-3D-Vektor-Demo
'
' (c) 1993 by Lucky of ST
'
' Drckt auch mal Space!
'
DIM o(100,2),l(100),px(100),py(100),dx(100),dy(100)
scr1=XBIOS(2)
oscr=scr1
RESERVE FRE(0)-32256
scr2=(HIMEM+256) AND &HFFFFFF00
BOUNDARY 0
SETCOLOR 0,0
REPEAT
  INC a
  READ o(a,0),o(a,1),o(a,2)
UNTIL o(a,0)=-1000
CLR a
REPEAT
  INC a
  READ l(a)
UNTIL l(a)=-2
DATA 0,125,100,95,125,31,59,125,-81,-59,125,-81,-95,125,31,0,-125,0,-1000,0,0
DATA 1,2,6,0,4,-1,2,3,6,0,3,-1,3,6,4,1,5,-1,4,5,6,0,3,-1,1,6,5,0,2,-1,1,2,3,4,5,1,6,-2
CLIP  OFFSET 319,199
REPEAT
  VOID XBIOS(5,L:scr2,L:scr1,-1)
  SWAP scr1,scr2
  CLS
  draw_obj(0,0,0,a*2,a,a/3,NOT filled,o())
  ADD a,4
  a$=INKEY$
  filled=filled XOR (a$=" ")
UNTIL a$=CHR$(27)
VOID XBIOS(5,L:oscr,L:oscr,-1)
RESERVE
PROCEDURE draw_obj(x,y,z,wx,wy,wz,filled,VAR o())
  LOCAL a,b,px,pz
  a=1
  WHILE o(a,0)>-1000
    py(a)=o(a,1)*COSQ(wx)+o(a,2)*SINQ(wx)
    pz=o(a,2)*COSQ(wx)-o(a,1)*SINQ(wx)
    px=o(a,0)*COSQ(wy)+pz*SINQ(wy)
    pz=1+(pz*COSQ(wy)-o(a,0)*SINQ(wy)-z)/1000
    px(a)=x+(px*COSQ(wz)+py(a)*SINQ(wz))*pz
    py(a)=y+(py(a)*COSQ(wz)-px*SINQ(wz))*pz
    INC a
  WEND
  a=1
  REPEAT
    CLR b
    REPEAT
      dx(b)=px(l(a))
      dy(b)=py(l(a))
      INC b
      INC a
    UNTIL l(a+2)<0
    IF (dx(1+l(a))-dx(0))*(dy(2-l(a))-dy(0))-(dy(1+l(a))-dy(0))*(dx(2-l(a))-dx(0))<0
      IF filled
        DEFFILL ,2,l(a+1)
        POLYFILL b,dx(),dy()
      ELSE
        dx(b)=dx(0)
        dy(b)=dy(0)
        POLYLINE b+1,dx(),dy()
      ENDIF
    ENDIF
    ADD a,3
  UNTIL l(a-1)<-1
RETURN
