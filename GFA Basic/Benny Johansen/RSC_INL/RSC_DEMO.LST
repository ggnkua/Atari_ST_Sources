'
INLINE a_%,3000
BLOAD "rsc_demo.rsc",a_%    ! detta „r n”dv„ndigt under programmering eftersom
' END                       ! f”r„ndringar l„gger sej i INLINE och d† sparar
'                           ! man kanske med en "kn„pp" RSC-fil
'
~@rsrc_convert(a_%)
~@rsrc_gaddr(0,0,tree%)
'
'  @rsrc_text() / ~@rsrc_text$()  l„mnar/h„mtar text i ALLA object
'
~FORM_ALERT(1,freestr_$(0))       ! vi har bara en alertbox i rsc-filen...
'
~FORM_CENTER(tree%,x,y,w,h)
~OBJC_DRAW(tree%,0,3,x,y,w,h)
~FORM_DO(tree%,0)
'
'
> FUNCTION rsrc_convert(inline_adress%)
$F%
'
'
LOCAL a_%,adr%,t0%,t1%,t2%,t3%
LOCAL a1,a2,a3,a4,a5,a6,a7,a8,a9,a10,a11
LOCAL a1&,a2&,a3&,a4&,a5&,a6&,a7&,a8&,a9&,a10&,a11&,a12&
LOCAL ob&,ted&,ico&,img&,fstr&,fimg&
'
a_%=inline_adress%
'
ob&=WORD{ADD(a_%,2)}             ! offset till data i rsc filen
ted&=WORD{ADD(a_%,4)}
ico&=WORD{ADD(a_%,6)}
img&=WORD{ADD(a_%,8)}
fstr&=WORD{ADD(a_%,10)}
fimg&=WORD{ADD(a_%,8)}           !  offset ligger EFTER img& ...
'
object_&=WORD{ADD(a_%,20)}       !  object
tree_&=WORD{ADD(a_%,22)}         !  tr„d
tedinfo_&=WORD{ADD(a_%,24)}      !  tedinfo
icon_&=WORD{ADD(a_%,26)}         !  iconer
image_&=WORD{ADD(a_%,28)}        !  bitmaps
freestring_&=WORD{ADD(a_%,30)}   !  Free String
freeimg_&=WORD{ADD(a_%,32)}      !  Free Image
'
DIM tree_%(tree_&)
DIM object_|(MUL(object_&,24))
DIM tedinfo_|(MUL(tedinfo_&,28))
DIM icon_|(MUL(icon_&,36))       ! 36,14,24 = l„ngden
DIM image_|(MUL(image_&,14))     ! p† enskild data-header
DIM freeimg_|(MUL(freeimg_&,14))
'
DIM freestr_$(freestring_&)       ! inneh†ller free strings/alert strings
DIM freeimg_%(image_&)            ! adress till Free img data
'
adr%=ADD(a_%,36)            ! offset till alla "textdata"
'
FOR i&=0 TO object_&-1
  a1=INT{ADD(a_%,ob&)}               ! ob_next
  a2=INT{ADD(a_%,ADD(ob&,2))}        ! ob_head
  a3=INT{ADD(a_%,ADD(ob&,4))}        ! ob_tail
  a4=WORD{ADD(a_%,ADD(ob&,6))}       ! ob_type
  a5=WORD{ADD(a_%,ADD(ob&,8))}       ! ob_flags
  a6=WORD{ADD(a_%,ADD(ob&,10))}      ! ob_state
  a7=LONG{ADD(a_%,ADD(ob&,12))}      ! ob_spec
  a8=INT{ADD(a_%,ADD(ob&,16))}       ! ob_x
  a9=INT{ADD(a_%,ADD(ob&,18))}       ! ob_y
  a10=INT{ADD(a_%,ADD(ob&,20))}      ! ob_w
  a11=INT{ADD(a_%,ADD(ob&,22))}      ! ob_h
  '
  SELECT BYTE(a4)                     ! MSTE vara BYTE annars g†r det †t
  CASE 26,28,32                       ! h-vete om "Ext.Type" Anv„nds...
    a7=adr%                           ! Text
    dum$=CHAR{adr%}
    ADD adr%,LEN(dum$)+1
    '
  CASE 21,22,29,30
    '
    t1%=adr%                          ! te_ptext
    dum$=CHAR{adr%}
    ADD adr%,LEN(dum$)+1
    '
    t2%=adr%                          ! te_ptmplt
    dum$=CHAR{adr%}
    ADD adr%,LEN(dum$)+1
    '
    t3%=adr%                          ! te_pvalid
    dum$=CHAR{adr%}
    ADD adr%,LEN(dum$)+1
    '
    a1&=WORD{ADD(a_%,ADD(ted&,12))}
    a2&=WORD{ADD(a_%,ADD(ted&,14))}
    a3&=WORD{ADD(a_%,ADD(ted&,16))}
    a4&=WORD{ADD(a_%,ADD(ted&,18))}
    a5&=WORD{ADD(a_%,ADD(ted&,20))}
    a6&=WORD{ADD(a_%,ADD(ted&,22))}
    a7&=WORD{ADD(a_%,ADD(ted&,24))}
    a8&=WORD{ADD(a_%,ADD(ted&,26))}
    a7=cted&
    @tedinfo(t1%,t2%,t3%,a1&,a2&,a3&,a4&,a5&,a6&,a7&,a8&)
    ADD ted&,28
  CASE 23
    t1%=ADD(a_%,LONG{ADD(a_%,img&)})
    a1&=WORD{ADD(a_%,ADD(img&,4))}
    a2&=WORD{ADD(a_%,ADD(img&,6))}
    a3&=WORD{ADD(a_%,ADD(img&,8))}
    a4&=WORD{ADD(a_%,ADD(img&,10))}
    a5&=WORD{ADD(a_%,ADD(img&,12))}
    a7=cimg&
    @bitmap(t1%,a1&,a2&,a3&,a4&,a5&)
    ADD img&,14
  CASE 31
    t0%=ADD(a_%,LONG{ADD(a_%,ico&)})
    t1%=ADD(a_%,LONG{ADD(a_%,ico&+4)})
    t2%=adr%
    dum$=CHAR{adr%}
    ADD adr%,LEN(dum$)+1
    '
    a1&=WORD{ADD(a_%,ADD(ico&,12))}
    a2&=WORD{ADD(a_%,ADD(ico&,14))}
    a3&=WORD{ADD(a_%,ADD(ico&,16))}
    a4&=WORD{ADD(a_%,ADD(ico&,18))}
    a5&=WORD{ADD(a_%,ADD(ico&,20))}
    a6&=WORD{ADD(a_%,ADD(ico&,22))}
    a7&=WORD{ADD(a_%,ADD(ico&,24))}
    a8&=WORD{ADD(a_%,ADD(ico&,26))}
    a9&=WORD{ADD(a_%,ADD(ico&,28))}
    a10&=WORD{ADD(a_%,ADD(ico&,30))}
    a11&=WORD{ADD(a_%,ADD(ico&,32))}
    a12&=WORD{ADD(a_%,ADD(ico&,34))}
    a7=cicn&
    @icon(t0%,t1%,t2%,a1&,a2&,a3&,a4&,a5&,a6&,a7&,a8&,a9&,a10&,a11&,a12&)
  DEFAULT
    ' PRINT "k"
  ENDSELECT
  @object(a1,a2,a3,a4,a5,a6,a7,a8,a9,a10,a11)
  ADD ob&,24
NEXT i&
'
FOR i&=0 TO freestring_&-1     ! Freestring/alert
  freestr_$(i&)=CHAR{adr%}
  ADD adr%,LEN(freestr_$(i&))+1
NEXT i&
'
off%=14*(image_&-freeimg_&)
FOR i=0 TO freeimg_&-1
  t0%=a_%+LONG{a_%+fimg&+off%}        ! adressen till imgdatan
  freeimg_%(cfimg&)=t0%
  a1&=WORD{a_%+fimg&+4+off%}
  a2&=WORD{a_%+fimg&+6+off%}
  a3&=WORD{a_%+fimg&+8+off%}
  a4&=WORD{a_%+fimg&+10+off%}
  a5&=WORD{a_%+fimg&+12+off%}
  @freeimg(t0%,a1&,a2&,a3&,a4&,a5&)
  ADD off%,14
NEXT i
'
RETURN TRUE
ENDFUNC
> FUNCTION rsrc_gaddr(f&,i&,VAR adr%)
$F%
LOCAL e&
CLR e&,adr%
i&=ABS(i&)
SELECT f&
CASE 0
IF i&<tree_&
  adr%=tree_%(i&)
ENDIF
CASE 5
IF i&<freestr_&
  adr%=V:freestr_$(i&)
ENDIF
CASE 6
IF i&<freeimg_&
  adr%=ADD(V:freeimg_|(0),MUL(i&,14))
ENDIF
ENDSELECT
IF adr%>0
e&=1
ENDIF
RETURN e&
ENDFUNC
> FUNCTION rsrc_free
$F%
'
CLR tree_&,object_&,string_&,tedinfo_&,image_&,icon_&,freestr_&,freeimg_&
ERASE frestr_$(),freeimg_%()
ERASE tree_%(),object_|(),tedinfo_|()
ERASE image_|(),icon_|()
ERASE freeimg_|()
'
RETURN 1
ENDFUNC
'
> PROCEDURE rsrc_text(tree%,obj&,a$)
SELECT BYTE(OB_TYPE(tree%,obj&))
CASE 26,28,32
CHAR{OB_SPEC(tree%,obj&)}=a$
CASE 21,22,29,30
CHAR{{OB_SPEC(tree%,obj&)}}=a$
CASE 31
CHAR{{OB_SPEC(tree%,obj&)+8}}=a$
ENDSELECT
RETURN
> FUNCTION rsrc_text$(tree%,obj&)
SELECT BYTE(OB_TYPE(tree%,obj&))
CASE 26,28,32
RETURN CHAR{OB_SPEC(tree%,obj&)}
CASE 21,22,29,30
RETURN CHAR{{OB_SPEC(tree%,obj&)}}
CASE 31
RETURN CHAR{{OB_SPEC(tree%,obj&)+8}}
ENDSELECT
ENDFUNC
'
> PROCEDURE set_tedinfo(tree%,obj&,ptext$,ptmplt$,pvalid$)
'
CHAR{{OB_SPEC(tree%,obj&)}}=ptext$
CHAR{{ADD(OB_SPEC(tree%,obj&),4)}}=ptmplt$
CHAR{{ADD(OB_SPEC(tree%,obj&),8)}}=pvalid$
'
WORD{ADD(OB_SPEC(tree%,3),24)}=LEN(ptext$)+1   ! Detta MSTE vara med
WORD{ADD(OB_SPEC(tree%,3),26)}=LEN(ptmplt$)+1  ! om man vill ha det bra ...
'
RETURN
> PROCEDURE get_tedinfo(tree%,obj&,VAR ptext$,ptmplt$,pvalid$)
ptext$=CHAR{{OB_SPEC(tree%,obj&)}}
ptmplt$=CHAR{{OB_SPEC(tree%,obj&)+4}}
pvalid$=CHAR{{OB_SPEC(tree%,obj&)+8}}
RETURN
' --------- Konverterings Procedurer ------------------------------------------
> PROCEDURE object(p1&,p2&,p3&,p4&,p5&,p6&,p7%,p8&,p9&,p10&,p11&)
'  LOCAL ta%
'
ta%=ADD(V:object_|(0),MUL(cobj&,24))
WORD{ta%}=p1&
WORD{ADD(ta%,2)}=p2&
WORD{ADD(ta%,4)}=p3&
WORD{ADD(ta%,6)}=p4&
WORD{ADD(ta%,8)}=p5&
CARD{ADD(ta%,10)}=p6&
SELECT BYTE(p4&)
CASE 26,28,32
LONG{ADD(ta%,12)}=p7%
CASE 21,22,29,30
LONG{ADD(ta%,12)}=ADD(V:tedinfo_|(0),MUL(p7%,28))
CASE 23
LONG{ADD(ta%,12)}=ADD(V:image_|(0),MUL(p7%,14))
CASE 31
LONG{ADD(ta%,12)}=ADD(V:icon_|(0),MUL(p7%,36))
DEFAULT
LONG{ADD(ta%,12)}=p7%
ENDSELECT
WORD{ADD(ta%,16)}=p8&
WORD{ADD(ta%,18)}=p9&
WORD{ADD(ta%,20)}=p10&
WORD{ADD(ta%,22)}=p11&
~RSRC_OBFIX(V:object_|(0),cobj&)
IF p1&=TRUE
tree_%(ctre&)=ta%
INC ctre&
ENDIF
INC cobj&
RETURN
> PROCEDURE tedinfo(a1%,a2%,a3%,p1&,p2&,p3&,p4&,p5&,p6&,p7&,p8&)
'  LOCAL ta%
'
ta%=ADD(V:tedinfo_|(0),MUL(cted&,28))
'
LONG{ta%}=a1%          !V:tedinfo_$(cted&,0)
LONG{ADD(ta%,4)}=a2%   !V:tedinfo_$(cted&,1)
LONG{ADD(ta%,8)}=a3%   !V:tedinfo_$(cted&,2)
'
WORD{ADD(ta%,12)}=p1&
WORD{ADD(ta%,14)}=p2&
WORD{ADD(ta%,16)}=p3&
WORD{ADD(ta%,18)}=p4&
WORD{ADD(ta%,20)}=p5&
WORD{ADD(ta%,22)}=p6&
WORD{ADD(ta%,24)}=p7&
WORD{ADD(ta%,26)}=p8&
INC cted&
RETURN
> PROCEDURE icon(a0%,a1%,a2%,p1&,p2&,p3&,p4&,p5&,p6&,p7&,p8&,p9&,p10&,p11&,p12&)
'
ta%=ADD(V:icon_|(0),MUL(cicn&,36))
LONG{ta%}=a0%
LONG{ADD(ta%,4)}=a1%   !ADD(a0%,MUL(DIV(p6&,8),p7&))
LONG{ADD(ta%,8)}=a2%   !V:icon_$(cicn&,1) TEXTEN
'
WORD{ADD(ta%,12)}=p1&
WORD{ADD(ta%,14)}=p2&
WORD{ADD(ta%,16)}=p3&
WORD{ADD(ta%,18)}=p4&
WORD{ADD(ta%,20)}=p5&
WORD{ADD(ta%,22)}=p6&
WORD{ADD(ta%,24)}=p7&
WORD{ADD(ta%,26)}=p8&
WORD{ADD(ta%,28)}=p9&
WORD{ADD(ta%,30)}=p10&
WORD{ADD(ta%,32)}=p11&
WORD{ADD(ta%,34)}=p12&
INC cicn&
RETURN
> PROCEDURE bitmap(a0%,p1&,p2&,p3&,p4&,p5&)
a%=ADD(V:image_|(0),MUL(cimg&,14))
LONG{a%}=a0%  !V:image_$(cimg&)
WORD{ADD(a%,4)}=p1&
WORD{ADD(a%,6)}=p2&
WORD{ADD(a%,8)}=p3&
WORD{ADD(a%,10)}=p4&
WORD{ADD(a%,12)}=p5&
INC cimg&
RETURN
> PROCEDURE freeimg(t0%,p1&,p2&,p3&,p4&,p5&)
ta%=ADD(V:freeimg_|(0),MUL(cfimg&,14))
LONG{ta%}=t0% !V:freeimg_$(cfimg&)
WORD{ADD(ta%,4)}=p1&
WORD{ADD(ta%,6)}=p2&
WORD{ADD(ta%,8)}=p3&
WORD{ADD(ta%,10)}=p4&
WORD{ADD(ta%,12)}=p5&
INC cfimg&
RETURN
' -----------------------------------------------------------------------------
