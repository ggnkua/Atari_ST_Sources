.
.Creation des fichiers n‚cessaires …
.l'animation d'un fond compos‚ de motifs
.par Jedi of Sector One from The Heavy Killers
.
VSYNC
CLS
VSYNC
SETCOLOR 0,0
SETCOLOR 15,1911
.
.Chargement de l'image dans laquelle
.on va d‚couper le motif
.
ALERT 2," | |    Charger une image | ",0," Degas | Neo | Quit ",format%
SELECT format%
CASE 3
  EDIT
CASE 1
  FILESELECT "\*.PI1","",nfile$
  nfile$=nfile$+".PI1"
  IF EXIST(nfile$)
    OPEN "I",#1,nfile$
    SEEK #1,2
    pal$=INPUT$(32,#1)
    img$=INPUT$(32000,#1)
    CLOSE #1
  ELSE
    RUN
  ENDIF
CASE 2
  FILESELECT "\*.NEO","",nfile$
  nfile$=nfile$+".NEO"
  IF EXIST(nfile$)
    OPEN "I",#1,nfile$
    SEEK #1,4
    pal$=INPUT$(32,#1)
    SEEK #1,128
    img$=INPUT$(32000,#1)
    CLOSE #1
  ELSE
    RUN
  ENDIF
ENDSELECT
yash:
VSYNC
GRAPHMODE 3
FOR a%=0 TO 15
  SETCOLOR a%,0
NEXT a%
SPUT img$
~XBIOS(6,L:V:pal$)
VSYNC
IF DPEEK(&HFFFF8240)=DPEEK(&HFFFF825E)
  SDPOKE &HFFFF825E,NOT DPEEK(&HFFFF8240)
ENDIF
WHILE MOUSEK
WEND
HIDEM
xol%=512
yol%=512
REPEAT
  MOUSE x1%,y1%,z%
  IF x1%<>xol% OR y1%<>yol%
    VSYNC
    SPUT img$
    LINE x1%,0,x1%,199
    LINE 0,y1%,319,y1%
    xol%=x1%
    yol%=y1%
  ENDIF
UNTIL z%
xol%=512
yol%=512
WHILE z%
  MOUSE x2%,y2%,z%
  IF x2%<>xol% OR y2%<>yol%
    VSYNC
    SPUT img$
    BOX x1%,y1%,x2%,y2%
    xol%=x2%
    yol%=y2%
  ENDIF
WEND
VSYNC
SPUT img$
GRAPHMODE 1
GET x1%,y1%,x2%,y2%,bloc$
VSYNC
CLS
PUT 0,0,bloc$
sizex%=SUCC(ABS(x1%-x2%))
sizey%=SUCC(ABS(y1%-y2%))
PRINT AT(1,24);"Taille en X: ";sizex%''"Taille en Y: ";sizey%
PRINT "C'est bon ??? ( O/N )";
REPEAT
  nob$=UPPER$(INPUT$(1))
UNTIL nob$="O" OR nob$="N"
IF nob$="N"
  GOTO yash
ENDIF
CLS
PRINT "Entrez le nom du fichier destin‚"
PRINT "… recevoir le motif lui-meme"
FILESELECT "\*.SCT","MOTIF.SCT",f$
f$=f$+".SCT"
IF f$=".SCT"
  RUN
ENDIF
CLS
PRINT AT(1,25);"Sauvegarde en cours...";
PUT 0,0,bloc$
OPEN "o",#1,f$
PRINT #1,MKI$(sizex%)+MKI$(sizey%)
BPUT #1,XBIOS(2),MUL(sizey%,160)
CLOSE #1
.
.Courbe
.
sector_one:
CLS
PRINT "Parametres relatifs a la courbe"
INPUT "Coefficient 1 ";c1%
INPUT "Coefficient 2 ";c2%
INPUT "Coefficient 3 ";c3%
INPUT "Coefficient 4 ";c4%
CLS
.
.Repr‚sentation graphique de la fonction
.
FOR angle%=0 TO 359
  x=159.5
  x=x+159.5*COSQ(angle%*c1%)*SINQ(angle%*c2%)
  y=99.5
  y=y+99.5*COSQ(angle%*c3%)*SINQ(angle%*c4%)
  VSYNC
  PSET ROUND(x),ROUND(y),15
NEXT angle%
PRINT AT(1,25);"C'est bon ? ( O/N )";
REPEAT
  key$=UPPER$(INPUT$(1))
UNTIL key$="O" OR key$="N"
IF key$="N"
  GOTO sector_one
ENDIF
PRINT AT(1,1);"Entrez le nom du fichier"
PRINT AT(1,2);"destin‚ … recevoir la courbe"
FILESELECT "\*.INL","COURBE.INL",nfile$
nfile$=nfile$+".INL"
IF nfile$=".INL"
  RUN
ENDIF
.
.Sauvegarde de la courbe
.
OPEN "o",#1,nfile$
staf%=MUL(sizey%,320)
FOR angle%=0 TO 359
  PRINT AT(1,3);"Traitement position #";angle%;"/359";
  x=100
  x=x+100*COSQ(angle%*c1%)*SINQ(angle%*c2%)
  y=100
  y=y+100*COSQ(angle%*c3%)*SINQ(angle%*c4%)
  dx%=MUL(MOD(ROUND(x),sizex%),staf%)
  dy%=MUL(MOD(ROUND(y),sizey%),160)
  adresse%=ADD(dx%,dy%)
  PRINT #1,MKL$(adresse%);
NEXT angle%
CLOSE #1
CLS
PRINT "Entrez le nom du fichier destin‚"
PRINT "… recevoir la palette du dessin"
FILESELECT "\*.INL","PALETTE.INL",f$
f$=f$+".INL"
IF f$=".INL"
  RUN
ENDIF
BSAVE f$,V:pal$,32
EDIT
