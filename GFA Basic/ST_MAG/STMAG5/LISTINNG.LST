.
.D‚coupage d'un sprite dans une image Neo ou Degas,
.recodage du motif selon le nombre de plans souhait‚s,
.creation de la routine d'effacage,
.mise en place de la routine d'affichage,
.calcul d'une courbe parametrable bien centr‚e,
.et sauvegarde de celle-ci ainsi que de la palette !
.Code : Jedi of Sector One from The Heavy Killers
.Bon courage pour taper tout ‡a...
.
VSYNC                       !Soon coming:
CLS                         !The Bee Forol Megademo
VSYNC                       !Avec la participation de:
SETCOLOR 0,0                !ODC,T32,KCB,DBA,Zap Crtn,
SETCOLOR 15,1911            !et surtout de Sector One!
ALERT 2," | |    Charger une image | ",0," Degas | Neo | Quit ",format%
SELECT format%              !JakkaronhŠmlakrŠm2maron
CASE 3                      !antubb2soasanndizuigram
  EDIT                      !Quit ?!?
CASE 1                      !Image au format Degas
  FILESELECT "\*.PI1","",nfile$
  nfile$=nfile$+".PI1"      !Extension par d‚faut .PI1
  IF EXIST(nfile$)          !Ca existe au moins ?!
    OPEN "I",#1,nfile$      !On ouvre
    SEEK #1,2               !On saute
    pal$=INPUT$(32,#1)      !On lit
    img$=INPUT$(32000,#1)   !On lit la suite
    CLOSE #1                !Et on referme
  ELSE                      !Ca existe pas ???
    RUN                     !C'est reparti pour un tour
  ENDIF                     !Salut Klaus !
CASE 2                      !Image Neochrome
  FILESELECT "\*.NEO","",nfile$
  nfile$=nfile$+".NEO"      !Je recherche un gentil
  IF EXIST(nfile$)          !‚diteur qui accepterait
    OPEN "I",#1,nfile$      !de commercialiser mon bel
    SEEK #1,4               !utilitaire de musique
    pal$=INPUT$(32,#1)      !'soundchip'...
    SEEK #1,128             !Contactez-moi vite !
    img$=INPUT$(32000,#1)   !Merci d'avance...
    CLOSE #1                !Utilisation simple,
  ELSE                      !routine hyper-rapide,
    RUN                     !gestion de samples,
  ENDIF                     !ripping d'instruments,
ENDSELECT                   !effets sp‚ciaux,
VSYNC                       !et j'en passe...
GRAPHMODE 3                 !Pour voir les contours
FOR a%=0 TO 15              !Met toutes les couleurs
  SETCOLOR a%,0             !En noir avant l'affichage
NEXT a%                     !de l'image charg‚e.
SPUT img$                   !Et zou !
~XBIOS(6,L:V:pal$)          !La palette...
VSYNC                       !Attend qu'elle s'installe
.
.Si la couleur de fond est confondue avec celle
.utilis‚e en particulier pour l'affichage du texte et
.de la souris, on change un peu la palette histoire
.de voir quelque chose...
.
IF DPEEK(&HFFFF8240)=DPEEK(&HFFFF825E)
  SDPOKE &HFFFF825E,NOT DPEEK(&HFFFF8240)
ENDIF
WHILE MOUSEK                !On se calme et on cesse
WEND                        !d'appuyer sur la souris
HIDEM                       !C'est malin, vous lui
xol%=512                    !avez fait peur et
yol%=512                    !maintenant elle se cache!
REPEAT                      !Ca, ce sont des
  MOUSE x1%,y1%,z%          !instructions trŠs
  IF x1%<>xol% OR y1%<>yol% !sympathiques, mais qui
    VSYNC                   !n'ont rien de trŠs
    SPUT img$               !technique.
    LINE x1%,0,x1%,199      !Alors on passe...
    LINE 0,y1%,319,y1%      !Trace le viseur
    xol%=x1%                !Pour ‚viter le
    yol%=y1%                !clignottement
  ENDIF                     !Sector One est compos‚ de
UNTIL z%                    !-Ace ( codeur )
xol%=512                    !-Eclipse ( grafixx )
yol%=512                    !-Thunderboy ( code, gfx )
WHILE z%                    !-Shame ( muzaxx )
  MOUSE x2%,y2%,z%          !-Spinders ( codeur )
  IF x2%<>xol% OR y2%<>yol% !-White Ninja ( gfx, zax )
    VSYNC                   !-Virgil ( soundtraxx )
    SPUT img$               !-Maverick ( codeur )
    BOX x1%,y1%,x2%,y2%     !-Jedi ( codeur, muzaxx )
    xol%=x2%                !Dum...dum...dum...
    yol%=y2%                !Rien de special
  ENDIF                     !L… non plus...
WEND                        !Bac
VSYNC                       !Bec
SPUT img$                   !Bic
GRAPHMODE 1                 !Boc
GET x1%,y1%,x2%,y2%,bloc$   !Buc
VSYNC                       !Byc
CLS                         !Efface
PUT 0,0,bloc$               !Le bloc selectionn‚
sizex%=SUCC(ABS(x1%-x2%))   !Largeur du sprite
sizey%=SUCC(ABS(y1%-y2%))   !Hauteur du sprite
sizex%=ADD(sizex%,15) AND &HFFF0
PRINT AT(1,22);"Taille en X: ";sizex%''"Taille en Y: ";sizey%
PRINT "Soit ";sizex%*sizey%/16;" blocs de 16 pixels."
INPUT "Nombre de plans: ",nplan%
nplan%=MAX(1,MOD(nplan%,5)) !de 1 … 4 plans seulement
ace%=SHR(sizex%,1)          !Ce qu'il faut prendre...
DEC sizey%                  !Admettons...
IF nplan%<>4                !En 4 plans, on garde tout
  FOR ligne%=0 TO sizey%
    FOR col%=0 TO ace% STEP 8
      adresse%=XBIOS(2)+col%+ligne%*160
      SELECT nplan%
      CASE 1                !1 plan
        CARD{adresse%+2}=0  !=> Vire plan 2
        LONG{adresse%+4}=0  !et plans 3&4
      CASE 2                !2 plans
        LONG{adresse%+4}=0  !=> Vire plans 3&4
      CASE 3                !3 plans
        CARD{adresse%+6}=0  !=> Vire plan 4
      ENDSELECT
    NEXT col%
  NEXT ligne%
ENDIF
PRINT "Ok pour le sprite ? ( O/N )";
REPEAT
  key$=UPPER$(INPUT$(1))
  IF key$="N"
    RUN
  ENDIF
UNTIL key$="O"
GET 0,0,319,167,sprite$
CLS
PRINT "Entrez le nom du fichier .LST"
PRINT "destin‚ … l'effacage du motif"
FILESELECT "\*.LST","",nfile$
nfile$=nfile$+".LST"
IF nfile$=""
  EDIT
ENDIF
OPEN "o",#1,nfile$          !Effacage du sprite
FOR ligne%=0 TO sizey%      !Ligne
  FOR col%=0 TO ace% STEP 8 !Colonne
    offset%=col%+ligne%*160 !Offset
    SELECT nplan%           !Suivant les plans
    CASE 1                  !Vire juste le 1er
      PRINT #1,"CARD{jedi%+";offset%;"}=0"
    CASE 2                  !Vire 1 et 2
      PRINT #1,"LONG{jedi%+";offset%;"}=0"
    CASE 3                  !Vire 1,2 et 3
      PRINT #1,"LONG{jedi%+";offset%;"}=0"
      PRINT #1,"CARD{jedi%+";offset%+4;"}=0"
    CASE 4                  !Vire tout
      PRINT #1,"LONG{jedi%+";offset%;"}=0"
      PRINT #1,"LONG{jedi%+";offset%+4;"}=0"
    ENDSELECT
  NEXT col%
NEXT ligne%
CLOSE #1
CLS
PRINT "Entrez le nom du fichier .LST"
PRINT "destin‚ … l'affichage du sprite"
FILESELECT "\*.LST","",nfile$
nfile$=nfile$+".LST"
IF nfile$=""
  EDIT
ENDIF
OPEN "o",#1,nfile$
FOR phase%=0 TO 15
  CLS
  PUT phase%,0,sprite$      !Pr‚decalage du motif
  PRINT AT(1,25);"Phase nø";phase%;"/15";
  PRINT #1,"PROCEDURE sprite";phase%
  FOR ligne%=0 TO sizey%
    FOR col%=0 TO ace% STEP 8
      offset%=col%+ligne%*160
      alpha%=ADD(XBIOS(2),offset%)
      SELECT nplan%         !Comme l'effacage...
      CASE 1
        IF CARD{alpha%}
          PRINT #1,"CARD{jedi%+";offset%;"}=";
          PRINT #1,CARD{alpha%}
        ENDIF
      CASE 2
        IF LONG{alpha%}
          PRINT #1,"LONG{jedi%+";offset%;"}=";
          PRINT #1,LONG{alpha%}
        ENDIF
      CASE 3
        IF LONG{alpha%}
          PRINT #1,"LONG{jedi%+";offset%;"}=";
          PRINT #1,LONG{alpha%}
        ENDIF
        IF CARD{alpha%+4}
          PRINT #1,"CARD{jedi%+";offset%+4;"}=";
          PRINT #1,CARD{alpha%+4}
        ENDIF
      CASE 4
        IF LONG{alpha%}
          PRINT #1,"LONG{jedi%+";offset%;"}=";
          PRINT #1,LONG{alpha%}
        ENDIF
        IF LONG{alpha%+4}
          PRINT #1,"LONG{jedi%+";offset%+4;"}=";
          PRINT #1,LONG{alpha%+4}
        ENDIF
      ENDSELECT
    NEXT col%
  NEXT ligne%
  PRINT #1,"RETURN"
NEXT phase%
CLOSE #1
sector_one:
CLS
extreme_x%=SUB(320,sizex%)
extreme_y%=SUB(200,sizey%)
middle_x=extreme_x%/2       !Centrage de la courbe
middle_y=extreme_y%/2
PRINT "Parametres relatifs a la courbe"
INPUT "Coefficient 1 ";c1%
INPUT "Coefficient 2 ";c2%
INPUT "Coefficient 3 ";c3%
INPUT "Coefficient 4 ";c4%
CLS
FOR angle%=0 TO 359
  x=middle_x
  x=x+middle_x*COSQ(angle%*c1%)*SINQ(angle%*c2%)
  y=middle_y
  y=y+middle_y*COSQ(angle%*c3%)*SINQ(angle%*c4%)
  VSYNC
  PSET ROUND(x),ROUND(y),15 ! Repr‚sentation
NEXT angle%
PRINT AT(1,25);"C'est bon ? ( O/N )";
REPEAT
  key$=UPPER$(INPUT$(1))
UNTIL key$="O" OR key$="N"
IF key$="N"
  GOTO sector_one           !Non ? => on refait
ENDIF
PRINT AT(1,1);"Entrez le nom du fichier"
PRINT AT(1,2);"destin‚ … recevoir la courbe"
FILESELECT "\*.INL","COURBE.INL",nfile$
nfile$=nfile$+".INL"
IF nfile$=""
  EDIT
ENDIF
OPEN "o",#1,nfile$
FOR angle%=0 TO 359
  PRINT AT(1,3);"Traitement position #";angle%;"/359";
  x=middle_x
  x=x+middle_x*COSQ(angle%*c1%)*SINQ(angle%*c2%)
  y=middle_y
  y=y+middle_y*COSQ(angle%*c3%)*SINQ(angle%*c4%)
  adrx%=AND(SHR(ROUND(x),1),65528)
  adry%=MUL(160,ROUND(y))
  adresse%=ADD(adrx%,adry%) !L'offset,en fait
  deca%=AND(ROUND(x),15)    !Le d‚calage
  PRINT #1,MKI$(adresse%);  !On ‚crit tout ‡a
  PRINT #1,MKI$(SUCC(deca%));
NEXT angle%
CLOSE #1
CLS
PRINT "Entrez le nom du petit fichier"
PRINT "contenant la palette"
FILESELECT "\*.INL","PALETTE.INL",nfile$
nfile$=nfile$+".INL"
IF nfile$=""
  EDIT
ENDIF
BSAVE nfile$,V:pal$,32      !Sauve pal$, les couleurs
EDIT                        !et ciao !
