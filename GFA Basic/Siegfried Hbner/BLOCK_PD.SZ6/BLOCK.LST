'
' siehe Begleitartikel in PD-SZENE 6!
'
Sget Screen$            ! Schnellste Art 32000 Byte freizumachen
Nam$="*.PI*"            ! pic oder pi3 - natÅrlich auch Doodle oder Ñhnlich
Print At(30,3);"Bild laden .........."
Fileselect "\*.pi*","",Nam$
If Right$(Nam$)="\" Or Nam$=""
  Edit
Endif
Open "i",#1,Nam$
L%=Lof(#1)                 ! wie lang ?
If L%>32000                ! ist es ein Degas Bild (32034 oder 32066)
  Seek #1,34               ! Farbinfo Åberspringen
Endif
Bget #1,Varptr(Screen$),32000  ! eleganter als direkt auf den Bildschirm
Close #1
Sput Screen$
Graphmode 3                ! XOR
Hidem
Repeat
  X%=-1
  Y%=-1
  D%=0
  Repeat
    Mouse X1%,Y1%,K%
    If X1%+Y1%<>X%+Y%      ! Mit andern Worten 'HAT SICH MAUS BEWEGT?'
      Vsync                ! besonders mit Blitter wichtig
      Line X%,0,X%,399     ! Hintergrund wieder herstellen (XOR)
      Line 0,Y%,639,Y%
      Vsync
      Line X1%,0,X1%,399   ! neues Fadenkreuz zeichnen
      Line 0,Y1%,639,Y1%   ! keine Bildumschaltung (SORRY)
      X%=X1%               ! Memo-Variable auf Aktuelle Position
      Y%=Y1%
    Endif
    If K%=1                ! MAUS ?
      @Warte
      Swap X1%,X2%         ! Position retten, X1% AUF 0
      Swap Y1%,Y2%
      Fin!=Not Fin!
      If Fin!=False        ! Zwei DurchgÑnge ??
        Showm
        Nam$="*.blk"
        Fileselect "\*.blk","",Nam$
        If Right$(Nam$)="\" Or Nam$=""
          Edit
        Else
          Sput Screen$     ! Fadenkreuz Beseitigen
          Get Min(X1%,X2%),Min(Y1%,Y2%),Max(X1%,X2%),Max(Y1%,Y2%),Block$
          ' FÅr Leute die von rechts nach links oder unten nach oben 'AUFZIEHEN'
          Open "o",#1,Nam$ ! Block abspeichern
          Print #1,Block$
          Close #1         ! kaum zu glauben, das war's
        Endif
        Alert 0,"Noch'n Gedicht?  ",0," Ja | Nein ",D%
        Hidem
      Endif
      X%=-1                 !Memo-Variable auûerhalb des Schirm's
      Y%=-1
    Endif
  Until D%
  @Warte
  Sput Screen$
Until D%=2
'
Procedure Warte
  While Mousek
  Wend
Return
'
' Routine zum Lesen eines Block's
'
Open "i",#1,"test.blk"    ! eventuell Name Ñndern
L%=Lof(#1)                ! wie lang ist das Ding
Block$=Input$(L%,#1)      ! her damit
Close                     ! klappe dicht
'
Repeat
  Put Random(600)-100,Random(400)-50,Block$          ! wasndas
Until Mousek Or Inkey$<>""
