VOID XBIOS(5,L:XBIOS(2)-32000,L:-1,-1)
BLOAD "hamurabi.pi1",XBIOS(3)-34
VOID XBIOS(6,L:XBIOS(3)-32)
VOID XBIOS(5,L:-1,L:XBIOS(3),-1)
VOID INP(2)
'
' ************************************************
' * Hammurabi -  ett klassiskt spel till Atari ST
' * (c) 1990 Sea Kay Workshop
' * Clas Kristiansson
' * P†b”rjat           900807
' * Senast f”r„ndrat   911204
' * N†gra f† f”r„ndringar gjorda sedan
' * publiceringen i Hemdator 1191.
' * Bl.a. har alla utskrifter anpassats till
' * l†guppl”sning.
' ************************************************
'
@intro
@init
@main
'
> PROCEDURE main
  DO
    @rapport
    @inmatning
  LOOP
RETURN
'
> PROCEDURE inmatning
  @kopa_mark
  @salja_mark
  @ge_till_mat
  @sadd
  @berakna
RETURN
'
> PROCEDURE berakna
  y=c
  h=d*y
  e=0
  c=RANDOM(5)+1
  ' r†ttor
  IF EVEN(c)
    e=INT(s/c)
    s=s-e+h
    c=RANDOM(5)+1
  ENDIF
  ' barn
  i=INT(c*(20*a+s)/p/100+1)
  ' m„tta
  c=INT(q/20)
  ' pest
  q=INT(10*(2*RND-0.3))
  ' avsatt
  d=p-c
  d=MAX(d,0)
  IF d>0.45*p
    PRINT d;" medborgare svalt ihj„l"
    PRINT "resten avsatte dig"
    PRINT "som f”r„dare"
    PRINT "mot landet."
    @slut
  ELSE
    INC z
    p1=((z-1)*p1+d*100/p)/z
    p=c
    d1=d1+d
  ENDIF
RETURN
'
> PROCEDURE sadd
  REPEAT
    er=0
    INPUT "Hur m†nga hektar skall vi bes†";d
    IF d=0
      c=RANDOM(5)+1
    ENDIF
    IF d<0
      PRINT "Hmm! Vi „r visst p† v†rt"
      PRINT "sk„mtsamma hum”r i dag!!!"
      er=1
    ENDIF
    IF d>a
      PRINT "Hmm! Du „ger bara ";a;" hektar."
      er=1
    ENDIF
    IF INT(d/2)>s
      PRINT "Hmm! Du har bara ";s;" ton vete"
      PRINT "och n†gon kredit g†r ej att f†!"
      er=1
    ENDIF
    IF d>=10*p
      PRINT "Vi har endast ";p;" medborgare"
      PRINT "som kan bruka †krarna"
      er=1
    ENDIF
  UNTIL er=0
  s=s-INT(d/2)
  c=RANDOM(5)+1
RETURN
'
> PROCEDURE ge_till_mat
  REPEAT
    er=0
    PRINT "Hur mycket vill du"
    INPUT "ge ditt folk att „ta";q
    IF q<0
      PRINT "Negativ Mat???? Skall du rycka"
      PRINT "f”dan ur dina medborgares mun?"
      er=1
    ENDIF
    IF q>s
      PRINT "Hmm! Du har bara ";s;" ton vete"
      PRINT "och n†gon kredit g†r ej att f†!"
      er=1
    ENDIF
  UNTIL er=0
  s=s-q
  c=1
RETURN
'
> PROCEDURE salja_mark
  REPEAT
    er=0
    PRINT "Hur m†nga hektar"
    INPUT "vill du s„lja";q
    IF q<0
      PRINT "Hmm! Det verkar skumt."
      PRINT "     Det g†r v„l inte."
      er=1
    ENDIF
    IF q>=a
      PRINT "Hmm! Du „ger bara"
      PRINT a;" hektar."
      er=1
    ENDIF
  UNTIL er=0
  a=a-q
  s=s+y*q
  c=0
RETURN
'
> PROCEDURE kopa_mark
  er=0
  REPEAT
    er=0
    PRINT "Hur m†nga hektar"
    INPUT "vill du k”pa.";q
    IF q<0
      PRINT "Hmm! Det verkar skumt."
      PRINT "     Det g†r v„l inte."
      er=1
    ENDIF
    IF y*q>s
      PRINT "Det kostar";y*q
      PRINT "Hmm! Du har bara ";s;" ton vete"
      PRINT "och n†gon kredit g†r ej att f†!"
      er=1
      er=1
    ENDIF
  UNTIL er=0
  a=a+q
  s=s-y*q
  c=0
RETURN
'
> PROCEDURE rapport
  CLS
  PRINT "Ers H”ghet!"
  PRINT "Under †r ";z;" s† svalt"
  PRINT d;" medborgare ihj„l."
  PRINT " andra sidan s† flyttade"
  PRINT i;" personer hit."
  p=p+i
  IF q>0
  ELSE
    p=INT(p/2)
    PRINT "Pesten „r ”ver oss"
    PRINT "H„lften av medborgarna"
    PRINT "finns ej mer."
  ENDIF
  PRINT
  PRINT "         Befolkning: ";p;" Medborgare"
  PRINT "          Ytstorlek: ";a;" Hektar"
  PRINT "              Sk”rd: ";y;" Ton/Hektar"
  PRINT "    R†ttorna †t upp: ";e;" Ton"
  PRINT "     Kvar i Ladorna: ";s;" Ton"
  IF z=11
    @avslutning
  ENDIF
  y=RANDOM(10)+17
  PRINT "Ink”pspris f”r mark: ";y;" Ton"
  PRINT
RETURN
'
> PROCEDURE avslutning
  PRINT "Under din ";z-1;" †r l†nga „mbetsperiod"
  PRINT "svalt ";INT(p1+0.5);"% av medborgarna ihj„l."
  PRINT "D.v.s. ";d1;" personer dog."
  l=a/p
  PRINT "Ditt rike „gde fr†n b”rjan"
  PRINT "10 hektar/person och nu har riket"
  PRINT INT(l+0.5);" Hektar per person."
  IF p1>33 OR l<7
    PRINT "Oj oj oj. Det var inte bra."
    @slut
  ENDIF
  IF p1>10 OR l<10
    PRINT "Ptja! Inte s† illa"
    @slut
  ENDIF
  PRINT "Mycket bra gjort"
  @slut
RETURN
'
> PROCEDURE slut
  VOID INP(2)
  CLS
  PRINT "En g†ng till? J/N"
  DO
    i$=UPPER$(INKEY$)
    IF i$="J"
      RUN
    ELSE
      IF i$="N"
        EDIT
      ENDIF
    ENDIF
  LOOP
RETURN
'
> PROCEDURE init
  d1=0
  p1=0
  z=0
  p=95
  s=2800
  h=3000
  e=h-s
  y=3
  a=h/y
  i=5
  q=1
  d=0
RETURN
'
> PROCEDURE intro
  CLS
  RESTORE intro
  REPEAT
    READ i$
    PRINT i$
  UNTIL i$=" "
  VOID INP(2)
RETURN
'
> PROCEDURE data
intro:
  DATA "V„lkommen till HAMMURABI"
  DATA "ett klassiskt spel till Atari ST"
  DATA ""
  DATA "F”rs”k att regera det forntida Sumerien"
  DATA "under en tio-†rsperiod."
  DATA ""
  DATA "Detta „r en utveckling av ett spel"
  DATA "som har f„rdats fr†n Focals och Dec via"
  DATA "Creative Computing och David Ahl till"
  DATA "Atari ST och Clas Kristiansson"
  DATA ""
  DATA "F”rmodligen g†r det att spela i alla"
  DATA "uppl”sningar men det"
  DATA "„r skrivet p† en monokrom 1040STF"
  DATA "F”rb„ttringar (snygg grafik, ljud etc.)"
  DATA "mottages tacksamt och kan skickas till"
  DATA ""
  DATA "Svenska Hemdator Nytt"
  DATA "att: Clas Kristiansson"
  DATA "Box 152
  DATA "448 01 Floda"
  DATA ""
  DATA "(Tryck valfri Tangent)"
  DATA " "
RETURN
