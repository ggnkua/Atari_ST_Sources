' ------------------------------------------------------------------------------
' - 'Fliegende Dialoge' fÅr GFA-BASIC 3.x                                      -
' -                                                                            -
' - von Gregor Duchalski, Baueracker 15a, D-44627 Herne                        -
' - eMail-Kontakt: Gregor_Duchalski@do.maus.ruhr.de                            -
' -                                                                            -
' - Bibliothek fÅr zusÑtzliche Routinen, Beschreibung in EXTENDED.TXT          -
' - Nur fÅr registrierte Benutzer!                                             -
' - Last Change 30.07.94                                                       -
' ------------------------------------------------------------------------------
' ZusÑtzliche Tastaturkommandos...
' Einfach FORM_KEYBD in 'rsc_do' durch @form_keybd ersetzen.
'
> FUNCTION form_keybd(rsc_adr%,edit_obj&,key&,a&,VAR next_obj&,key_back&,idx&)
$F%
LOCAL scan|,cont&
'
' Ersetzt FORM_KEYBD()...
'
key_back&=0
cont&=1
'
SELECT key&
  '
  ' ------------------------------------------------- SHIFT right
CASE &H4D36
  IF edit_obj&
    ~OBJC_EDIT(rsc_adr%,edit_obj&,0,idx&,3,idx&)
    ~OBJC_EDIT(rsc_adr%,edit_obj&,0,idx&,1,idx&)
  ENDIF
  ' ------------------------------------------------- SHIFT left
CASE &H4B34
  '
  IF edit_obj&
    ~OBJC_EDIT(rsc_adr%,edit_obj&,0,idx&,3,idx&)
    '
    a&=BYTE{{OB_SPEC(rsc_adr%,edit_obj&)}}
    BYTE{{OB_SPEC(rsc_adr%,edit_obj&)}}=0
    '
    ~OBJC_EDIT(rsc_adr%,edit_obj&,0,idx&,1,idx&)
    BYTE{{OB_SPEC(rsc_adr%,edit_obj&)}}=a&
  ENDIF
  '
  ' ------------------------------------------------- Cursor runter
CASE &H5000
  next_obj&=@form_search_flag(rsc_adr%,edit_obj&,3,1)
  ' ------------------------------------------------- Cursor rauf
CASE &H4800
  next_obj&=@form_search_flag(rsc_adr%,edit_obj&,3,-1)
  ' ------------------------------------------------- SHIFT runter/HOME
CASE &H5032,&H4737                                  ! =>letztes Feld
  REPEAT
    next_obj&=edit_obj&
    edit_obj&=@form_search_flag(rsc_adr%,edit_obj&,3,1)
  UNTIL edit_obj&=0
  ' ------------------------------------------------- SHIFT rauf, HOME
CASE &H4838,&H4700                                  ! =>erstes Feld
  next_obj&=@form_search_flag(rsc_adr%,0,3,1)
  ' ------------------------------------------------- HELP/UNDO
CASE &H6200,&H6100
  key_back&=key&
  ' ------------------------------------------------- Alles andere
DEFAULT
  scan|=BYTE(SHR(key&,8))                           ! SCAN-Code
  '
  IF BYTE(key&)>0 OR scan|=75 OR scan|=77           ! Cursor rauf/runter
    cont&=FORM_KEYBD(rsc_adr%,edit_obj&,key&,0,next_obj&,key&)
    '                                               ! Kein Ende&TAB u. EDITABLE
    IF cont&>0 AND BYTE(key&)<>9 AND BTST(OB_FLAGS(rsc_adr%,edit_obj&),3)
      '
      scan|=BYTE{{OB_SPEC(rsc_adr%,edit_obj&)+8}+idx&}! Maske 'A' oder 'N'...
      IF scan|=65 OR (scan|=78 AND (BYTE(key&)<48 OR BYTE(key&)>57))
        key&=BCLR(key&,5)                             ! ...Groûschrift setzen
      ENDIF
      '
      ~OBJC_EDIT(rsc_adr%,edit_obj&,key&,idx&,2,idx&) ! ...Eingabe
    ENDIF
    '
  ELSE
    key_back&=key&
  ENDIF
  '
ENDSELECT
'
RETURN cont&
ENDFUNC
> FUNCTION form_search_flag(rsc_adr%,obj&,flag&,step&)
$F%
'
' Unterroutine fÅr form_keybd. Sucht das nÑchste/vorhergehende Objekt mit
' dem gesetzten Bit 'flag&' in OB_FLAGS...
'
' Sonderfall: Letztes Objekt ist edierbar, Cursor runter:
IF NOT (BTST(OB_FLAGS(rsc_adr%,obj&),5)=TRUE AND step&>0)
'
REPEAT
  ADD obj&,step&
  '
  IF BTST(OB_FLAGS(rsc_adr%,obj&),flag&) AND (NOT BTST(OB_STATE(rsc_adr%,obj&),3))
    RETURN obj&
  ENDIF
UNTIL (obj&<=0) OR BTST(OB_FLAGS(rsc_adr%,obj&),5)
'
ENDIF
'
RETURN 0
ENDFUNC
' ------------------------------------------------------------------------------
' SOLID Flydials...
' Einfach die Prozedur 'rsc_movedial' ersetzen.
'
> PROCEDURE rsc_movedial(tree&,edit_obj&,idx&)
LOCAL x&,y&,w&,h&,fx&,fy&,mx&,my&,mb&,shift&,rsc%
'
w&=rscw&(tree&)
h&=rsch&(tree&)
'
~GRAF_MKSTATE(mx&,my&,x&,y&)
' Nicht zusÑtzl. rechte Maustaste oder SHIFT/CONTROL/ALT...
IF NOT (x&=3 OR AND(y&,&X1111)>0)
rsc%=MALLOC(@getsize(w&,h&))                    ! Dialog retten
ENDIF
IF rsc%                                           ! Hat geklappt...
rsc_bitblt(0,0,0,rsc%,w&,h&,rscx&(tree&),rscy&(tree&),w&,h&,0,0)
ENDIF
'
~GRAF_MOUSE(4,0)                                  ! FLAT_HAND
'
~GRAF_MKSTATE(mx&,my&,mb&,shift&)
fx&=SUB(mx&,SUCC(rscx&(tree&)))                   ! Relativer Mausfaktor
fy&=SUB(my&,SUCC(rscy&(tree&)))
'
mx&=rscx&(tree&)
my&=rscy&(tree&)
' ------------------------------------------------- Bewegen
IF rsc%                                           ! SOLID...
REPEAT
  '
  ~GRAF_MKSTATE(x&,y&,mb&,shift&)
  '
  x&=SUB(PRED(x&),fx&)
  y&=SUB(PRED(y&),fy&)                          ! Clipping...
  x&=MIN(MAX(x&,deskx&),SUB(ADD(deskx&,deskw&),w&))
  y&=MIN(MAX(y&,desky&),SUB(ADD(desky&,deskh&),h&))
  '
  IF mx&<>x& OR my&<>y&
    rsc_flipping(rsc_handle%(tree&),rsc%,mx&,my&,w&,h&,x&,y&)
    mx&=x&
    my&=y&
    '
  ENDIF
  '
UNTIL mb&=0
'
~MFREE(rsc%)
'
ELSE                                              ! Nicht SOLID...
rsc_bitblt(rsc_handle%(tree&),rscw&(tree&),rsch&(tree&),0,0,0,0,0,rscw&(tree&),rsch&(tree&),rscx&(tree&),rscy&(tree&))
~GRAF_DRAGBOX(rscw&(tree&),rsch&(tree&),rscx&(tree&),rscy&(tree&),deskx&,desky&,deskw&,deskh&,x&,y&)
ENDIF
' ------------------------------------------------- Hinsetzen
rscx&(tree&)=x&
rscy&(tree&)=y&
OB_X(rsc_adr%(tree&),0)=ADD(x&,3)
OB_Y(rsc_adr%(tree&),0)=ADD(y&,3)
'
IF rsc%=0                                         ! Nicht SOLID...
rsc_bitblt(0,0,0,rsc_handle%(tree&),rscw&(tree&),rsch&(tree&),rscx&(tree&),rscy&(tree&),rscw&(tree&),rsch&(tree&),0,0)
~OBJC_DRAW(rsc_adr%(tree&),0,10,rscx&(tree&),rscy&(tree&),rscw&(tree&),rsch&(tree&))
'
IF edit_obj&
  rsc_cursor(rsc_adr%(tree&),edit_obj&,idx&)    ! ...Cursor ein
ENDIF
ENDIF
'
~GRAF_MOUSE(0,0)
'
RETURN
> PROCEDURE rsc_flipping(rsc_back%,rsc%,ax&,ay&,w&,h&,x&,y&)
LOCAL rb&,rh&,gw&,gh&
'
gw&=ABS(SUB(x&,ax&))         ! Verschiebbreite
gh&=ABS(SUB(y&,ay&))         ! Verschiebhîhe
rb&=SUB(w&,gw&)              ! Restbreite
rh&=SUB(h&,gh&)              ! Resthîhe
'
IF rb&<0 OR rh&<0            ! rsc% auûerhalb von rsc_back%
'
' Gesamten Hintergrund restaurieren -----
rsc_bitblt(rsc_back%,w&,h&,0,0,0,0,0,w&,h&,ax&,ay&)
'
' Gesamten Dialog aktualisieren -----
rsc_bitblt(0,0,0,rsc_back%,w&,h&,x&,y&,w&,h&,0,0)
'
ELSE IF x&>ax&               ! nach rechts...
'
IF y&>ay&                  ! nach rechts-unten veschoben
  '
  ' Hintergrund restaurieren -----
  rsc_bitblt(rsc_back%,w&,h&,0,0,0,0,gh&,gw&,rh&,ax&,ADD(ay&,gh&))
  rsc_bitblt(rsc_back%,w&,h&,0,0,0,0,0,w&,gh&,ax&,ay&)
  '
  ' Dialog aktualisieren -----
  rsc_bitblt(rsc_back%,w&,h&,rsc_back%,w&,h&,gw&,gh&,rb&,rh&,0,0)
  rsc_bitblt(0,0,0,rsc_back%,w&,h&,x&,ADD(y&,rh&),rb&,gh&,0,rh&)
  rsc_bitblt(0,0,0,rsc_back%,w&,h&,ADD(x&,rb&),y&,gw&,h&,rb&,0)
  '
ELSE                      ! nach rechts-oben / rechts verschoben
  '
  ' Hintergrund restaurieren -----
  rsc_bitblt(rsc_back%,w&,h&,0,0,0,0,0,gw&,rh&,ax&,ay&)
  rsc_bitblt(rsc_back%,w&,h&,0,0,0,0,rh&,w&,gh&,ax&,ADD(ay&,rh&))
  '
  ' Dialog aktualisieren -----
  rsc_bitblt(rsc_back%,w&,h&,rsc_back%,w&,h&,gw&,0,rb&,rh&,0,gh&)
  rsc_bitblt(0,0,0,rsc_back%,w&,h&,x&,y&,rb&,gh&,0,0)
  rsc_bitblt(0,0,0,rsc_back%,w&,h&,ADD(x&,rb&),y&,gw&,h&,rb&,0)
  '
ENDIF
'                         ! nach links / oben / unten...
ELSE IF y&>ay&              ! nach links-unten / unten verschoben
'
' Hintergrund restaurieren -----
rsc_bitblt(rsc_back%,w&,h&,0,0,0,0,0,rb&,gh&,ax&,ay&)
rsc_bitblt(rsc_back%,w&,h&,0,0,0,rb&,0,gw&,h&,ADD(ax&,rb&),ay&)
'
' Dialog aktualisieren -----
rsc_bitblt(rsc_back%,w&,h&,rsc_back%,w&,h&,0,gh&,rb&,rh&,gw&,0)
rsc_bitblt(0,0,0,rsc_back%,w&,h&,x&,y&,gw&,rh&,0,0)
rsc_bitblt(0,0,0,rsc_back%,w&,h&,x&,ADD(y&,rh&),w&,gh&,0,rh&)
'
ELSE                        ! nach links-oben / links / oben verschoben
'
' Hintergrund restaurieren -----
rsc_bitblt(rsc_back%,w&,h&,0,0,0,rb&,0,gw&,rh&,ADD(ax&,rb&),ay&)
rsc_bitblt(rsc_back%,w&,h&,0,0,0,0,rh&,w&,gh&,ax&,ADD(ay&,rh&))
'
' Dialog aktualisieren -----
rsc_bitblt(rsc_back%,w&,h&,rsc_back%,w&,h&,0,0,rb&,rh&,gw&,gh&)
rsc_bitblt(0,0,0,rsc_back%,w&,h&,x&,y&,w&,gh&,0,0)
rsc_bitblt(0,0,0,rsc_back%,w&,h&,x&,ADD(y&,gh&),gw&,rh&,0,gh&)
'
ENDIF
'
rsc_bitblt(rsc%,w&,h&,0,0,0,0,0,w&,h&,x&,y&)
'
RETURN
' ------------------------------------------------------------------------------
' Zeichengenaues Cursor-Positionieren mit der Maus...
' Nur die entsprechenden 2 Zeilen in 'rsc_do' (ziemlich am Ende) entREMen.
'
> PROCEDURE rsc_set_cursor(rsc_adr%,mx&,my&,VAR edit_obj&,next_obj&,idx&)
LOCAL x&,a&,i&,ob_spec%,a$
'
ob_spec%=OB_SPEC(rsc_adr%,next_obj&)
'
~OBJC_OFFSET(rsc_adr%,next_obj&,x&,a&)          ! Absolute X-Position
'
x&=DIV(SUB(mx&+SHR(wchar&,2),x&),wchar&)        ! Spaltenposition
'
a$=CHAR{{ADD(ob_spec%,4)}}                      ! Maske lesen
'
FOR a&=1 TO x&                                  ! Pos. im Eingabefeld...
IF MID$(a$,a&,1)="_"
  INC i&                                      ! ...nur relevante
ENDIF
NEXT a&
SUB x&,(x&-i&)
'
~OBJC_EDIT(rsc_adr%,edit_obj&,0,idx&,3,idx&)    ! Cursor aus
'
ob_spec%=ADD({ob_spec%},x&)
'
a&=BYTE{ob_spec%}                               ! Zeichen merken
BYTE{ob_spec%}=0                                ! ...und durch 0 ersetzen
'
~OBJC_EDIT(rsc_adr%,next_obj&,0,idx&,1,idx&)    ! Cursor setzen
BYTE{ob_spec%}=a&                               ! ...und Zeichen restaurieren
'
edit_obj&=next_obj&
'
RETURN
' ------------------------------------------------------------------------------
' Setzen und Auslesen der Popup-MenÅeintrÑge...
'
> PROCEDURE rsc_write_popup(tree&,button&,default&)
LOCAL i&,popup&,obj&
'
' Belegt einen Popup-Button mit dem Text des Popup-Eintrags
' 'default&' (also von 1-x)...
'
' tree&   : Index des Objektbaumes
' button& : Objektnummer des Buttons
' default&: Nummer des zu setzenden MenÅ-Eintrages (1-x)
'
popup&=SUB(SHR&(OB_TYPE(rsc_adr%(tree&),button&),8),30)! Nr. des Popup-Baumes
obj&=1
'
FOR i&=1 TO PRED(popup&)
obj&=OB_NEXT(rsc_adr%(rsc_popup&),obj&)              ! Objektnr. des Popups
NEXT i&
'                                                      ! Button belegen
rsc_text(tree&,button&,@rsc_text$(rsc_popup&,ADD(obj&,default&)))
RETURN
> FUNCTION rsc_read_popup(tree&,button&)
$F%
LOCAL a&,i&,popup&,obj&,a$
'
' Gibt den Index des gesetzten Popup-Eintrags zurÅck...
'
' tree&   : Index des Objektbaumes
' button& : Objektnummer des Buttons
'
popup&=SHR&(OB_TYPE(rsc_adr%(tree&),button&),8)-30     ! Nr. des Popup-Baumes
obj&=1
'
FOR i&=1 TO PRED(popup&)
obj&=OB_NEXT(rsc_adr%(rsc_popup&),obj&)              ! Objektnr. des Popups
NEXT i&
'                                                      ! Button belegen
a$=@rsc_text$(tree&,button&)
'
a&=OB_TAIL(rsc_adr%(rsc_popup&),obj&)
DO WHILE a$<>@rsc_text$(rsc_popup&,a&)
DEC a&
LOOP UNTIL a&<=obj&
'
RETURN SUB(a&,obj&)
ENDFUNC
' ------------------------------------------------------------------------------
' Setzt ein Popup auf 'anz&' EintrÑge...
'
> PROCEDURE rsc_set_popup(popup&,anz&)
LOCAL a&,obj&
'
' popup&: Objektnummer des Popup-MenÅs
' anz&  : Neue Anzahl EintrÑge
'
' ----------------------------------------- Alle EintrÑge aktivieren...
obj&=OB_TAIL(rsc_adr%(rsc_popup&),popup&) ! Letzter Eintrag des Popup's
'
a&=OB_NEXT(rsc_adr%(rsc_popup&),obj&)     ! Darauf folgender
'
WHILE a&=popup&                           ! Zeigt auf's Parent-Objekt...
INC obj&                                ! ...nÑchster Eintrag
'
a&=OB_NEXT(rsc_adr%(rsc_popup&),obj&)
IF a&>0 AND a&<=SUCC(obj&)              ! Existiert...
~OBJC_ADD(rsc_adr%(rsc_popup&),popup&,obj&)
ENDIF                                   ! ...wieder aktivieren
'
WEND
'
' ----------------------------------------- EintrÑge entfernen...
a&=OB_TAIL(rsc_adr%(rsc_popup&),popup&)   ! Letzter Eintrag des Popup's
'
FOR obj&=a& DOWNTO ADD(popup&,SUCC(anz&)) ! Von hinten nach vorne...
~OBJC_DELETE(rsc_adr%(rsc_popup&),obj&) ! ...entfernen
NEXT obj&
'
' ----------------------------------------- Neue Hîhe setzen
OB_H(rsc_adr%(rsc_popup&),popup&)=MUL(hchar&,anz&)
'
RETURN
' ------------------------------------------------------------------------------
' MenÅshortcut ermitteln...
'
> FUNCTION rsc_shortcut(tree&,scan|,shift&,VAR title&)
$F%
LOCAL a&,obj&,rsc_adr%,s$,t$
'
' tree&      : Index des MenÅbaumes
' scan|      : SCAN-Code der gedrÅckten Taste
' shift&     : Status der tastaturumschalttasten
' VAR title& : Bei Erfolg Objektnummer des MenÅtitels
'
SELECT scan|
CASE 2 TO 10                                   ! 1-2...
s$=CHR$(ADD(47,scan|))
CASE 15                                        ! TAB...
s$="I"
shift&=BSET(shift&,2)                        ! ...entspricht "^I"
'
CASE 59 TO 68,84 TO 93                         ! Ftasten...
IF scan|<84
s$="F"+STR$(SUB(scan|,58))                 ! ...normal
ELSE
s$="F"+STR$(SUB(scan|,83))                 ! ...mit Shift
ENDIF
CASE 71                                        ! ClrHome...
s$="CLR"
CASE 83                                        ! DELETE...
s$="DEL"
CASE 97                                        ! UNDO...
s$="UNDO"
CASE 98                                        ! HELP...
s$="HELP"
DEFAULT                                        ! Alles andere...
rsc_adr%={XBIOS(16,L:-1,L:-1,L:-1)}
s$=CHR$(BCLR(BYTE{ADD(rsc_adr%,scan|)},5))
ENDSELECT
'
IF BTST(shift&,3)                              ! Alternate...
s$=CHR$(7)+s$
ENDIF
IF BTST(shift&,2)                              ! Control...
s$="^"+s$
ENDIF
IF BTST(shift&,0) OR BTST(shift&,1)            ! Shift...
s$=CHR$(1)+s$
ENDIF
'
title&=2                                       ! Position des Titels
obj&=5                                         ! Beginn der eigentl. MenÅs
rsc_adr%=rsc_adr%(tree&)                       ! öbersichtlicher
'
REPEAT                                         ! alle Objekte d. MenÅbaums
INC obj&
'
IF BYTE(OB_TYPE(rsc_adr%,obj&))=28           ! G_STRING...
IF NOT BTST(OB_STATE(rsc_adr%,obj&),3)     ! disabled ?
'
t$=TRIM$(CHAR{OB_SPEC(rsc_adr%,obj&)})   ! MenÅpunkt-Text
'
a&=RINSTR(t$," ")
IF a&                                    ! Shortcut vorhanden ?
  '
  IF MID$(t$,SUCC(a&))=s$                ! hier der vermutete Shortcut
    RETURN obj&                          ! gefunden
  ENDIF
  '
ENDIF
ENDIF
'
ELSE IF BYTE(OB_TYPE(rsc_adr%,obj&))=20      ! G_STRING ?
INC title&
ENDIF
UNTIL BTST(OB_FLAGS(rsc_adr%,obj&),5)          ! LAST_OB ?
'
title&=0
RETURN 0                                       ! nicht gef. oder disabled
ENDFUNC
' ------------------------------------------------------------------------------
' Fliegende ALERT-Box...
'
> FUNCTION rsc_alert(icon&,text$,button$,def&)
$F%
LOCAL a&,b&,h&,i&,l&,len&,w&,x&,y&,z&,ab&,ah&,rsc&,rsc_adr%,a$
'
' Alert-Box darstellen...
'
IF rsc_alert&>-1                                       ! Wenn's den Baum gibt:
rsc_adr%=rsc_adr%(rsc_alert&)                        ! rsc_alert=GLOBAL
'
text$=text$+"|"
button$=button$+"|"
' ---------------------------------------------------- Icon bestimmen
OB_FLAGS(rsc_adr%,2)=BSET(OB_FLAGS(rsc_adr%,2),7)    ! Hidden...
OB_FLAGS(rsc_adr%,3)=BSET(OB_FLAGS(rsc_adr%,3),7)
OB_FLAGS(rsc_adr%,4)=BSET(OB_FLAGS(rsc_adr%,4),7)
'
OB_FLAGS(rsc_adr%,SUCC(icon&))=BCLR(OB_FLAGS(rsc_adr%,SUCC(icon&)),7)
' ---------------------------------------------------- Buttons
OB_FLAGS(rsc_adr%,10)=OR(&X101,AND(&X1111111100000000,OB_FLAGS(rsc_adr%,10)))    ! EXIT+SELECTABLE
OB_FLAGS(rsc_adr%,11)=OR(&X101,AND(&X1111111100000000,OB_FLAGS(rsc_adr%,11)))    ! EXIT+SELECTABLE
OB_FLAGS(rsc_adr%,12)=OR(&X100101,AND(&X1111111100000000,OB_FLAGS(rsc_adr%,12))) ! LAST_OB+EXIT+SELECT.
'                                                    ! Default-Button...
OB_FLAGS(rsc_adr%,9+def&)=BSET(OB_FLAGS(rsc_adr%,9+def&),1)
' ---------------------------------------------------- Textzeilen setzen
FOR i&=1 TO 5
a&=INSTR(text$,"|")
a$=LEFT$(text$,MIN(30,MAX(0,PRED(a&))))
text$=MID$(text$,SUCC(a&))
'
z&=MAX(z&,-i&*(a$<>""))                            ! Hîchste belegte Zeile
l&=MAX(l&,LEN(a$))                                 ! LÑngste Zeile
'
rsc_text(rsc_alert&,ADD(4,i&),a$)                  ! Setzen
NEXT i&
' ---------------------------------------------------- Button-Texte setzen
FOR i&=1 TO 3
a&=INSTR(button$,"|")
a$=LEFT$(button$,MIN(10,MAX(0,PRED(a&))))
button$=MID$(button$,SUCC(a&))
'
ADD b&,-(a$<>"")                                   ! Anzahl Buttons
len&=MAX(len&,LEN(a$))                             ! LÑngster Button
'
rsc_text(rsc_alert&,ADD(9,i&),a$)                  ! Setzen
NEXT i&
' ---------------------------------------------------- Dialog-Breite Ñndern
l&=MAX(l&,MUL(10,b&))                                ! Minimal-Breite
'
w&=OB_W(rsc_adr%,3)+MUL(wchar&,ADD(l&,5))
OB_W(rsc_adr%,0)=w&                                  ! Neue Breite
'
OB_X(rsc_adr%,1)=w&-PRED(OB_W(rsc_adr%,1))           ! Flydial verschieben
rscw&(rsc_alert&)=ADD(w&,6)
' ---------------------------------------------------- Dialog-Hîhe Ñndern
h&=OB_H(rsc_adr%,10)+MUL(hchar&,ADD(z&,2))
OB_H(rsc_adr%,0)=h&                                  ! Neue Hîhe
rsch&(rsc_alert&)=ADD(h&,6)
' ---------------------------------------------------- Buttons plazieren
len&=MUL(wchar&,MAX(8,SUCC(len&)))
OB_W(rsc_adr%,10)=len&                               ! Breite anpassen..
OB_W(rsc_adr%,11)=len&
OB_W(rsc_adr%,12)=len&
'
SELECT b&                                            ! Anzahl Buttons...
CASE 1
OB_FLAGS(rsc_adr%,11)=BSET(OB_FLAGS(rsc_adr%,11),7)! ...hidden
OB_FLAGS(rsc_adr%,12)=BSET(OB_FLAGS(rsc_adr%,12),7)! ...hidden
' OB_X(rsc_adr%,10)=SHR(SUB(w&,OB_W(rsc_adr%,10)),1)
OB_X(rsc_adr%,10)=SUB(w&,OB_W(rsc_adr%,10))-2*wchar&
'
CASE 2
OB_FLAGS(rsc_adr%,12)=BSET(OB_FLAGS(rsc_adr%,12),7)! ...hidden
x&=w&-OB_W(rsc_adr%,11)-2*wchar&
OB_X(rsc_adr%,11)=x&
OB_X(rsc_adr%,10)=x&-OB_W(rsc_adr%,10)-wchar&
'
CASE 3
x&=w&-OB_W(rsc_adr%,12)-SHL(wchar&,1)              ! ...Mitte
OB_X(rsc_adr%,12)=x&
OB_X(rsc_adr%,11)=x&-OB_W(rsc_adr%,10)-wchar&
OB_X(rsc_adr%,10)=OB_X(rsc_adr%,11)-OB_W(rsc_adr%,10)-wchar&
'
ENDSELECT
'
y&=h&-OB_H(rsc_adr%,10)-SHR(hchar&,1)
OB_Y(rsc_adr%,10)=y&
OB_Y(rsc_adr%,11)=y&
OB_Y(rsc_adr%,12)=y&
' ---------------------------------------------------- Anzeigen
~FORM_CENTER(rsc_adr%(rsc_alert&),rscx&(rsc_alert&),rscy&(rsc_alert&),rscw&(rsc_alert&),rsch&(rsc_alert&))
'
rsc_draw(rsc_alert&,FALSE)
'
rsc&=@rsc_do(rsc_alert&,0,a&)
'
rsc_back(rsc_alert&)
' ----------------------------------------------------
OB_STATE(rsc_adr%,rsc&)=BCLR(OB_STATE(rsc_adr%,rsc&),0)! SELECTED lîschen
'
RETURN SUB(rsc&,9)                                   ! Nummer des Buttons 1-3
ENDIF
'                                                      ! Normaler Alert...
WHILE INSTR(button$,"[")>0
button$=LEFT$(button$,PRED(INSTR(button$,"[")))+MID$(button$,SUCC(INSTR(button$,"[")))
WEND                                                   ! ...'[' rausfiltern
'
RETURN FORM_ALERT(def&,"["+STR$(icon&)+"]["+text$+"]["+button$+"]")
ENDFUNC
' ------------------------------------------------------------------------------
' Ersatz fÅr RSRC_LOAD(file$). Die RSC-Datei kann in einem INLINE direkt im
' Programmcode abgelegt werden, diese Funktion bekommt die Adresse des INLINES
' Åbergeben und initialisiert die Resource...
'
> FUNCTION rsrc_conv(r_ra%)
$F%
'     r_ra%                            ! RSC INLINE Adresse
LOCAL r_buf!                           ! TRUE=RSC Daten puffern
LOCAL r_pa%                            ! Puffer-Adresse
LOCAL r_o&,r_t&                        ! Laufvar
LOCAL r_ob&                            ! Beginn des Objekt Feldes
LOCAL r_no&                            ! Anzahl OBJECTS
LOCAL r_nt&                            ! Anzahl TREES
LOCAL r_rs&                            ! LÑnge des INLINEs
LOCAL r_napt%                          ! neue TREE Table Adresse
LOCAL r_gb%                            ! Adresse des AES Global Feldes
LOCAL r_of%                            ! Objekt Feld
LOCAL r_ns&                            ! Anzahl der freien Strings
LOCAL r_ni&                            ! Anzahl der freien Images
LOCAL r_adr%,r_i&                      ! Hilfsvar, Laufvar
'
r_buf!=(BYTE{ADD(BASEPAGE,256)}=96)    ! Im Interpreter? ==>puffern
'
r_rs&=WORD{ADD(r_ra%,&H22)}            ! LÑnge des INLINEs
IF r_buf! AND DIM?(rsc_buf|())>0       ! Ist RSC Buf schon dimensioniert?...
r_rs&=0
ENDIF
'
IF r_rs&>0
'
IF r_buf!                            ! RSC Daten puffern (nur Interpreter)...
DIM rsc_buf|(r_rs&)
r_pa%=ADD({*rsc_buf|()},4)
BMOVE r_ra%,r_pa%,r_rs&
r_ra%=r_pa%
ENDIF
'
r_napt%=ADD(r_ra%,WORD{ADD(r_ra%,&H12)})       ! neue Adresse der Tree Tabelle
r_gb%={ADD(GB,4)}                    ! Adr. des AES-GLOBAL-Feldes
{ADD(r_gb%,10)}=r_napt%              ! GLOBAL(5/6): neue Tabellen Adr.
r_ob&=WORD{ADD(r_ra%,2)}             ! bisheriger Start Objekt Feld
r_of%=ADD(r_ra%,r_ob&)               ! Neue Startadr. Objekt Feld
r_no&=PRED(WORD{ADD(r_ra%,&H14)})    ! Anzahl der Objekte im File -1
r_nt&=PRED(WORD{ADD(r_ra%,&H16)})    ! Anzahl der Trees in File  -1
'
FOR r_o&=0 TO r_no&                  ! Alle Objekte durchgehen...
~RSRC_OBFIX(r_of%,r_o&)            ! ...Koordinaten umrechnen
'
SELECT BYTE(OB_TYPE(r_of%,r_o&))   ! Objekt-Typ...
'
CASE 21,22,29,30,31                ! ...G_TEXT,G_BOXTEXT,G_FTEXT,G_FBOXTEXT,G_ICON
OB_SPEC(r_of%,r_o&)=ADD(OB_SPEC(r_of%,r_o&),r_ra%)
{OB_SPEC(r_of%,r_o&)}=ADD({OB_SPEC(r_of%,r_o&)},r_ra%)
{ADD(OB_SPEC(r_of%,r_o&),4)}=ADD({ADD(OB_SPEC(r_of%,r_o&),4)},r_ra%)
{ADD(OB_SPEC(r_of%,r_o&),8)}=ADD({ADD(OB_SPEC(r_of%,r_o&),8)},r_ra%)
'
CASE 24                            ! ...G_USERDEF
OB_SPEC(r_of%,r_o&)=ADD(OB_SPEC(r_of%,r_o&),r_ra%)
{OB_SPEC(r_of%,r_o&)}=ADD({OB_SPEC(r_of%,r_o&)},r_ra%)
{ADD(OB_SPEC(r_of%,r_o&),4)}=ADD({ADD(OB_SPEC(r_of%,r_o&),4)},r_ra%)
'
CASE 23                            ! ...G_IMAGE
OB_SPEC(r_of%,r_o&)=ADD(OB_SPEC(r_of%,r_o&),r_ra%)
{OB_SPEC(r_of%,r_o&)}=ADD({OB_SPEC(r_of%,r_o&)},r_ra%)
'
CASE 26,28,32                      ! ...G_BUTTON,G_STRING,G_TITLE
OB_SPEC(r_of%,r_o&)=ADD(OB_SPEC(r_of%,r_o&),r_ra%)
ENDSELECT
'
NEXT r_o&
'
FOR r_t&=0 TO r_nt&
{ADD(r_napt%,MUL(4,r_t&))}=ADD({ADD(r_napt%,MUL(4,r_t&))},r_ra%)
NEXT r_t&
'
r_ns&=PRED(WORD{ADD(r_ra%,&H1E)})
IF r_ns&>-1
r_adr%=ADD(r_ra%,WORD{ADD(r_ra%,&HA)})
FOR r_i&=0 TO r_ns&
{ADD(r_adr%,MUL(4,r_i&))}=ADD({ADD(r_adr%,MUL(4,r_i&))},r_ra%)
NEXT r_i&
ENDIF
'
r_ni&=PRED(WORD{ADD(r_ra%,&H20)})
IF r_ni&>-1
r_adr%=ADD(r_ra%,WORD{ADD(r_ra%,&H10)})
'
FOR r_i&=0 TO r_ni&
{ADD(r_adr%,MUL(4,r_i&))}=ADD({ADD(r_adr%,MUL(4,r_i&))},r_ra%)
{{ADD(r_adr%,MUL(4,r_i&))}}=ADD({{ADD(r_adr%,MUL(4,r_i&))}},r_ra%)
NEXT r_i&
'
ENDIF
'
ENDIF
'
RETURN r_rs&
ENDFUNC
' ------------------------------------------------------------------------------
' Retten und Restaurieren eines Dialogs zum Aufruf einer Dateiauswahlbox...
'
> PROCEDURE rsc_save_dialog(tree&)
LOCAL x&,y&,w&,h&,i&,a%
'
' Rettet den(die) Dialog(e) z.B. fÅr den Aufruf eines Fileselectors...
'                                               ! Kein Fenster-Dialog...
IF NOT (rsc_handle%(tree&)>0 AND rsc_handle%(tree&)<1000)
'
x&=rscx&(tree&)
y&=rscy&(tree&)
FOR i&=1 TO SHR(LEN(rsc_list$),1)             ! Grîûten Dialog rausfinden...
tree&=CVI(MID$(rsc_list$,SUCC(SHL(PRED(i&),1))))
x&=MIN(x&,rscx&(tree&))
y&=MIN(y&,rscy&(tree&))
w&=MAX(w&,ADD(rscx&(tree&),rscw&(tree&)))
h&=MAX(h&,ADD(rscy&(tree&),rsch&(tree&)))
NEXT i&
SUB w&,x&
SUB h&,y&
'
a%=MALLOC(@getsize(w&,h&)+8)
IF a%                                         ! Hintergrund retten...
rsc_bitblt(0,0,0,ADD(a%,8),w&,h&,x&,y&,w&,h&,0,0)
'
INT{a%}=x&                                  ! Koordinaten eintragen...
INT{ADD(a%,2)}=y&
INT{ADD(a%,4)}=w&
INT{ADD(a%,6)}=h&
'
rsc_save%=a%                                ! In globaler Variablen merken...
'
GRAPHMODE 4                                 ! Dialog disablen...
DEFFILL 0,2,4
~GRAF_MOUSE(256,0)                          ! Hidem
'
PBOX SUCC(x&),SUCC(y&),ADD(PRED(x&),PRED(w&)),ADD(PRED(y&),PRED(h&))
'
~GRAF_MOUSE(257,0)                          ! Showm
DEFFILL 1,0
GRAPHMODE 1
'
ENDIF
'
ENDIF
'
RETURN
> PROCEDURE rsc_restore_dialog(tree&)
LOCAL w&,h&,i&
'
' Restauriert den(die) Dialog(e) z.B. nach einem Fileselector...
'                                               ! Kein Fenster-Dialog...
IF NOT (rsc_handle%(tree&)>0 AND rsc_handle%(tree&)<1000)
'
IF rsc_save%                                  ! Hintergrund wurde gerettet...
w&=INT{ADD(rsc_save%,4)}
h&=INT{ADD(rsc_save%,6)}
rsc_bitblt(ADD(rsc_save%,8),w&,h&,0,0,0,0,0,w&,h&,INT{rsc_save%},INT{ADD(rsc_save%,2)})
'
~MFREE(rsc_save%)                           ! ...freigeben
rsc_save%=0
'
ELSE                                          ! Nicht gerettet...
'
FOR i&=1 TO SHR(LEN(rsc_list$),1)           ! Dialog-Liste durchgehen...
tree&=CVI(MID$(rsc_list$,SUCC(SHL(PRED(i&),1))))
~OBJC_DRAW(rsc_adr%(tree&),0,100,rscx&(tree&),rscy&(tree&),rscw&(tree&),rsch&(tree&))
NEXT i&                                     ! ...und zeichnen
'
ENDIF
'
ENDIF
'
RETURN
' ------------------------------------------------------------------------------
' Setzen und Abfragen einzelner Bits in OB_STATE und OB_FLAGS...
'
> PROCEDURE rsc_state(tree&,obj&,bit&,stat!)
'
' tree& : Index des Objektbaumes
' obj&  : Objektnummer
' bit&  : Zu setzendes Bit
' stat! : TRUE =Bit setzen, FALSE =Bit lîschen
'
IF stat!
OB_STATE(rsc_adr%(tree&),obj&)=BSET(OB_STATE(rsc_adr%(tree&),obj&),bit&)
ELSE
OB_STATE(rsc_adr%(tree&),obj&)=BCLR(OB_STATE(rsc_adr%(tree&),obj&),bit&)
ENDIF
'
RETURN
> PROCEDURE rsc_flags(tree&,obj&,bit&,stat!)
'
' tree& : Index des Objektbaumes
' obj&  : Objektnummer
' bit&  : Zu setzendes Bit
' stat! : TRUE =Bit setzen, FALSE =Bit lîschen
'
IF stat!
OB_FLAGS(rsc_adr%(tree&),obj&)=BSET(OB_FLAGS(rsc_adr%(tree&),obj&),bit&)
ELSE
OB_FLAGS(rsc_adr%(tree&),obj&)=BCLR(OB_FLAGS(rsc_adr%(tree&),obj&),bit&)
ENDIF
'
RETURN
DEFFN rsc_flags(tree&,obj&,bit&)=BTST(OB_FLAGS(rsc_adr%(tree&),obj&),bit&)
DEFFN rsc_state(tree&,obj&,bit&)=BTST(OB_STATE(rsc_adr%(tree&),obj&),bit&)
' ------------------------------------------------------------------------------
' Behandlung eines Tristate-Buttons...
'
> PROCEDURE rsc_tristate(tree&,obj&)
'
IF @rsc_state(tree&,obj&,2)                ! War gefÅllt...
rsc_state(tree&,obj&,2,FALSE)
rsc_state(tree&,obj&,0,TRUE)             ! ...jetzt selektiert
'
ELSE IF NOT @rsc_state(tree&,obj&,0)       ! War nicht selektiert
rsc_state(tree&,obj&,2,TRUE)             ! ...jetzt gefÅllt
'
ELSE                                       ! War selektiert...
rsc_state(tree&,obj&,0,FALSE)            ! ...jetzt leer
ENDIF
rsc_draw_obj(tree&,obj&)                   ! Zeichnen
'
RETURN
' ------------------------------------------------------------------------------
' Zeichnen eines einzelnen Objektes...
'
> PROCEDURE rsc_draw_obj(tree&,obj&)
LOCAL x&,y&
'
~OBJC_OFFSET(rsc_adr%(tree&),obj&,x&,y&)
~OBJC_DRAW(rsc_adr%(tree&),obj&,8,x&,y&,OB_W(rsc_adr%(tree&),obj&),OB_H(rsc_adr%(tree&),obj&))
'
RETURN
' ------------------------------------------------------------------------------
' Globales Setzen und Abfragen mehrerer Radio-Buttons...
'
> PROCEDURE rsc_set_radio(tree&,anz&,select&,VAR obj&())
LOCAL i&
'
' tree&   : Index des Objektbaumes
' anz&    : Anzahl Objekte
' select& : Index des zu selektierenden Objektes (1-x)
' obj&()  : Feld mit den Objektnummern
'
FOR i&=0 TO anz&           ! Alle Buttons deselektieren...
OB_STATE(rsc_adr%(tree&),obj&(i&))=BCLR(OB_STATE(rsc_adr%(tree&),obj&(i&)),0)
NEXT i&
'                          ! Nur den gewÅnschten selektieren...
OB_STATE(rsc_adr%(tree&),obj&(select&))=BSET(OB_STATE(rsc_adr%(tree&),obj&(select&)),0)
'
RETURN
> FUNCTION rsc_get_radio(tree&,anz&,obj&,VAR obj&())
$F%
LOCAL i&
'
IF obj&>0                  ! Objekt suchen...
'
FOR i&=0 TO anz&
IF obj&(i&)=obj&
RETURN i&            ! ...der gewÅnschte Button ist selektiert
ENDIF
NEXT i&
'
ELSE                       ! Radio-Button suchen...
FOR i&=0 TO anz&
IF BTST(OB_STATE(rsc_adr%(tree&),obj&(i&)),0)
RETURN i&            ! ...selektierter zurÅck
ENDIF
NEXT i&
'
ENDIF
'
RETURN -1                  ! Kein Button selektiert
ENDFUNC
'
> FUNCTION rsc_get_radio2(tree&,anz&,rsc&,obj&)
$F%
LOCAL i&
'
' obj& = Erstes Objekt
' --------------------------------------------------
IF rsc&>0                                          ! Objekt suchen...
'
FOR i&=0 TO anz&
IF obj&+i&=rsc&
RETURN i&
ENDIF
NEXT i&
'
ELSE                                               ! Radio-Button suchen...
FOR i&=0 TO anz&
IF @rsc_state(tree&,obj&+i&,0)
RETURN i&
ENDIF
NEXT i&
'
ENDIF
'
RETURN -1
ENDFUNC
' ------------------------------------------------------------------------------
' Gibt die Anzahl der MaustastendrÅcke (1 oder 2) zurÅck, mit denen ein Dialog
' verlassen wurde und maskiert das Bit 15 wieder aus...
'
> FUNCTION rsc_clicks(VAR rsc&)
$F%
LOCAL mc&
'
mc&=BTST(rsc&,15)                                  ! Doppelklick?
IF mc&
rsc&=AND(rsc&,&X111111111111111)                 ! Bit 15 ausmaskieren
ENDIF
'
RETURN 1-mc&
ENDFUNC
' ------------------------------------------------------------------------------
' GET/PUT-Ersatz auf Basis des im INLINE integrierten BITBLT
'
> FUNCTION rsc_get(x&,y&,w&,h&)
LOCAL a%
'
a%=ADD(@getsize(w&,h&),4)          ! Speicherbedarf
a%=MALLOC(a%)                      ! Speicher anfordern
'
IF a%                              ! Hat geklappt...
INT{a%}=w&                       ! Breite eintragen
INT{ADD(a%,2)}=h&                ! Hîhe
'                                ! Kopieren...
~C:rsc_bitblt%(L:0,0,0,0,planes&,L:ADD(a%,4),w&,h&,0,planes&,x&,y&,w&,h&,0,0,1)
ENDIF
'
RETURN a%                          ! Adresse zurÅck
ENDFUNC
> PROCEDURE rsc_put(a%,x&,y&,m&)
LOCAL w&,h&
'
IF a%                              ! Gibt's den Block Åberhaupt...
w&=INT{a%}                       ! Breite holen
h&=INT{ADD(a%,2)}                ! Hîhe
'
~GRAF_MOUSE(256,0)               ! Hidem
~C:rsc_bitblt%(L:ADD(a%,4),w&,h&,0,planes&,L:0,0,0,0,planes&,0,0,w&,h&,x&,y&,m&)
~GRAF_MOUSE(257,0)               ! Showm
'
ENDIF
'
RETURN
' ------------------------------------------------------------------------------
' Sauberer Redraw eines Objektes, nur fÅr Fensterdialoge!...
'
> PROCEDURE rsc_redraw_obj(tree&,obj&)
LOCAL x&,y&,w&,h&
'
' Sendet einen Redraw-Event fÅr ein Objekt
' --------------------------------------------------
~OBJC_OFFSET(rsc_adr%(tree&),obj&,x&,y&)
w&=OB_W(rsc_adr%(tree&),obj&)
h&=OB_H(rsc_adr%(tree&),obj&)
'
appl_write(ap_id&,20,rsc_handle%(tree&),x&-3,y&-3,w&+6,h&+6,"")   ! WM_REDRAW
'
RETURN
> PROCEDURE appl_write(id&,msg&,handle&,x&,y&,w&,h&,a$)
' LOCAL b$
'
' Sendet eine Message an ein Programm. Dabei kann es sich auch um
' das eigene Programm handeln (id&=ap_id&), siehe 'rsc_redraw_obj'
' --------------------------------------------------
INLINE msg%,256
'
INT{msg%}=msg&                                     ! 0 Message-ID
INT{msg%+2}=ap_id&                                 ! 1 Unsere ID
INT{msg%+4}=0                                      ! 2 Keine öberlÑnge
INT{msg%+6}=handle&                                ! 3
INT{msg%+8}=x&                                     ! 4 MenÅpunkt
INT{msg%+10}=y&                                    ! 5
INT{msg%+12}=w&                                    ! 6
INT{msg%+14}=h&                                    ! 7
'
IF x&=0                                            ! String (z.B. fÅr VA_START)...
CHAR{msg%+16}=LEFT$(a$,240)
{msg%+6}=msg%+16
ENDIF
'
~APPL_WRITE(id&,16,msg%)                           ! Weg damit
'
RETURN
' ------------------------------------------------------------------------------
' Eine Busy-Maus in der Form eines kleines Balles...
'
INLINE m_busy%,592
m_adr%=m_busy%
'
> PROCEDURE busy_mouse
'
IF m_adr%=ADD(m_busy%,518)
m_adr%=m_busy%
'
ELSE
ADD m_adr%,74
ENDIF
'
~GRAF_MOUSE(255,m_adr%)
'
RETURN
' ------------------------------------------------------------------------------
