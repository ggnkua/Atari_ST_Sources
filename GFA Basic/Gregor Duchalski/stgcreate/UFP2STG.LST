' ------------------------------------------------------------------------------
' -                        UFP-Info zu STG Compiler                            -
' -                                                                            -
' -                      (c) 6-7/94 Gregor Duchalski                           -
' -                                                                            -
' - Konvertiert die Datendateien des PC-Programms UFP-INFO in einen ST-Guide-  -
' - Hypertext-Source. Dieser Quellcode ist Freeware, die compilierte Version   -
' - darf aber nur im Originalzustand weitergegeben werden!                     -
' ------------------------------------------------------------------------------
' ERGO-PP-Kommandos: CMP -> Compilierte Version (immer)
'
$m500000
RESERVE 500000                                     ! Nur im Interpreter -CMP
CLIP OFF
'
cconws2(CHR$(27)+"E")
cconws("-------------------------------------------")
cconws("UFP2STG, UFP-Info converter for ST-Guide")
cconws("v0.90 (17.07.94), ½1994 by Greedy Gregalski")
cconws("")
' --------------------------------------------------
homedir$=CHR$(ADD(GEMDOS(25),65))+":"+DIR$(0)+"\"  ! Heimatdirectory...
homedir$="D:\ST-GUIDE\UFP2STG\"                    ! Nur im Interpreter -CMP
f_stg&=20                                          ! Filehandle fr das Ziel STG-File
f_inf&=21                                          ! Filehandle fr das INF-File
' --------------------------------------------------
inf_laden                                          ! Ggf. INF-Datei laden
' --------------------------------------------------
lf$=CHR$(13)+CHR$(10)                              ! Linefeed
LET rem$="##"+STRING$(78,"-")                      ! Bemerkung
a_no$="@{0}"                                       ! Attribute aus
' --------------------------------------------------
open_dest                                          ! Zieldatei ”ffnen
make_main_screen                                   ! Hauptscreen erzeugen
'
main
'
close_dest                                         ! Zieldatei schliežen
' --------------------------------------------------
> PROCEDURE inf_laden
  LOCAL i&,file$
  '
  file$=homedir$+"UFP2STG.INF"
  '
  IF EXIST(file$)=FALSE                            ! INF nicht gefunden...
    cconws(CHR$(7)+"--> Error, UFP2STG.INF not found!")
    ~INP(2)
    EDIT                                           ! ...Schluž
  ENDIF
  ' ------------------------------------------------
  OPEN "I",#f_inf&,file$
  '
  input(FALSE,p_source$)                           ! Pfad der UFP-Dateien
  input(TRUE,f_stg$)                               ! Ziel STG-Datei
  input(TRUE,f_main$)                              ! Name der Hauptindexdatei
  input(TRUE,p_pic$)                               ! Pfad mit Bildern
  '
  FOR i&=1 TO 3
    LINE INPUT #f_inf&,file$                       ! ...Dummies
  NEXT i&
  '
  input(FALSE,a_fett$)                             ! Fettschrift
  input(FALSE,a_hell$)                             ! Wird fr Hervorhebungen benutzt
  input(FALSE,a_attr$)                             ! Wird fr Hervorhebungen benutzt
  '
  FOR i&=1 TO 5
    LINE INPUT #f_inf&,file$                       ! ...Dummies
  NEXT i&
  ' ------------------------------------------------
  '
RETURN
> PROCEDURE input(main_path!,VAR a$)
  LOCAL a&
  '
  LINE INPUT #f_inf&,a$
  ' ------------------------------------------------
  a&=INSTR(a$,";")                                 ! Kommentar abschneiden
  IF a&
    a$=LEFT$(a$,PRED(a&))
    a$=TRIM$(a$)
  ENDIF
  ' ------------------------------------------------
  IF main_path!                                    ! Im UFP-Pfad...
    IF MID$(a$,2,2)<>":\"
      a$=p_source$+a$                              ! ...Pfad davorh„ngen
    ENDIF
    '
  ENDIF
  '
RETURN
' --------------------------------------------------
> PROCEDURE make_main_screen
  LOCAL a$,i&
  '
  ' ------------------------------------------------
  OPEN "I",#1,f_main$
  '
  INPUT #1,main_anz&
  '
  DIM main$(main_anz&)
  RECALL #1,main$(),1 TO main_anz&,a%              ! MAIN.DAT laden
  '
  CLOSE #1
  ' ------------------------------------------------
  WHILE NOT EOF(#f_inf&)                           ! Screen aus INF bernehmen...
    LINE INPUT #f_inf&,a$
    w(a$)                                          ! ...Zeilen einfach bernehmen
  WEND
  CLOSE #f_inf&
  ' ------------------------------------------------
  FOR i&=1 TO main_anz&
    w(main$(i&))
  NEXT i&
  '
  w_endnode
  ' ------------------------------------------------
  '
RETURN
'
> PROCEDURE main
  LOCAL i&
  '
  FOR i&=1 TO main_anz&
    '
    cconws("")
    cconws("-"+main$(i&))
    make_kapitel(main$(i&))                        ! Ein Kapitel anlegen
    '
  NEXT i&
  '
RETURN
'
> PROCEDURE make_kapitel(kapitel$)
  LOCAL anz&,a&,i&,f_dat&,f_idx&,f_dtx&,l%,a%,line$,f_dat$,f_idx$,f_dtx$
  '
  f_dat$=p_source$+UPPER$(LEFT$(kapitel$,8))
  f_dat$=@replace$(f_dat$," ","_")
  '
  f_idx$=f_dat$+".IDX"
  f_dtx$=f_dat$+".DTX"
  f_dat$=f_dat$+".DAT"
  ' ------------------------------------------------
  f_dat&=1                                         ! Alle 3 Dateien ”ffnen...
  f_idx&=2
  f_dtx&=3
  OPEN "I",#f_dat&,f_dat$
  OPEN "I",#f_idx&,f_idx$
  OPEN "I",#f_dtx&,f_dtx$
  ' ------------------------------------------------
  SEEK #f_dat&,LOF(#f_dat&)-2                      ! Zur Sicherheit: LF am Ende checken...
  a&=INP&(#1)
  IF a&<>3338                                      ! Kein LF am Ende...
    CLOSE #f_dat&
    '
    OPEN "A",#f_dat&,f_dat$
    OUT& #f_dat&,3338                              ! ...anh„ngen
    CLOSE #f_dat&
    '
    OPEN "I",#f_dat&,f_dat$
    '
  ELSE
    SEEK #f_dat&,0
  ENDIF
  ' ------------------------------------------------
  INPUT #f_dat&,anz&                               ! Anzahl Eintr„ge in diesem Kapitel
  '
  DIM a$(anz&)
  RECALL #f_dat&,a$(),1 TO anz&,a%
  '
  w_node(kapitel$)                                 ! Erstmal einen Kapitel-Screen anlegen...
  FOR i&=1 TO anz&
    w_link(a$(i&))
  NEXT i&
  w_endnode
  '
  ERASE a$()
  '
  SEEK #f_dat&,0
  INPUT #f_dat&,anz&                               ! Und nun wieder von vorne
  ' ------------------------------------------------
  cconws2(CHR$(27)+"j")                            ! Cursor-Pos. speichern
  '
  FOR i&=1 TO anz&
    pos%=LOC(#f_dtx&)
    LINE INPUT #f_dat&,line$                       ! Screen-Titel
    l%=@inp4(2)
    len%=l%-pos%                                   ! L„nge des Screens in Byte
    '
    write_screen(line$,len%)
    '
  NEXT i&
  ' ------------------------------------------------
  CLOSE #f_dtx&
  CLOSE #f_idx&
  CLOSE #f_dat&
  '
RETURN
> PROCEDURE write_screen(line$,len%)
  LOCAL a&
  '
  cconws2(CHR$(27)+"k"+CHR$(27)+"l"+line$)
  '
  adr$=SPACE$(len%)
  BGET #f_dtx&,V:adr$,len%                           ! Screen laden
  '
  IF line$<>""
    '
    w(rem$)
    w_node(line$)                                    ! Screen-Titel
    w(a_fett$+line$+a_no$)                           ! Nochmal, sieht sch”ner aus
    ' ------------------------------------------------
    DO                                               ! Hervorhebungen...
      '
      a&=INSTR(adr$,"$86")
      EXIT IF a&=0
      '
      adr$=LEFT$(adr$,PRED(a&))+a_attr$+MID$(adr$,a&+3)   ! ...fett&kursiv
      a&=INSTR(adr$,lf$,a&)
      adr$=LEFT$(adr$,PRED(a&))+a_no$+MID$(adr$,a&)  ! ...wieder abschalten
      '
    LOOP
    ' ------------------------------------------------
    DO                                               ! Farbinformation...
      '
      a&=INSTR(adr$,"$6")
      EXIT IF a&=0
      '
      adr$=LEFT$(adr$,PRED(a&))+a_hell$+MID$(adr$,a&+2)   ! ...hell
      a&=INSTR(adr$,lf$,a&)
      '
    LOOP
    ' ------------------------------------------------
    DO                                               ! Querverweise...
      '
      a&=INSTR(adr$,CHR$(30))
      EXIT IF a&=0
      '
      adr$=LEFT$(adr$,PRED(a&))+MID$(adr$,a&+2)      ! ...ersten 2 Zeichen berspringen
      '
    LOOP
    ' ------------------------------------------------
    DO                                               ! ?...
      '
      a&=INSTR(adr$,CHR$(31))
      EXIT IF a&=0
      '
      b&=INSTR(adr$,lf$,a&)
      adr$=LEFT$(adr$,PRED(a&))+MID$(adr$,b&+2)      ! ...diese Zeilen ignorieren
      '
    LOOP
    ' ------------------------------------------------
    BPUT #f_stg&,V:adr$,LEN(adr$)                    ! Screen schreiben
    ' ------------------------------------------------
    line$=@replace$(LEFT$(line$,8)," ","_")
    '
    IF INSTR(line$,",")
      line$=@replace$(line$,",","_")
    ENDIF
    '
    IF INSTR(line$,".")
      line$=@replace$(line$,".","_")
    ENDIF
    '
    line$=p_pic$+line$+".IMG"
    IF EXIST(line$)=TRUE
      w("@image "+line$+" 1 10 10")                  ! ...Bild einbinden
    ENDIF
    ' ------------------------------------------------
    w_endnode
    '
  ENDIF
  '
RETURN
' --------------------------------------------------
> PROCEDURE open_dest
  LOCAL lof%,a$
  '
  OPEN "O",#f_stg&,f_stg$
  ' ------------------------------------------------
  IF EXIST(f_source$)=TRUE                         ! Text vorschalten...
    '
    OPEN "I",#1,f_source$
    lof%=MIN(32700,LOF(#1))
    a$=INPUT$(lof%,#1)
    CLOSE #1
    '
    PRINT #f_stg&;a$;
    '
  ENDIF
  '
RETURN
> PROCEDURE close_dest
  '
  CLOSE #f_stg&
  '
RETURN
'
> PROCEDURE w_node(a$)
  '
  PRINT #f_stg&;"@node """+a$+""""
  '
RETURN
> PROCEDURE w_endnode
  '
  PRINT #f_stg&;"@endnode"
  '
RETURN
> PROCEDURE w_link(a$)
  '
  IF a$<>""
    PRINT #f_stg&;"@{"""+a$+""" Link """+a$+"""}"
  ENDIF
  '
RETURN
> PROCEDURE w(a$)
  '
  PRINT #f_stg&;a$
  '
RETURN
' --------------------------------------------------
> FUNCTION replace$(a$,old$,new$)
LOCAL a&,l&,len&
'
' Ersetzt in 'a$' alle Vorkommen von 'old$' durch 'new$'
' --------------------------------------------------
l&=LEN(old$)
len&=LEN(new$)
'
a&=INSTR(a$,old$)
WHILE a&
  a$=LEFT$(a$,PRED(a&))+new$+MID$(a$,ADD(a&,l&))
  a&=INSTR(a$,old$,ADD(a&,len&))
WEND
'
RETURN a$
ENDFUNC
> FUNCTION inp4(k&)
$F%
LOCAL a%
'
a%=INP%(#k&)
'
a&=ROL&(a%,8)
b&=ROL&(SWAP(a%),8)
'
a%=OR(CARD(b&),SHL(a&,16))
'
RETURN a%
ENDFUNC
'
> PROCEDURE cconws(a$)
'
a$=a$+CHR$(13)+CHR$(10)+CHR$(0)
~GEMDOS(9,L:V:a$)
'
RETURN
> PROCEDURE cconws2(a$)
'
a$=a$+CHR$(0)
~GEMDOS(9,L:V:a$)
'
RETURN
