' *******************************************
' *                 ROTOR                   *
' *          erstellt am 5.7.1991           *
' * von -Christian Roth und Matthias Brust- *
' *******************************************
'
CLIP 0,0,639,399     ! nur nicht zu viel zeichnen
raster               ! Eingaberaster zeichnen
shadow!=TRUE         ! schattieren, yeah!
' Die folgenden 2 Zahlen gelten fÅr 512 KB,
' ansonsten je nach Speicher vergrîûern
punkte_max|=50       ! maximale Zahl der Eckpunkte
rotationen_max|=70   ! maximale Zahl der Rotationen
anz_pu&=MUL(rotationen_max|,punkte_max|)
DIM x_3d&(anz_pu&),y_3d&(anz_pu&),z_3d&(anz_pu&)
DIM x_trans(anz_pu&),y_trans(anz_pu&),z_trans(anz_pu&)
DIM x_2d&(3),y_2d&(3)
anz_fl&=MUL(rotationen_max|,punkte_max|-1)
' Arrays fÅr Farbgebung und Mittelwerte
DIM mittel(anz_fl&),farbe|(anz_fl&)
' Arrays fÅr FlÑche und deren Sortierung
DIM flaeche&(anz_fl&,3),fl_sort&(anz_fl&)
'
beo_x&=0       ! Koordinaten
beo_y&=0       ! des Beobachters
beo_z&=5000
'
licht_x&=900   ! Koordinaten
licht_y&=-300  ! der Licht-
licht_z&=500  ! quelle
'
licht_intent=0.8  ! LichtintensitÑt
'
hintergrund_intent=0.3 ! Hintergrundint.
'
' Start-Anzahl der Rotationen
rot_anz|=35
'
x_wink=10           ! Drehwinkel
y_wink=0            ! um die 3 Raum-
z_wink=0            ! achsen
'
x_skal=1        ! Skalierung
y_skal=1
z_skal=1
'
x_transl&=320   ! Mittelpunkt
y_transl&=200   ! des Bildschirms
'
menu_init
DO
  ON MENU
LOOP
'
' MenÅ initialisieren
PROCEDURE menu_init
  LOCAL i|
  DIM a$(17)
  FOR i|=0 TO 17    ! MenÅ-
    READ a$(i|)     ! punkte
  NEXT i|           ! einlesen
  MENU a$()
  ERASE a$()
  ON MENU GOSUB menu_choose
  MENU 14,1         ! Schattierung? Ja!
RETURN
'
' MenÅ-Verteilung
PROCEDURE menu_choose
  LOCAL i|,dummy|,back$,old_sh!
  i|=MENU(0)
  REPEAT
  UNTIL MOUSEK=0
  MENU OFF
  GET 0,0,639,18,back$           ! MenÅ retten
  SELECT i|
  CASE 1                         ! Info
    ALERT 4,"    ROTOR Version 1.1 |(c) 1991 by Christian Roth |     & Matthias Brust",1," Super! ",dummy|
  CASE 10                        ! Rotationskîrper
    rot_koerper_eingeben(TRUE)   ! eingeben und
    zeichnen                     ! zeichnen
  CASE 11                ! nachtrÑglich schattieren
    old_sh!=shadow!      ! spart Rechenzeit beim
    shadow!=TRUE         ! Rotieren
    farbe
    zeichnen
    shadow!=old_sh!
  CASE 12                ! Rotation, Translation,
    transformieren       ! Vergrîûerung
  CASE 14                ! automatische Schattierung
    shadow!=NOT shadow!  ! an- oder ausschalten
    IF shadow!
      MENU 14,1
    ELSE
      MENU 14,0
    ENDIF
  CASE 16                ! Raus hier!
    END
  ENDSELECT
  PUT 0,0,back$          ! MenÅ erneuern
RETURN
'
' Transformation aller Punkte
PROCEDURE transform
  LOCAL x_trans,y_trans,z_trans,i&
  PRINT AT(33,12);"                 "
  PRINT AT(35,12);"Transformation"
  sinx=SINQ(x_wink)   ! Sinus und Cosinus
  siny=SINQ(y_wink)   ! interpoliert (SINQ
  sinz=SINQ(z_wink)   ! und COSQ) vorbe-
  cosx=COSQ(x_wink)   ! rechnen => spart
  cosy=COSQ(y_wink)   ! Rechenzeit
  cosz=COSQ(z_wink)
  a=cosy*cosz     ! Elemente der
  b=cosy*sinz     ! Rotationsmatrix
  c=-siny         ! berechnen
  d=sinx*siny*cosz-cosx*sinz
  e=sinx*siny*sinz+cosx*cosz
  f=sinx*cosy
  g=cosx*siny*cosz+sinx*sinz
  h=cosx*siny*sinz-sinx*cosz
  j=cosx*cosy
  FOR i&=0 TO anz_pu&-1
    x_trans=x_3d&(i&)*x_skal        ! Skalierung
    y_trans=y_3d&(i&)*y_skal
    z_trans=z_3d&(i&)*z_skal
    x_trans(i&)=x_trans*a+y_trans*b+z_trans*c   ! Rotationen
    y_trans(i&)=x_trans*d+y_trans*e+z_trans*f   ! um die 3
    z_trans(i&)=x_trans*g+y_trans*h+z_trans*j   ! Achsen
  NEXT i&
RETURN
'
PROCEDURE zeichne_punkt(nr&)
  LOCAL x&,y&
  x&=punkt_x&(nr&)
  y&=punkt_y&(nr&)
  LINE x&-3,y&+3,x&+3,y&-3      ! Kreuzchen an
  LINE x&-3,y&-3,x&+3,y&+3      ! Punkt-Pos.
  LINE 643-x&,y&+3,637-x&,y&-3  ! malen
  LINE 643-x&,y&-3,637-x&,y&+3
  IF nr&=0                      ! 1. = Punkt
    DRAW x&,y&
  ELSE                          ! dann Linien
    DRAW punkt_x&(nr&-1),punkt_y&(nr&-1)
    DRAW  TO x&,y&
    DRAW 640-punkt_x&(nr&-1),punkt_y&(nr&-1)
    DRAW  TO 640-x&,y&
  ENDIF
RETURN
'
PROCEDURE raster
  LOCAL i&                   ! Raster mit
  CLS                        ! entsprechendem
  DEFLINE 3                  ! Linienmuster
  FOR i&=40 TO 640 STEP 40   ! zeichnen
    LINE i&,0,i&,400
  NEXT i&
  FOR i&=40 TO 400 STEP 40
    LINE 0,i&,640,i&
  NEXT i&
  DEFLINE 1
  DRAW 0,200 TO 640,200
  DRAW 320,0 TO 320,400
RETURN
'
' Rotationskîrper berechnen
PROCEDURE rot_koerper_eingeben(flag!)
  LOCAL step_wink&,spalte&,next_spalte&,fl_spalte&
  LOCAL punkt&,flaeche&,n1&,n2&
  IF flag!
    REPEAT
    UNTIL MOUSEK=0
    koerper_eingeben   ! erst Kîrper eingeben
  ENDIF
  PRINT AT(33,12);"                 "
  PRINT AT(35,12);"Berechnung"
  ' Anzahl der Punkte=Anzahl d. Rotationen*eingegebene Punkte
  anz_pu&=MUL(rot_anz|,punkte&)
  ' Anzahl der FlÑchen=Anzahl d. Rotationen*(eingegebene Punkte-1)
  anz_fl&=MUL(rot_anz|,punkte&-1)
  spalte&=0
  next_spalte&=punkte&
  fl_spalte&=0
  step_wink&=360/rot_anz|    ! 'Step'-Winkel berechnen
  ' Rotationskîrper berechnen (Rotation um Y-Achse)
  FOR winkel&=step_wink& TO 360 STEP step_wink&
    IF next_spalte&=anz_pu&  ! Bei der letzten FlÑche sind
      next_spalte&=0         ! die Endpunkte die Punkte der
    ELSE                     ! ersten Reihe
      ssin=SINQ(winkel&)     ! Sin und Cos wird îfter be-
      ccos=COSQ(winkel&)     ! nîtigt, deshalb vorberechnen
      ' Urpunkte um winkel& drehen
      FOR i&=0 TO punkte&-1
        x=x_3d&(i&)          ! Koordinaten d. Urpunkte
        z=z_3d&(i&)
        punkt&=ADD(next_spalte&,i&)
        x_3d&(punkt&)=x*ccos+z*ssin  ! um die y-Achse drehen
        y_3d&(punkt&)=y_3d&(i&)      ! und Punkte ins 3D-
        z_3d&(punkt&)=-x*ssin+z*ccos ! Array speichern
      NEXT i&
    ENDIF
    ' FlÑchen zusammensetzen:
    FOR i&=0 TO punkte&-2
      flaeche&=ADD(fl_spalte&,i&)
      n1&=ADD(spalte&,i&)
      n2&=ADD(next_spalte&,i&)
      flaeche&(flaeche&,0)=n1&        ! Punktnummern speichern
      flaeche&(flaeche&,1)=ADD(n1&,1) ! Reihenfolge:
      flaeche&(flaeche&,2)=ADD(n2&,1) ! 1---4
      flaeche&(flaeche&,3)=n2&        ! |   |
    NEXT i&                           ! 2---3
    spalte&=next_spalte&              ! dasselbe
    ADD next_spalte&,punkte&          ! mit der
    ADD fl_spalte&,punkte&-1          ! nÑchsten Spalte
  NEXT winkel&
  transform   ! transformieren(rotieren...)
  verdecke    ! Painter-Algorithmus
  '           ! zum Verdecken der FlÑchen
RETURN
'
' FlÑchen projizieren
PROCEDURE zeichnen
  LOCAL i&,k|,flaeche&,punkt&
  projektion  ! Punkte auf 2D bringen
  DEFFILL 1,0,0
  CLS
  FOR i&=0 TO anz_fl&-1
    flaeche&=fl_sort&(i&)
    FOR k|=0 TO 3  ! Translation aller Punkte
      punkt&=flaeche&(flaeche&,k|)
      x_2d&(k|)=x_transl&+x_trans(punkt&)
      y_2d&(k|)=y_transl&-y_trans(punkt&)
    NEXT k|
    IF shadow!     ! wenn schattieren
      IF farbe|(flaeche&)<>8 ! Farbe wÑhlen
        DEFFILL 1,2,farbe|(flaeche&)
      ELSE
        DEFFILL 1,0,0
      ENDIF
      POLYFILL 4,x_2d&(),y_2d&() ! Pinsel raus!
    ELSE
      POLYFILL 4,x_2d&(),y_2d&()
    ENDIF
  NEXT i&
RETURN
'
' Transformation des Objekts
PROCEDURE transformieren
  LOCAL key%,v!
  REPEAT
    KEYTEST key%            ! Taste gedrÅckt?
    key%=key% AND 255       ! Ascii-Code
    IF key%>48 AND key%<57  ! zwischen 1-8
      v!=TRUE               ! auch verdecken
      SELECT key%
      CASE 49               ! '1': um X-Achse
        ADD x_wink,30       ! drehen
      CASE 50               ! '2': X-Achse zu-
        SUB x_wink,30       ! rÅckdrehen
      CASE 53               ! '5': um Z-Achse
        ADD z_wink,30       ! drehen
      CASE 54               ! '6': Z-Achse zu-
        SUB z_wink,30       ! rÅckdrehen
        ' Die folgende Drehung um die Y-Achse
        ' bringt bei diesem Rotationskîrper gar
        ' nichts, da er durch Rotation um die Y-
        ' Achse entsteht, ist aber bei Rotations-
        ' kîrpern um X oder Z-Achse sinnvoll.
      CASE 51               ! '3': um Y-Achse
        ADD y_wink,30       ! drehen
      CASE 52               ! '4': Y-Achse zu-
        SUB y_wink,30       ! rÅckdrehen
      CASE 55               ! Objekt mit
        MUL x_skal,1.2      ! Faktor 1.2
        MUL y_skal,1.2      ! vergrîûern
        MUL z_skal,1.2      ! Verdecken nicht
        v!=FALSE            ! nîtig
      CASE 56               ! Objekt mit
        DIV x_skal,1.2      ! Faktor 1.2
        DIV y_skal,1.2      ! verkleinern
        DIV z_skal,1.2      ! Verdecken nicht
        v!=FALSE            ! nîtig
      ENDSELECT
      transform
      IF v!=TRUE            ! Verdecken nîtig?
        verdecke
      ENDIF
      zeichnen
    ELSE IF MOUSEK=1
      x_transl&=MOUSEX      ! Objekt
      y_transl&=MOUSEY      ! verschieben
      zeichnen
    ENDIF
  UNTIL MOUSEK=2
RETURN
'
' Errechnet die Farbe aller FlÑchen
PROCEDURE farbe
  LOCAL i&,p1&,p2&,p3&
  PRINT AT(33,12);"                 "
  PRINT AT(36,12);"Farbgebung"
  FOR i&=0 TO anz_fl&-1
    p1&=flaeche&(i&,0)  ! Punktnummern holen
    p2&=flaeche&(i&,1)
    p3&=flaeche&(i&,2)
    p1x=x_trans(p1&)    ! Punktkoordinaten,die
    p1y=y_trans(p1&)    ! mehrmals benîtigt
    p1z=z_trans(p1&)    ! werden
    qx=x_trans(p2&)-p1x ! Koordinaten werden
    qy=y_trans(p2&)-p1y ! fÅrs Vektorprodukt
    qz=z_trans(p2&)-p1z ! benîtigt
    rx=x_trans(p3&)-p1x
    ry=y_trans(p3&)-p1y
    rz=z_trans(p3&)-p1z
    ox=qy*rz-qz*ry      ! Vektorprodukt be-
    oy=qz*rx-qx*rz      ! rechnen
    oz=qx*ry-qy*rx
    lx=p1x-licht_x&     ! Vektor von Licht-
    ly=p1y-licht_y&     ! quelle zur FlÑche
    lz=p1z-licht_z&
    alpha=ox^2+oy^2+oz^2 ! Betrag des Vektors
    alpha=SQR(alpha*(lx^2+ly^2+lz^2))
    IF alpha<>0
      alpha=(ox*lx+oy*ly+oz*lz)/alpha
    ENDIF
    farb=hintergrund_intent
    IF alpha<0           ! FlÑche beleuchtet?
      farb=farb-licht_intent*alpha ! IntensitÑt
    ENDIF                ! errechnen
    farbe|(i&)=9-(farb-INT(farb))*8 ! Farbwert zwischen
  NEXT i&                         ! 0 und 8
RETURN
'
' Punkte des Rotationskîrpers
PROCEDURE koerper_eingeben
  LOCAL ascii|,i&
  DIM punkt_x&(punkte_max|),punkt_y&(punkte_max|)
  punkte&=-1
  raster         ! Raster zeichnen
  DEFMOUSE 5
  REPEAT
    REPEAT
      ascii|=ASC(RIGHT$(INKEY$))
      IF ascii|=97 AND punkte&>=0 ! mit Undo letzten
        DEC punkte&               ! Punkt lîschen
        raster
        IF punkte&>=0
          FOR i&=0 TO punkte&     ! Punkte
            zeichne_punkt(i&)     ! zeichnen
          NEXT i&
        ENDIF
      ELSE IF ascii|=98       ! mit Help alle
        punkte&=-1            ! Punkte lîschen
        raster
      ENDIF
    UNTIL MOUSEK=1 OR ascii|=27
    REPEAT
    UNTIL MOUSEK=0
    IF ascii|<>27             ! Wenn nicht ESC
      INC punkte&             ! gedrÅckt, Punkt
      punkt_x&(punkte&)=MOUSEX ! im Array speichern
      punkt_y&(punkte&)=MOUSEY ! und
      zeichne_punkt(punkte&)   ! zeichnen
    ENDIF
  UNTIL ascii|=27 OR punkte&=punkte_max|
  INC punkte&
  DEFMOUSE 0
  FOR i&=0 TO punkte&-1
    x_3d&(i&)=punkt_x&(i&)-320 ! Punkte von 2D nach
    y_3d&(i&)=200-punkt_y&(i&) ! 3D Åbertragen
    z_3d&(i&)=0
  NEXT i&
  ERASE punkt_x&()
  ERASE punkt_y&()
RETURN
'
' Zentralprojektion aller Raumpunkte
PROCEDURE projektion
  LOCAL i&,i
  FOR i&=0 TO anz_pu&-1
    i=z_trans(i&)-beo_z&
    x_trans(i&)=beo_x&-beo_z&*(x_trans(i&)-beo_x&)/i
    y_trans(i&)=beo_y&-beo_z&*(y_trans(i&)-beo_y&)/i
  NEXT i&
RETURN
'
' Painter-Algorithmus zum Verdecken
' von FlÑchen
PROCEDURE verdecke
  LOCAL i&,i1&,i|,z_summe,mittel
  PRINT AT(33,12);"                 "
  PRINT AT(33,12);"FlÑchen sortieren"
  FOR i&=0 TO anz_fl&-1
    z_summe=0
    FOR i|=0 TO 3       ! Z-Summe berechnen
      ADD z_summe,z_trans(flaeche&(i&,i|))
    NEXT i|
    mittel=z_summe/4    ! Mittelwert bilden
    mittel(i&)=mittel
    i1&=0
    DO                  ! FlÑche einsortieren
      EXIT IF mittel(fl_sort&(i1&))<=mittel OR i1&>=i&
      INC i1&
    LOOP
    INSERT fl_sort&(i1&)=i&
  NEXT i&
  IF shadow!   ! wenn nîtig => Farbgebung
    farbe
  ENDIF
RETURN
'
menu_points:
DATA  ROTOR,  öber ROTOR    ,a,a,a,a,a,a,""
DATA  Optionen,  Eingeben,  Schattieren,  Transformieren,-----------------,  Schattierung ,-----------------,  Ende,""
DATA  Parameter,  Beobachter,  Lichtquelle,  Schrittwinkel ,  Raster,""
