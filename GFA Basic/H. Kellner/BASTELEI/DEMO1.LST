'
'   DEMO1.GFA
'
'   GFA-Basic Version 3.5E D
'
'
'   Ausgabebeispiel fr die
'
'   Interface-Proceduren und -Funktionen
'
'
GOSUB first
'
ON BREAK GOSUB last
'
i%=1
GOSUB init(&HFFFF)
d_ir!=TRUE
PRINT AT(2,2);"Alle Ports als Ausg„nge :"
PRINT AT(2,4;;"0000000000111111"
PRINT AT(2,5);"0123456789012345"
PRINT AT(2,9);"Bit=1 ---> Ausgang High"
PRINT AT(2,10);"Bit=0 ---> Ausgang Low"
'
ALERT 1,"RESET-Taste|    des| Interface| bet„tigen !",1," OK ",but%
'
REPEAT
  FOR j%=1 TO 16
    IF BTST(i%,j%-1)=-1
      b$=b$+"1"
    ELSE
      b$=b$+"0"
    ENDIF
  NEXT j%
  PRINT AT(2,7);b$
  b$=""
  GOSUB outword(NOT i%)
  IF d_ir!
    i%=SHL(i%,1)
    IF i%=&H8000
      d_ir!=NOT d_ir!
    ENDIF
  ELSE
    i%=SHR(i%,1)
    IF i%=1
      d_ir!=NOT d_ir!
    ENDIF
  ENDIF
  PAUSE 2
UNTIL dummy=99
'
> PROCEDURE first               !  Dieses Unterprogramm muž immer als erstes
  '                              aufgerufen werden. Hier wird die Assembler-
  '                              routine in den Speicher geladen und alle
  '                              Variablen gesetzt.
  '
  '                              Aufruf durch 'GOSUB first' oder '@first'
  '                              oder nur durch 'first'
  '
  DIM bin_code%(500),regs%(16)
  '
  adr%=VARPTR(bin_code%(0))
  '
  BLOAD "INTERFAC.MOD",adr%
  '
  index%=0
  '
  WHILE (index%<500) AND (bin_code%(index%)<>&H4AFCDB9F)
    index%=index%+1
  WEND
  '
  IF index%=500 THEN
    ALERT 3,"Startadresse nicht gefunden",1,"Dann nicht",index%
    END
  ENDIF
  '
  init%=VARPTR(bin_code%(SUCC(index%)))
  deinit%=init%+4
  read%=deinit%+4
  write%=read%+4
  readreg%=write%+4
  wrreg%=readreg%+4
  '
RETURN
'
> PROCEDURE init(dir%)          !  Datenrichtungsregister setzen
  '                              z.B. ddir%=&X1010101010101010
  '                              Bit=0 ---> Eingang
  '                              Bit=1 ---> Ausgang
  '
  '                              Aufruf durch 'GOSUB init(ddir%)' oder
  '                              '@init(ddir%)' oder nur durch 'init(ddir%)'
  '
  regs%(0)=dir%
  RCALL init%,regs%()
  '
RETURN
'
> PROCEDURE outword(word%)      !  Ausgabe der Datenbits fr jeden Ausgang
  '                              z.B. dword%=&X1000
  '                              Bit=0 ---> Ausgang low
  '                              Bit=1 ---> Ausgang high
  '
  '                              Aufruf durch 'GOSUB outword(dword%)' oder
  '
  '                              '@outword(dword%)' oder nur durch
  '                              'outword(dword%)'
  '
  regs%(0)=word%
  RCALL write%,regs%()
  '
RETURN
'
> PROCEDURE last                !  Dieses Unterprogramm muž immer als letztes
  '                              aufgerufen werden. Hier wird die parallele
  '                              Schnittstelle wieder auf den Druckerbetrieb
  '                              umgeschalten.
  '
  '                              Aufruf durch 'GOSUB last' oder '@last'
  '                              oder nur durch 'last'
  RCALL deinit%,regs%()
  END
  '
RETURN
'
END
