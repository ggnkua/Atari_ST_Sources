'               The GFA Demo! Part 2 - (C) ST Format 1991.
'                      Written by James Beswick.
'
' Refer to your MARCH issue of ST FORMAT for the tutorial for this demo.
'     DON'T TRYING MERGING THIS WITH THE LAST PART OF THE TUTORIAL!
' You'll need the file GFADEMO.PI1 in the same directory as this file for
' this demo to work.
'
'             - HOLD DOWN THE SPACE BAR TO EXIT THE DEMO -
'
'
Cls                             ! CLear the Screen.
Setcolor 0,0                    ! Set background colour to black.
Setcolor 15,1911                ! Set text colour to white.
'
Bload "gfademo.pi1",Xbios(3)-34
Void Xbios(6,L:Xbios(3)-32)
'
Get 156,1,259,14,Author$        ! \
Get 97,17,158,28,Magazine$      !  > Get the 'Sprites'.
Get 0,0,91,51,Logo$             ! /
Cls
'
' ** SET UP THE SCREEN **
'
Put 114,74,Logo$
'
For F=0 To 150                  ! Loop this idented part 150 times!
  X=Random(319)                 ! A random number between 0-319 is stored in X.
  Y=Random(199)                 ! A random number between 0-199 is stored in Y.
  C=Random(5)                   ! A random number between 0-4 is stored in C.
  Color C                       ! The dot will be coloured by a colour 0-4.
  A=Point(X,Y)                  ! Detect if dot's overwriting the logo.
  If A=0
    Plot X,Y                    ! A dot is then placed at the random X/Y values.
  Endif
Next F                          ! That only took about a second!
'
' ** DO THE ANIMATION **
'
Deffill 0,2,8                   ! Make sure that the box will be a solid black.
Repeat                          ! Start of loop (looped part is idented).
  Clr Count                     ! Clear the variable COUNT to the value of 0.
  Repeat                        ! You can have loops inside loops in GFA!
    For T=105 To 111            ! Loop (again!) this idented part.
      Vsync                     ! SYNCronise the Vertical.
      Put T,183,Author$         ! Animate my name!
    Next T                      ! End of this loop.
    For T=111 To 105 Step -1    ! the whole of this loop is exactly the same
      Vsync                     ! as the last one, but this time, it animates
      Put T,183,Author$         ! my name back to its original position.
    Next T
    Add Count,1                 ! Increase the value of COUNT by 1.
  Until Inkey$<>"" Or Count=10  ! Stop when a key is pressed or COUNT=10.
  '
  Pbox 105,183,214,199          ! Black out my name from the screen with a box.
  Clr Count                     ! Clear the value of COUNT to 0.
  Repeat                        ! Another beginning of a loop.
    For T=127 To 133            ! This is the same as the last loop, except
      Vsync                     ! that this time it animates the words
      Put T,183,Magazine$       ! 'ST FORMAT'.
    Next T
    For T=133 To 127 Step -1
      Vsync
      Put T,183,Magazine$
    Next T
    Add Count,1
  Until Inkey$<>"" Or Count=10  ! Stop when a key is pressed or COUNT=10.
  Pbox 105,183,214,199          ! Black out ST FORMAT with a black box.
  Clr Count                     ! Clear the value of COUNT to 0.
Until Inkey$<>""                ! Stop when a key is pressed.
'
'
' ** END OF DEMO INTRODUCTION **
'
X1=0                            ! These are four 'variables' which store four
X2=0                            ! values, which will be used to position each
X3=319                          ! box to wipe out the screen.
X4=199
'
Color 0                         ! Boxes will be coloured black.
Repeat                          ! Start of loop...
  Box X1,X2,X3,X4               ! Draw the box.
  Add X1,1                      ! Increase the value stored in X1 by 1.
  Add X2,1                      ! Increase the value stored in X2 by 1.
  Sub X3,1                      ! Decrease the value stored in X3 by 1.
  Sub X4,1                      ! Decrease the value stored in X4 by 1.
Until X1=100                    ! ...end loop when the value in X1 is 100.
'
Setcolor 0,1911                 ! Reset background colour.
Setcolor 15,0                   ! Reset text colour.
Edit                            ! Exit straight to the editor.
