' *******************************************
' BITBLT - Anwendung des GFA-BASIC-Befehls
' Beispiel-Programm zur Anwendung der
' Prozeduren 'get' und 'put'
' in GFA-Basic 3.6
' Markus Holtwiesche
' (c) 1992 MAXON Computer
' *******************************************
'
' MFDB einrichten
@mfdb_init
' testbild zeichnen
DEFFILL 1,2,8
PBOX 100,100,200,250
GRAPHMODE 3
PCIRCLE 250,175,70
PBOX 80,150,350,200
' Ausschnitt einlesen
get(80,100,350,250,bildadresse%)
' Clipping setzen
CLIP OFF
CLIP 50,50 TO 589,349
' Bild l”schen
' und Clipping-Rechteck darstellen
GRAPHMODE 1
DEFFILL 1,2,2
BOUNDARY 0
PBOX 50,50,589,349
BOX 50,50,589,349
PRINT AT(1,1);"Taste drcken!"
~INP(2)
'
' Ausschnitt ber den Bildschirm wandern lassen
FOR i%=0 TO 399 STEP 30
  get(i%,i%,i%+270,i%+150,hintergrundadresse%)
  put(i%,i%,bildadresse%,3)
  PAUSE 30
  put(i%,i%,hintergrundadresse%,3)
NEXT i%
'
' vor dem Programmende sollten alle eventuell an-
' geforderten Speicherbl”cke wieder freigegeben
' werden
' deshalb Vorsicht beim Programmabbruch
' mit Control-Shift-Alternate
IF bildadresse%>0
  ~MFREE(bildadresse%)
ENDIF
IF hintergrundadresse%>0
  ~MFREE(hintergrundadresse%)
ENDIF
END
' Programmende
' *******************************************
'
PROCEDURE mfdb_init
  ' richtet MFDB ein
  ' muž einmal zum Programmstart aufgerufen 
  ' werden
  DIM src.mfdb%(8)
  DIM des.mfdb%(8)
  DIM gp.par%(8)
  @vq_extnd
  fd_nplanes%=INTOUT(4) !Anzahl der Bildebenen
  src.mfdb%(4)=0   !reserv.
  src.mfdb%(5)=fd_nplanes%
  des.mfdb%(4)=0   !reserv.
  des.mfdb%(5)=fd_nplanes%
RETURN
'
PROCEDURE vq_extnd
  ' Auskunftsfunktion des VDI
  ' Nach dem Aufruf stehen in INTOUT(0) bis
  ' INTOUT(44) diverse Parameter der Rechner-
  ' konfiguration; siehe auch [2]
  DPOKE CONTRL+2,0
  DPOKE CONTRL+4,6
  DPOKE CONTRL+6,1
  DPOKE CONTRL+8,45
  DPOKE CONTRL+12,V~H
  INTIN(0)=1  ! 0=normale-  1=erweiterte Auskunft
  VDISYS 102
RETURN
'
PROCEDURE get(xs0%,ys0%,xs1%,ys1%,VAR bildadr%)
  ' liest beliebig grožen Bildausschnitt in
  ' Speicherblock
  LOCAL b.len%
  ' Bildl„nge ausrechnen
  b.len%=((xs1%-xs0%+16) AND &HFFF0)*
                      (ys1%-ys0%+1)/8*fd_nplanes%
  IF b.len%>0
    ' nur weiter, wenn Bildl„nge gr”žer 0
    IF bildadr%>0
      ' wenn Bildadresse schonmal vergeben
      ' Speicher freigeben
      ~MFREE(bildadr%)
    ENDIF
    ' Speicher reservieren:
    ' Bildl„nge+Brokratie(Header)
    bildadr%=MALLOC(b.len%+12)
    IF bildadr%>0
      ' wenn Speicher reserviert
      src.mfdb%(0)=0   ! 0=BILDSCHIRM!!!
      src.mfdb%(1)=(xs1%-xs0%+16) AND &HFFF0
      src.mfdb%(2)=ys1-ys0%+1
      src.mfdb%(3)=src.mfdb%(1)/16
      des.mfdb%(0)=bildadr%+12 !SPEICHERADRESSE!!
      des.mfdb%(1)=src.mfdb%(1)
      des.mfdb%(2)=src.mfdb%(2)
      des.mfdb%(3)=src.mfdb%(3)
      gp.par%(0)=xs0%
      gp.par%(1)=ys0%
      gp.par%(2)=xs1%
      gp.par%(3)=ys1%
      gp.par%(4)=0
      gp.par%(5)=0
      gp.par%(6)=xs1%-xs0%
      gp.par%(7)=ys1%-ys0%
      gp.par%(8)=3  !Kopier-Modus: 3=replace
      ' Bild in Speicher einlesen
      BITBLT src.mfdb%(),des.mfdb%(),gp.par%()
      ' Header setzen
      {bildadr%}=xs1%-xs0%
      {bildadr%+4}=ys1%-ys0%
      {bildadr%+8}=fd_nplanes%
    ENDIF
  ENDIF
RETURN
'
PROCEDURE put(xd0%,yd0%,bildadr%,mod%)
  ' zeichnet den eingelesenen Bildausschnitt
  ' an beliebiger Stelle auf den Bildschirm
  IF bildadr%>0
    ' wenn Bildadresse vorhanden
    src.mfdb%(0)=bildadr%+12  ! SPEICHERADRESSE!!
    ' Rasterdaten aus Header lesen   
    src.mfdb%(1)=({bildadr%}+16) AND &HFFF0
    src.mfdb%(2)={bildadr%+4}+1
    '
    src.mfdb%(3)=src.mfdb%(1)/16
    des.mfdb%(0)=0  ! 0=BILDSCHIRM!!
    des.mfdb%(1)=src.mfdb%(1)
    des.mfdb%(2)=src.mfdb%(2)
    des.mfdb%(3)=src.mfdb%(3)
    gp.par%(0)=0
    gp.par%(1)=0
    gp.par%(2)={bildadr%}
    gp.par%(3)={bildadr%+4}
    gp.par%(4)=xd0%
    gp.par%(5)=yd0%
    gp.par%(6)=xd0%+gp.par%(2)
    gp.par%(7)=yd0%+gp.par%(3)
    gp.par%(8)=mod%
    ' Bild auf den Schirm kopieren
    BITBLT src.mfdb%(),des.mfdb%(),gp.par%()
  ENDIF
RETURN
