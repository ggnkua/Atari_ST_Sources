DIM pal(15,2),palette(15)
READ larg,haut,niveau,facteur,translation,fact,fact2,fact3,amode,amode1
DATA 20,15,4,4,6,2,2,20,0,0
@menu.in
@menu
'
> PROCEDURE menu.in
  LOCAL h,hh,lon,aa$,a$,k,kk,kkk,ak,aa,bb,cc
  DIM text$(200),gecran%(32000)
  aad%=V:gecran%(0)
  FOR h=0 TO 15
    SETCOLOR h,0
  NEXT h
  DIM boite(19,4)
  FOR hh=1 TO 19
    FOR h=1 TO 4
      READ boite(hh,h)
    NEXT h
    SUB boite(hh,2),1
    SUB boite(hh,4),1
  NEXT hh
  BLOAD "a:\cartomap\menu",XBIOS(2)
  DEFTEXT 1,0,0,4
  TEXT 67,39,larg
  TEXT 67,45,haut
  TEXT 67,55,facteur
  TEXT 67,60,niveau
  TEXT 67,80,translation
  TEXT 74,114,fact
  TEXT 74,120,fact2
  TEXT 74,134,fact3
  TEXT 74,149,amode
  TEXT 74,155,amode1
  GET 1,1,319,199,menu$
  CLIP 0,0,320,200
  DIM fond$(0)
  OPEN "i",#1,"a:\cartomap\fond"
  RECALL #1,fond$(),1544,a
  CLOSE #1
  h=0
  OPEN "i",#1,"a:\cartomap\cartomap.asc"
  REPEAT
    INPUT #1,aa$
    INC h
    text$(h)=RIGHT$(aa$,LEN(aa$))
    VSYNC
  UNTIL EOF(#1)
  CLOSE #1
  FOR h=0 TO 20
    FOR hh=0 TO 20
      PUT 49*hh-12,47*h,fond$(0)
    NEXT hh
  NEXT h
  SGET a$
  CLIP 0,0,320,200
  GRAPHMODE 1
  DEFFILL 5,2,8
  PBOX 50,30,321-50,135
  DEFFILL 6,2,4
  PBOX 55,35,321-55,130
  DEFFILL 7,2,8
  PBOX 60,40,321-60,125
  DEFTEXT 9,0,0,4
  GRAPHMODE 2
  TEXT 65,60,"demo.carto/map.maker.2&3D(v1.92)"
  TEXT 112,75,"programing.Pac.R"
  TEXT 118,90,"GFA basic 3.5"
  GRAPHMODE 3
  TEXT 65,60,"demo.carto/map.maker.2&3D(v1.92)"
  TEXT 112,75,"programing.Pac.R"
  TEXT 118,90,"GFA basic 3.5"
  DEFTEXT 6,0,0,8
  TEXT 105,180,"PRESS FIRE"
  GRAPHMODE 1
  BMOVE XBIOS(3),aad%,32000
  SPUT a$
  FOR hh=1 TO 8
    FOR h=0 TO 14
      VSETCOLOR hh,h*256+hh*32+2*hh
      PAUSE 0.2
    NEXT h
  NEXT hh
  @palmenu
  VSETCOLOR 5,2,2,2
  VSETCOLOR 6,3,3,3
  VSETCOLOR 7,4,4,4
  PAUSE 50
  FOR h=0 TO 100
    BMOVE aad%,XBIOS(3)-100*160+h*160,16000
    BMOVE aad%+16000,XBIOS(3)+200*160-h*160,16000
  NEXT h
  PAUSE 50
  REPEAT
    PUT 49*RAND(10)-12,47*RAND(10),fond$(0),6
  UNTIL STRIG(1)<>0
  FOR h=0 TO 800
    PUT 49*RAND(10)-12,47*RAND(10),fond$(0),3
  NEXT h
  SPUT a$
  ak=-4
  bb=2*256+1*16+3
  cc=1*256+2*16+2
  GRAPHMODE 2
  DEFTEXT 8,0,0,4
  @screen(1)
  @screen(2)
  REPEAT
    INC kk
    INC kkk
    IF kkk>=100
      INC ak
      kkk=95
    ENDIF
    SPUT a$
    FOR k=ak+4 TO ak+24
      IF k=<199
        TEXT 60,10*k-2*kk+180,text$(k)
      ENDIF
      EXIT IF STRIG(1)<>0
    NEXT k
    @screen(2)
    SWAP bb,cc
    SETCOLOR 2,bb
    SETCOLOR 3,cc
  UNTIL STRIG(1)<>0
  @screen(0)
RETURN
> PROCEDURE menu
  LOCAL h,a$
  CLIP 0,0,320,200
  @efface1
  @palmenu
  REPEAT
    REPEAT
      COLOR 11
      PUT 1,1,menu$
      DEFLINE RAND(6)+1
      BOX boite(choix,1),boite(choix,2)+2,boite(choix,3),boite(choix,4)+2
      PAUSE 3
      j=STICK(1)
      IF j=8 THEN
        INC choix
      ENDIF
      IF j=4 THEN
        DEC choix
      ENDIF
      IF choix<1 THEN
        choix=19
      ENDIF
      IF choix>19
        choix=1
      ENDIF
      a$=INKEY$
      IF a$="q" THEN
        CLS
        REPEAT
          a$=INKEY$
          DEFTEXT 1,0,0,4
          TEXT 110,50,"quitter : o/n"
        UNTIL a$<>""
        IF a$="o"
          QUIT
        ENDIF
      ENDIF
    UNTIL STRIG(1)<>0
    DEFLINE 1,1
    HIDEM
    SELECT choix
    CASE 1
      @in(larg,63,39,3,1,"   ",larg)
      @in(haut,63,45,3,1,"   ",haut)
      @in(niveau,63,55,3,1,"   ",niveau)
      @in(facteur,63,60,3,1,"   ",facteur)
      @generer(larg,haut,niveau,facteur)
      CLR carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
    CASE 2
      CLR carte,carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
      @in(translation,63,80,3,1,"  ",translation)
      @translation1(translation,larg,haut,niveau)
    CASE 3
      CLR carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
      @in(fact,70,114,3,1,"   ",fact)
      @in(fact2,70,120,3,1,"   ",fact2)
    CASE 4
      CLR carte33d,carte63d
      @in(fact3,70,134,3,1,"  ",fact3)
    CASE 5
      CLR carte23d,carte33d,carte43d,carte53d,carte63d
      @in(amode,70,149,3,1,"   ",amode)
      @in(amode1,70,155,3,1,"  ",amode1)
    CASE 6
      CLR carte,carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
      @charger
    CASE 7
      @sauver
    CASE 12
      @palette
    ENDSELECT
    IF gener=1
      CLIP 0,0,320,200
      SELECT choix
      CASE 8
        @carte(larg,haut,niveau)
      CASE 9
        CLR carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
        @rol1(larg,haut,niveau)
      CASE 10
        CLR carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
        @rol2(larg,haut,niveau)
      CASE 11
        CLR carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
        @zoom(larg,haut,niveau)
      CASE 13
        @carte13d(larg,haut,fact,fact2)
      CASE 14
        @carte23d(larg,haut,fact,fact2,amode,amode1)
      CASE 15
        @carte33d(larg,haut,fact,fact2,fact3,amode,amode1)
      CASE 16
        @carte43d(larg,haut,fact,fact2,amode,amode1)
      CASE 17
        @carte53d(larg,haut,fact,amode,amode1)
      CASE 18
        @carte63d(larg,haut,fact,fact2,fact3,amode,amode1)
      CASE 19
        FOR h=0 TO larg
          FOR hh=0 TO haut
            pos(h,hh)=pos2(h,hh)
          NEXT hh
        NEXT h
        CLR carte,cart13d,carte23d,carte33d,carte43d,carte53d,carte63d
        @carte(larg,haut,niveau)
      ENDSELECT
    ENDIF
    @palmenu
  UNTIL ttt
RETURN
> PROCEDURE generer(l,k,n,f)
  LOCAL h,hh,hhh
  IF gener=1 THEN
    ERASE pos(),pos1(),pos2()
  ENDIF
  DIM pos(l,k),pos1(l,k),pos2(l,k)
  gener=1
  CLR carte,carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
  @efface1
  @palinit
  @temp(l,k,f+3)
  BOX 0,0,l,k
  IF l<150 THEN
    @palaffiche
  ENDIF
  FOR h=0 TO l
    FOR hh=0 TO k
      pos(h,hh)=TRUNC(RAND(f*2.1+1)-f)
      IF pos(h,hh)>=2
        PSET h,hh,pos(h,hh)
      ENDIF
    NEXT hh
  NEXT h
  FOR hhh=0 TO n
    FOR h=1 TO l-1
      FOR hh=1 TO k-1
        pos1(h,hh)=pos(h,hh)+pos(h,hh-1)+pos(h,hh+1)+pos(h+1,hh)+pos(h-1,hh)+pos(h+1,hh-1)+pos(h+1,hh+1)+pos(h-1,hh-1)+pos(h-1,hh+1)
        IF pos1(h,hh)>3
          COLOR 1
          PLOT h,hh
        ENDIF
      NEXT hh
    NEXT h
    SWAP pos1(),pos()
  NEXT hhh
  FOR h=0 TO l
    FOR hh=0 TO k
      pos(h,hh)=pos(h,hh)/(8^n)
      pos2(h,hh)=pos(h,hh)
    NEXT hh
  NEXT h
  @ecart(l,k)
  REPEAT
    SOUND 1,15,#4000+RAND(10),0.1
    WAVE 1,10,15210,6500,1
  UNTIL STRIG(1)<>0
  SOUND 1,0,0,0
  SOUND 2,0,0,0
  SOUND 3,0,0,0
  @efface1
RETURN
> PROCEDURE carte(l,k,n)
  LOCAL h,hh
  @efface1
  @palinit
  DEFFILL 1,2,4
  PBOX 0,0,320,200
  IF carte=1 THEN
    PUT 1,1,carte$
  ELSE
    FOR h=0 TO l
      FOR hh=0 TO k
        COLOR pos(h,hh)+6
        PLOT h,hh
        COLOR 1
        BOX h*15-2,hh*7+k+4,(h+1)*15-2,(hh+1)*7+k+4
        DEFTEXT pos(h,hh)+6,0,0,4
        TEXT h*15,hh*7+k+10,ABS(TRUNC(pos(h,hh)))
      NEXT hh
    NEXT h
    GET 1,1,l,k,carte$
    carte=1
  ENDIF
  REPEAT
  UNTIL STRIG(1)<>0
  @efface1
RETURN
> PROCEDURE carte13d(l,k,fact,fact2)
  LOCAL h,hh
  CLS
  @palinit
  IF carte13d=1 THEN
    PUT 1,5,carte13d$
    PAUSE 10
  ELSE
    @boite1
    FOR hh=1 TO k-1
      FOR h=0 TO ll-1
        COLOR pos(h,hh)+6
        LINE h*fact+10,90-fact2*(pos(h,hh)-hh),(h+1)*fact+10,90-fact2*(pos(h+1,hh)-hh)
      NEXT h
    NEXT hh
    GET 1,5,319,199,carte13d$
    carte13d=1
  ENDIF
  REPEAT
  UNTIL STRIG(1)<>0
  @efface1
RETURN
> PROCEDURE carte23d(l,k,fact,fact2,mode,mode1)
  LOCAL h,hh
  CLS
  @palinit
  IF carte23d=1 THEN
    PUT 1,5,carte23d$
    PAUSE 10
  ELSE
    @boite1
    FOR hh=1 TO k-2
      FOR h=0 TO l-2
        DIM x(5),y(5)
        @couleur(mode,mode1)
        x(0)=h*fact+10
        y(0)=90-fact2*(pos(h,hh)-hh)
        x(1)=(h+1)*fact+10
        y(1)=90-fact2*(pos(h+1,hh)-hh)
        x(2)=(h+2)*fact+10
        y(2)=90-fact2*(pos(h+2,hh)-hh)
        x(5)=h*fact+10
        y(5)=90-fact2*(pos(h,hh+1)-(hh+1))
        x(4)=(h+1)*fact+10
        y(4)=90-fact2*(pos(h+1,hh+1)-(hh+1))
        x(3)=(h+2)*fact+10
        y(3)=90-fact2*(pos(h+2,hh+1)-(hh+1))
        POLYFILL 6,x(),y()
        IF mode1=1
          POLYLINE 6,x(),y()
        ENDIF
        ERASE x(),y()
      NEXT h
    NEXT hh
    GET 1,5,319,199,carte23d$
    carte23d=1
  ENDIF
  REPEAT
  UNTIL STRIG(1)<>0
  @efface1
RETURN
> PROCEDURE carte33d(l,k,fact,fact2,fact3,mode,mode1)
  LOCAL h,hh
  CLS
  @palinit
  IF carte33d=1 THEN
    PUT 1,5,carte33d$
    PAUSE 10
  ELSE
    @boite1
    CLIP 0,5,315,201
    FOR hh=1 TO k-2
      FOR h=0 TO l-2
        DIM x(5),y(5)
        @couleur(mode,mode1)
        x(0)=h*fact+10
        y(0)=90-fact2*5*COSQ(fact3*pos(h,hh))+2*hh
        x(1)=(h+1)*fact+10
        y(1)=90-fact2*5*COSQ(fact3*pos(h+1,hh))+2*hh
        x(2)=(h+2)*fact+10
        y(2)=90-fact2*5*COSQ(fact3*pos(h+2,hh))+2*hh
        x(5)=h*fact+10
        y(5)=90-fact2*5*COSQ(fact3*pos(h,hh+1))+2*(hh+1)
        x(4)=(h+1)*fact+10
        y(4)=90-fact2*5*COSQ(fact3*pos(h+1,hh+1))+2*(hh+1)
        x(3)=(h+2)*fact+10
        y(3)=90-fact2*5*COSQ(fact3*pos(h+2,hh+1))+2*(hh+1)
        POLYFILL 6,x(),y()
        IF mode1=1
          POLYLINE 6,x(),y()
        ENDIF
        ERASE x(),y()
      NEXT h
    NEXT hh
    GET 1,5,319,199,carte33d$
    carte33d=1
  ENDIF
  REPEAT
  UNTIL STRIG(1)<>0
  @efface1
RETURN
> PROCEDURE carte43d(l,k,fact,fact2,mode,mode1)
  LOCAL h,hh
  CLS
  @palinit
  IF carte43d=1 THEN
    PUT 1,5,carte43d$
    PAUSE 10
  ELSE
    @boite1
    FOR hh=2 TO k-2
      FOR h=0 TO l-2
        DIM x(5),y(5)
        @couleur(mode,mode1)
        IF hh=k-2
          DEFFILL 0,2,8
        ENDIF
        x(0)=h*fact+10
        y(0)=140-fact2*(pos(h,hh)-hh)
        x(1)=(h+1)*fact+10
        y(1)=140-fact2*(pos(h+1,hh)-hh)
        x(2)=x(1)
        y(2)=200
        x(3)=x(0)
        y(3)=200
        POLYFILL 4,x(),y()
        IF mode1=1
          POLYLINE 4,x(),y()
        ENDIF
        ERASE x(),y()
      NEXT h
    NEXT hh
    GET 1,5,319,199,carte43d$
    carte43d=1
  ENDIF
  REPEAT
  UNTIL STRIG(1)<>0
  @efface1
RETURN
> PROCEDURE carte53d(l,k,fact,mode,mode1)
  LOCAL h,hh,kk
  CLS
  @palinit
  IF carte53d=1 THEN
    PUT 1,5,carte53d$
    PAUSE 10
  ELSE
    @boite1
    FOR hh=1 TO k-1
      FOR h=0 TO ll-1
        COLOR pos(h,hh)+6
        LINE h*fact+10,90-fact2*(pos(h,hh)-hh),(h+1)*fact+10,90-fact2*(pos(h+1,hh)-hh)
        FOR kk=h*fact+10 TO (h+1)*fact+10
          c=pos(h,hh)+RAND(mode1+1)-mode1
          IF c>=15
            c=RAND(2)+14
          ENDIF
          IF c<=1
            c=RAND(2)+1
          ENDIF
          COLOR c
          IF hh=k-1
            COLOR 0
          ENDIF
          de=(fact2*(pos(h,hh)-pos(h+1,hh))/fact)*(kk-h*fact-10)
          LINE kk,90-fact2*(pos(h,hh)-hh)+de,kk,200
        NEXT kk
      NEXT h
    NEXT hh
    GET 1,5,319,199,carte53d$
    carte53d=1
  ENDIF
  REPEAT
  UNTIL STRIG(1)<>0
  @efface1
RETURN
> PROCEDURE carte63d(l,k,fact,fact2,fact3,mode,mode1)
  LOCAL h,hh
  CLS
  @palinit
  IF carte63d=1 THEN
    PUT 1,5,carte63d$
    PAUSE 10
  ELSE
    @boite1
    FOR hh=1 TO k-1
      FOR h=0 TO ll-1
        COLOR pos(h,hh)+6
        FOR kk=h*fact+10 TO (h+1)*fact+10
          c=pos(h,hh)+6+RAND(mode1+1)-mode1
          IF c>=15
            c=RAND(2)+14
          ENDIF
          IF c<=1
            c=RAND(2)+1
          ENDIF
          COLOR c
          IF hh=haut-1
            COLOR 0
          ENDIF
          de=((fact2*fact4*COSQ(fact3*pos(h,hh))-fact2*fact4*COSQ(pos(h+1,hh)))/fact)*(kk-h*fact-10)
          LINE kk,90-fact2*fact4*COSQ(fact3*pos(h,hh))+fact2*hh+fact2*fact4*COSQ(fact3*de),kk,200
        NEXT kk
      NEXT h
    NEXT hh
    GET 1,5,319,199,carte63d$
    carte63d=1
  ENDIF
  REPEAT
  UNTIL STRIG(1)<>0
  @efface1
RETURN
> PROCEDURE translation1(translation,l,k,n)
  LOCAL h,hh
  CLR carte,carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
  FOR h=0 TO l
    FOR hh=0 TO k
      pos(h,hh)=pos(h,hh)+translation
    NEXT hh
  NEXT h
  @carte(larg,haut,niveau)
RETURN
> PROCEDURE couleur(mode,mode1)
  IF mode=0 OR mode=1 THEN
    c=pos(h+1,hh)+6
    m=8
    SELECT c
    CASE 16 TO 20
      m=8
    CASE 21 TO 25
      m=7
    CASE 26 TO 35
      m=6
    CASE 36 TO 40
      m=4
    CASE 41 TO 45
      m=1
    ENDSELECT
  ENDIF
  '
  IF pos(h+1,hh)+6>15
    c=15
  ENDIF
  IF pos(h+1,hh)+6<1
    c=1
  ENDIF
  '
  IF mode=1
    IF pos(h,hh)>pos(h+1,hh) AND pos(h+1,hh)+6>16
      c=14
    ENDIF
    IF pos(h,hh)>pos(h+1,hh+1) AND pos(h+1,hh+1)+6>20
      c=13
    ENDIF
  ENDIF
  '
  DEFFILL c,2,m
  IF mode1=1
    COLOR c-1
  ENDIF
RETURN
> PROCEDURE zoom(l,k,n)
  LOCAL h,hh
  DEFFILL 1,2,4
  COLOR 1
  IF carte=1
    @efface1
    @palinit
    PAUSE 20
    HIDEM
    @screen(1)
    @screen(2)
    REPEAT
      PBOX 0,0,320,200
      PUT 1,1,carte$
      BOX MOUSEX,MOUSEY,MOUSEX+l/2,MOUSEY+k/2
      xx=MOUSEX
      yy=MOUSEY
      @screen(2)
      VSYNC
      VSYNC
    UNTIL STRIG(1)<>0 AND xx<l/2 AND yy<k/2
    @screen(0)
    '
    FOR h=0 TO l STEP 2
      FOR hh=0 TO k STEP 2
        pos(h,hh)=pos(xx+h/2,yy+hh/2)
      NEXT hh
    NEXT h
    FOR h=1 TO l-1 STEP 2
      FOR hh=0 TO k STEP 2
        pos(h,hh)=(pos(h-1,hh)+pos(h+1,hh))*0.5
      NEXT hh
    NEXT h
    FOR h=0 TO l
      FOR hh=1 TO k-1 STEP 2
        pos(h,hh)=(pos(h,hh-1)+pos(h,hh+1))*0.5
      NEXT hh
    NEXT h
    choix=8
    CLR carte,carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
    @carte(l,k,n)
  ENDIF
RETURN
> PROCEDURE rol1(l,k,n)
  LOCAL h,hh
  CLR carte,carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
  FOR hh=1 TO k
    FOR h=1 TO l
      pos1(h-1,hh)=pos(h,hh)
    NEXT h
    pos1(l,hh)=pos(0,hh)
  NEXT hh
  SWAP pos1(),pos()
  @carte(l,k,n)
RETURN
> PROCEDURE rol2(l,k,n)
  LOCAL h,hh
  CLR carte,carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
  FOR h=1 TO l
    FOR hh=1 TO k
      pos1(h,hh-1)=pos(h,hh)
    NEXT hh
    pos1(h,haut)=pos(h,0)
  NEXT h
  SWAP pos1(),pos()
  @carte(l,k,n)
RETURN
> PROCEDURE charger
  LOCAL h,hh
  SETCOLOR 1,7,7,7
  ON ERROR GOSUB menu
  FILESELECT "a:\*.map",nom$,nom$
  DEFFILL 11,2,8
  PBOX 86,176,86,176
  OPEN "u",#1,nom$
  INPUT #1,larg,haut,fact,fact2,fact3,amode,amode1
  IF gener=1
    ERASE pos(),pos1(),pos2()
    CLR carte,carte13d,carte23d,carte33d,carte43d,carte53d,carte63d
  ENDIF
  DIM pos(larg,haut),pos1(larg,haut),pos2(larg,haut)
  FOR h=0 TO larg
    FOR hh=0 TO haut
      INPUT #1,pos(h,hh)
      pos1(h,hh)=pos(h,hh)
      pos2(h,hh)=pos(h,hh)
    NEXT hh
  NEXT h
  CLOSE #1
  gener=1
  @palmenu
  PUT 1,1,menu$
  DEFTEXT 1,0,0,4
  TEXT 67,39,larg
  TEXT 67,45,haut
  TEXT 67,55,facteur
  TEXT 67,60,niveau
  TEXT 67,80,translation
  TEXT 74,114,fact
  TEXT 74,120,fact2
  TEXT 74,134,fact3
  TEXT 74,149,amode
  TEXT 74,155,amode1
  GET 1,1,319,199,menu$
RETURN
> PROCEDURE sauver
  LOCAL a$
  CLIP 0,0,320,200
  IF gener=1
    SETCOLOR 1,7,7,7
    @efface1
    CLS
    REPEAT
      a$=INKEY$
      DEFTEXT 1,0,0,4
      TEXT 90,50,"sauvegarde de la carte"
      TEXT 90,60," en fichier MAP : o/n "
    UNTIL a$<>""
    IF a$="o"
      TEXT 110,80,"sous quel nom ?"
      LOCATE 14,12
      INPUT "",nom$
      nom$=RIGHT$(nom$,8)+".MAP"
      LOCATE 14,12
      PRINT nom$
      PAUSE 50
      @efface1
      PUT 1,1,menu$
      DEFFILL 11,2,8
      PBOX 86,187,86,187
      OPEN "o",#1,nom$
      WRITE #1,larg,haut,fact,fact2,fact3,amode,amode1
      ERASE pos2()
      DIM pos2&(larg,haut)
      FOR h=0 TO larg
        FOR hh=0 TO haut
          pos2&(h,hh)=TRUNC(pos(h,hh))
        NEXT hh
      NEXT h
      FOR h=0 TO larg
        FOR hh=0 TO haut
          WRITE #1,pos2&(h,hh)
        NEXT hh
      NEXT h
      CLOSE #1
    ENDIF
    @palmenu
    SETCOLOR 1,7,7,7
    @efface1
    REPEAT
      a$=INKEY$
      DEFTEXT 1,0,0,4
      TEXT 90,50,"sauvegarde de la carte"
      TEXT 90,60," en fichier IMG : o/n "
    UNTIL a$<>""
    IF a$="o"
      REPEAT
        TEXT 95,80,"quelle image : nø.3d"
        a$=INKEY$
      UNTIL a$>"0" AND a$<"7"
      SELECT a$
      CASE "1"
        IF carte13d=1
          PUT 1,1,carte13d$
          img=1
        ENDIF
      CASE "2"
        IF carte23d=1
          PUT 1,1,carte23d$
          img=1
        ENDIF
      CASE "3"
        IF carte33d=1
          img=1
          PUT 1,1,carte33d$
        ENDIF
      CASE "4"
        IF carte43d=1
          PUT 1,1,carte43d$
          img=1
        ENDIF
      CASE "5"
        IF carte53d=1
          PUT 1,1,carte53d$
          img=1
        ENDIF
      CASE "6"
        IF carte63d=1
          PUT 1,1,carte63d$
          img=1
        ENDIF
      ENDSELECT
      @palinit
      GET 1,4,315,195,image$
      IF img=1
        DEFTEXT 1,0,0,4
        TEXT 100,10,"sous quel nom ?"
        LOCATE 14,4
        INPUT "",nom$
        nom$=RIGHT$(nom$,8)+".IMG"
        LOCATE 14,4
        PRINT nom$
        PAUSE 50
        PUT 1,4,image$
        BSAVE nom$,XBIOS(2),32000
      ELSE
        TEXT 100,100,"pas d'image a ce nø"
        PAUSE 50
      ENDIF
    ENDIF
    CLS
    @palmenu
  ENDIF
RETURN
> PROCEDURE in(aa,x,y,cc,dd,text$,VAR bb)
  LOCAL t$,a$
  DEFLINE 1,1
  COLOR 0
  BOX boite(choix,1),boite(choix,2)+2,boite(choix,3),boite(choix,4)+2
  DEFTEXT 1,0,0,4
  TEXT x,y,text$
  PAUSE 10
  REPEAT
    a$=INKEY$
    IF a$<>""
      t$=t$+a$
    ENDIF
    t$=RIGHT$(t$,cc)
    TEXT x,y,t$
  UNTIL STRIG(1)<>0
  IF t$<>""
    bb=VAL(t$)
  ELSE
    bb=aa
  ENDIF
  TEXT x,y,STR$(bb)
  IF dd=1
    GET 1,1,319,199,menu$
  ENDIF
RETURN
> PROCEDURE screen(a%)
  IF a%=1
    DIM adr%(9000)
    old%=XBIOS(2)
    pbase%=XBIOS(2)
    lbase%=V:adr%(0)
  ENDIF
  IF a%=2
    SWAP lbase%,pbase%
    VOID XBIOS(5,L:pbase%,L:lbase%,-1)
  ENDIF
  IF a%=0
    VOID XBIOS(5,L:old%,L:old%,-1)
    ERASE adr%()
  ENDIF
RETURN
> PROCEDURE temp(l,k,n)
  LOCAL t,t$
  DEFLINE 1,1
  COLOR 1
  BOX 3,5,315,13
  t=TRUNC((0.6*l*k*(n+3)/200))
  t$="temp de generation evalue a : "+STR$(t)+"  secondes"
  DEFTEXT 1,0,0,4
  TEXT 5,10,t$
  PAUSE 50
  CLS
RETURN
> PROCEDURE ecart(l,k)
  LOCAL h,hh,max,min
  FOR h=0 TO l
    FOR hh=0 TO k
      IF pos(h,hh)>max THEN
        max=pos(h,hh)
      ENDIF
      IF pos(h,hh)<min THEN
        min=pos(h,hh)
      ENDIF
    NEXT hh
  NEXT h
  COLOR 1
  DEFFILL 0
  DEFLINE 1,1
  DEFTEXT 1,0,0,4
  PBOX 3,50,125,90
  BOX 3,50,125,90
  TEXT 5,60,90,"le minimun est ... "
  TEXT 95,60,TRUNC(min)
  TEXT 5,70,90,"le maximun est ... "
  TEXT 95,70,TRUNC(max)
  TEXT 5,80,90,"ecart maxi est ... "
  TEXT 95,80,TRUNC(max-min)
RETURN
> PROCEDURE efface1
  LOCAL h
  COLOR 0
  GRAPHMODE 2
  FOR h=0 TO 200
    DEFLINE RAND(6)+1,1,0,0
    LINE 0,h,320,h
  NEXT h
  GRAPHMODE 1
  FOR h=0 TO 200
    DEFLINE 1,1,0,0
    LINE 0,h,320,h
  NEXT h
RETURN
> PROCEDURE boite1
  IF larg*fact>300 THEN
    BOX 1,5,315,199
    CLIP 1,5,315,201
    ll=310/fact
  ELSE
    BOX 1,5,larg*fact+20,201
    ll=larg
  ENDIF
RETURN
> PROCEDURE palinit
  LOCAL h
  IF palette=1 THEN
    FOR h=0 TO 15
      VSETCOLOR h,palette(h)
    NEXT h
  ELSE
    READ pal0
    FOR h&=0 TO 15
      READ a,b,c
      SETCOLOR h&,a,b,c
    NEXT h&
  ENDIF
RETURN
> PROCEDURE palmenu
  RESTORE pal
  FOR h&=0 TO 15
    READ a,b,c
    SETCOLOR h&,a,b,c
  NEXT h&
RETURN
> PROCEDURE palaffiche
  LOCAL h
  FOR h=0 TO 15
    COLOR 1
    DEFFILL h,2,8
    BOX 150+10*h,0,150+10*(h+1),31
    PBOX 151+10*h,1,149+10*(h+1),30
  NEXT h
RETURN
> PROCEDURE palette
  LOCAL h
  @efface1
  @palinit
  @palaffiche
  COLOR 1
  DEFTEXT 1,0,0,4
  LINE 0,40,320,40
  FOR h=2 TO 15
    COLOR 1
    BOX 20,40+8*h,50,48+8*h
    BOX 50,40+8*h,80,48+8*h
    BOX 80,40+8*h,120,48+8*h
    BOX 130,40+8*h,160,48+8*h
    TEXT 135,46+8*h,h
    aa=pal(h,0)
    bb=pal(h,1)
    cc=pal(h,2)
    @in(pal(h,0),25,40+8*h+6,2,0,"..",aa)
    @in(pal(h,1),55,40+8*h+6,2,0,"..",bb)
    @in(pal(h,2),85,40+8*h+6,2,0,"..",cc)
    pal(h,0)=aa
    pal(h,1)=bb
    pal(h,2)=cc
    palette(h)=pal(h,0)*256+pal(h,1)*16+pal(h,2)
    VSETCOLOR h,palette(h)
  NEXT h
  palette(1)=6*256
  REPEAT
  UNTIL STRIG(1)<>0
  palette=1
  @efface1
RETURN
'
DATA 5,21,89,62
DATA 5,69,89,84
DATA 5,104,89,123
DATA 5,123,89,138
DATA 5,138,89,161
DATA 34,169,89,181
DATA 34,181,89,193
DATA 99,36,194,64
DATA 99,70,194,98
DATA 99,104,194,132
DATA 99,138,194,166
DATA 99,172,194,183
DATA 200,36,310,56
DATA 200,62,310,82
DATA 200,88,310,108
DATA 200,114,310,134
DATA 200,140,310,160
DATA 200,166,310,183
DATA 99,189,310,197
pal:
DATA 0,0,0,3,1,3,2,1,3,1,2,2,2,0,2,0,0,3,1,2,5,1,4,7,2,2,3,3,3,4,6,1,0,3,2,3,0,3,7,4,2,2,3,4,2,0,3,2
pal0:
DATA 0,0,0,0,3,7,1,4,7,6,7,5,6,6,6,2,6,5,6,6,5,0,5,4,1,5,2,0,4,2,0,3,2,6,5,4,0,2,2,7,6,5,4,3,2,0,0,6
