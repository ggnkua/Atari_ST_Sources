' GFA Systemtechnik GmbH, 1986
'
'
'
' ******************************************************
' * Beispiel 1 fr den Aufruf der formatierten Eingabe *
' ******************************************************
Cls
Print "Bitte geben Sie etwas ein :"
Print
Print "1. Sie mssen etwas eingeben"
Print "2. Sie k”nnen nur Zahlen, einmal den Punkt und am Anfang ein Minus setzen"
Print "3. Sie k”nnen die Routine mit der ESC-Taste verlassen"
' GOSUB Form_eingabe(Zeile, Spalte, Max. Eingabel„nge, (0/1) 1=Drcken der ENTER-Taste an der ersten Stelle nicht erlaubt, (0/1) 1=Nur Eingabe von (-,1,2,3,4,5,6,7,8,9,0,.) erlaubt, (0/1) 1=Ausstieg durch Drcken der ESC-Taste m”glich
@Form_eingabe(40,20,11,1,1,1)
Cls
If Eingabe$=Chr$(27)
  Print "Ihre Eingabe lautete ---> ESC <---"
Else
  Print "Ihre Eingabe lautete ---> ";Eingabe$;" <---"
Endif
Pause 50
' ******************************************************
' * Beispiel 2 fr den Aufruf der formatierten Eingabe *
' ******************************************************
Cls
Print "Bitte geben Sie etwas ein :"
Print
Print "1. Sie k”nnen sofort ENTER drcken"
Print "2. Sie k”nnen Buchstaben und Zahlen eingeben"
Print "3. Sie k”nnen die Routine nicht mit der ESC-Taste verlassen"
' GOSUB Form_eingabe(Zeile, Spalte, Max. Eingabel„nge, (0/1) 0=Drcken der ENTER-Taste an der ersten Stelle erlaubt, (0/1) 0=Jede Eingabe erlaubt, (0/1) 0=Ausstieg durch Drcken der ESC-Taste nicht m”glich
@Form_eingabe(60,8,17,0,0,0)
Cls
If Eingabe$=Chr$(27)
  Print "Ihre Eingabe lautete ---> ESC <---"
Else
  Print "Ihre Eingabe lautete ---> ";Eingabe$;" <---"
Endif
' ********************************
' *  Formatierte Eingabe-Routine *
' ********************************
'
Procedure Form_eingabe(X,Y,L,Enter,Num,Esc)
  Local Punkte$,Hc$,Flag!
  Punkte$=String$(L-1,".")+"+"
  Eingabe$=""
  Print At(X,Y);"_";Left$(Eingabe$+Punkte$,L);
  Repeat
    Repeat
      Hc$=Inkey$
    Until Len(Hc$)=1
    If Hc$<" "
      On Instr(Chr$(13)+Chr$(8)+Chr$(27),Hc$) Gosub Return,Bs,Esc
    Else
      If Len(Eingabe$)<L
        If Num
          @Num
        Else
          Eingabe$=Eingabe$+Hc$
        Endif
      Else
        @Bell
      Endif
    Endif
    Print At(X,Y);Left$(Eingabe$+"_"+Punkte$,L);
  Until Flag!
Return
Procedure Return
  If Eingabe$="" And Enter=1
    @Bell
  Else
    Print At(X,Y);Eingabe$+Space$(L-Len(Eingabe$));
    Flag!=1
  Endif
Return
Procedure Bs
  If Len(Eingabe$)
    Eingabe$=Left$(Eingabe$,Len(Eingabe$)-1)
  Else
    @Bell
  Endif
Return
Procedure Esc
  If Esc=1
    Eingabe$=Chr$(27)
    Flag!=1
  Endif
Return
Procedure Num
  If Instr("0123456789",Hc$) Or (Hc$="-" And Eingabe$="") Or (Hc$="." And Instr(Eingabe$,".")=0)
    Eingabe$=Eingabe$+Hc$
  Else
    @Bell
  Endif
Return
Procedure Bell
  Out 2,7
Return
