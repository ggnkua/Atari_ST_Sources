' *******************************************
' *                                         *
' *      Icon-Packer in GFA-Basic 3.x       *
' *         Autor : K.-L. Dietsch           *
' *                                         *
' *        (c) 1992 MAXON Computer          *
' *******************************************
'
max_icon%=255 ! maximale Anzahl der Icons
icb%=34       ! L„nge des ICONBLOCKs 34/36
color%=0      ! 1 bis 15 = Farbe „ndern nach
'
DIM info%(max_icon%,5)
' ,0 = welche Maske
' ,1 = anzahl der Daten fr eine Maske
' ,2 = Farben + Buchstabe des Icons
' ,3 = x-position des Buchstaben
' ,4 = y-position des Buchstaben
' ,5 = test auf Gleichheit
'
DIM rsc|(64*1024)
DIM daten%(32*max_icon%-1)
DIM maske%(32*max_icon%-1)
rsc%=VARPTR(rsc|(0))
daten%=VARPTR(daten%(0))
maske%=VARPTR(maske%(0))
'
pfad$=CHR$(65+GEMDOS(25))+":\*.RSC"
filename$=""
n_anz%=0
'
REPEAT
  x%=@load_rsc(rsc%,pfad$,filename$)
  IF x%=2 ! laden erfolgreich
    PRINT AT(1,1);"Icons entnehmen",,
    i%=@take_icons(rsc%,daten%,maske%,n_anz%,info%())
  ENDIF
UNTIL x%=0 OR i%=0 ! Abbruch oder genug Icons
'
IF n_anz%=0
  PRINT AT(1,1);"Kein Icon geladen !!!!!!!"
  END
ENDIF
'
ALERT 2,"Soll gepackt werden ?",2," Nein |  Ja  ",pac%
DEC pac%              ! 0 = nicht ... , 1 = packen
'
IF pac%=1
  PRINT AT(1,1);"gleich Icons suchen",
  GOSUB suche_gleiche_masken(pac%,rsc%,n_anz%,maske%,info%())
ELSE
  GOSUB init_info(n_anz%,info%())
ENDIF
'
i%=1
REPEAT
  PRINT AT(1,1);"RSC berechnen",,
  s_anz%=@make_resource(pac%,rsc%,daten%,maske%,icb%,color%,n_anz%,size%,info%())
  IF pac%=1
    filename$="ICON_PAC.RSC"
  ELSE
    filename$="ICON_N_"+STR$(i%)+".RSC"
  ENDIF
  '
  REPEAT
    x%=@save_rsc(rsc%,size%,pfad$,filename$)
  UNTIL x%=1
  '
  INC i%
  SUB n_anz%,s_anz%
  IF n_anz%>0 AND pac%=0
    FOR x%=0 TO n_anz%-1
      FOR a%=0 TO 4
        info%(x%,a%)=info%(x%+s_anz%,a%)
      NEXT a%
    NEXT x%
    '
    BMOVE daten%+s_anz%*128,daten%,n_anz%*128
    BMOVE maske%+s_anz%*128,maske%,n_anz%*128
  ENDIF
UNTIL n_anz%=0 OR pac%=1
'
END
'
FUNCTION load_rsc(rsc%,VAR pfad$,filename$)
  LOCAL x%,i%,dummy$
  '
  dummy$=filename$
  PRINT AT(1,1);"Icons laden",,
  x%=FSEL_INPUT(pfad$,dummy$,i%)
  IF x% AND i%=1
    x%=RINSTR(pfad$,"\")
    filename$=dummy$
    dummy$=LEFT$(pfad$,x%)+filename$
    '
    IF EXIST(dummy$)=FALSE
      IF dummy$<>""
        ALERT 1,"Nicht vorhanden !",1," OK ",i%
      ENDIF
    ELSE
      OPEN "I",#1,dummy$
      i%=LOF(#1)
      BGET #1,rsc%,i%
      CLOSE #1
      RETURN 2
    ENDIF
  ELSE
    IF i%=0
      RETURN 0
    ENDIF
  ENDIF
  '
  RETURN 1
ENDFUNC
'
FUNCTION save_rsc(rsc%,size%,VAR pfad$,filename$)
  LOCAL x%,i%,dummy$
  '
  dummy$=filename$
  PRINT AT(1,1);"Icons speichern",,
  x%=FSEL_INPUT(pfad$,dummy$,i%)
  IF x% AND i%=1
    x%=RINSTR(pfad$,"\")
    filename$=dummy$
    dummy$=LEFT$(pfad$,x%)+filename$
    IF dummy$<>""
      i%=1
      IF EXIST(dummy$)=TRUE
        ALERT 0,"Datei ist schon vorhanden !|    šberschreiben ?",2," JA |NEIN",i%
      ENDIF
      IF i%=1
        OPEN "O",#1,dummy$
        BPUT #1,rsc%,size%
        CLOSE #1
        '
        RETURN 1
      ENDIF
    ENDIF
  ELSE
    IF i%=0
      RETURN 1
    ENDIF
  ENDIF
  '
  RETURN 0
ENDFUNC
'
FUNCTION take_icons(rsc%,daten%,maske%,VAR anz%,info%())
  LOCAL i%
  LOCAL anz_obj%,obj_start%,obj_adr%,icon_adr%
  '
  anz_obj%=CARD{rsc%+20}
  obj_start%=rsc%+CARD{rsc%+2}
  ' alle Objekte untersuchen
  FOR i%=0 TO anz_obj%-1
    obj_adr%=obj_start%+24*i%
    ' Icon ?
    IF CARD{obj_adr%+6}=31
      icon_adr%=rsc%+LONG{obj_adr%+12}
      IF CARD{icon_adr%+22}=32 AND CARD{icon_adr%+24}=32
        BMOVE rsc%+LONG{icon_adr%},maske%+anz%*128,128
        BMOVE rsc%+LONG{icon_adr%+4},daten%+anz%*128,128
        info%(anz%,2)=CARD{icon_adr%+12}
        info%(anz%,3)=CARD{icon_adr%+14}
        info%(anz%,4)=CARD{icon_adr%+16}
        INC anz%
        IF anz%=max_icon%
          RETURN 0
        ENDIF
      ENDIF
    ENDIF
  NEXT i%
  '
  RETURN 1
ENDFUNC
'
PROCEDURE init_info(anz%,VAR info%())
  LOCAL i%
  '
  FOR i%=0 TO anz%-1
    info%(i%,0)=i%
    info%(i%,1)=-1
  NEXT i%
RETURN
'
PROCEDURE suche_gleiche_masken(pac%,rsc%,anz%,maske%,VAR info%())
  LOCAL i%,y%,a%,test%,gleich!
  '
  FOR i%=0 TO anz%-1
    test%=0
    FOR y%=0 TO 63
      ADD test%,CARD{maske%+128*i%+2*y%}
    NEXT y%
    info%(i%,5)=test%
    info%(i%,0)=-1
    info%(i%,1)=-1
  NEXT i%
  FOR i%=0 TO anz%-2
    IF info%(i%,1)=-1
      info%(i%,0)=i%
      info%(i%,1)=1
      FOR a%=i%+1 TO anz%-1
        IF info%(i%,5)=info%(a%,5)
          gleich!=TRUE
          FOR y%=0 TO 31
            IF LONG{maske%+128*i%+4*y%}<>LONG{maske%+128*a%+4*y%}
              gleich!=FALSE
            ENDIF
          NEXT y%
          IF gleich!
            info%(a%,0)=i%
            info%(a%,1)=0
            INC info%(i%,1)
          ENDIF
        ENDIF
      NEXT a%
    ENDIF
  NEXT i%
  i%=anz%-1
  IF info%(i%,1)=-1
    info%(i%,0)=i%
    info%(i%,1)=1
  ENDIF
RETURN
'
FUNCTION make_resource(pac%,rsc%,daten%,maske%,icb%,color%,anz%,VAR size%,info%())
  LOCAL i%,x%,y%,a%,icon%,obj%
  '
  CARD{rsc%}=1         ! Version
  CARD{rsc%+10}=0      ! Freestring-Index-Tabelle
  CARD{rsc%+12}=36     ! String-Adresse
  IF pac%=1
    CARD{rsc%+CARD{rsc%+12}}=0        ! Strings
    CARD{rsc%+14}=CARD{rsc%+12}+2     ! Images
    x%=64+2
  ELSE
    y%=MIN(anz%,208)
    ADD y%,MOD(y%,2)
    FOR i%=0 TO y%-1
      BYTE{rsc%+CARD{rsc%+12}+i%}=0   ! Strings
    NEXT i%
    x%=64+y%
    CARD{rsc%+14}=CARD{rsc%+12}+y%   ! Images
  ENDIF
  CARD{rsc%+16}=0      ! Free-Images
  CARD{rsc%+22}=1      ! Anzahl der B„ume
  CARD{rsc%+24}=0      ! Anzahl der Tedinfos
  CARD{rsc%+28}=0      ! Anzahl der Bitblks
  CARD{rsc%+30}=0      ! Anzahl der Strings
  CARD{rsc%+32}=0      ! Anzahl der Bilder
  CARD{rsc%+36}=0      ! Strings
  '
  y%=24+icb%+128
  a%=CARD{rsc%+14}
  i%=0
  REPEAT
    IF info%(i%,1)=0 AND pac%=1
      EXIT IF x%+y%>64*1024
      info%(i%,1)=info%(info%(i%,0),1)
    ELSE
      EXIT IF x%+y%+128>64*1024
      BMOVE maske%+128*i%,rsc%+a%,128
      info%(i%,1)=a%
      ADD a%,128
      ADD x%,128
    ENDIF
    BMOVE daten%+128*i%,rsc%+a%,128
    info%(i%,0)=a%
    ADD a%,128
    ADD x%,y%
    INC i%
  UNTIL i%=anz%
  anz%=i%
  '
  CARD{rsc%+20}=anz%+1 ! Anzahl der Objekte
  CARD{rsc%+6}=a%      ! Iconblock
  CARD{rsc%+2}=CARD{rsc%+6}+icb%*anz% ! Objekt
  CARD{rsc%+18}=CARD{rsc%+2}+24*(anz%+1) ! Tree
  CARD{rsc%+26}=anz% ! Anzahl der Iconblocks
  CARD{rsc%+4}=CARD{rsc%+2} ! TEDINFO
  CARD{rsc%+8}=CARD{rsc%+6} ! Bitblock
  LONG{rsc%+CARD{rsc%+18}}=CARD{rsc%+2} ! Tree
  CARD{rsc%+34}=CARD{rsc%+18}+4 ! L„nge
  '
  obj%=CARD{rsc%+2}
  CARD{rsc%+obj%}=&HFFFF
  CARD{rsc%+obj%+2}=1
  CARD{rsc%+obj%+4}=anz%
  CARD{rsc%+obj%+6}=20
  CARD{rsc%+obj%+8}=0
  CARD{rsc%+obj%+10}=16
  LONG{rsc%+obj%+12}=135424
  CARD{rsc%+obj%+16}=0
  CARD{rsc%+obj%+18}=0
  CARD{rsc%+obj%+20}=79
  CARD{rsc%+obj%+22}=1+3*(DIV(anz%,15)+SGN(MOD(anz%,15)))
  '
  FOR i%=0 TO anz%-1
    obj%=CARD{rsc%+2}+(i%+1)*24
    icon%=CARD{rsc%+6}+i%*icb%
    ' object
    IF i%=anz%-1
      CARD{rsc%+obj%}=0
    ELSE
      CARD{rsc%+obj%}=i%+2
    ENDIF
    CARD{rsc%+obj%+2}=&HFFFF
    CARD{rsc%+obj%+4}=&HFFFF
    CARD{rsc%+obj%+6}=31
    CARD{rsc%+obj%+8}=0
    CARD{rsc%+obj%+10}=0
    LONG{rsc%+obj%+12}=icon%
    CARD{rsc%+obj%+16}=2+5*MOD(i%,15)
    CARD{rsc%+obj%+18}=1+3*DIV(i%,15)
    CARD{rsc%+obj%+20}=9
    CARD{rsc%+obj%+22}=2050
    ' iconblk
    LONG{rsc%+icon%}=info%(i%,1)
    LONG{rsc%+icon%+4}=info%(i%,0)
    LONG{rsc%+icon%+8}=CARD{rsc%+12}+i%*(1-pac%)
    ' Farbe + Buchstabe
    IF color%=0 ! alles so lassen
      CARD{rsc%+icon%+12}=info%(i%,2)
    ELSE
      BYTE{rsc%+icon%+12}=SHL(color%,4)
      BYTE{rsc%+icon%+13}=BYTE(info%(i%,2))
    ENDIF
    CARD{rsc%+icon%+14}=info%(i%,3)
    CARD{rsc%+icon%+16}=info%(i%,4)
    CARD{rsc%+icon%+18}=20
    CARD{rsc%+icon%+20}=0
    CARD{rsc%+icon%+22}=32
    CARD{rsc%+icon%+24}=32
    CARD{rsc%+icon%+26}=0
    CARD{rsc%+icon%+28}=32
    CARD{rsc%+icon%+30}=72
    CARD{rsc%+icon%+32}=8
    IF icb%=36
      CARD{rsc%+icon%+34}=0
    ENDIF
  NEXT i%
  size%=CARD{rsc%+34}
  RETURN anz%
ENDFUNC
