' ******************************************************************************
' *                                                                            *
' *                    Funktionsinterpreter Version1.3a                        *
' *                              by                                            *
' *                          Dirk  Wente                                       *
' *                          Hinterstr. 1                                      *
' *                          475 Unna - Lnern                                 *
' *                                                                            *
' *                          ½  DIWE 9/88                                      *
' *                                                                            *
' ******************************************************************************
'
'
' +++++   Reservierte Variable und Felder :   +++++
'
' var_funktion$()
' var_berechnung_fkt$()
' var_eingabezeichen$()
' var_funktion_feldindex%
' var_funktion$      ( Eingabestring der Fkt )
' var_klammerzaehler%
' var_fehlerzeiger%
' var_zahlzaehler%
' var_operatorzaehler%
' x   ( šbergabewert an Procedure fip_berechnung )
' y   ( Rckgabewert von    "           "        )
'
' +++++++++++++++++++++++++++++++++++++++++++++++++
'
'
' +++++++++++ Reservierte Prozedurnamen +++++++++++
'
' (fip_proc's : )
' fip_initialisierung,fip_parser,fip_nullpunkt,fip_einlesen,fip_zahl
' fip_operator,fip_eingabezeichen,fip_klammer,fip_doppelklammer
' fip_doppeloperator,fip_doppeloperand,fip_einopklammertest
' fip_prioritaet,fip_rechteklammer,fip_linkeklammer,fip_roffset
' fip_minus,fip_berechnung,fip_vartrans,fip_offset,fip_einopklammer
' fip_einopberechnung,fip_fehler,fip_zweiopklammer
'
' +++++++++++++++++++++++++++++++++++++++++++++++++
'
'
' ++++++ erlaubte Operatoren und Funktionen +++++++
'
' (,),*,/,+,-,^,.
' abs,atn (=arctan),cos,sin,tan,ep (fr exp),int,ln,log
' sgn,sqr,sec,csc,arcsin,sinh,cosh,tanh,sech,csch,coth
' arcsinh,arccosh,arccsch
'
' +++++++++++++++++++++++++++++++++++++++++++++++++
'
'
'               +++      Funktioninterpreter V1.3a     +++
'
' ************************ fip_initialisierung *********************************
'
'
PROCEDURE fip_initialisierung
  DIM var_funktion$(255),var_berechnung_fkt$(255),var_eingabezeichen$(35)
  DATA (,),*,/,+,-,^,.
  DATA abs,atn,cos,sin,tan,ep,int,ln,log,sgn,sqr
  DATA sec,csc,arcsin,sinh,cosh,tanh,sech,csch,coth
  DATA arcsinh,arccosh,arccsch,arctan,arccos,@
  var_operatorzaehler%=0
  REPEAT
    INC var_operatorzaehler%
    READ operator$
    var_eingabezeichen$(var_operatorzaehler%)=operator$
  UNTIL operator$="@"
  DEC var_operatorzaehler%
RETURN
'
'
' ******************************************************************************
'
'
' ***************** Fip_parser **************
'
'
PROCEDURE fip_parser
  LOCAL i%,j%,zn%,za%,zahl$,zl%,l%,k%,egzf%,opa%,ope%,lv%,roff%,prio$
  LOCAL rk%,rkz%,kzz%,lk%,lkz%,rolv%
  IF LEFT$(var_funktion$)="-"
    var_funktion$="0"+var_funktion$
  ENDIF
  IF LEN(STR$(VAL(var_funktion$)))=LEN(var_funktion$) OR var_funktion$="x"
    var_funktion$="0+"+var_funktion$
  ENDIF
  GOSUB fip_nullpunkt
  GOSUB fip_einlesen
  GOSUB fip_zahl
  GOSUB fip_operator
  GOSUB fip_eingabezeichen
  IF var_fehlerzeiger%=1
    GOSUB fip_klammer
    IF var_fehlerzeiger%=1
      GOSUB fip_doppelklammer
      IF var_fehlerzeiger%=1
        GOSUB fip_doppeloperator
        IF var_fehlerzeiger%=1
          GOSUB fip_doppeloperand
          IF var_fehlerzeiger%=1
            GOSUB fip_einopklammertest
            GOSUB fip_prioritaet
            GOSUB fip_minus
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF
RETURN
'
'
' ****************************************
'
'
' ********************* folgende proc's geh”ren zum parser *********************
'
'
PROCEDURE fip_nullpunkt
  j%=0
  i%=LEN(var_funktion$)
  REPEAT
    j%=INSTR(var_funktion$,".",j%)
    IF j%>=1
      IF MID$(var_funktion$,j%-1,1)<"0" OR MID$(var_funktion$,j%-1,1)>"9"
        var_funktion$=LEFT$(var_funktion$,j%-1)+"0"+MID$(var_funktion$,j%,i%)
        INC i%
        INC j%
      ENDIF
    ENDIF
    INC j%
  UNTIL j%>=i% OR INSTR(var_funktion$,".",j%)=0
RETURN
'
'
PROCEDURE fip_einlesen
  var_funktion_feldindex%=LEN(var_funktion$)
  IF var_funktion_feldindex%=1
    var_funktion_feldindex%=3
  ENDIF
  IF var_funktion_feldindex%=0
    var_funktion$=var_funktion$+"1-1"
    var_funktion_feldindex%=3
  ENDIF
  j%=0
  REPEAT
    INC j%
    IF MID$(var_funktion$,j%,1)=" " THEN
      var_funktion$=LEFT$(var_funktion$,j%-1)+MID$(var_funktion$,j%+1,var_funktion_feldindex%)
      DEC var_funktion_feldindex%
      DEC j%
    ELSE
      var_funktion$(j%)=MID$(var_funktion$,j%,1)
    ENDIF
  UNTIL j%>=var_funktion_feldindex%
RETURN
'
'
PROCEDURE fip_zahl
  zn%=0
  j%=0
  REPEAT
    INC j%
    IF MID$(var_funktion$,j%,1)>="0" AND MID$(var_funktion$,j%,1)<="9" OR MID$(var_funktion$,j%,1)="." OR MID$(var_funktion$,j%,1)="x"
      INC zn%
      IF MID$(var_funktion$,j%,1)="x"
        var_funktion$(j%)="x@"
      ELSE
        IF j%>=3
          IF MID$(var_funktion$,j%-1,1)="-" AND MID$(var_funktion$,j%-2,2)="("
            DEC j%
          ENDIF
        ENDIF
        za%=j%
        zahl$=STR$(VAL(RIGHT$(var_funktion$,var_funktion_feldindex%-za%+1)))
        zl%=LEN(zahl$)
        var_funktion$(j%)=zahl$+"@"
        REPEAT
          INC za%
          var_funktion$(za%)=var_funktion$(za%+zl%-1)
        UNTIL za%>=var_funktion_feldindex%-zl%+1
        SUB var_funktion_feldindex%,zl%-1
        l%=0
        var_funktion$=""
        REPEAT
          INC l%
          var_funktion$=var_funktion$+LEFT$(var_funktion$(l%))
        UNTIL l%=var_funktion_feldindex%
      ENDIF
    ENDIF
  UNTIL j%>=var_funktion_feldindex%
  var_zahlzaehler%=zn%
RETURN
'
'
PROCEDURE fip_operator
  j%=0
  REPEAT
    INC j%
    IF RIGHT$(var_funktion$(j%))<>"@"
      k%=0
      REPEAT
        INC k%
        IF var_funktion$(j%)=var_eingabezeichen$(k%)
          egzf%=1
          k%=8
        ELSE
          egzf%=0
        ENDIF
      UNTIL k%>=8
      IF egzf%=0
        opa%=j%
        ope%=j%
        REPEAT
          INC ope%
        UNTIL RIGHT$(var_funktion$(ope%))="@" OR var_funktion$(ope%)="(" OR ope%=var_funktion_feldindex%+1
        DEC ope%
        k%=j%
        REPEAT
          INC k%
          var_funktion$(j%)=var_funktion$(j%)+var_funktion$(k%)
        UNTIL k%>=ope%
        k%=j%
        REPEAT
          INC k%
          var_funktion$(k%)=var_funktion$(k%+ope%-opa%)
        UNTIL k%>=var_funktion_feldindex%-ope%+opa%
        SUB var_funktion_feldindex%,ope%-opa%
      ENDIF
    ENDIF
  UNTIL j%>=var_funktion_feldindex%
RETURN
'
'
PROCEDURE fip_eingabezeichen
  j%=0
  REPEAT
    INC j%
    var_fehlerzeiger%=0
    IF RIGHT$(var_funktion$(j%))="@"
      var_fehlerzeiger%=1
    ELSE
      l%=0
      REPEAT
        INC l%
        IF var_funktion$(j%)=var_eingabezeichen$(l%)
          var_fehlerzeiger%=1
          l%=var_operatorzaehler%
        ENDIF
      UNTIL l%>=var_operatorzaehler%
    ENDIF
    IF var_fehlerzeiger%=0
      j%=var_funktion_feldindex%
    ENDIF
  UNTIL j%>=var_funktion_feldindex%
RETURN
'
'
PROCEDURE fip_klammer
  k%=0
  j%=0
  var_klammerzaehler%=0
  REPEAT
    INC j%
    IF var_funktion$(j%)="("
      INC k%
      INC var_klammerzaehler%
    ENDIF
    IF var_funktion$(j%)=")"
      DEC k%
    ENDIF
  UNTIL j%>=var_funktion_feldindex%
  IF k%<>0
    var_fehlerzeiger%=0
  ELSE
    var_fehlerzeiger%=1
  ENDIF
RETURN
'
'
PROCEDURE fip_doppelklammer
  j%=0
  REPEAT
    INC j%
    IF var_funktion$(j%)<>"(" AND var_funktion$(j%+1)<>")"
      var_fehlerzeiger%=1
    ELSE
      IF var_funktion$(j%)=")" AND var_funktion$(j%+1)="(" OR var_funktion$(j%)="(" AND var_funktion$(j%+1)=")"
        var_fehlerzeiger%=0
        j%=var_funktion_feldindex%
        DEC j%
      ELSE
        var_fehlerzeiger%=1
      ENDIF
    ENDIF
  UNTIL j%>=var_funktion_feldindex%-1
RETURN
'
'
PROCEDURE fip_doppeloperator
  j%=0
  REPEAT
    INC j%
    IF var_funktion$(j%)<>"(" AND var_funktion$(j%)<>")" AND RIGHT$(var_funktion$(j%))<>"@"
      IF var_funktion$(j%+1)<>"(" AND LEN(var_funktion$(j%+1))<2 AND RIGHT$(var_funktion$(j%+1))<>"@"
        var_fehlerzeiger%=0
        j%=var_funktion_feldindex%
        DEC j%
      ELSE
        var_fehlerzeiger%=1
        INC j%
      ENDIF
    ENDIF
  UNTIL j%>=var_funktion_feldindex%-1
RETURN
'
'
PROCEDURE fip_doppeloperand
  j%=0
  REPEAT
    INC j%
    IF RIGHT$(var_funktion$(j%))="@" AND RIGHT$(var_funktion$(j%+1))="@"
      var_fehlerzeiger%=0
      j%=var_funktion_feldindex%
      DEC j%
    ELSE
      var_fehlerzeiger%=1
    ENDIF
  UNTIL j%>=var_funktion_feldindex%-1
RETURN
'
'
PROCEDURE fip_einopklammertest
  j%=0
  REPEAT
    INC j%
    IF var_funktion$(j%)<>"(" AND var_funktion$(j%)<>")" AND RIGHT$(var_funktion$(j%))<>"@" AND LEN(var_funktion$(j%))>1
      IF var_funktion$(j%+1)<>"("
        var_fehlerzeiger%=0
        j%=var_funktion_feldindex%
        DEC j%
      ELSE
        var_fehlerzeiger%=1
        INC j%
      ENDIF
    ENDIF
  UNTIL j%>=var_funktion_feldindex%-1
RETURN
'
'
PROCEDURE fip_prioritaet
  lv%=var_funktion_feldindex%
  INC lv%
  REPEAT
    DEC lv%
    IF LEN(var_funktion$(lv%))>1 AND RIGHT$(var_funktion$(lv%))<>"@"
      GOSUB fip_rechteklammer
      roff%=lv%
      GOSUB fip_roffset
      var_funktion$(lv%)="("
      INC var_klammerzaehler%
    ENDIF
  UNTIL lv%=0
  prios:
  DATA ^,*,/
  RESTORE prios
  REPEAT
    READ prio$
    lv%=var_funktion_feldindex%
    REPEAT
      IF var_funktion$(lv%)=prio$
        GOSUB fip_rechteklammer
        GOSUB fip_linkeklammer
      ENDIF
      DEC lv%
    UNTIL lv%=0
  UNTIL prio$="/"
  RESTORE
RETURN
'
'
PROCEDURE fip_rechteklammer
  rk%=lv%
  DEC rk%
  rkz%=0
  kzz%=0
  REPEAT
    INC rk%
    IF var_funktion$(rk%)="("
      INC rkz%
      kzz%=1
    ELSE
      IF var_funktion$(rk%)=")"
        DEC rkz%
        kzz%=1
      ENDIF
    ENDIF
  UNTIL (RIGHT$(var_funktion$(rk%))="@" AND kzz%=0) OR (rkz%=0 AND kzz%=1)
  roff%=rk%
  INC roff%
  GOSUB fip_roffset
  var_funktion$(rk%+1)=")"
RETURN
'
'
PROCEDURE fip_linkeklammer
  lk%=lv%
  INC lk%
  lkz%=0
  kzz%=0
  REPEAT
    DEC lk%
    IF var_funktion$(lk%)="("
      DEC lkz%
      kzz%=1
    ELSE
      IF var_funktion$(lk%)=")"
        INC lkz%
        kzz%=1
      ENDIF
    ENDIF
  UNTIL (RIGHT$(var_funktion$(lk%))="@" AND kzz%=0) OR (lkz%=0 AND kzz%=1)
  roff%=lk%
  GOSUB fip_roffset
  var_funktion$(lk%)="("
  INC var_klammerzaehler%
  INC lv%
RETURN
'
'
PROCEDURE fip_roffset
  rolv%=var_funktion_feldindex%
  INC rolv%
  REPEAT
    DEC rolv%
    var_funktion$(rolv%+1)=var_funktion$(rolv%)
  UNTIL rolv%<roff%
  INC var_funktion_feldindex%
RETURN
'
'
PROCEDURE fip_minus
  IF var_funktion$(1)="-"
    roff%=1
    GOSUB fip_roffset
    var_funktion$(1)="0@"
    INC var_zahlzaehler%
  ENDIF
  j%=var_funktion_feldindex%
  REPEAT
    IF var_funktion$(j%)="-" AND var_funktion$(j%-1)="(" AND j%>1
      roff%=j%
      GOSUB fip_roffset
      var_funktion$(j%)="0@"
      INC var_zahlzaehler%
    ENDIF
    DEC j%
  UNTIL j%<=1
RETURN
'
' ******************************************************************************
'
'
' *************** berechnung ************
'
'
PROCEDURE fip_berechnung
  LOCAL bkz%,bs1%,bs2%,bi%,b1,b11,b2,jo%,jk%,off%,k%,j%,ke%
  GOSUB fip_vartrans
  REPEAT
    j%=0
    ke%=0
    REPEAT
      INC j%
      IF var_berechnung_fkt$(j%)="("
        INC ke%
      ENDIF
    UNTIL ke%>=bkz% OR j%>=bi%
    IF RIGHT$(var_berechnung_fkt$(j%+1))="@" AND var_berechnung_fkt$(j%+2)=")"
      GOSUB fip_einopklammer
    ELSE
      GOSUB fip_zweiopklammer
    ENDIF
  UNTIL bs2%<1
  y=VAL(var_berechnung_fkt$(1))
RETURN
'
'
' ***************************************
'
'
' ********************  folgende proc's geh”ren zu berechnug *******************
'
'
PROCEDURE fip_vartrans
  bkz%=var_klammerzaehler%
  bs1%=var_zahlzaehler%
  bi%=0
  REPEAT
    INC bi%
    var_berechnung_fkt$(bi%)=var_funktion$(bi%)
    IF var_berechnung_fkt$(bi%)="x@"
      var_berechnung_fkt$(bi%)=STR$(x)+"@"
    ENDIF
  UNTIL bi%>=var_funktion_feldindex%
  bs2%=bi%
  SUB bs2%,bs1%
RETURN
'
'
PROCEDURE fip_offset
  jo%=jk%
  REPEAT
    var_berechnung_fkt$(jo%)=var_berechnung_fkt$(jo%+off%)
    INC jo%
  UNTIL jo%>=bi%+1
  DEC bs2%
  DEC bi%
RETURN
'
'
PROCEDURE fip_einopklammer
  var_berechnung_fkt$(j%)=var_berechnung_fkt$(j%+1)
  k%=j%
  REPEAT
    INC k%
    var_berechnung_fkt$(k%)=var_berechnung_fkt$(k%+2)
  UNTIL k%+2>=bi%
  SUB bs2%,2
  SUB bi%,2
  DEC bkz%
  IF LEN(var_berechnung_fkt$(j%-1))>1
    GOSUB fip_einopberechnung
  ENDIF
RETURN
'
'
PROCEDURE fip_einopberechnung
  ON ERROR GOSUB fip_fehler
  jk%=j%
  b1=VAL(var_berechnung_fkt$(jk%))
  IF var_berechnung_fkt$(jk%-1)="sin"
    var_berechnung_fkt$(jk%-1)=STR$(SIN(b1))+"@"
  ELSE
    IF var_berechnung_fkt$(jk%-1)="cos"
      var_berechnung_fkt$(jk%-1)=STR$(COS(b1))+"@"
    ELSE
      IF var_berechnung_fkt$(jk%-1)="tan"
        var_berechnung_fkt$(jk%-1)=STR$(TAN(b1))+"@"
      ELSE
        IF var_berechnung_fkt$(jk%-1)="abs"
          var_berechnung_fkt$(jk%-1)=STR$(ABS(b1))+"@"
        ELSE
          IF var_berechnung_fkt$(jk%-1)="arccos"
            b11=b1
            IF b1=-1 OR b1=1
              b1=b1+1.0E-10
            ENDIF
            var_berechnung_fkt$(jk%-1)=STR$(-ATN(b1/SQR(ABS(-b1*b1+1)))+PI/2)+"@"
            b1=b11
          ELSE
            IF var_berechnung_fkt$(jk%-1)="atn" OR var_berechnung_fkt$(jk%-1)="arctan"
              var_berechnung_fkt$(jk%-1)=STR$(ATN(b1))+"@"
            ELSE
              IF var_berechnung_fkt$(jk%-1)="ep"
                var_berechnung_fkt$(jk%-1)=STR$(EXP(b1))+"@"
              ELSE
                IF var_berechnung_fkt$(jk%-1)="int"
                  var_berechnung_fkt$(jk%-1)=STR$(INT(b1))+"@"
                ELSE
                  IF var_berechnung_fkt$(jk%-1)="ln"
                    b11=b1
                    IF b1<=0
                      var_berechnung_fkt$(jk%-1)="0@"
                    ELSE
                      var_berechnung_fkt$(jk%-1)=STR$(LOG(b1))+"@"
                    ENDIF
                    b1=b11
                  ELSE
                    IF var_berechnung_fkt$(jk%-1)="log"
                      b11=b1
                      IF b1<=0
                        var_berechnung_fkt$(jk%-1)="0@"
                      ELSE
                        var_berechnung_fkt$(jk%-1)=STR$(LOG10(b1))+"@"
                      ENDIF
                      b1=b11
                    ELSE
                      IF var_berechnung_fkt$(jk%-1)="sgn"
                        var_berechnung_fkt$(jk%-1)=STR$(SGN(b1))+"@"
                      ELSE
                        IF var_berechnung_fkt$(jk%-1)="sqr"
                          b11=b1
                          IF b1<=0
                            var_berechnung_fkt$(jk%-1)="0@"
                          ELSE
                            var_berechnung_fkt$(jk%-1)=STR$(SQR(b1))+"@"
                          ENDIF
                          b1=b11
                        ELSE
                          IF var_berechnung_fkt$(jk%-1)="sec"
                            var_berechnung_fkt$(jk%-1)=STR$(1/COS(b1))+"@"
                          ELSE
                            IF var_berechnung_fkt$(jk%-1)="csc"
                              var_berechnung_fkt$(jk%-1)=STR$(1/SIN(b1))+"@"
                            ELSE
                              IF var_berechnung_fkt$(jk%-1)="arcsin"
                                b11=b1
                                IF b1=-1 OR b1=1
                                  b1=b1+1.0E-10
                                ENDIF
                                var_berechnung_fkt$(jk%-1)=STR$(ATN(b1/SQR(ABS(-b1*b1+1))))+"@"
                                b1=b11
                              ELSE
                                IF var_berechnung_fkt$(jk%-1)="sinh"
                                  var_berechnung_fkt$(jk%-1)=STR$((EXP(b1)-EXP(-b1))/2)+"@"
                                ELSE
                                  IF var_berechnung_fkt$(jk%-1)="cosh"
                                    var_berechnung_fkt$(jk%-1)=STR$((EXP(b1)+EXP(-b1))/2)+"@"
                                  ELSE
                                    IF var_berechnung_fkt$(jk%-1)="tanh"
                                      var_berechnung_fkt$(jk%-1)=STR$(-EXP(-b1)/(EXP(b1)+EXP(-b1))*2+1)+"@"
                                    ELSE
                                      IF var_berechnung_fkt$(jk%-1)="sech"
                                        var_berechnung_fkt$(jk%-1)=STR$(2/(EXP(b1)+EXP(-b1)))+"@"
                                      ELSE
                                        IF var_berechnung_fkt$(jk%-1)="csch"
                                          var_berechnung_fkt$(jk%-1)=STR$(2/(EXP(b1)-EXP(-b1)))+"@"
                                        ELSE
                                          IF var_berechnung_fkt$(jk%-1)="coth"
                                            var_berechnung_fkt$(jk%-1)=STR$(EXP(-b1)/(EXP(b1)-EXP(-b1))*2+1)+"@"
                                          ELSE
                                            IF var_berechnung_fkt$(jk%-1)="arcsinh"
                                              var_berechnung_fkt$(jk%-1)=STR$(LOG(ABS(b1+SQR(b1*b1+1))))+"@"
                                            ELSE
                                              IF var_berechnung_fkt$(jk%-1)="arccosh"
                                                b11=b1
                                                IF b1<=0
                                                  var_berechnung_fkt$(jk%-1)="0@"
                                                ELSE
                                                  var_berechnung_fkt$(jk%-1)=STR$(LOG(ABS(b1+SQR(ABS(b1*b1-1)))))+"@"
                                                ENDIF
                                                b1=b11
                                              ELSE
                                                IF var_berechnung_fkt$(jk%-1)="arccsch"
                                                  var_berechnung_fkt$(jk%-1)=STR$(LOG(ABS((SGN(b1)*SQR(b1*b1+1)+1)/b1)))+"@"
                                                ELSE
                                                ENDIF
                                              ENDIF
                                            ENDIF
                                          ENDIF
                                        ENDIF
                                      ENDIF
                                    ENDIF
                                  ENDIF
                                ENDIF
                              ENDIF
                            ENDIF
                          ENDIF
                        ENDIF
                      ENDIF
                    ENDIF
                  ENDIF
                ENDIF
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF
  off%=1
  GOSUB fip_offset
  ON ERROR
RETURN
'
'
PROCEDURE fip_fehler
  IF ERR=0
    SUB b1,1.0E-15
    RESUME
  ENDIF
  IF ERR=1
    RESUME NEXT
  ENDIF
  IF ERR=2
    RESUME NEXT
  ENDIF
  IF ERR=5
    b1=0
    RESUME
  ENDIF
  IF ERR=6
    b1=1.0E-15
    RESUME
  ENDIF
RETURN
'
'
PROCEDURE fip_zweiopklammer
  ON ERROR GOSUB fip_fehler
  jk%=j%
  INC jk%
  REPEAT
    IF var_berechnung_fkt$(jk%)="*" OR var_berechnung_fkt$(jk%)="/" OR var_berechnung_fkt$(jk%)="+" OR var_berechnung_fkt$(jk%)="-" OR var_berechnung_fkt$(jk%)="^"
      b1=VAL(var_berechnung_fkt$(jk%-1))
      b2=VAL(var_berechnung_fkt$(jk%+1))
      IF var_berechnung_fkt$(jk%)="+"
        ADD b1,b2
        var_berechnung_fkt$(jk%-1)=STR$(b1)+"@"
      ELSE
        IF var_berechnung_fkt$(jk%)="-"
          SUB b1,b2
          var_berechnung_fkt$(jk%-1)=STR$(b1)+"@"
        ELSE
          IF var_berechnung_fkt$(jk%)="*"
            MUL b1,b2
            var_berechnung_fkt$(jk%-1)=STR$(b1)+"@"
          ELSE
            IF var_berechnung_fkt$(jk%)="/"
              IF b2=0
                b2=1.0E-11
              ENDIF
              DIV b1,b2
              var_berechnung_fkt$(jk%-1)=STR$(b1)+"@"
            ELSE
              IF var_berechnung_fkt$(jk%)="^"
                IF b1<0 AND ABS(b2)<1
                  var_berechnung_fkt$(jk%-1)="0@"
                ELSE
                  var_berechnung_fkt$(jk%-1)=STR$(b1^b2)+"@"
                ENDIF
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ENDIF
      off%=2
      GOSUB fip_offset
      DEC bi%
    ELSE
      INC jk%
    ENDIF
  UNTIL var_berechnung_fkt$(jk%)=")" OR var_berechnung_fkt$(jk%)="" OR jk%>=bi%
  ON ERROR
RETURN
'
'
