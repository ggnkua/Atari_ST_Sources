$m8000
RESERVE 8000
zeiger%=MALLOC(65536)
IF zeiger%=0
  ~FORM_ALERT(1,"[3][Kein Platz fr|Bildbuffer!][ Abbruch ]")
ELSE
  CLS
  PRINT AT(27,5);" ******************"
  PRINT TAB(27);"Sternbild erzeugen"
  PRINT TAB(27);"******************"
  PRINT
  PRINT
  PRINT
  @eingabe                                      ! Parameter abfragen
  PRINT
  PRINT "Berechnung des Bildes:"
  bild$="STERN"+STR$(segmente%)+".BYT"
  @bild_erzeugen
  PRINT AT(1,19);"Abspeichern des Bildes im File: ";bild$
  BSAVE bild$,zeiger%,65536
  ~MFREE(zeiger%)
ENDIF
RESERVE
END
'
' Unterroutinen
'
> PROCEDURE eingabe
  '
  ' Eingabe aller Werte mit Fehlertest
  '
  INPUT "Mittelpunktkoordinate X        : ";mitte.x
  INPUT "Mittelpunktkoordinate Y        : ";mitte.y
  '
  DO
    INPUT "Anzahl der Kreissegmente       : ";segmente%
    IF segmente%>2 AND segmente%<1000
      ok!=TRUE
    ELSE
      PRINT AT(43,13);CHR$(7);"Anzahl muž >1 und <1000 sein!"
      PRINT AT(1,13);
      ok!=FALSE
    ENDIF
  LOOP UNTIL ok!
  '
  DO
    INPUT "Amplitude der hellen Segmente  : ";hell|
    IF hell|=0
      PRINT AT(43,14);CHR$(7);"Wert muž >0 sein!"
      PRINT AT(1,14);
      ok!=FALSE
    ELSE
      ok!=TRUE
    ENDIF
  LOOP UNTIL ok!
  '
  DO
    INPUT "Amplitude der dunklen Segmente : ";dunkel|
    IF hell|<=dunkel|
      PRINT AT(43,15);CHR$(7);"Wert muž < ";hell|;" sein!"
      PRINT AT(1,15);
      ok!=FALSE
    ELSE
      ok!=TRUE
    ENDIF
  LOOP UNTIL ok!
  '
RETURN
> PROCEDURE bild_erzeugen
  '
  ' erzeugt einen Siemens-Stern
  '
  halb.segment=PI/segmente%             ! Winkel eines Teilsegments
  segment=2*halb.segment                ! Winkel eines Segments
  dpi=2*PI
  '
  FOR y%=0 TO 255
    PRINT AT(1,18);"Zeile: ";RIGHT$("  "+STR$(y%+1),3);
    FOR x%=0 TO 255
      x=SUB(x%,1)-mitte.x
      y=mitte.y-SUB(y%,1)
      w=ABS(ATN(y/x))
      IF y<0            !...dann Q-III oder Q-IV
        IF x>0
          w=dpi-w                       ! Q-IV
        ELSE
          w=w+PI                        ! Q-III
        ENDIF
      ELSE              !...dann Q-I oder Q-II
        IF x<0
          w=PI-w                        ! Q-II
        ENDIF
      ENDIF
      IF (w MOD segment)<halb.segment
        BYTE{ADD(zeiger%,ADD(x%,SHL(y%,8)))}=hell|
      ELSE
        BYTE{ADD(zeiger%,ADD(x%,SHL(y%,8)))}=dunkel|
      ENDIF
    NEXT x%
  NEXT y%
  '
RETURN
