' ***********************************************************************
' *                                                                     *
' *             Kaitel 1 Einfhrung in GFA - Basic                      *
' *                                                                     *
' *                     Das erste Spiel                                 *
' *                                                                     *
' ***********************************************************************
'
' **** Stellt die Aufl”sung (vertikal + horizontal) des  Rechners fest ***
xmax&=WORK_OUT(0)
ymax&=WORK_OUT(1)
' **********************
aufbau
REPEAT
  REPEAT
    xbb&=xb&
    ybb&=yb&
    ' ********* Links und Recht fr den Ball *******
    ADD xb&,nx&
    ADD yb&,ny&
    IF nx&=2
      IF xb&>xmax&-10 ! Wenn Ball Rechts angekommen
        nx&=-2
      ENDIF
    ELSE
      IF xb&<10 ! Wenn Ball Links angekommen
        nx&=2
      ENDIF
    ENDIF
    ' *************** Fast das gleiche fr die Y Richtung ********************
    IF ny&=2
      IF yb&>ymax&-10 ! Wenn Ball am unteren Rand angekommen
        init
      ENDIF
    ELSE
      IF yb&<10 ! Wenn Ball am oberen Rand angekommen
        ny&=2
      ENDIF
    ENDIF
    CIRCLE xb&,yb&,10
    CIRCLE xbb&,ybb&,10
    LINE x&,y&,x&+20,y&
    x&=MOUSEX
    IF x&=>xmax&-20
      x&=xmax&-20
    ENDIF
    LINE x&,y&,x&+20,y&
    IF y&<yb&+10 AND x&<xb&+10 AND x&+10>xb&-10
      ny&=-2
    ENDIF
  UNTIL INP(-2)=TRUE !verlasse Schleife, wenn Taste gedrckt
UNTIL BCLR(INP(2),5)=81 !verlasse Schleife, wenn Taste = "Q"
> PROCEDURE aufbau ! Bildschirmaufbau
  CLS
  HIDEM
  BOX 0,0,xmax&,ymax& !Zeichnet Umrandung
  init
  GRAPHMODE 3 ! Schaltet auf XOR
  CIRCLE xb&,yb&,10
  LINE x&,y&,x&+20,y&
RETURN
> PROCEDURE init  ! Initialisiert die Variablen
  xb&=50
  yb&=50
  nx&=2
  ny&=2
  y&=ymax&-20
  x&=MOUSEX
RETURN
