' ------------------------------------------------------------------------------
' -                                TWO-VIEW                                    -
' -                       Datei-Viewer fÅr TWO-IN-ONE                          -
' -                                                                            -
' -          Beispiel-Applikation fÅr GEM-Programmierung unter GFA-BASIC       -
' -          und den ERGO!pro-PrÑprozessor                                     -
' -                                                                            -
' -                    (c) 08/93-12/94 Gregor Duchalski                        -
' ------------------------------------------------------------------------------
' - Dieser Quellcode ist _nicht_ Public Domain!                                -
' - Das compilierte Programm darf nicht weitergegeben werden!                  -
' - Vor dem ersten Start bitte zunÑchst die 4 INLINEs in 'inlines' einladen!   -
' ------------------------------------------------------------------------------
' ERGO!-Befehle: CMP -> comp. Programm (immer)
'                GER -> fÅr deutsche Version
'                ENG -> fÅr englische Version
$m10000
init
inf_laden
'
main
'
> PROCEDURE init
  LOCAL a|,a&
  '
  CLIP OFF                                           ! LINEA frei
  ~WIND_GET(0,7,deskx&,desky&,deskw&,deskh&)         ! Hintergrundfenster
  ' -------------------------------------------------- Compiler...
  IF BYTE{ADD(BASEPAGE,256)}<>96                     ! -CMP
    a|=BYTE{BASEPAGE+128}                            ! Kommandozeile...
    IF a|<>0 AND a|<>127
      cmd$=CHAR{BASEPAGE+129}                        ! ...auslesen
    ENDIF
    '
    homedir$=CHR$(ADD(GEMDOS(25),65))+":"+DIR$(0)+"\"! ...Heimat-Directory
    ' --CMP
    ' ----------------------------------------------------------------------
  ELSE                                               ! Interpreter...
    ~FORM_DIAL(3,0,0,0,0,deskx&,desky&,deskw&,deskh&)
    '
    homedir$="D:\TWOINONE\TWO_VIEW\"                 ! ...Heimat-Directory
    ' cmd$=homedir$+"TWO_VIEW.TXT"                    ! ...Beispieltext
  ENDIF
  ' --CMP
  ' --------------------------------------------------
  ~GRAF_HANDLE(wchar&,hchar&,a&,a&)                  ! Zeichenhîhe/-breite
  ' --------------------------------------------------
  INTIN(0)=0
  INTIN(1)=3
  '
  VDISYS 39,2,0                                      ! vst_alignment
  ' --------------------------------------------------
  DEFFILL 1,0
  BOUNDARY 0
  ' --------------------------------------------------
  DIM q_ras%(5),z_ras%(5),par%(8),m$(20)             ! FÅr BITBLT und MENU
  ' --------------------------------------------------
  gem_init                                           ! GEM-Konstanten
  ' --------------------------------------------------
  inlines
  ' --------------------------------------------------
  '
RETURN
> PROCEDURE gem_init
  '
  ' GEM-Definitionen & Konstanten
  ' --------------------------------------------------
  vdi_pb%=ADD(GB,24)            ! VDI-Parameterblock
  '
  ap_id&=APPL_INIT()            ! Unsere Applikation ID
  ' --------------------------------------------------
  DIM msg&(7)                   ! AES-Message-Puffer
  ' ++SYM
  wm_redraw|=20                 ! AES-Messages...
  wm_topped|=21
  wm_closed|=22
  wm_fulled|=23
  wm_arrowed|=24
  wm_hslid|=25
  wm_vslid|=26
  wm_sized|=27
  wm_moved|=28
  '
  wa_uppage|=0
  wa_dnpage|=1
  wa_upline|=2
  wa_dnline|=3
  wa_lfpage|=4
  wa_rtpage|=5
  wa_lfline|=6
  wa_rtline|=7
  '
  wm_bottomed|=33
  wm_bottom|=25
  '
  mn_selected&=10
  '
  end_update|=0                 ! WIND_UPDATE...
  beg_update|=1
  end_mctrl|=2
  beg_mctrl|=3
  '
  wc_border|=0                  ! WIND_CALC...
  wc_work|=1
  '
  m_off&=256                    ! GRAF_MOUSE...
  m_on&=257
  '
  va_start&=&H4711              ! AV-Protokoll...
  av_accwindclose&=&H4726
  '
  mp_readable&=&H40
  ' ++SYM
  '
RETURN
'
> PROCEDURE inf_laden
  LOCAL dummy$
  '
  ' LÑdt das TWO_VIEW.INF-File
  ' --------------------------------------------------
  ~GRAF_MOUSE(2,0)
  '
  IF @open_i(homedir$+"TWO_VIEW.INF")=0
    '
    INPUT #1,dummy$
    INPUT #1,wc_x&
    INPUT #1,wc_y&
    INPUT #1,wc_w&
    INPUT #1,wc_h&
    INPUT #1,dummy$
    '
    CLOSE #1
  ENDIF
  '
  ~GRAF_MOUSE(0,0)
  '
RETURN
> PROCEDURE inf_sichern
  '
  ' Speichert das TWO_VIEW.INF-File
  ' --------------------------------------------------
  ~GRAF_MOUSE(2,0)
  OPEN "O",#1,homedir$+"TWO_VIEW.INF"
  '
  PRINT #1;"2VIEW-2"
  PRINT #1;wc_x&
  PRINT #1;wc_y&
  PRINT #1;wc_w&
  PRINT #1;wc_h&
  PRINT #1;""
  '
  CLOSE #1
  ~GRAF_MOUSE(0,0)
  '
RETURN
' ----------------------------------------------------
> PROCEDURE main
  LOCAL stat&
  '
  ' --------------------------------------------------
  stat&=@t_laden(cmd$)                               ! Datei laden...
  IF stat&                                           ! ...Fehler aufgetreten
    SELECT stat&
    CASE -33,10
      ~FORM_ALERT(1,"[3][ |TWO-VIEW:|Die Datei|.."+RIGHT$(cmd$,28)+"|konnte nicht geladen werden!][  OK  ]") ! -ENG
      ~FORM_ALERT(1,"[3][ |TWO-VIEW:|Couldn't load|.."+RIGHT$(cmd$,28)+"][  OK  ]")                          ! -GER
      '
    CASE 1
    DEFAULT
      ~FORM_ALERT(1,"[3][ |TWO-VIEW:| |Fehler "+STR$(stat&)+" aufgetreten... ][  OK  ]")                  ! -ENG
      ~FORM_ALERT(1,"[3][ |TWO-VIEW:| |Error "+STR$(stat&)+" occured... ][  OK  ]")                       ! -GER
    ENDSELECT
    '
    EDIT                                             ! Schluû
  ENDIF
  ' --------------------------------------------------
  IF @fenster_init(cmd$)=0                           ! Hauptschleife beginnt...
    '
    REPEAT
      '
      evnt&=EVNT_MULTI(&X10011,258,3,0,0,0,0,0,0,0,0,0,0,0,V:msg&(0),0,mx&,my&,mb&,shift&,key&,mc&)
      '
      IF BTST(evnt&,4)                               ! Message-Event...
        IF msg&(0)=mn_selected&
          menu_auswerten(msg&(4))                    ! ...MenÅpunkt angeklickt
        ELSE
          fenster_auswerten(msg&(0),msg&(1),msg&(2),msg&(3),msg&(4),msg&(5),msg&(6),msg&(7))
        ENDIF
      ENDIF
      '
      IF BTST(evnt&,0)                               ! Tastatur-Event...
        tastatur_auswerten(shift&,key&)
      ENDIF
      '
      ' IF BTST(evnt&,1)                             ! Button-Event...
      ' maus_auswerten(shift&,mx&,my&,mb&,mc&)       ! ...gibt's hier nicht
      ' ENDIF
      '
    UNTIL prg_exit!                                  ! ...bis globaler Ausgang
    '
    fenster_exit
    '
  ENDIF
  ' --------------------------------------------------
  ~MFREE(t_puffer%)                                  ! -CMP
  '
RETURN
'
> PROCEDURE menu_auswerten(nr&)
  LOCAL a$,b$
  '
  ' Wertet einen angeklickten MenÅpunkt aus. Wird auch von 'tastatur_
  ' auswerten' aufgerufen.
  ' --------------------------------------------------
  SELECT nr&
    '
  CASE m_about|
    RESTORE version
    READ a$,b$
    ~FORM_ALERT(1,"[1][TWO-VIEW v"+a$+"|"+b$+" Two-in-One |Ω93/94 Gregor Duchalski, Im|Wiescherfeld 21, D-44627 Herne|eMail: @do.maus.ruhr.de][  OK  ]")
    '
  CASE m_open|
    IF @t_laden("")=0
      set_vslider(w_handle&)                         ! Slider setzen
      fenster_redraw(w_handle&,wa_x&,wa_y&,wa_w&,wa_h&)
    ENDIF
    '
  CASE m_save|
    inf_sichern
    '
  CASE m_quit|
    prg_exit!=TRUE
    '
  ENDSELECT
  '
  MENU OFF
  '
RETURN
'
> PROCEDURE tastatur_auswerten(shift&,key&)
  LOCAL ascii|,scan|
  '
  ' Tastenklick auswerten
  ' --------------------------------------------------
  ascii|=BCLR(BYTE(key&),5)                          ! ASCII-Code (nur Groûschr.
  scan|=BYTE(SHR(key&,8))                            ! SCAN-Code
  '
  SELECT ascii|
  CASE 19                                            ! <CONTROL S>...
    menu_auswerten(m_save|)                          ! ...INF sichern
    '
  CASE 17                                            ! <CONTROL Q>...
    menu_auswerten(m_quit|)                          ! ...Schluû
    '
  DEFAULT                                            ! Jede andere Taste...
    scan_code(shift&,scan|)                          ! ...SCAN-Code nehmen
  ENDSELECT
  '
RETURN
> PROCEDURE scan_code(VAR shift&,scan|)
  '
  ' PRINT scan|
  '
  ' --------------------------------------------------
  SELECT scan|
  CASE 72                                            ! Pfeil rauf...
    IF AND(shift&,&X11)                              ! ...+SHIFT...
      fenster_auswerten(wm_arrowed|,ap_id&,0,w_handle&,wa_uppage|,0,0,0)
    ELSE
      fenster_auswerten(wm_arrowed|,ap_id&,0,w_handle&,wa_upline|,0,0,0)
    ENDIF
    '
  CASE 80                                            ! Pfeil runter...
    IF AND(shift&,&X11)                              ! ...+SHIFT...
      fenster_auswerten(wm_arrowed|,ap_id&,0,w_handle&,wa_dnpage|,0,0,0)
    ELSE
      fenster_auswerten(wm_arrowed|,ap_id&,0,w_handle&,wa_dnline|,0,0,0)
    ENDIF
    '
  CASE 75                                            ! Pfeil links...
    IF AND(shift&,&X11)                              ! ...+SHIFT...
      fenster_auswerten(wm_arrowed|,ap_id&,0,w_handle&,wa_lfpage|,0,0,0)
    ELSE
      fenster_auswerten(wm_arrowed|,ap_id&,0,w_handle&,wa_lfline|,0,0,0)
    ENDIF
    '
  CASE 77                                            ! Pfeil rechts...
    IF AND(shift&,&X11)                              ! ...+SHIFT...
      fenster_auswerten(wm_arrowed|,ap_id&,0,w_handle&,wa_rtpage|,0,0,0)
    ELSE
      fenster_auswerten(wm_arrowed|,ap_id&,0,w_handle&,wa_rtline|,0,0,0)
    ENDIF
    '
  CASE 71                                            ! CLR-HOME...
    IF AND(shift&,&X11)                              ! ...+SHIFT...
      fenster_auswerten(wm_vslid|,ap_id&,0,w_handle&,1000,0,0,0)
    ELSE
      fenster_auswerten(wm_vslid|,ap_id&,0,w_handle&,0,0,0,0)
    ENDIF
    '
  CASE 98                                            ! HELP...
    menu_auswerten(m_about|)
    '
  ENDSELECT
  '
RETURN
' ----------------------------------------------------
> PROCEDURE t_clear
  '
  ' Initialisiert die Ausgabepuffer-Variablen auf den Anfang des Bereiches
  ' --------------------------------------------------
  t_start%=t_puffer%
  CLR hiz&,liz&
  anfs&=1
  anfz&=1
  '
RETURN
> FUNCTION t_enough(l%)
$F%
LOCAL a%,b%
'
' Ermittelt, ob der Text mit der LÑnge 'l%' noch in den Textpuffer passen
' wÅrde (=0). Wenn nicht, so wird die Grîûe des zusÑtzlich erforderlichen
' Speichers zurÅckgeliefert.
' --------------------------------------------------
a%=ADD(t_start%,l%)           ! So groû wÅrde es
b%=ADD(t_puffer%,t_len%)      ! Maximaler Wert
'
IF a%>=b%                     ! WÑre zu groû...
  RETURN SUB(a%,b%)
ENDIF
'
RETURN 0                      ! Paût rein
ENDFUNC
> FUNCTION t_puffer(lof%)
$F%
'
' Fordert den Ausgabepuffer an und initialisiert die Verwaltungs-Variablen.
' Wenn die Speicheranforderung fehlschlÑgt, wird eine Fehlermeldung ausgegeben!
' --------------------------------------------------
IF t_puffer%                                       ! Es gibt noch einen Puffer...
~MFREE(t_puffer%)                                ! ...erst freigeben
ENDIF
'
t_puffer%=MALLOC(lof%)
t_start%=t_puffer%                                 ! Schreibmarke setzen
'
IF t_puffer%=0                                     ! Fehlgeschlagen...
t_len%=0
ENDIF
'
t_len%=lof%                                        ! Grîûe des Puffers
'
RETURN t_puffer%                                   ! <>0 zurÅck
ENDFUNC
'
> FUNCTION t_laden(file$)
$F%
LOCAL stat&,new_hiz&,lof%
'
' LÑdt einen ASCII-Text (zusÑtzlich) in den Ausgabe-Puffer...
' RETURN : 0 -> alles OK
' --------------------------------------------------
IF file$=""                                        ! Keine Datei angegeben
stat&=1
file$=@fileselect$("Text laden...",homedir$+"*.*","") ! -ENG
file$=@fileselect$("Load text...",homedir$+"*.*","")  ! -GER
ENDIF
' --------------------------------------------------
IF file$<>""                                       ! Existiert...
~GRAF_MOUSE(2,0)
'
stat&=@open_i(file$)                             ! ôffnen...
IF stat&=0
' ----------------------------------------------
lof%=SUCC(LOF(#1))                             ! +1 wegen Nullbyte am Ende
IF lof%=1                                      ! Null Bytes lang...
  RETURN 10                                    ! ...Schluû
ENDIF
'
t_clear                                        ! ...von vorne versuchen
'
IF @t_enough(lof%)                             ! Paût immer noch nicht...
  t_puffer%=@t_puffer(lof%)                    ! ...grîûeren anfordern
  ' --------------------------------------------
  IF t_puffer%=0                               ! Hat nicht geklappt...
    RETURN 8                                   ! Error!
  ENDIF
  '
ENDIF
' ----------------------------------------------
BGET #1,t_start%,PRED(lof%)                    ! Text laden...
'
CLOSE #1
'
t_wandeln(t_start%,lof%)                       ! LFs ersetzen
'
new_hiz&=@hiz(t_start%,lof%,liz&)              ! Hîchste Zeile des neuen StÅcks...
'
ADD hiz&,new_hiz&                              ! ..wird zu der alten addiert
ADD t_start%,lof%                              ! Zeiger erhîhen
'
ENDIF
'
~GRAF_MOUSE(0,0)
'
ENDIF
'
RETURN stat&                                       ! Fehler-Code
ENDFUNC
> PROCEDURE t_wandeln(t_start%,VAR lof%)
LOCAL e%
'
' Ersetzt alle LF im Text durch <SPACE-NULL>...
'
' a%=t_start%
e%=ADD(t_start%,PRED(lof%))                        ! Endadresse
' --------------------------------------------------
~C:chng_lf_asm%(L:t_start%,L:PRED(lof%))           ! Wandeln
' --------------------------------------------------
IF BYTE{PRED(e%)}=0                                ! Bereits Null am Ende...
DEC lof%                                         ! ...ein Byte weniger
ELSE                                               ! Keine Null am Ende...
BYTE{e%}=0                                       ! ...Sicherheitsnullbyte
ENDIF
' --------------------------------------------------
'
RETURN
' ----------------------------------------------------
> FUNCTION fenster_init(cmd$)
$F%
LOCAL a&
'
' -------------------------------------------------- Ausgabe-Fenster
w_attr&=&X111111111111                             ! +SYM
w_handle&=WIND_CREATE(w_attr&,deskx&,desky&,deskw&,deskh&)
IF w_handle&<=0
RETURN 1                                         ! ...Error
ENDIF
'
CHAR{wt%}=" TWO-VIEW "                             ! Titel...
~WIND_SET(w_handle&,2,CARD(SWAP(wt%)),CARD(wt%),0,0)
'
CHAR{wi%}=LEFT$(" "+cmd$,80)                       ! Info...
~WIND_SET(w_handle&,3,CARD(SWAP(wi%)),CARD(wi%),0,0)
'
IF wc_w&=0                                         ! Fensterkoordinaten...
wc_x&=deskx&
wc_y&=desky&
wc_h&=deskh&/2
wc_w&=deskw&/2
ENDIF
'
' fenster_snappen(1,wc_x&,wc_y&,wc_w&,wc_h&)
' -------------------------------------------------- Dropdown-MenÅ
' ++SYM
m_about|=23
m_open|=32
m_save|=33
m_quit|=35
' ++SYM
'
RESTORE menu
'
a&=-1
REPEAT
INC a&
READ m$(a&)
UNTIL m$(a&)="#"
m$(a&)=""
'
MENU m$()
' --------------------------------------------------
~WIND_UPDATE(beg_update|)
'
~WIND_OPEN(w_handle&,wc_x&,wc_y&,wc_w&,wc_h&)      ! ...îffnen
'
fenster_get
set_vslider(w_handle&)
set_hslider(w_handle&)
'
~WIND_UPDATE(end_update|)
' --------------------------------------------------
'
RETURN 0
ENDFUNC
> PROCEDURE fenster_exit
'
~WIND_CLOSE(w_handle&)                             ! Weg mit dem Fenster...
~WIND_DELETE(w_handle&)
'
MENU KILL                                          ! Weg mit der MenÅzeile
'
RETURN
'
> PROCEDURE fenster_auswerten(msg&,id&,len&,wh&,x&,y&,w&,h&)
LOCAL rx&,ry&,rb&,rh&,alt_z&,alt_s&
'
IF wh&=w_handle&                                   ! Ist unser Text-Fenster gemeint?...
'
~WIND_UPDATE(beg_update|)
'
SELECT msg&
' ----------------------------------------------
CASE wm_redraw|                                  ! Fenster redrawn...
fenster_redraw(wh&,x&,y&,w&,h&)
'
CASE wm_moved|,wm_sized|                         ! Fenster bewegen...
fenster_bewegen(wh&,x&,y&,w&,h&)
'
CASE wm_topped|                                  ! Fenster toppen...
~WIND_SET(wh&,10,0,0,0,0)
'
CASE wm_closed|                                  ! Fenster schlieûen...
prg_exit!=TRUE
'
CASE wm_fulled|                                  ! Volle Grîûe...
'
~WIND_GET(wh&,7,x&,y&,w&,h&)                   ! Maximale Grîûe
~WIND_GET(wh&,5,rx&,ry&,rb&,rh&)               ! Aktuelle Ausmaûe
'
IF rb&=w& AND rh&=h&                           ! ...bereits Maximalgrîûe
~WIND_GET(wh&,6,x&,y&,w&,h&)                 ! ...alte Grîûe
ENDIF
'
fenster_bewegen(wh&,x&,y&,w&,h&)
'
CASE wm_bottomed|                                ! Fenster in den Hintergrund (WINX)...
~WIND_UPDATE(beg_update|)
~WIND_SET(wh&,wm_bottom|,0,0,0,0)              ! ...in den Hintergrund
~WIND_UPDATE(end_update|)
' ----------------------------------------------
CASE wm_vslid|                                   ! Rechter Slider bewegt...
alt_z&=@anfz(x&)
IF alt_z&<>anfz&
anfz&=alt_z&
set_vslider(wh&)
fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
ENDIF
'
CASE wm_hslid|                                   ! Unterer Slider bewegt...
alt_s&=@anfs(x&)
IF alt_s&<>anfs&
anfs&=alt_s&
set_hslider(wh&)
fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
ENDIF
' ----------------------------------------------
CASE wm_arrowed|                                 ! Fensterpfeile...
'
alt_z&=anfz&
alt_s&=anfs&
'
SELECT x&
' --------------------------------------------
CASE wa_upline|                                ! Zeile hoch...
ADD anfz&,anfz&>1
IF anfz&<>alt_z&
  IF @fenster_top(wh&)                       ! Fenster 'on top'...
    rsc_bitblt(0,0,0,0,0,0,wa_x&,wa_y&,wa_w&,wa_h&-hchar&,wa_x&,wa_y&+hchar&)
    fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,hchar&)
  ELSE                                       ! Nicht oben...
    fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
  ENDIF
ENDIF
' --------------------------------------------
CASE wa_dnline|                                ! Zeile runter...
SUB anfz&,@lz<hiz&
IF anfz&<>alt_z&
  IF @fenster_top(wh&)                      ! Fenster 'on top'...
    rsc_bitblt(0,0,0,0,0,0,wa_x&,wa_y&+hchar&,wa_w&,wa_h&-hchar&,wa_x&,wa_y&)
    fenster_redraw(wh&,wa_x&,wa_y&+wa_h&-hchar&,wa_w&,hchar&)
  ELSE                                       ! Nicht oben...
    fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
  ENDIF
ENDIF
' --------------------------------------------
CASE wa_lfline|                                ! Spalte links...
ADD anfs&,anfs&>1
IF anfs&<>alt_s&
  IF @fenster_top(wh&)                       ! Fenster 'on top'...
    rsc_bitblt(0,0,0,0,0,0,wa_x&,wa_y&,wa_w&-wchar&,wa_h&,wa_x&+wchar&,wa_y&)
    fenster_redraw(wh&,wa_x&,wa_y&,wchar&,wa_h&)
  ELSE                                       ! Nicht oben...
    fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
  ENDIF
ENDIF
' --------------------------------------------
CASE wa_rtline|                                ! Spalte rechts...
SUB anfs&,@ls<liz&
IF anfs&<>alt_s&
  IF @fenster_top(wh&)                       ! Fenster 'on top'...
    rsc_bitblt(0,0,0,0,0,0,wa_x&+wchar&,wa_y&,wa_w&-wchar&,wa_h&,wa_x&,wa_y&)
    fenster_redraw(wh&,wa_x&+wa_w&-wchar&,wa_y&,wchar&,wa_h&)
  ELSE                                       ! Nicht oben...
    fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
  ENDIF
ENDIF
' --------------------------------------------
CASE wa_uppage|                                ! Seite hoch...
anfz&=MAX(1,SUB(anfz&,feh&))
IF anfz&<>alt_z&
  fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
ENDIF
' --------------------------------------------
CASE wa_dnpage|                                ! Seite runter...
anfz&=MIN(hiz&-PRED(feh&),ADD(anfz&,feh&))
IF anfz&<>alt_z&
  fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
ENDIF
' --------------------------------------------
CASE wa_lfpage|                                ! Seite links...
anfs&=MAX(1,SUB(anfs&,feb&))
IF anfs&<>alt_s&
  fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
ENDIF
' --------------------------------------------
CASE wa_rtpage|                                ! Seite rechts...
anfs&=MIN(liz&-PRED(feb&),ADD(anfs&,feb&))
IF anfs&<>alt_s&
  fenster_redraw(wh&,wa_x&,wa_y&,wa_w&,wa_h&)
ENDIF
' --------------------------------------------
ENDSELECT
'
IF anfz&<>alt_z&                               ! Hat sich was getan...
set_vslider(wh&)                             ! ...Slider setzen
ENDIF
IF anfs&<>alt_s&                               ! Hat sich was getan...
set_hslider(wh&)                             ! ...Slider setzen
ENDIF
' ----------------------------------------------
ENDSELECT
'
~WIND_UPDATE(end_update|)
'
ENDIF
'
RETURN
'
> PROCEDURE fenster_redraw(wh&,x&,y&,w&,h&)
LOCAL rx&,ry&,rb&,rh&
'
' --------------------------------------------------
~WIND_UPDATE(beg_update|)
~GRAF_MOUSE(m_off&,0)
'
~WIND_GET(wh&,11,rx&,ry&,rb&,rh&)                  ! 1. Rechteck holen
'
REPEAT
'
IF RC_INTERSECT(x&,y&,w&,h&,rx&,ry&,rb&,rh&)     ! Im Arbeitsbereich...
'
CLIP rx&,ry&,rb&,rh&
fenster_redraw_part(rx&,ry&,rb&,rh&)
CLIP OFF
'
ENDIF
'
~WIND_GET(wh&,12,rx&,ry&,rb&,rh&)
UNTIL rb&=0 AND rh&=0                              ! ...solange bis kein Redraw mehr nîtig
'
~GRAF_MOUSE(m_on&,0)
~WIND_UPDATE(end_update|)
'
RETURN
> PROCEDURE fenster_redraw_part(rx&,ry&,rb&,rh&)
LOCAL x&,y&,z&,z1&,z2&,adr%
'
' Redraw einen Fensterbereich, d.h. gibt die entsprechenden Textzeilen aus.
' --------------------------------------------------
z1&=@z(ry&)                                        ! Erste auszugebende Zeile
z1&=MAX(z1&,1)
'
z2&=@z(ADD(ry&,PRED(rh&)))                         ! Letzte auszugebende Zeile
z2&=MIN(z2&,hiz&)
'
x&=wa_x&-wchar&*PRED(anfs&)                        ! Start X-Position
y&=ADD(@y(z1&),PRED(hchar&))                       ! Start Y-Position
'
PBOX rx&,ry&,ADD(rx&,PRED(rb&)),ADD(ry&,PRED(rh&)) ! Bereich zunÑchst weiû
'
adr%=@zeilenadr(z1&)                               ! Adresse der 1. auszugeb. Zeile
'
FOR z&=z1& TO z2&
~C:v_gtext_asm%(L:vdi_pb%,L:*adr%,x&,y&)
' TEXT x&,y&,@zeile$(z&)                         ! WÑre auch mîglich, langsamer
ADD y&,hchar&
NEXT z&
'
RETURN
'
> PROCEDURE fenster_bewegen(wh&,x&,y&,w&,h&)
LOCAL a&
'
' Wird beim Verschieben und Vergrîûern des Fensters aufgerufen.
' Die Fenster-Koordinaten werden ggf. gesnappt, anfs&/anfz& ggf. angepaût.
' --------------------------------------------------
~WIND_UPDATE(beg_update|)
' --------------------------------------------------
' fenster_snappen(wh&,x&,y&,w&,h&)                   ! Koordinaten auf Zeichen snappen
'
~WIND_SET(wh&,5,x&,y&,w&,h&)                       ! Neue Position setzen
'
fenster_get                                        ! Neue Werte holen
' --------------------------------------------------
IF @ls>liz&                                        ! anfs& ggf. anpassen...
a&=anfs&
anfs&=MAX(1,SUB(liz&,PRED(feb&)))
'
IF a&<>anfs&                                     ! Redraw auslîsen...
~FORM_DIAL(3,0,0,0,0,wa_x&,wa_y&,wa_w&,wa_h&)
ENDIF
ENDIF
' --------------------------------------------------
IF @lz>hiz&                                        ! anfz& ggf. anpassen...
a&=anfz&
anfz&=MAX(1,SUB(hiz&,PRED(feh&)))
'
IF a&<>anfz&                                     ! Redraw auslîsen...
~FORM_DIAL(3,0,0,0,0,wa_x&,wa_y&,wa_w&,wa_h&)
ENDIF
ENDIF
' --------------------------------------------------
set_vslider(wh&)                                   ! Slider setzen
set_hslider(wh&)                                   ! Slider setzen
'
~WIND_UPDATE(end_update|)
'
RETURN
> PROCEDURE fenster_snappen(wh&,VAR x&,y&,w&,h&)     ! -CMP
LOCAL ax&,ay&,ab&,ah&
'
' Snappt die Fensterkoordinaten auf ganze Zeichen. Wird momentan nicht
' benutzt, kann aber durch entfernen der REMs vor den Aufrufen problem-
' los integriert werden.
' --------------------------------------------------
x&=MAX(deskx&,x&)
' w&=MAX(160,w&)
' h&=MAX(80,h&)
'
~WIND_CALC(wc_work|,w_attr&,x&,y&,w&,h&,ax&,ay&,ab&,ah&)
'
ax&=MUL(DIV(ax&,wchar&),wchar&) ! ...durch Zeichenbreite
ay&=MUL(DIV(ay&+PRED(hchar&),hchar&),hchar&) ! ...durch Zeichenhîhe
ab&=MUL(DIV(ab&,wchar&),wchar&) ! ...durch Zeichenbreite
ah&=MUL(DIV(ah&,hchar&),hchar&) ! ...durch Zeichenhîhe
'
~WIND_CALC(wc_border|,w_attr&,ax&,ay&,ab&,ah&,x&,y&,w&,h&)
'
' y&=MAX(desky&,y&)
'
RETURN
'
> PROCEDURE fenster_get
'
' Holt die aktuellen Fensterwerte
' --------------------------------------------------
~WIND_UPDATE(beg_update|)
'
~WIND_GET(w_handle&,5,wc_x&,wc_y&,wc_w&,wc_h&)     ! Gesamtausmaûe
~WIND_GET(w_handle&,4,wa_x&,wa_y&,wa_w&,wa_h&)     ! Arbeitsbereich
'
feh&=DIV(wa_h&,hchar&)                             ! Hîhe in Zeichen
feb&=DIV(wa_w&,wchar&)                             ! Breite in Zeichen
'
~WIND_UPDATE(end_update|)
'
RETURN
'
> FUNCTION fenster_top(handle&)
$F%
LOCAL a!,b!,ax&,ay&,ab&,ah&,rx&,ry&,rb&,rh&
'
' Gibt <>0 zurÅck, wenn das Fenster oben liegt oder die ArbeitsflÑche frei ist.
' Wenn das Fenster teilweise aus dem Bildschirm ragt, kommt FALSE zurÅck.
' --------------------------------------------------
' ~WIND_UPDATE(beg_update|)
'
~WIND_GET(handle&,4,ax&,ay&,ab&,ah&)    ! Arbeitsbereich
~WIND_GET(handle&,11,rx&,ry&,rb&,rh&)   ! Gesamtbereich
'
' ~WIND_UPDATE(end_update|)
'                                       ! Freie ArbeitsflÑche...
a!=(ax&=rx&) AND (ay&=ry&) AND (ab&=rb&) AND (ah&=rh&)
'                                       ! VollstÑndig auf dem Screen...
b!=ax&>=deskx& AND ay&>=desky& AND ADD(ax&,ab&)<=ADD(deskx&,deskw&) AND ADD(ay&,ah&)<=ADD(desky&,deskh&)
'
RETURN (a! AND b!)
ENDFUNC
'
> PROCEDURE set_vslider(wh&)
'
~WIND_SET(wh&,16,@vslider_siz,0,0,0)               ! ...rechter Slider Grîûe
~WIND_SET(wh&,9,@vslider_pos,0,0,0)                ! ...rechter Slider Pos
'
RETURN
> PROCEDURE set_hslider(wh&)
'
~WIND_SET(wh&,15,@hslider_siz,0,0,0)               ! ...unterer Slider Grîûe
~WIND_SET(wh&,8,@hslider_pos,0,0,0)                ! ...unterer Slider Pos
'
RETURN
' ----------------------------------------------------
> PROCEDURE diverse_funktionen
'
' DEFFN comp=NOT (BYTE{ADD(BASEPAGE,256)}=96)        ! Compiliert?
'
DEFFN zeilenadr(z&)=C:zeileadr_asm%(L:t_puffer%,z&)
DEFFN hiz(adr%,l&,VAR liz&)=C:hizliz_asm%(L:adr%,L:V:liz&,PRED(l&))
' DEFFN zeile$(z&)=CHAR{C:zeileadr_asm%(L:t_puffer%,z&)}
'
DEFFN anfs(a&)=SUCC(SCALE(a&,MAX(1,SUB(liz&,feb&)),1000))
DEFFN anfz(a&)=SUCC(SCALE(a&,MAX(1,SUB(hiz&,feh&)),1000))
'
DEFFN hslider_siz=MIN(1000,SCALE(1000,feb&,MAX(@ls,liz&,1)))
DEFFN hslider_pos=MIN(1000,SCALE(1000,PRED(anfs&),MAX(1,PRED(anfs&),SUB(liz&,feb&))))
DEFFN vslider_siz=MIN(1000,SCALE(1000,feh&,MAX(@lz,hiz&,1)))
DEFFN vslider_pos=MIN(1000,SCALE(1000,PRED(anfz&),MAX(1,PRED(anfz&),SUB(hiz&,feh&))))
'
DEFFN ls=ADD(anfs&,PRED(feb&))                     ! Letzte Spalte im Fenster
DEFFN lz=ADD(anfz&,PRED(feh&))                     ! Letzte Zeile im Fenster
DEFFN z(y&)=ADD(SUCC(DIV(SUB(y&,SUCC(wa_y&)),hchar&)),PRED(anfz&))
DEFFN y(z&)=ADD(wa_y&,MUL(SUB(z&,anfz&),hchar&))
'
DEFFN pfad$(a$)=LEFT$(a$,RINSTR(a$,"\"))           ! Extrahiert Pfadnamen
'
RETURN
'
> FUNCTION open_i(a$)
$F%
'
' Eine relativ sichere Dateiîffnen-Funktion.
' Gibt 0 zurÅck, wenn es geklappt hat.
' --------------------------------------------------
IF EXIST(a$)=TRUE
'
OPEN "I",#1,a$
'
RETURN 0                            ! Hat geklappt
ENDIF
'
RETURN -33                            ! Error!
ENDFUNC
'
> FUNCTION fileselect$(a$,pfad$,file$)
LOCAL b&
'
' Ersatz fÅr FILESELECT, der LINE-A benutzt.
' --------------------------------------------------
~WIND_UPDATE(1)
~WIND_UPDATE(3)
'
IF INT{{GB+4}}<&H140                               ! Altes GEM/TOS...
~FSEL_INPUT(pfad$,file$,b&)
'
ELSE                                               ! Sonst mit Titelzeile...
~@fsel_exinput(a$,pfad$,file$,b&)
ENDIF
'
~WIND_UPDATE(2)
~WIND_UPDATE(0)
'
IF b&=0 !OR file$=""
RETURN ""
ENDIF
'
RETURN @pfad$(pfad$)+file$
ENDFUNC
> FUNCTION fsel_exinput(a$,VAR pfad$,file$,b&)
$F%
'
a$=a$+CHR$(0)         ! Titel
pfad$=pfad$+CHR$(0)+SPACE$(400)
file$=file$+CHR$(0)+SPACE$(150)
'
GCONTRL(0)=91
GCONTRL(1)=0
GCONTRL(2)=2
GCONTRL(3)=3
GCONTRL(4)=0
'
ADDRIN(0)=V:pfad$     ! Pfad
ADDRIN(1)=V:file$     ! Datei
ADDRIN(2)=V:a$        ! Titel
'
GEMSYS
'
pfad$=CHAR{V:pfad$}   ! Pfad
file$=CHAR{V:file$}   ! Dateiname
'
b&=GINTOUT(1)         ! Abbruch=0, OK=1
'
RETURN GINTOUT(0)     ! Fehlercode
ENDFUNC
'
> PROCEDURE rsc_bitblt(a%,w&,h&,b%,rb&,rh&,ax&,ay&,ab&,ah&,gx&,gy&)
'
' Rasterkopierer, kopiert den Bereich ax,ay,ab,ah nach gx,gy
' a%,w&,h&,b%,rb&,rh& und q_ras%()/z_ras%() werden hier ignoriert,
' da als Quell- und Zielraster der Bildschirm benutzt wird
' --------------------------------------------------
IF ab&>0 AND ah&>0
'
par%(0)=ax&                   ! X1-Quelle
par%(1)=ay&                   ! Y1-Quelle
par%(2)=ADD(ax&,PRED(ab&))    ! X2-Quelle
par%(3)=ADD(ay&,PRED(ah&))    ! Y2-Quelle
par%(4)=gx&                   ! X1-Ziel
par%(5)=gy&                   ! Y1-Ziel
par%(6)=ADD(gx&,PRED(ab&))    ! X2-Ziel
par%(7)=ADD(gy&,PRED(ah&))    ! Y2-Ziel
par%(8)=3                     ! Modus
'
~GRAF_MOUSE(256,0)            ! HIDEM
'
BITBLT q_ras%(),z_ras%(),par%()
'
~GRAF_MOUSE(257,0)            ! SHOWM
'
ENDIF
'
RETURN
' ----------------------------------------------------
> PROCEDURE daten
'
version:
DATA 0.97 (30.12.94)
' --ENG
DATA Dateiviewer fÅr
' --ENG
DATA Fileviewer for
'
menu:
' --ENG
DATA TWO_VIEW
DATA   öber Two-View...
DATA --------------------
DATA 1,2,3,4,5,6
DATA
DATA Datei
DATA "  ôffnen...          ^O "
DATA "  Einstell. sichern  ^S "
DATA "------------------------"
DATA "  Beenden            ^Q "
DATA #
' --ENG
DATA TWO_VIEW
DATA   About Two-View...
DATA --------------------
DATA 1,2,3,4,5,6
DATA
DATA File
DATA "  Open...        ^O "
DATA "  Save settings  ^S "
DATA "--------------------"
DATA "  Quit           ^Q "
DATA #
'
RETURN
PROCEDURE inlines
'
' -------------------------------------------------- Feste Adressen
INLINE wt%,81
INLINE wi%,81
' -------------------------------------------------- Assembler-Routinen
INLINE zeileadr_asm%,26
INLINE hizliz_asm%,50
INLINE chng_lf_asm%,42
INLINE v_gtext_asm%,76
'
RETURN
