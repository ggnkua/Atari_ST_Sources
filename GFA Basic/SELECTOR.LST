DIM eintrag$(20),gewaehlt!(20)
FOR i%=0 TO 20
  READ eintrag$(i%)
NEXT i%
DATA W„hlen Sie,Dies,ist,eine,Demo,des,Pop-,Up-,Mens,der,Zeitschrift
DATA ST-Magazin,68000er,aus,einem,Artikel,ber,GFA-,BASIC,Version,3.0
'
selector(150,50,20,eintrag$(),gewaehlt!())
'
PRINT "Gew„hlt wurde:"
FOR i%=1 TO 20
  IF gewaehlt!(i%)
    PRINT eintrag$(i%)
  ENDIF
NEXT i%
'
PROCEDURE selector(x&,y&,n&,VAR t$(),g!())
  LOCAL ein&,h&,b&,i&,m0&,m1&,y_pos&,c&,f&,oben&,r$,f_pos&,mx&,my&,mk&,xb2%
  ARRAYFILL g!(),FALSE
  xb2%=XBIOS(2)
  '
  ' *** H”he h& in Pixel und maximale Anzahl ein& der   ***
  ' *** gleichzeitig sichtbaren Eintr„ge bestimmen.     ***
  ' *** Breite b& des Selektors in Pixel und Position   ***
  ' *** so korrigieren, daž er auf den Bildschirm pažt. ***
  IF n&>10          ! Bestimme die H”he h& der Selektorbox in Pixel
    h&=260          ! sowie die Anzahl der sichtbaren Eintr„ge ein&
    ein&=13         ! (maximal 10 Eintr„ge + šberschrift und Scroll-
  ELSE              ! boxen sollen sichtbar sein).
    h&=n&*20+20
    ein&=n&
  ENDIF
  '
  b&=LEN(t$(0))            ! Bestimme die Breite der Selektorbox
  FOR i&=0 TO n&           ! mit Hilfe der maximalen Eintragl„nge.
    b&=MAX(LEN(t$(i&)),b&)
  NEXT i&
  b&=(b&+4)*8              ! b = Textbreite + 4 Leerzeichen in Pixel.
  x&=MIN(x&,639-b&-16)     ! Selektorposition so korrigieren, daž
  y&=MIN(y&,125)           ! er auf den Bildschirm pažt.
  x&=MAX(x&,10)
  y&=MAX(y&,20)
  '
  GET x&-3,y&-3,x&+b&+3,y&+h&+3,r$   ! Rette den Bereich unter dem Selektor.
  '
  ' *** Zeichne den Rahmen des Selektors ***
  ACLIP 0,0,639,399,1
  m1&=-1                                           ! Schwarzes Fllmuster.
  ARECT x&-3,y&-2,x&+b&+3,y&+h&+2,1,0,V:m1&,0      ! Unterlage des Selektors.
  m0&=0                                            ! weižes Fllmuster.
  ARECT x&,SUCC(y&),x&+b&,PRED(y&+h&),1,0,V:m0&,0  ! Platz fr die Eintr„ge.
  '
  ' *** Zeichne die Unterteilungen ***
  y_pos&=y&+20
  FOR i&=1 TO ein&
    HLINE x&,y_pos&,x&+b&,1,0,V:m1&,0
    ADD y_pos&,20
  NEXT i&
  '
  ' *** Zeichne falls n”tig die Scrollpfeile und bestimme ***
  ' *** die Y-Position y& des ersten Texteintrages.       ***
  c&=b&/8-2         ! Anzahl der Zeichen des l„ngsten Eintrags.
  IF ein&=13        ! Falls Scrollpfeile erforderlich sind.
    y_pos&=y&+42    ! Y-Position des ersten Texteintrages.
    ATEXT x&+8,y&+22,2,STRING$(c&,1)   ! Scrollpfeile fr rauf.
    ATEXT x&+8,y&+242,2,STRING$(c&,2)  ! Scrollpfeile fr runter.
  ELSE
    y_pos&=y&+22    ! Y-Position des ersten Texteintrages.
  ENDIF
  '
  ' *** Schreibe die šberschrift und die Texteintr„ge. ***
  ATEXT x&+8+(c&-LEN(t$(0)))*4,y&+7,1,t$(0)       ! šberschrift.
  FOR i&=1 TO MIN(ein&,10)
    ATEXT x&+8+(c&-LEN(t$(i&)))*4,y_pos&,2,t$(i&) ! Texteintr„ge.
    ADD y_pos&,20
  NEXT i&
  '
  ' *** Selektor-Verwaltung ***
  oben&=0      ! Offset des obersten sichtbaren zum ersten Eintrag.
  f&=-1     ! Voreinstellung Maus aužerhalb Selektor.
  REPEAT
    MOUSE mx&,my&,mk&   ! Mausabfrage.
    '
    ' *** Falls die Maus im Selektor ist, ver- ***
    ' *** walte das Invertieren der Eintr„ge.  ***
    IF mx&>x& AND mx&<x&+b& AND my&>y&+20 AND my&<y&+h& ! Falls im Selektor.
      IF TRUNC((my&-y&)/20)<>f&          ! Falls Maus auf neuem Feld.
        IF f&<>-1                        ! Falls altes Feld invertiert.
          ARECT SUCC(x&),y&+f&*20+2,PRED(x&+b&),y&+f&*20+18,1,2,V:m1&,0
        ENDIF
        f&=TRUNC((my&-y&)/20)            ! Nummer des neuen Feldes.
        ARECT SUCC(x&),y&+f&*20+2,PRED(x&+b&),y&+f&*20+18,1,2,V:m1&,0
      ENDIF
    ELSE
      IF f&<>-1                          ! Falls altes Feld invertiert.
        ARECT SUCC(x&),y&+f&*20+2,PRED(x&+b&),y&+f&*20+18,1,2,V:m1&,0
      ENDIF
      f&=-1                              ! Maus aužerhalb des Selektors.
    ENDIF
    '
    ' *** Falls mit der linken Maustaste ein Eintrag gew„hlt wurde. ***
    IF mk&=1
      IF (f&>1 AND f&<12 AND ein&=13) OR (f&>0 AND f&<11 AND ein&<>13)
        f_pos&=f&+oben&           ! Nummer des angeklickten Eintrags.
        IF ein&=13
          DEC f_pos&              ! Einer weniger wegen oberem Scrollpfeil.
        ENDIF
        g!(f_pos&)=NOT g!(f_pos&) ! Negiere den gew„hlten Eintrag.
        '
        ' *** Wenn der Eintrag gew„hlt wurde, so zeichne ***
        ' *** ein H„ckchen, ansonsten l”sche es.         ***
        IF g!(f_pos&)
          ARECT SUCC(x&),y&+f&*20+2,PRED(x&+b&),y&+f&*20+20-2,1,2,V:m1&,0
          ATEXT PRED(x&+b&)-8,y&+f&*20+2,2,CHR$(8)
          ARECT SUCC(x&),y&+f&*20+2,PRED(x&+b&),y&+f&*20+20-2,1,2,V:m1&,0
        ELSE
          ARECT PRED(x&+b&)-8,y&+f&*20+2,PRED(x&+b&),y&+f&*20+20-2,1,0,V:m1&,0
        ENDIF
        WHILE MOUSEK              ! Warte, bis die Maustaste
        WEND                      ! wieder losgelassen wird.
        MOUSE mx&,my&,mk&
      ENDIF
    ENDIF
    '
    ' *** Wenn ein Scrollpfeil gew„hlt wurde und noch ein Eintrag ***
    ' *** gescrollt werden kann, so verschiebe den Bildschirmaus- ***
    ' *** schnitt und schreibe den erscheinenden Eintrag. Falls   ***
    ' *** dieser gew„hlt ist, so setze das H„ckchen.              ***
    IF ein&=13 AND (f&=1 OR f&=12) AND mk&  ! Scrollpfeil gew„hlt.
      IF f&=1                               ! Falls oberer Scrollpfeil.
        IF oben&>0                          ! Falls noch ein Eintrag.
          RC_COPY xb2%,SUCC(x&),y&+40,PRED(b&),h&-80 TO xb2%,SUCC(x&),y&+60
          ARECT SUCC(x&),y&+41,PRED(x&+b&),y&+59,1,0,V:m0&,0
          ATEXT x&+8+(c&-LEN(t$(oben&)))*4,y&+42,2,t$(oben&)
          IF g!(oben&)                           ! Falls gew„hlt,
            ATEXT PRED(x&+b&)-8,y&+42,2,CHR$(8)  ! so das H„ckchen.
          ENDIF
          DEC oben&                              ! Neues Feld als oberstes.
        ENDIF
      ELSE                                       ! Falls untere Scrollpfeile.
        IF oben&+11<=n&                     ! Falls noch ein Eintrag.
          RC_COPY xb2%,SUCC(x&),y&+60,PRED(b&),h&-80 TO xb2%,SUCC(x&),y&+40
          ARECT SUCC(x&),y&+221,PRED(x&+b&),y&+239,1,0,V:m0&,0
          ATEXT x&+8+(c&-LEN(t$(oben&+11)))*4,y&+222,2,t$(oben&+11)
          IF g!(oben&+11)                        ! Falls gew„hlt,
            ATEXT PRED(x&+b&)-8,y&+222,2,CHR$(8) ! so daž H„ckchen.
          ENDIF
          INC oben&                              ! Neues Feld als oberstes.
        ENDIF
      ENDIF
      '
      WHILE MOUSEK=2    ! Rechte Maustaste: zeilenweises Scrollen.
      WEND
      mk&=0  ! Es war nur ein Scrolling, Selektor wird nicht verlassen.
    ENDIF
    '
    ' *** Verlassen mit rechter Maustaste aužerhalb des Selektors. ***
  UNTIL mk&=2 AND f&=-1
  PUT x&-3,y&-3,r$       ! Restauriere den Bereich unter dem Selektor.
RETURN
