RESERVE 100000
DEFMOUSE 0
reso&=WORD(XBIOS(88,W:-1))
a!=BTST(reso&,0)
b!=BTST(reso&,1)
c!=NOT (BTST(reso&,3))
d!=NOT (BTST(reso&,6))
e!=NOT (BTST(reso&,7))
f!=NOT (BTST(reso&,8))
IF NOT ((a! AND b! AND c! AND d! AND e! AND f!))
  ALERT 3,"THE MULTIMAPPER 030| RUN ONLY ON| 320*(200..240) 256 COLORS",0,"OK",rep%
  '  EDIT
ENDIF
INLINE head%,128
screen%=MALLOC(64000+40*320)
part%=MALLOC(64000+1024)
block%=MALLOC(4+1024+64000*4+8*320+100000)
pal%=block%+4
map%=block%+4+1024+64000*4
motif%=block%+1028
trans%=MALLOC(1024)
fond%=MALLOC(512)
reserve%=MALLOC(300000)
general%=MALLOC(64000+1024)
undo%=MALLOC(512)
black%=MALLOC(50*320)
map_black%=MALLOC(100)
DIM processor%(256*4)
BLOAD "motif.im8",part%
BLOAD "general.im8",general%
BMOVE general%,pal%,1024
VOID XBIOS(5,L:screen%,L:screen%,W:3,W:&X11)
VSYNC
good_colors
general
> PROCEDURE general
  ' This saves the previous screen image in a string variable
  ' -------------------------------------------------------------->
  '  GOSUB zest_window(1,1,320,201)
  '  GOSUB zest_button(11,33,59,97)
  '  GOSUB zest_button(17,39,33,55)
  '  GOSUB zest_button(35,39,51,55)
  ' GOSUB zest_button(35,57,51,73)
  ' GOSUB zest_button(17,57,33,73)
  '  GOSUB zest_button(17,75,33,91)
  '  GOSUB zest_button(35,75,51,91)
  ' BMOVE XBIOS(3),reserve%+1024,64000
  ' FOR t%=0 TO 1024-4 STEP 4
  ' LPOKE reserve%+t%,LPEEK(&HF8000+t%)
  ' NEXT t%
  '  BSAVE "GENERAL.IM8",reserve%,64000+1024
  f_xbios6(general%)
gen_aff:
  BMOVE general%+1024,XBIOS(3),64000
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>6 AND mx%<19 AND my%>5 AND my%<15 AND MOUSEK=1 THEN
      GOSUB zest_button_press(6,4,15,13)
      ' This dummy command forces an exit of the loop and the procedure...
      ALERT 3,"DO YOU REALY WANT TO QUIT",0,"YES|NO",rep%
      EXIT IF rep%=1
      GOTO gen_aff
    ENDIF
    IF mx%>17 AND mx%<33 AND my%>39 AND my%<55 AND MOUSEK=1 THEN
      GOSUB zest_button_press(17,39,33,55)
      GOSUB load
      ' Here is where your own code goes for button number 2
    ENDIF
    IF mx%>35 AND mx%<51 AND my%>39 AND my%<55 AND MOUSEK=1 THEN
      GOSUB zest_button_press(35,39,51,55)
      ' Here is where your own code goes for button number 3
      bloc
    ENDIF
    IF mx%>35 AND mx%<51 AND my%>57 AND my%<73 AND MOUSEK=1 THEN
      GOSUB zest_button_press(35,57,51,73)
      ' Here is where your own code goes for button number 4
      map
    ENDIF
    IF mx%>17 AND mx%<33 AND my%>57 AND my%<73 AND MOUSEK=1 THEN
      GOSUB zest_button_press(17,57,33,73)
      ' Here is where your own code goes for button number 5
      GOSUB save
    ENDIF
    IF mx%>17 AND mx%<33 AND my%>75 AND my%<91 AND MOUSEK=1 THEN
      GOSUB zest_button_press(17,75,33,91)
      ' Here is where your own code goes for button number 6
      info
    ENDIF
    IF mx%>35 AND mx%<51 AND my%>75 AND my%<91 AND MOUSEK=1 THEN
      GOSUB zest_button_press(35,75,51,91)
      ' Here is where your own code goes for button number 7
      copyright
      GOTO gen_aff
    ENDIF
  LOOP
  ' Display the previous screen image before returning to the main program
RETURN
> PROCEDURE load
aff_load:
  BMOVE general%+1024,XBIOS(3),64000
  ' This saves the previous screen image in a string variable
  ' -------------------------------------------------------------->
  GOSUB zest_button(87,56,143,67)
  GOSUB zest_button(87,69,143,80)
  GOSUB zest_button(87,82,143,93)
  GOSUB zest_button(87,95,143,106)
  GOSUB zest_button(87,108,143,119)
  GOSUB zest_button(87,121,143,132)
  GOSUB zest_button(87,134,143,145)
  GRAPHMODE 2
  DEFTEXT 255,0,0,4
  TEXT 91,63,"LOAD IM8"
  TEXT 91,76,"LOAD BIN"
  TEXT 91,89,"LOAD MTR"
  TEXT 91,102,"LOAD MAP"
  TEXT 91,115,"LOAD PNT"
  TEXT 91,128,"LOAD PI1"
  TEXT 91,141,"LOAD GIF"
  HARDCOPY
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>87 AND mx%<143 AND my%>56 AND my%<67 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,56,143,67)
      ' Here is where your own code goes for button number 1
      GOSUB load_im8
      GOTO aff_load
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>69 AND my%<80 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,69,143,80)
      GOSUB load_bin
      GOTO aff_load
      ' Here is where your own code goes for button number 2
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>82 AND my%<93 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,82,143,93)
      ' Here is where your own code goes for button number 3
      GOSUB load_mtr
      GOTO aff_load
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>95 AND my%<106 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,95,143,106)
      GOSUB load_map
      GOTO aff_load
      ' Here is where your own code goes for button number 4
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>108 AND my%<119 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,108,143,119)
      GOSUB load_pnt
      GOTO aff_load
      ' Here is where your own code goes for button number 5
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>121 AND my%<132 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,121,143,132)
      GOSUB load_pi1
      GOTO aff_load
      ' Here is where your own code goes for button number 6
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>134 AND my%<145 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,134,143,145)
      ' Here is where your own code goes for button number 7
      GOSUB load_gif
      GOTO aff_load
    ENDIF
  LOOP UNTIL MOUSEK=2
  ' Display the previous screen image before returning to the main program
  BMOVE general%+1024,XBIOS(3),64000
  '  SPUT putback$
RETURN
> PROCEDURE load_pi1
  FILESELECT "\*.PI1","PIC.PI1",pic$          !ON CHARGE LES MOTIFS
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF EXIST(pic$)
    BLOAD pic$,reserve%
    FOR col%=0 TO 15
      base%=reserve%+2
      get&=DPEEK(base%+2*col%)
      b&=AND(get&,&X111)
      v&=AND(get&,&X1110000)
      v&=SHR&(v&,4)
      r&=AND(get&,&X11100000000)
      r&=SHR&(r&,8)
      '
      ad%=VARPTR(couleur%)
      rp&=SHL(r&,5)
      vp&=SHL(v&,5)
      bp&=SHL(b&,5)
      '
      POKE ad%,rp&
      POKE ad%+1,vp&
      POKE ad%+3,bp&
      LPOKE pal%+(4*col%),couleur%
      LPOKE pal%+4*col%,couleur%
    NEXT col%
    '
    '
    ' l'image proprement dite
    i%=reserve%+34
    pas%=0
    FOR adr%=0 TO 64000-16 STEP 16
      BMOVE i%+pas%,motif%+adr%,8
      ADD pas%,8
    NEXT adr%
  ENDIF
RETURN
> PROCEDURE save
  ' This saves the previous screen image in a string variable
  ' -------------------------------------------------------------->
sa_aff:
  BMOVE general%+1024,XBIOS(3),64000
  GOSUB zest_button(87,56,143,67)
  GOSUB zest_button(87,69,143,80)
  GOSUB zest_button(87,82,143,93)
  GOSUB zest_button(87,95,143,106)
  GOSUB zest_button(87,108,143,119)
  GRAPHMODE 2
  DEFTEXT 255,0,0,4
  TEXT 91,63,"SAVE IM8"
  TEXT 91,76,"SAVE BIN"
  TEXT 91,89,"SAVE MTR"
  TEXT 91,102,"SAVE MAP"
  TEXT 91,115,"SAVE PNT"
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>87 AND mx%<143 AND my%>56 AND my%<67 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,56,143,67)
      GOSUB save_im8
      GOTO sa_aff
      ' Here is where your own code goes for button number 1
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>69 AND my%<80 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,69,143,80)
      GOSUB save_bin
      GOTO sa_aff
      ' Here is where your own code goes for button number 2
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>82 AND my%<93 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,82,143,93)
      ' Here is where your own code goes for button number 3
      GOSUB save_mtr
      GOTO sa_aff
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>95 AND my%<106 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,95,143,106)
      GOSUB save_map
      GOTO sa_aff
      ' Here is where your own code goes for button number 4
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>108 AND my%<119 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,108,143,119)
      GOSUB save_pnt
      GOTO sa_aff
      ' Here is where your own code goes for button number 5
    ENDIF
  LOOP UNTIL MOUSEK=2
  ' Display the previous screen image before returning to the main program
  BMOVE general%+1024,XBIOS(3),64000
RETURN
> PROCEDURE bloc
bloc_aff:
  BMOVE general%+1024,XBIOS(3),64000
  GOSUB zest_button(87,56,143,67)
  GOSUB zest_button(87,69,143,80)
  GRAPHMODE 2
  DEFTEXT 255,0,0,4
  TEXT 91,63,"  EDIT  "
  TEXT 91,76,"  CLEAR "
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>87 AND mx%<143 AND my%>56 AND my%<67 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,56,143,67)
      ' Here is where your own code goes for button number 1
      16_16
      GOTO bloc_aff
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>69 AND my%<80 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,69,143,80)
      ALERT 3,"DO YOU REALY WANT TO CLEAR|THE CURRENT BLOCS ?",0,"YES|NO",rep%
      IF rep%=1
        FOR t%=motif% TO (motif%+64000*4)-4 STEP 4
          LPOKE t%,0
        NEXT t%
      ENDIF
      GOTO bloc_aff
      ' Here is where your own code goes for button number 2
    ENDIF
  LOOP UNTIL MOUSEK=2
  ' Display the previous screen image before returning to the main program
  BMOVE general%+1024,XBIOS(3),64000
RETURN
> PROCEDURE map
map_aff:
  BMOVE general%+1024,XBIOS(3),64000
  f_xbios6(general%)
  ' This saves the previous screen image in a string variable
  ' -------------------------------------------------------------->
  GOSUB zest_button(87,56,143,67)
  GOSUB zest_button(87,69,143,80)
  GOSUB zest_button(87,82,143,93)
  GRAPHMODE 2
  DEFTEXT 255,0,0,4
  TEXT 91,63,"  EDIT  "
  TEXT 91,76," NEW MAP"
  TEXT 91,89," MODIFIE"
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>87 AND mx%<143 AND my%>56 AND my%<67 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,56,143,67)
      ' Here is where your own code goes for button number 1
      IF blk_l%*blk_h%=0
        ALERT 3,"NO USEABLE MAP IN RAM",0,"OK",rep%
      ELSE
        mapper
      ENDIF
      GOTO map_aff
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>69 AND my%<80 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,69,143,80)
      ' Here is where your own code goes for button number 2
      ALERT 3,"DO YOU REALY WANT TO CLEAR|THE CURRENT MAP ?",0,"YES|NO",rep%
      IF rep%=1
        '
        BMOVE general%+1024,XBIOS(3),64000
        GOSUB zest_button(87,56,143,67)
        GOSUB zest_button(87,69,143,80)
        GOSUB zest_button(87,82,143,93)
        GRAPHMODE 2
        DEFTEXT 255,0,0,4
        TEXT 91,63,"  EDIT  "
        TEXT 91,76," NEW MAP"
        TEXT 91,89," MODIFIE"
        '
        new_map
      ENDIF
      GOTO map_aff
    ENDIF
    IF mx%>87 AND mx%<143 AND my%>82 AND my%<93 AND MOUSEK=1 THEN
      GOSUB zest_button_press(87,82,143,93)
      modify_map
      GOTO map_aff
      ' Here is where your own code goes for button number 3
    ENDIF
  LOOP UNTIL MOUSEK=2
  BMOVE general%+1024,XBIOS(3),64000
  ' Display the previous screen image before returning to the main program
RETURN
> PROCEDURE info
  GOSUB zest_window(49+40,49,102+150,148)
  GOSUB zest_text_box(53+40,73-2,98+150,144)
  DEFTEXT 1,0,0,4
  TEXT 120,80,"LARGEUR MAP"
  TEXT 120+9*8,80,blk_l%
  TEXT 120,80+7,"HAUTEUR MAP"
  TEXT 120+9*8,80+7,blk_h%
  FOR plan|=0 TO 7
    a%=motif%
    REPEAT
      get&=WORD(DPEEK(a%+plan|*2))
      ADD a%,16
    UNTIL get&<>0 OR a%=motif%+64000*4-16
    IF get&<>0
      DEFTEXT 251,0,0,4
      TEXT 110,95+plan|*6,"BITPLANE N¯"
      TEXT 112+11*6,95+plan|*6,plan|+1
      TEXT 110+13*6,95+plan|*6,"USED"
    ELSE
      DEFTEXT 252,0,0,4
      TEXT 110,95+plan|*6,"BITPLANE N¯"
      TEXT 112+11*6,95+plan|*6,plan|+1
      TEXT 110+13*6,95+plan|*6,"NOT USED"
    ENDIF
  NEXT plan|
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>54+40 AND mx%<67+40 AND my%>53 AND my%<63 AND MOUSEK=1 THEN
      GOSUB zest_button_press(54+40,52,63+40,61)
      ' This dummy command forces an exit of the loop and the procedure...
      EXIT IF 1=1
    ENDIF
  LOOP
  ' Display the previous screen image before returning to the main program
  BMOVE general%+1024,XBIOS(3),64000
RETURN
> PROCEDURE new_map
  ' -------------------------------------------------------------->
  GOSUB zest_button(154,74,215+60,145)
  GOSUB zest_text_box(160,81,209+60,138)
  LOCATE 22,13
  INPUT "LARGEUR ",blk_l%
  LOCATE 22,14
  INPUT "HAUTEUR ",blk_h%
  IF blk_h%<12            !************************************************
    blk_hm%=12            !                                               *
    max_h%=blk_h%         !   ON GERE LES MAPS DONT UNE DES DIMENTIONS    *
  ELSE                    ! EST INFERIEURE A CELLE D'UN ECRAN STANDART    *
    blk_hm%=blk_h%        !                                               *
    max_h%=12             !                                               *
  ENDIF                   !                                               *
  IF blk_l%<20            !                                               *
    blk_lm%=20            !                                               *
    max%=blk_l%           !                                               *
  ELSE                    !                                               *
    max%=20               !                                               *
    blk_lm%=blk_l%        !                                               *
  ENDIF                   !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!*
  FOR g%=map% TO map%+(blk_lm%*blk_hm%*2)
    POKE g%,999        ! ON MET DU NOIR
  NEXT g%
  haut%=0
  bas%=0
  gauche%=0
  droite%=0
  DPOKE block%,blk_l%
  DPOKE block%+2,blk_h%
  BMOVE general%+1024,XBIOS(3),64000
RETURN
> PROCEDURE 16_16
  ' This saves the previous screen image in a string variable
  ' SGET putback$
  ' -------------------------------------------------------------->
  f_xbios6(pal%)
  GOSUB zest_window(1,1,320,200)
  GOSUB zest_button(8,30,134,114)
  GOSUB zest_button(8,124,134,194)
  GOSUB zest_text_box(16,38,83,105)
  GOSUB zest_text_box(93,38,112,57)
  GOSUB zest_text_box(145,30,308,193)
  GOSUB zest_text_box(93,70,112,89)
  BMOVE part%+1024,XBIOS(3),64000
  HIDEM
  '
  RC_COPY block%+1024+4,lastw_x%,lastw_y%,16,16 TO XBIOS(3),95,40
  '
  COLOR 252
  BOX 91,36,91+24,36+24
  COLOR 253
  BOX 91,68,91+24,68+24
  '
  org_x%=95
  org_y%=40
  zoom
  recopy
  RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
  '
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>5 AND mx%<16 AND my%>3 AND my%<14 AND MOUSEK=1 THEN
      GOSUB zest_button_press(6,4,15,13)
      ' This dummy command forces an exit of the loop and the procedure...
      EXIT IF 1=1
    ENDIF
    IF MOUSEK=2
      BMOVE XBIOS(3),reserve%,64000
      edit_pal
      REPEAT
      UNTIL MOUSEK<>2
      BMOVE reserve%,XBIOS(3),64000
    ENDIF
    IF mx%>93 AND mx%<93+20 AND my%>38 AND my%<38+20 AND MOUSEK=1
      COLOR 252
      BOX 91,36,91+24,36+24
      COLOR 253
      BOX 91,68,91+24,68+24
      '
      org_x%=95
      org_y%=40
      zoom
      recopy
      '
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
    ENDIF
    IF mx%>93 AND mx%<93+20 AND my%>70 AND my%<70+20 AND MOUSEK=1
      COLOR 252
      BOX 91,68,91+24,68+24
      COLOR 253
      BOX 91,36,91+24,36+24
      '
      org_x%=95
      org_y%=72
      zoom
      recopy
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
      '
    ENDIF
    IF mx%>16 AND mx%<32 AND my%>134 AND my%<150 AND MOUSEK=1 THEN
      GOSUB zest_button_press(16,134,32,150)
      RC_COPY XBIOS(3),95,40,16,16 TO fond%,0,0
      RC_COPY XBIOS(3),95,72,16,16 TO XBIOS(3),95,40
      RC_COPY fond%,0,0,16,16 TO XBIOS(3),95,72
      zoom
      recopy
      ' Here is where your own code goes for button number 2
    ENDIF
    IF mx%>39 AND mx%<55 AND my%>134 AND my%<150 AND MOUSEK=1 THEN
      GOSUB zest_button_press(39,134,55,150)
      ' Here is where your own code goes for button number 3
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
      trame
      zoom
      recopy
    ENDIF
    IF mx%>62 AND mx%<78 AND my%>134 AND my%<150 AND MOUSEK=1 THEN
      GOSUB zest_button_press(62,134,78,150)
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
      rotate
      zoom
      recopy
    ENDIF
    IF mx%>85 AND mx%<101 AND my%>134 AND my%<150 AND MOUSEK=1 THEN
      GOSUB zest_button_press(85,134,101,150)
      ' Here is where your own code goes for button number 5
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
      flip_h
      zoom
      recopy
    ENDIF
    IF mx%>108 AND mx%<124 AND my%>134 AND my%<150 AND MOUSEK=1 THEN
      GOSUB zest_button_press(108,134,124,150)
      ' Here is where your own code goes for button number 6
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
      flip_v
      zoom
      recopy
    ENDIF
    IF mx%>16 AND mx%<32 AND my%>157 AND my%<173 AND MOUSEK=1 THEN
      GOSUB zest_button_press(16,157,32,173)
      ' Here is where your own code goes for button number 7
      RC_COPY undo%,0,0,16,16 TO XBIOS(3),org_x%,org_y%
      zoom
      recopy
    ENDIF
    IF mx%>39 AND mx%<55 AND my%>157 AND my%<173 AND MOUSEK=1 THEN
      GOSUB zest_button_press(39,157,55,173)
      ' Here is where your own code goes for button number 8
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
      get
    ENDIF
    IF mx%>62 AND mx%<78 AND my%>157 AND my%<173 AND MOUSEK=1 THEN
      GOSUB zest_button_press(62,157,78,173)
      ' Here is where your own code goes for button number 9
      valide
    ENDIF
    IF mx%>85 AND mx%<101 AND my%>157 AND my%<173 AND MOUSEK=1 THEN
      GOSUB zest_button_press(85,157,101,173)
      ' Here is where your own code goes for button number 10
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
      combine
      zoom
      recopy
    ENDIF
    IF mx%>108 AND mx%<124 AND my%>157 AND my%<173 AND MOUSEK=1 THEN
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
      fill!=TRUE
      GOSUB zest_button_press(108,157,124,173)
      ' Here is where your own code goes for button number 11
    ENDIF
    draw
  LOOP
  f_xbios6(general%)
RETURN
> PROCEDURE edit_pal
  GOSUB init_color
  GOSUB draw_screen_color
  GOSUB monitor_mouse_color
RETURN
> PROCEDURE init_color
  CLS
  DEFTEXT 1,0,0,13
  DEFMOUSE 0
  BOUNDARY 0
  DEFFILL 253,1,9
  PBOX 0,0,319,199
RETURN
> PROCEDURE load_mtr
  FILESELECT "\*.mtr","level_x.mtr",mtr$
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF EXIST(mtr$)
    BLOAD mtr$,block%
  ENDIF
  blk_l%=DPEEK(block%)
  blk_h%=DPEEK(block%+2)
  begin_map
RETURN
> PROCEDURE save_mtr
  FILESELECT "\*.mtr","level_x.mtr",mtr$
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF mtr$<>""
    BSAVE mtr$,block%,64000*4+4+1024+(blk_l%*blk_h%*2)
  ENDIF
RETURN
> PROCEDURE load_im8
  FILESELECT "\*.IM8","BLOCK.IM8",pic$          !ON CHARGE LES MOTIFS
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF EXIST(pic$)
    BLOAD pic$,block%+4               !ON CHARGE LES MOTIFS
  ENDIF
RETURN
> PROCEDURE save_im8
  FILESELECT "\*.IM8","BLOCK.IM8",pic$          !ON sauve LES MOTIFS
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF pic$<>""
    BSAVE pic$,block%+4,64000+1024               !ON sauve LES MOTIFS
  ENDIF
RETURN
> PROCEDURE mapper
  HIDEM
  FOR a%=0 TO 3
    BMOVE black%,XBIOS(3)+a%*320*50,320*50
  NEXT a%
  f_xbios6(pal%)
  affiche(gauche%,droite%,haut%,bas%)
  REPEAT
    pos_x%=(INT(MOUSEX/16))*16            ! X SUR DES MULTIPLES DE 16
    pos_y%=(INT(MOUSEY/16))*16            ! Y SUR DES MULTIPLES DE 16
    IF pos_y%\16>blk_h%-1                 ! POUR LES ECRANTS PLUS PETIT QUE LE STANDART
      pos_y%=(blk_h%-1)*16
    ENDIF
    IF pos_x%\16>blk_l%-1                 !IDEM POUR LES X
      pos_x%=(blk_l%-1)*16
    ENDIF
    IF pos_y%>176
      pos_y%=176
    ENDIF
    get$=INKEY$                           ! MEMORISER UNE TOUCHE PRESSêE
    get$=UPPER$(get$)
    IF pos_x%\16=19 AND (droite%-gauche%<blk_l%-20)       !SOURIS DANS LE BORD DROIT
      GOSUB a_droite
    ENDIF
    IF pos_x%=0 AND (droite%-gauche%>0)                   !SOURIS DANS LE BORD GAUCHE
      GOSUB a_gauche
    ENDIF
    IF MOUSEY<10 AND (bas%-haut%>0)                       !SOURIS EN HAUT
      GOSUB a_haut
    ENDIF
    IF MOUSEY>180 AND (bas%-haut%<blk_h%-12)              !SOURIS EN BAS
      GOSUB a_bas
    ENDIF
    RC_COPY XBIOS(3),pos_x%,pos_y%,16,16 TO fond%,0,0
    GOSUB pose
    VSYNC
    RC_COPY fond%,0,0,16,16 TO XBIOS(3),pos_x%,pos_y%
    IF MOUSEK=2
      GOSUB choix                  !BOUTON GAUCHE POUR CHOISIR LE MOTIF
    ENDIF
  UNTIL get$=" "
  SHOWM
RETURN
> PROCEDURE pose
  blkx%=blk% MOD 20
  '  blky%=blk%\20
  adry%=(blk%\20)*320*16
  RC_COPY motif%+adry%,blkx%*16,0,16,16 TO XBIOS(3),pos_x%,pos_y%
  COLOR 252
  BOX pos_x%,pos_y%,pos_x%+15,pos_y%+15
  IF MOUSEK=1
    RC_COPY motif%+adry%,blkx%*16,0,16,16 TO fond%,0,0
    GOSUB traite
  ENDIF
RETURN
> PROCEDURE traite
  DPOKE map%+(INT(pos_x%/16)+INT(pos_y%/16)*blk_l%+(droite%-gauche%)+((bas%-haut%)*blk_l%))*2,blk%
RETURN
> PROCEDURE a_droite
  INC droite%
  BMOVE XBIOS(3)+16,XBIOS(3),max_h%*5120
  FOR a%=0 TO (max_h%-1)*16 STEP 16
    po%=DPEEK(map%+(19+droite%-gauche%+(a%*blk_l%)\16+(bas%-haut%)*blk_l%)*2)
    pox%=po% MOD 20
    '    poy%=po%\20
    adry%=po%\20
    RC_COPY motif%+adry%*320*16,pox%*16,0,16,16 TO XBIOS(3),304,a%
  NEXT a%
RETURN
> PROCEDURE a_gauche
  BMOVE XBIOS(3),XBIOS(3)+16,max_h%*5120
  INC gauche%
  FOR a%=0 TO (max_h%-1)*16 STEP 16
    po%=DPEEK(map%+(droite%-gauche%+(a%*blk_l%)\16+(bas%-haut%)*blk_l%)*2)
    pox%=po% MOD 20
    '    poy%=po%\20
    adry%=po%\20
    RC_COPY motif%+adry%*320*16,pox%*16,0,16,16 TO XBIOS(3),0,a%
  NEXT a%
RETURN
> PROCEDURE a_haut
  INC haut%
  BMOVE XBIOS(3),XBIOS(3)+320*16,64000-(8*320)-16*320
  FOR a%=0 TO (max%*16)-16 STEP 16
    po%=DPEEK(map%+(droite%-gauche%+(bas%-haut%)*blk_l%+a%/16)*2)
    pox%=po% MOD 20
    '    poy%=po%\20
    adry%=po%\20
    RC_COPY motif%+adry%*320*16,pox%*16,0,16,16 TO XBIOS(3),a%,0
  NEXT a%
RETURN
> PROCEDURE a_bas
  INC bas%
  BMOVE XBIOS(3)+16*320,XBIOS(3),64000-(8*320)-16*320
  FOR a%=0 TO (max%*16)-16 STEP 16
    po%=DPEEK(map%+(droite%-gauche%+(bas%-haut%+11)*blk_l%+a%/16)*2)
    pox%=po% MOD 20
    '    poy%=po%\20
    adry%=po%\20
    RC_COPY motif%+adry%*320*16,pox%*16,0,16,16 TO XBIOS(3),a%,192-16
  NEXT a%
RETURN
> PROCEDURE save_map
  '
  FILESELECT "\*.MAP",tab$,save$
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF save$<>""
    BMOVE map%,map%+4,(blk_l%*blk_h%*2)
    DPOKE map%,blk_l%
    DPOKE map%+2,blk_h%
    BSAVE save$,map%,(blk_l%*blk_h%*2)+4
    BMOVE map%+4,map%,blk_l%*blk_h%*2
  ENDIF
  '
RETURN
> PROCEDURE save_bin
  FILESELECT "\*.bin","BLOCK.BIN",blk$
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF blk$<>""
    pas%=0
    end%=800*320
    DO
      SUB end%,4
      EXIT IF LPEEK(end%+motif%)<>0
    LOOP
    vert_line%=end%\320
    vert_bloc%=vert_line%\16
    debut%=motif%
    FOR y_blkb%=0 TO (vert_bloc%*16)-16 STEP 16
      FOR x_blkb%=0 TO 320-8 STEP 16
        FOR y%=0 TO 15 STEP 1
          '        FOR x%=0 TO 14 STEP 2
          '            g%=DPEEK(debut%+y_blkb%*320+x_blkb%+x%+y%*320)
          '           DPOKE reserve%+pas%+1024,g%
          '        NEXT x%
          BMOVE debut%+y_blkb%*320+x_blkb%+y%*320,reserve%+pas%+1024,16
          ADD pas%,16
        NEXT y%
      NEXT x_blkb%
    NEXT y_blkb%
    BMOVE pal%,reserve%,1024
    BSAVE blk$,reserve%,(vert_bloc%*320*16)+1024
  ENDIF
RETURN
> PROCEDURE load_bin
  FILESELECT "\*.bin","BLOCK.BIN",blk$
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF EXIST(blk$)
    BLOAD blk$,reserve%
    pas%=0
    debut%=motif%
    FOR y_blkb%=0 TO 800-16 STEP 16
      FOR x_blkb%=0 TO 320-8 STEP 16
        FOR y%=0 TO 15 STEP 1
          '         FOR x%=0 TO 14 STEP 2
          '        g%=DPEEK(reserve%+pas%+1024)
          '            DPOKE (debut%+y_blkb%*320+x_blkb%+x%+y%*320),g%
          BMOVE reserve%+pas%+1024,debut%+y_blkb%*320+x_blkb%+y%*320,16
          ADD pas%,16
          '     NEXT x%
        NEXT y%
      NEXT x_blkb%
    NEXT y_blkb%
    BMOVE reserve%,pal%,1024
  ENDIF
RETURN
> PROCEDURE load_gif
  ALERT 3,"Cette option n'est pas|encore disponible",0,"ok",rep%
RETURN
> PROCEDURE load_pnt
  LOCAL r&,v&,b&,t%,rouge%,vert%,bleu%,vdi%,hard%
  FILESELECT "\*.PNT","IMAGE.PNT",pnt$
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF EXIST(pnt$)
    BLOAD pnt$,reserve%
    IF DPEEK(reserve%+6)=256 AND ((DPEEK(reserve%+10)=200) OR (DPEEK(reserve%+10=240))) AND DPEEK(reserve%+8)=320 AND (LPEEK(reserve%+16)=(DPEEK(reserve%+8)*DPEEK(reserve%+10)))
      vdi%=0
      FOR t%=128 TO 128+((256-1)*3*2) STEP 6
        r&=WORD(DPEEK(t%+reserve%))
        v&=WORD(DPEEK(t%+reserve%+2))
        b&=WORD(DPEEK(t%+reserve%+4))
        rouge%=VARPTR(long%)
        vert%=rouge%+1
        bleu%=rouge%+3
        POKE rouge%,SHR(r&,2)
        POKE vert%,SHR(v&,2)
        POKE bleu%,SHR(b&,2)
        vdi_hard(vdi%)
        LPOKE block%+4+hard%*4,long%
        vdi%=vdi%+1
      NEXT t%
      BMOVE reserve%+128+256*3*2,motif%,64000
    ELSE
      ALERT 3,"ONLY 320*(200..240) UNPACKED| 256 COLORS PICTURES| SORRY...",0,"OK",rep%
    ENDIF
  ENDIF
RETURN
> PROCEDURE save_pnt
  FILESELECT "\*.PNT","PIC.PNT",pnt$
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  IF pnt$<>""
    BMOVE head%,reserve%,128
    FOR t%=0 TO 255
      vdi_hard(t%)
      '
      base%=block%+4
      get%=LPEEK(base%+4*hard%)
      r&=PEEK(VARPTR(get%))
      r&=SHR(r&,2)
      v&=PEEK(VARPTR(get%)+1)
      v&=SHR(v&,2)
      b&=PEEK(VARPTR(get%)+3)
      b&=SHR(b&,2)
      '
      DPOKE reserve%+128+3*2*t%,WORD(15*r&)
      DPOKE reserve%+128+3*2*t%+2,WORD(15*v&)
      DPOKE reserve%+128+3*2*t%+4,WORD(15*b&)
    NEXT t%
    BMOVE block%+4+1024,reserve%+128+3*2*256,64000
    BSAVE pnt$,reserve%,64000+128+3*2*256
  ENDIF
RETURN
> PROCEDURE affiche(g%,d%,ho%,b%)
  FOR h%=0 TO max_h%-1
    FOR a%=0 TO (max%*16)-16 STEP 16
      po%=DPEEK(map%+((h%+b%-ho%)*blk_l%+a%/16+(d%-g%))*2)
      pox%=po% MOD 20
      '      poy%=po%\20
      adry%=(po%\20)*320*16
      RC_COPY motif%+adry%,pox%*16,0,16,16 TO XBIOS(3),a%,h%*16
    NEXT a%
  NEXT h%
RETURN
> PROCEDURE load_map
  FILESELECT "\*.MAP","LAR_HAUT.MAP",tab$     !ON CHARGE LA MAP
  BMOVE general%+1024,XBIOS(3),64000
  DEFFILL 0,1,9
  PBOX 0,200,320,239
  PRINT AT(1,4)
  IF EXIST(tab$)
    BLOAD tab$,map%
    blk_l%=DPEEK(map%)
    blk_h%=DPEEK(map%+2)
    BMOVE map%+4,map%,(blk_l%*blk_h%*2)+1000
    IF blk_h%<12            !************************************************
      blk_hm%=12            !                                               *
      max_h%=blk_h%         !   ON GERE LES MAPS DONT UNE DES DIMENTION     *
    ELSE                    ! EST INFERIEURE A CELLE D'UN ECRAN STANDART    *
      blk_hm%=blk_h%        !                                               *
      max_h%=12             !                                               *
    ENDIF                   !                                               *
    IF blk_l%<20            !                                               *
      blk_lm%=20            !                                               *
      max%=blk_l%           !                                               *
    ELSE                    !                                               *
      max%=20               !                                               *
      blk_lm%=blk_l%        !                                               *
    ENDIF                   !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!*
    haut%=0
    bas%=0
    gauche%=0
    droite%=0
    DPOKE block%,blk_l%
    DPOKE block%+2,blk_h%
  ENDIF
RETURN
> PROCEDURE begin_map
  IF blk_h%<12            !************************************************
    blk_hm%=12            !                                               *
    max_h%=blk_h%         !   ON GERE LES MAPS DONT UNE DES DIMENTION     *
  ELSE                    ! EST INFERIEURE A CELLE D'UN ECRAN STANDART    *
    blk_hm%=blk_h%        !                                               *
    max_h%=12             !                                               *
  ENDIF                   !                                               *
  IF blk_l%<20            !                                               *
    blk_lm%=20            !                                               *
    max%=blk_l%           !                                               *
  ELSE                    !                                               *
    max%=20               !                                               *
    blk_lm%=blk_l%        !                                               *
  ENDIF                   !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!*
  haut%=0
  bas%=0
  gauche%=0
  droite%=0
  DPOKE block%,blk_l%
  DPOKE block%+2,blk_h%
RETURN
> PROCEDURE modify_map
  GOSUB zest_button(154,74,215+60,145)
  GOSUB zest_text_box(160,81,209+60,138)
  LOCATE 22,13
  old_l%=blk_l%
  old_h%=blk_h%
  FOR a%=0 TO 99
    POKE map_black%+a%,999
  NEXT a%
  FOR a%=0 TO 999
    BMOVE map_black%,reserve%+a%*100,100
  NEXT a%
  INPUT "LARGEUR ",blk_l%
  IF blk_l%<old_l%
    FOR b%=0 TO blk_h%
      BMOVE map%+old_l%*2*b%,reserve%+blk_l%*b%*2,blk_l%*2
    NEXT b%
  ENDIF
  IF blk_l%>old_l%
    FOR b%=0 TO blk_h%
      BMOVE map%+old_l%*b%*2,reserve%+blk_l%*b%*2,old_l%*2
    NEXT b%
  ENDIF
  LOCATE 22,14
  INPUT "HAUTEUR ",blk_h%
  IF blk_l%<>old_l%
    BMOVE reserve%,map%,blk_l%*blk_h%*2
  ENDIF
  begin_map
RETURN
> PROCEDURE right
  GOSUB zest_window(49+40,49,102+150,148)
  GOSUB zest_text_box(53+40,73-2,98+150,144)
  DEFTEXT 252,0,0,6
  TEXT 100,70,"MULTIMAPPEUR  030"
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    FOR t%=0 TO 360 STEP 5
      IF mx%>54+40 AND mx%<67+40 AND my%>53 AND my%<63 AND MOUSEK=1 THEN
        GOSUB zest_button_press(54+40,52,63+40,61)
        EXIT IF 1=1
      ENDIF
      FOR a%=0 TO 100 STEP 4
        RC_COPY XBIOS(3),a%+100,60,4,20 TO XBIOS(3),a%+100,120+5*SINQ((a%+t%)*3.6)
      NEXT a%
      VSYNC
    NEXT t%
    ' This dummy command forces an exit of the loop and the procedure...
  LOOP
RETURN
> PROCEDURE copyright
  GOSUB zest_window(49+40,49,102+150,148)
  GOSUB zest_text_box(53+40,73-2,98+150,144)
  DEFTEXT 252,0,0,6
  TEXT 107,82,"MULTIMAPPEUR 030"
  DEFTEXT 253,0,0,4
  TEXT 105,95,"TO FALCON 030 COMPUTER "
  DEFTEXT 255,0,0,4
  TEXT 100,130,"(C) Copyright"
  TEXT 100,140,"DAVID ALLOZA 1993 FRANCE"
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>54+40 AND mx%<67+40 AND my%>53 AND my%<63 AND MOUSEK=1 THEN
      GOSUB zest_button_press(54+40,52,63+40,61)
      ' This dummy command forces an exit of the loop and the procedure...
      EXIT IF 1=1
    ENDIF
  LOOP
  ' Display the previous screen image before returning to the main program
  BMOVE general%+1024,XBIOS(3),64000
RETURN
> PROCEDURE zest_button(upper_x%,upper_y%,lower_x%,lower_y%)
  COLOR 1
  DEFLINE 1,1
  DEFFILL 254,2,8
  PBOX upper_x%,upper_y%,lower_x%,lower_y%
  DEFFILL 253,1,1
  PBOX upper_x%+1,upper_y%+1,lower_x%,lower_y%
  LINE upper_x%+1,lower_y%,lower_x%,lower_y%
  LINE upper_x%,lower_y%+1,lower_x%,lower_y%+1
  LINE lower_x%,upper_y%+1,lower_x%,lower_y%
  LINE lower_x%+1,upper_y%,lower_x%+1,lower_y%+1
RETURN
> PROCEDURE zest_button_press(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  GET upper_x%-1,upper_y%-1,lower_x%+1,lower_y%+1,button$
  GET upper_x%+1,upper_y%+1,lower_x%-2,lower_y%-2,shift_button$
  PUT upper_x%+2,upper_y%+2,shift_button$
  LINE upper_x%,upper_y%,lower_x%,upper_y%
  LINE upper_x%,upper_y%,upper_x%,lower_y%
  COLOR 254
  LINE upper_x%+1,lower_y%,lower_x%,lower_y%
  LINE upper_x%,lower_y%+1,lower_x%,lower_y%+1
  LINE lower_x%,upper_y%+1,lower_x%,lower_y%
  LINE lower_x%+1,upper_y%,lower_x%+1,lower_y%+1
  COLOR 1
  DO
    SHOWM
  LOOP UNTIL MOUSEK=0
  PAUSE 3
  IF fill!
    REPEAT
      fill
    UNTIL NOT fill!
  ENDIF
  PUT upper_x%-1,upper_y%-1,button$
RETURN
> PROCEDURE zest_info_box(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  DEFFILL 253,1,1
  PBOX upper_x%,upper_y%,lower_x%,lower_y%
  LINE upper_x%,upper_y%,lower_x%,upper_y%
  LINE upper_x%,upper_y%+1,lower_x%,upper_y%+1
  LINE upper_x%,upper_y%,upper_x%,lower_y%
  LINE upper_x%+1,upper_y%,upper_x%+1,lower_y%
  COLOR 254
  LINE upper_x%+1,lower_y%,lower_x%,lower_y%
  LINE upper_x%,lower_y%+1,lower_x%,lower_y%+1
  LINE lower_x%,upper_y%+1,lower_x%,lower_y%
  LINE lower_x%+1,upper_y%,lower_x%+1,lower_y%+1
  COLOR 1
RETURN
> PROCEDURE zest_text_box(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  DEFFILL 253,1,1
  PBOX upper_x%,upper_y%,lower_x%,lower_y%
  LINE upper_x%,upper_y%,lower_x%,upper_y%
  LINE upper_x%,upper_y%+1,lower_x%,upper_y%+1
  LINE upper_x%,upper_y%,upper_x%,lower_y%
  LINE upper_x%+1,upper_y%,upper_x%+1,lower_y%
  COLOR 254
  LINE upper_x%+1,lower_y%,lower_x%,lower_y%
  LINE upper_x%,lower_y%+1,lower_x%,lower_y%+1
  LINE lower_x%,upper_y%+1,lower_x%,lower_y%
  LINE lower_x%+1,upper_y%,lower_x%+1,lower_y%+1
  COLOR 1
  DEFFILL 254,2,8
  PBOX upper_x%+2,upper_y%+2,lower_x%-2,lower_y%-2
RETURN
> PROCEDURE zest_horiz_line(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  LINE upper_x%,upper_y%,lower_x%,lower_y%
  COLOR 254
  LINE upper_x%,upper_y%+1,lower_x%,lower_y%+1
  COLOR 1
RETURN
> PROCEDURE zest_vert_line(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  LINE upper_x%,upper_y%,lower_x%,lower_y%
  COLOR 254
  LINE upper_x%+1,upper_y%,lower_x%+1,lower_y%
  COLOR 1
RETURN
> PROCEDURE zest_line_box(upper_x%,upper_y%,lower_x%,lower_y%)
  DEFLINE 1,1
  COLOR 254
  BOX upper_x%,upper_y%,lower_x%,lower_y%
  COLOR 1
  LINE upper_x%-1,upper_y%,upper_x%-1,lower_y%
  LINE upper_x%+2,lower_y%-1,lower_x%-2,lower_y%-1
  LINE lower_x%-1,upper_y%+2,lower_x%-1,lower_y%-2
  LINE upper_x%-1,upper_y%-1,lower_x%,upper_y%-1
RETURN
> PROCEDURE zest_window(upper_x%,upper_y%,lower_x%,lower_y%)
  ' title bar
  GOSUB zest_button(upper_x%,upper_y%,lower_x%,upper_y%+15)
  ' window area
  GOSUB zest_button(upper_x%,upper_y%+17,lower_x%,lower_y%)
  ' close button
  GOSUB zest_button(upper_x%+5,upper_y%+3,upper_x%+14,upper_y%+12)
RETURN
'
> PROCEDURE draw_screen_color
  ' Create any ZeST box, button or window by passing the
  ' upper_x%, upper_y%, lower_x% and lower_y% coordinates
  ' to the correct procedure.
  ' -------------------------------------------------------------->
  GOSUB zest_text_box(130,9,310,189)
  GOSUB zest_info_box(27,100,43,179)
  GET 27,100,44,180,coverup1$
  PUT 53,100,coverup1$
  PUT 79,100,coverup1$
  '
  GOSUB zest_button(29,102,41,114)
  GOSUB zest_horiz_line(29,108,41,108)
  GET 29,102,42,115,slider1$
  PUT 55,102,slider1$
  PUT 81,102,slider1$
  '
  GOSUB zest_button(27,85,43,97)
  GOSUB zest_button(27+26,85,43+26,97)
  GOSUB zest_button(27+52,85,43+52,97)
  GRAPHMODE 2
  DEFTEXT 1,0,0,6
  TEXT 32,94,"R"
  TEXT 32+26,94,"V"
  TEXT 32+52,94,"B"
  GOSUB zest_text_box(27,183,43,193)
  GOSUB zest_text_box(27+26,183,43+26,193)
  GOSUB zest_text_box(27+52,183,43+52,193)
  GOSUB r_v_b(hard%)
  GOSUB update_slide
  '
  GOSUB zest_text_box(27,50,96,70)
  GOSUB zest_text_box(28,22,28+19,22+19)
  RC_COPY reserve%,org_x%,org_y%,16,16 TO XBIOS(3),30,24
  hard_vdi(hard%)
  DEFFILL vdi%,1,1
  PBOX 29,52,94,68
  aff_pal
  cadre(1)
RETURN
> PROCEDURE monitor_mouse_color
  DO
    SHOWM
    mx%=MOUSEX
    my%=MOUSEY
    IF mx%>27 AND mx%<43 AND my%>100 AND my%<179 AND MOUSEK=1 AND pos_y1%<>MOUSEY-6 AND MOUSEY-6>101 AND MOUSEY+6<178 THEN
      pos_y1%=MOUSEY-6
      r&=108+63-my%        !reflects;this;sliders;position;from;0;to;49
      PUT 27,100,coverup1$
      PUT 29,pos_y1%,slider1$
      aff_comp
      f_color
    ENDIF
    IF mx%>27+26 AND mx%<43+26 AND my%>100 AND my%<179 AND MOUSEK=1 AND pos_y1%<>MOUSEY-6 AND MOUSEY-6>101 AND MOUSEY+6<178 THEN
      pos_y1%=MOUSEY-6
      v&=108+63-my%
      PUT 27+26,100,coverup1$
      PUT 29+26,pos_y1%,slider1$
      aff_comp
      f_color
    ENDIF
    IF mx%>27+52 AND mx%<43+52 AND my%>100 AND my%<179 AND MOUSEK=1 AND pos_y1%<>MOUSEY-6 AND MOUSEY-6>101 AND MOUSEY+6<178 THEN
      pos_y1%=MOUSEY-6
      b&=108+63-my%
      PUT 27+52,100,coverup1$
      PUT 29+52,pos_y1%,slider1$
      aff_comp
      f_color
    ENDIF
    IF mx%>133 AND mx%<133+11*16 AND my%>12 AND my%<12+11*16 AND MOUSEK=1
      cadre(254)
      hard%=(mx%-133)\11+16*((my%-12)\11)
      cadre(1)
      r_v_b(hard%)
      update_slide
    ENDIF
  LOOP UNTIL MOUSEK=2
RETURN
> PROCEDURE draw_screen_16
  GOSUB zest_text_box(3,3,64+2+1,64+2+1)
  GOSUB zest_button(3+64+8,3,3+64+8+48+2,3+64+1)
  REPEAT
  UNTIL MOUSEK
RETURN
> PROCEDURE update_slide
  PUT 27,100,coverup1$
  PUT 29,102+63-r&,slider1$
  '
  PUT 27+26,100,coverup1$
  PUT 29+26,102+63-v&,slider1$
  '
  PUT 27+52,100,coverup1$
  PUT 29+52,102+63-b&,slider1$
  '
  hard_vdi(hard%)
  DEFFILL vdi%,1,1
  PBOX 29,52,94,68
  aff_comp
RETURN
> PROCEDURE aff_pal
  aff%=0
  FOR y%=12 TO 12+11*(16-1) STEP 11
    FOR x%=133 TO 133+11*(16-1) STEP 11
      hard_vdi(aff%)
      DEFFILL vdi%,1,1
      PBOX x%,y%,x%+9,y%+9
      INC aff%
    NEXT x%
  NEXT y%
RETURN
> PROCEDURE hard_vdi(hard%)
  vdi%=hard%
  SELECT hard%
  CASE 0,4,12
    vdi%=hard%
  CASE 1
    vdi%=2
  CASE 2
    vdi%=3
  CASE 3
    vdi%=6
  CASE 5
    vdi%=7
  CASE 6
    vdi%=5
  CASE 7
    vdi%=8
  CASE 8
    vdi%=9
  CASE 9
    vdi%=10
  CASE 10
    vdi%=11
  CASE 11
    vdi%=14
  CASE 13
    vdi%=15
  CASE 14
    vdi%=13
  CASE 15
    vdi%=255
  CASE 255
    vdi%=1
  ENDSELECT
RETURN
> PROCEDURE f_color
  ad%=VARPTR(col%)
  rp&=SHL(r&,2)
  vp&=SHL(v&,2)
  bp&=SHL(b&,2)
  POKE ad%,rp&
  POKE ad%+1,vp&
  POKE ad%+3,bp&
  SLPOKE &HFFFF9800+(4*hard%),col%
  SLPOKE pal%+4*hard%,col%
RETURN
> PROCEDURE f_xbios6(reg%)
  FOR t%=0 TO 1024-4 STEP 4
    SLPOKE t%+&HFFFF9800,LPEEK(reg%+t%)
  NEXT t%
RETURN
> PROCEDURE r_v_b(col%)
  base%=&HFFFF9800
  get%=LPEEK(base%+4*col%)
  r&=PEEK(VARPTR(get%))
  r&=SHR(r&,2)
  v&=PEEK(VARPTR(get%)+1)
  v&=SHR(v&,2)
  b&=PEEK(VARPTR(get%)+3)
  b&=SHR(b&,2)
RETURN
> PROCEDURE good_colors
  LPOKE pal%+4*253,&HB4B400B4
  LPOKE pal%+4*254,&HFFFF00FF
  r&=45
  v&=45
  b&=45
  hard%=253
  f_color
  r&=63
  v&=63
  b&=63
  hard%=254
  f_color
  r&=0
  v&=0
  b&=0
  hard%=255
  f_color
RETURN
> PROCEDURE cadre(col%)
  COLOR col%
  BOX (133+11*(hard% MOD 16))-1,(12+11*(hard%\16))-1,(133+11*(hard% MOD 16))+10,(12+11*(hard%\16))+10
RETURN
> PROCEDURE aff_comp
  GRAPHMODE 2
  DEFFILL 254,1,1
  PBOX 30,185,40,190
  PBOX 30+26,185,40+26,190
  PBOX 30+52,185,40+52,190
  DEFTEXT 1,0,0,4
  r$=RIGHT$("0"+STR$(r&),2)
  v$=RIGHT$("0"+STR$(v&),2)
  b$=RIGHT$("0"+STR$(b&),2)
  TEXT 30,190,r$
  TEXT 30+26,190,v$
  TEXT 30+52,190,b$
  GRAPHMODE 1
RETURN
> PROCEDURE flip_h
  FOR x%=0 TO 7
    dr%=15-x%
    RC_COPY XBIOS(3),x%+org_x%,org_y%,1,16 TO trans%,0,0
    RC_COPY XBIOS(3),dr%+org_x%,org_y%,1,16 TO XBIOS(3),x%+org_x%,org_y%
    RC_COPY trans%,0,0,1,16 TO XBIOS(3),dr%+org_x%,org_y%
  NEXT x%
RETURN
> PROCEDURE flip_v
  FOR y%=0 TO 7
    dr%=15-y%
    RC_COPY XBIOS(3),org_x%,y%+org_y%,16,1 TO trans%,0,0
    RC_COPY XBIOS(3),org_x%,dr%+org_y%,16,1 TO XBIOS(3),org_x%,org_y%+y%
    RC_COPY trans%,0,0,16,1 TO XBIOS(3),org_x%,dr%+org_y%
  NEXT y%
RETURN
> PROCEDURE zoom
  FOR x%=0 TO 159
    RC_COPY XBIOS(3),(x%\10)+org_x%,org_y%,1,16 TO trans%,x%,0
  NEXT x%
  FOR y%=0 TO 159
    RC_COPY trans%,0,(y%\10),160,1 TO XBIOS(3),147,y%+32
  NEXT y%
RETURN
> PROCEDURE recopy
  FOR y%=40 TO 88 STEP 16
    FOR x%=18 TO 18+3*16 STEP 16
      RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO XBIOS(3),x%,y%
    NEXT x%
  NEXT y%
RETURN
> PROCEDURE draw
  mx%=MOUSEX
  my%=MOUSEY
  d_org_x%=147
  d_org_y%=32
  IF my%<192 AND mx%>147 AND my%>32 AND mx%<307 AND MOUSEK=1
    RC_COPY XBIOS(3),org_x%,org_y%,16,16 TO undo%,0,0
  ENDIF
  IF my%<192 AND mx%>147 AND my%>32 AND mx%<307 AND MOUSEK=1
  e:
    mx%=MOUSEX
    my%=MOUSEY
    x%=(10*INT((mx%-d_org_x%)/10))
    y%=(10*INT((my%-d_org_y%)/10))
    hard_vdi(hard%)
    DEFFILL vdi%,1,1
    PBOX x%+147,y%+32,x%+9+147,y%+9+32
    PSET (x%\10)+org_x%,(y%\10)+org_y%,hard%
    recopy
  ENDIF
  IF MOUSEY<192 AND MOUSEX>147 AND MOUSEY>32 AND MOUSEX<307 AND MOUSEK=1
    GOTO e
  ENDIF
RETURN
> PROCEDURE get
  choix
  blkx%=blk% MOD 20
  blky%=blk%\20
  RC_COPY motif%+av%*320*16,blkx%*16,blky%*16,16,16 TO XBIOS(3),org_x%,org_y%
  lastw_x%=blkx%*16
  lastw_y%=blky%*16
  zoom
  recopy
RETURN
> PROCEDURE fill
  IF MOUSEX<307 AND MOUSEX>147 AND MOUSEY>32 AND MOUSEY<192 AND MOUSEK=1
    hard_vdi(hard%)
    DEFFILL vdi%,1,1
    FILL ((MOUSEX-147)/10)+org_x%,((MOUSEY-32)/10)+org_y%
    zoom
    recopy
    fill!=FALSE
  ENDIF
RETURN
> PROCEDURE choix
  work%=XBIOS(3)
  VOID XBIOS(5,L:motif%+av%*320*16,L:motif%+av%*320*16,-1)
  SHOWM
  REPEAT
    IF MOUSEY>190 AND av%<38
      ADD av%,1
      VOID XBIOS(5,L:motif%+av%*320*16,L:motif%+av%*320*16,-1)
      VSYNC
    ENDIF
    IF MOUSEY<16 AND av%>0
      SUB av%,1
      VOID XBIOS(5,L:motif%+av%*320*16,L:motif%+av%*320*16,-1)
      VSYNC
    ENDIF
    blk%=INT(MOUSEX/16)+(20*(INT(MOUSEY/16)+av%))
  UNTIL MOUSEK=1
  REPEAT
  UNTIL MOUSEK=0
  HIDEM
  VOID XBIOS(5,L:work%,L:work%,-1)
RETURN
> PROCEDURE valide
  old%=XBIOS(3)
  VOID XBIOS(5,L:motif%+av%*320*16,L:motif%+av%*320*16,-1)
  REPEAT
    pos_x%=(INT(MOUSEX/16))*16            ! X SUR DES MULTIPLES DE 16
    pos_y%=(INT(MOUSEY/16))*16            ! Y SUR DES MULTIPLES DE 16
    RC_COPY motif%+av%*320*16,pos_x%,pos_y%,16,16 TO fond%,0,0
    RC_COPY old%,org_x%,org_y%,16,16 TO motif%+av%*320*16,pos_x%,pos_y%
    VSYNC
    IF MOUSEK=1
      RC_COPY old%,org_x%,org_y%,16,16 TO fond%,0,0
      copy!=TRUE
    ENDIF
    RC_COPY fond%,0,0,16,16 TO motif%+av%*320*16,pos_x%,pos_y%
    IF MOUSEY>190 AND av%<38
      ADD av%,1
      VOID XBIOS(5,L:motif%+av%*320*16,L:motif%+av%*320*16,-1)
    ENDIF
    IF MOUSEY<16 AND av%>0
      SUB av%,1
      VOID XBIOS(5,L:motif%+av%*320*16,L:motif%+av%*320*16,-1)
    ENDIF
  UNTIL copy!=TRUE
  REPEAT
  UNTIL MOUSEK=0
  copy!=FALSE
  VOID XBIOS(5,L:old%,L:old%,-1)
RETURN
> PROCEDURE trame
  FOR y%=72 TO 72+15
    FOR x%=95 TO 95+16
      IF EVEN(x%+y%)
        PSET x%,y%-32,PTST(x%,y%)
      ENDIF
    NEXT x%
  NEXT y%
RETURN
> PROCEDURE rotate
  i%=0
  FOR y%=org_y% TO org_y%+15
    FOR x%=org_x% TO org_x%+15
      processor%(i%)=PTST(x%,y%)
      INC i%
    NEXT x%
  NEXT y%
  '
  i%=0
  FOR x%=org_x% TO org_x%+15
    FOR y%=org_y% TO org_y%+15
      PSET x%,y%,processor%(i%)
      INC i%
    NEXT y%
  NEXT x%
RETURN
> PROCEDURE combine
  FOR y%=40 TO 40+15
    FOR x%=95 TO 95+16
      a|=POINT(x%,y%+32)
      IF a|<>0
        COLOR a|
        PLOT x%,y%
      ENDIF
    NEXT x%
  NEXT y%
RETURN
> PROCEDURE vdi_hard(vdi%)
  hard%=vdi%
  SELECT vdi%
  CASE 1
    hard%=255
  CASE 2
    hard%=1
  CASE 3
    hard%=2
  CASE 6
    hard%=3
  CASE 7
    hard%=5
  CASE 5
    hard%=6
  CASE 8
    hard%=7
  CASE 9
    hard%=8
  CASE 10
    hard%=9
  CASE 11
    hard%=10
  CASE 14
    hard%=11
  CASE 15
    hard%=13
  CASE 13
    hard%=14
  CASE 255
    hard%=15
  ENDSELECT
RETURN
> PROCEDURE get_screen(save$)
  BMOVE XBIOS(3),reserve%+1024,64000
  FOR t%=0 TO 1024-4 STEP 4
    LPOKE reserve%+t%,LPEEK(&HFF9800+t%)
  NEXT t%
  BSAVE save$,reserve%,64000+1024
RETURN
VOID MFREE(screen%)
VOID MFREE(reserve%)
VOID MFREE(block%)
EDIT
