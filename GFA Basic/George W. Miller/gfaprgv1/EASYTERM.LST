'
' EasyTerm
' Written in GFA BASIC by George W. Miller
' Copyright 1988 MichTron, Inc.
' All Rights Reserved
'
Dim Strip$(93),Buf(127),Number$(9),Service$(9),Key$(19)
Dim Palette(15),Pass$(9),Prompt$(9)
Dim Script$(100)
'
'
'
Defmouse 2
Cls
Reserve Fre(0)-32767
'
' Configure RS-232 using XBIOS #15, rsconf, using following defaults:
'
Capture=0
Rsconf=15
Flow=0
Ucr=&X10001000
' UCR default setings:           GEnie defaults
'     no parity, 1 stop bit, 8 bits
'
Rsr=-1
Tsr=-1
Scr=-1
Full=0
Half=1
Baud=7
'
' definitions for XMODEM
'
Soh=1      ! Start Of Header
Eot=4      ! End Of Transmission
Ack=6      ! ACKnowledge
Nak=21     ! Negative AcKnowledge
Can=24     ! CANcel
Xon=17
Xoff=19
Dial_now=0
'
Spoke &H444,0                             ! Turn off Write Verify
Drive=Gemdos(&H19)
Path_name$=Space$(64)                     !Reserve space for path name
'
Ptr=Varptr(Path_name$)                     !Define pointer to variable
'
' Call GEMDOS function $47, D_getpath
' Pass the pointer to the buffer to hold the path name with a long word,
' and pass the drive to be used with a word.
'
Void Gemdos(&H47,L:Ptr,W:Drive+1)
'
' GEMDOS $47 returns the name at the address pointed to with a null (0) byte
' as the terminator. Search for this null byte, and define path_name$
' for fileselector.
'
For I=1 To 63
  If Asc(Mid$(Path_name$,I,1))=0
    Path_name$=Left$(Path_name$,I-1)
    I=63
  Endif
Next I
'
Data Desk,  EasyTerm
Data --------------------
Data 1,2,3,4,5,6,""
Data Baud,  19200 ,   9600 ,   4800 ,   3600 ,   2400 ,   2000
Data    1800 ,   1200 ,    600 ,    300 ,    150 ,    134
Data     110,     75,     50,""
Data RS-232,  Full Duplex ,  Half Duplex ,  8 Bit Word,  7 Bit Word
Data   No Parity,  Odd Parity,  Even Parity,  1 Stop Bit,  2 Stop Bits
Data ------------------,  Save RS232.CFG  ,  Load .CFG file  ,""
Data Files,  UL/ASCII  ,  UL/Xmodem  ,  DL/ASCII  ,  DL/Xmodem
Data ------------------,  Buffer OPEN  ,  Buffer CLOSED  ,  SAVE Buffer
Data   CLEAR Buffer  ,  Print Buffer
Data ------------------,  Load Buffer  ,  View Buffer
Data ------------------,  Execute File,""
Data DOS,  Free Space  ,  Create Folder  ,  Erase File  ,  Rename File
Data   Directory  ,----------------,  Disk A  ,  Disk B  ,  Disk C
Data   Disk D  ,  Disk E  ,  Disk F  ,""
Data Utilities,  Auto Dial  ,------------------------,  Printer ON  ,  Printer OFF
Data ------------------------,  Set Time/Date
Data ------------------------,  Define Function Keys
Data ------------------------,  Disk Verify ON  ,  Disk Verify OFF
Data ------------------------,  Script File
Data ------------------------,  Help  ,""
Data Exit,  Quit ,""
Data ***
'
Dr_map=Bios(10)
'
Rez=Xbios(4)
If Rez=0
  Alrt$="     EasyTerm|  requires Medium|or High Resolution"
  Alert 3,Alrt$,1,"OK",B
  End
Endif
'
@Save_palette
I=0
Do
  Read Strip$(I)
  Exit If Strip$(I)="***"
  Inc I
Loop
Strip$(I)=""
Strip$(I+1)=""
On Menu  Gosub Menue
'
Echo=Half          !default to Half duplex mode
Buf=On
Defmouse 0
'
Alrt$="EasyTerm Version 1.0|Written in GFA BASIC|  By George Miller|     May 1988"
Alert 0,Alrt$,1," OK ",B
'
Defmouse 2
Filename$=Path_name$+"\*.CFG"
@Have_file
If Is_it=0
  Defmouse 0
  Alrt$="   EasyTerm| |Load RS232.CFG?"
  Alert 2,Alrt$,1,"Yes|No",B
  If B=1
    Cls
    @Load_cfg
  Endif
Endif
'
Filename$=Path_name$+"\*.PHN"
@Have_file
If Is_it=0
  Defmouse 0
  Alrt$="       EasyTerm| |Load Autodial Directory?"
  Alert 2,Alrt$,1,"Yes|No",B
  '
  If B=2
    Goto Auto_fini
  Endif
  '
  Auto_load:
  Cls
  Print "Select Auto Dial File to load:"
  Fileselect Chr$(Drive+65)+":"+Path_name$+"\*.phn",B$,Filename$
  Defmouse 2
  If Filename$=""
    Goto Auto_fini
  Endif
  @Have_file
  If Is_it=-33
    Defmouse 0
    Alrt$="     EasyTerm| |Can't find that file."
    Alert 1,Alrt$,1,"OK",B
    Goto Auto_load
  Endif
  '
  Cls
  If Filename$=""
    Goto Auto_fini
  Endif
  '
  Open "I",#3,Filename$
  I=0
  For I=0 To 9
    Line Input #3,Service$(I);Number$(I);Pass$(I);Prompt$(I)
  Next I
  Close #3
  Auto_fini:
  B$=""
Endif
'
Filename$=Path_name$+"funct.key"
@Have_file
If Is_it=0
  Defmouse 0
  Alrt$="        EasyTerm| |Load Defined Function keys?"
  Alert 2,Alrt$,1,"Yes|No",B
  '
  If B=1
    Defmouse 2
    Print "Loading Pre-defined Function Keys:"
    Open "I",#3,"\funct.key"
    '
    For I=0 To 19
      Line Input #3;Key$(I)
    Next I
    '
    Close #3
    Defmouse 0
    '
    Cls
  Endif
Endif
'
Defmouse 0
@Check_it
@Rsconf
Hidem
Open "U",#1,"AUX:"
'
@Menue
Cls
Do
  '
  If Colour=1
    @Colour
  Endif
  If Dial_now=1
    @Dial_it
  Endif
  If Mousek=1
    @Check_it
    Hidem
  Endif
  Void Xbios(21,1)
  If Inp?(1)<>0
    T=Inp(1)
    If T=8
      Out 2,32
    Endif
    Out 2,T And 127
    '
    If Prnt!=-1
      Out 0,T
      If T=13
        Inc Prnt_cnt
        If Prnt_cnt=80
          Out 0,13
          Out 0,10
          Prnt_cnt=0
        Endif
      Endif
    Endif
    '
    If Capture!
      @Capture_it
    Endif
    '
  Endif
  If Inp?(2)<>0
    If Rez=1
      Print Chr$(27);"b";Chr$(1);
    Endif
    T=Inp(2)
    If T>=187 And T=<196          !check function key
      @Function_key
    Endif
    If T>=212 And T=<221          !check shifted-function key
      @Sfunction_key
    Endif
    If T=199
      Cls
      T=0
    Endif
    If T>=150 And T<=178
      @Alt_key
      Goto No_send
    Endif
    If T=226
      @Help
    Endif
    Out 1,T
    If Prnt!=-1
      Out 0,T
      Inc Prnt_cnt
      If Prnt_cnt=80
        Out 0,13
        Out 0,10
        Prnt_cnt=0
      Endif
    Endif
    '
    If T=8 And Echo=0
      Out 2,8
      Out 2,32
      Out 2,8
    Endif
    If Echo=1
      If T=8
        Out 2,8
        Out 2,32
      Endif
      Out 2,T And 127
    Endif
    If Capture!
      @Capture_it
    Endif
  Endif
  If Rez=1
    Print Chr$(27);"b";Chr$(3);
  Endif
  '
  No_send:
Loop
'
Close #1
'
' program should never get here, but if something goes wrong.....
'
End
'
Procedure Capture_it
  '
  Buffer$=Buffer$+Chr$(T)
  If T=13
    Capture$=Capture$+Buffer$
    Buffer$=""
  Endif
  Inc Capture
  If Capture=32766
    Out 1,Xoff
    Capture$=Capture$+Buffer$+Chr$(13)
    Buffer$=""
    Capture!=0
    Sget T$
    '
    Alert 2,"Save Capture buffer?",1,"YES|NO",B
    If B=1
      Open "A",#1,"MICHTERM.BUF"
      Print #1,Capture$
      Close #1
    Endif
    Capture$=""
    Out 1,Xon
  Endif
  '
Return
'
Procedure Alt_key
  '
  If Rez=1
    Print Chr$(27);"b";Chr$(2);
  Endif
  If T=159
    D=0
    If Baud=4
      D=1
    Endif
    If Baud=7
      D=2
    Endif
    If Baud=9
      D=3
    Endif
    Alrt$=" |Set Baud rate to:"
    Alert 1,Alrt$,D,"2400|1200|300",B
    If B=1
      Baud=4
    Endif
    If B=2
      Baud=7
    Endif
    If B=3
      Baud=9
    Endif
    @Rsconf
  Endif
  If T=178
    If Echo=Half
      Echo=Full
      Print
      Print Chr$(27);"p <FULL DUPLEX> ";Chr$(27);"q"
      Goto Changed
    Endif
    If Echo=Full
      Echo=Half
      Print
      Print Chr$(27);"p <HALF DUPLEX> ";Chr$(27);"q"
    Endif
    Changed:
  Endif
  If T=176
    If Capture!=-1
      Capture!=0
      Print
      Print Chr$(27);"p <CAPTURE BUFFER CLOSED> ";Chr$(27);"q"
      Goto Cap_changed
    Endif
    If Capture!=0
      Capture!=-1
      Capture=0
      Print
      Print Chr$(27);"p <CAPTURE BUFFER OPEN> ";Chr$(27);"q"
    Endif
    Cap_changed:
  Endif
  If T=174
    Capture$=""
    Print
    Print Chr$(27);"p <CAPTURE BUFFER CLEARED> ";Chr$(27);"q"
  Endif
  If T=161
    Drive=Gemdos(&H19)+1
    Disk_space=Dfree(Drive)
    Print
    Print Chr$(27);"p <Free Space remaining on Disk ";Chr$(Drive+64);": ";Disk_space;" Bytes.> ";Chr$(27);"q"
  Endif
  If T=153
    If Prnt!=0 And Prnt=-1
      Prnt!=-1
      Print
      Print Chr$(27);"p <PRINTER ON> ";Chr$(27);"q"
      Goto Changed_prnt
    Endif
    If Prnt!=-1 And Prnt=-1
      Prnt!=0
      Print
      Print Chr$(27);"p <PRINTER OFF> ";Chr$(27);"q"
      Changed_prnt:
    Endif
  Endif
  If T=172
    @Buf_text
  Endif
  If T=175
    If Peek(&H444)<>0
      Spoke &H444,0
      Print
      Print Chr$(27);"p <DISK VERIFY ON> ";Chr$(27);"q"
    Else
      Spoke &H444,1
      Print
      Print Chr$(27);"p <DISK VERIFY OFF> ";Chr$(27);"q"
    Endif
  Endif
  If T=150
    Xcol=Crscol
    Ylin=Crslin
    Alrt$="   EasyTerm| |Upload File|Select Protocol to use: "
    Alert 1,Alrt$,3,"Xmodem|ASCII|Cancel",B
    If B=1
      @Xmodem_send
    Endif
    If B=2
      @Ul_ascii
    Endif
    Print At(Xcol,Ylin);
  Endif
  If T=160
    Xcol=Crscol
    Ylin=Crslin
    Alrt$="    EasyTerm| |Download File|Select Protocol to use: "
    Alert 1,Alrt$,3,"Xmodem|ASCII|Cancel",B
    If B=1
      @Xmodem_receive
    Endif
    If B=2
      @Dl_ascii
    Endif
    Print At(Xcol,Ylin);
  Endif
  If T=173
    Print
    @Status
  Endif
  T=0
  If Rez=1
    Print Chr$(27);"b";Chr$(3);
    Print Chr$(27);"q";
  Endif
Return
'
Procedure Colour
  Setcolor 0,0,0,0
  Setcolor 1,7,0,0
  Setcolor 2,0,0,7
  Setcolor 3,7,7,7
  Colour=0
Return
'
Procedure Alt_colour
  Setcolor 0,7,7,7
  Setcolor 1,7,7,7
  Setcolor 2,0,0,7
  Setcolor 3,0,0,0
  Colour=1
Return
'
Procedure Buf_text
  '
  Out 1,19
  B_txt$=""
  Void Xbios(&H15,0)
  Sget Screen$
  Void Xbios(&H15,2)
  Org_col=Crscol
  Org_row=Crslin
  B_text$=""
  A=0
  For I=22 To 24
    Print At(I,0);Space$(80);
  Next I
  Rbox 1,181,639,195
  If Rez=1
    Print Chr$(27);"b";Chr$(1);
  Endif
  Print At(2,24);"> ";Chr$(27);"q";
  Trow=24
  Tcol=4
  Do
    Exit If A=13
    A=Inp(2)
    If A=8
      A=0
      Dec Tcol
      If Tcol<4
        Tcol=4
      Endif
      Print At(Tcol,Trow);" "
      Print At(Tcol,Trow);
      If Len(B_txt$)<>0
        B_txt$=Left$(B_txt$,Len(B_txt$)-1)
      Endif
    Endif
    If A<>0
      B_txt$=B_txt$+Chr$(A)
      Print At(Tcol,Trow);Chr$(A);
      Inc Tcol
      If Tcol=79
        Print At(2,24);"> ";Chr$(27);"q"+Space$(76)
        Tcol=4
      Endif
    Endif
  Loop
  If B_txt$=""
    Goto No_txt
  Endif
  For I=1 To Len(B_txt$)
    Out 1,Asc(Mid$(B_txt$,I,1))
  Next I
  T=13
  '
  No_txt:
  Void Xbios(&H15,0)
  Sput Screen$
  Print At(Org_col,Org_row);""
  Void Xbios(&H15,2)
  If Echo=1
    Print B_txt$
  Endif
  If Rez=1
    Print Chr$(27);"b";Chr$(3);
  Endif
  Out 1,17
Return
'
Procedure Check_it
  '
  Flag_it=0
  X%=Crscol
  Y%=Crslin
  Void Xbios(21,0)
  Sget Screen1$
  Cls
  Openw 0
  Menu Strip$()
  @Remove_check
  @Menu_check
  Showm
  Out 1,Xoff
  @Rt_bttn
  Do
    Vsync
    If Mousek=2
      Flag_it=1
    Endif
    On Menu
    Exit If Flag_it=1
  Loop
  '
  Out 1,Xon
  Closew 0
  Sput Screen1$
  '
  Print At(X%,Y%)
  Hidem
  '
Return
'
Procedure Menu_check
  '
  If Colour=0
    @Alt_colour
  Endif
  If Peek(&H444)=1
    Menu 82,0
    Menu 83,1
  Endif
  If Peek(&H444)=0
    Menu 82,1
    Menu 83,0
  Endif
  Prnt=Bios(8,0)
  If Prnt=0
    Menu 51,2
    Menu 75,2
    Menu 76,2
    Prnt!=0
  Endif
  If Prnt=-1
    Menu 51,3
    Menu 75,3
    Menu 76,3
  Endif
  '
  If Prnt!=-1 And Prnt<>0
    Menu 75,1
  Endif
  '
  If Prnt!=0 And Prnt<>0
    Menu 76,1
  Endif
  Menu Baud+11,1
  If Echo=Full
    Menu 28,1
  Else
    Menu 29,1
  Endif
  If Ucr And &X100000                    !test word length
    Menu 31,1
    Menu 30,0
  Else
    Menu 30,1
    Menu 31,0
  Endif
  If Ucr And &X100                        !test parity settings
    If Ucr And &X10                      !test parity (even)
      Menu 34,1
      Menu 33,0
      Menu 32,0
    Else
      Menu 33,1
      Menu 34,0
      Menu 32,0
    Endif
  Else
    Menu 33,0
    Menu 34,0
    Menu 32,1
  Endif
  '
  If Ucr And &X11000                      !test stop bits
    Menu 35,1
    Menu 36,0
  Endif
  '
  If Ucr And &X10000
    Menu 36,1
    Menu 35,0
  Endif
  '
  If Capture!=-1
    Menu 47,1
    Menu 48,0
  Endif
  If Capture!=0
    Menu 47,0
    Menu 48,1
  Endif
  '
  '
  For I=65 To 70
    Menu I,2         !not selectable
  Next I
  '
  For I=65 To 70
    Menu I,0         !not checked
  Next I
  '
  If Dr_map And &X11
    Menu 65,3                !Drive A
    Menu 66,3                !Drive B
  Endif
  '
  If Dr_map And &X100
    Menu 67,3                !Drive C
  Endif
  '
  If Dr_map And &X1000
    Menu 68,3                !Drive D
  Endif
  '
  If Dr_map And &X10000
    Menu 69,3                !Drive E
  Endif
  '
  If Dr_map And &X100000
    Menu 70,3                !Drive F
  Endif
  '
  Drive=Gemdos(&H19)
  Menu 65+Drive,1            !Place check mark on active drive
  Print At(1,1);
  @Status
Return
'
Procedure Menue
  '
  If Strip$(Menu(0))=Strip$(1)
    Menu Off
    Alrt$="   EasyTerm Version 1.0|     By George Miller|"+Chr$(&HBD)+" 1988 MichTron, Inc.|   All Rights Reserved"
    Alert 0,Alrt$,1," OK ",B
  Endif
  '
  If Menu(0)=30                  !Set word length to 8 bits
    Menu Off
    Ucr=Ucr And &X10010000
    @Menu_check
    @Rsconf
  Endif
  '
  If Menu(0)=31                  !Set word length to 7 bits
    Menu Off
    Ucr=Ucr Or &X10111000
    @Menu_check
    @Rsconf
  Endif
  '
  If Menu(0)=32                   !Set parity to none
    Menu Off
    Ucr=Ucr And &X11111000
    @Menu_check
    @Rsconf
  Endif
  '
  If Menu(0)=33                  !Set parity to odd
    Menu Off
    Ucr=Ucr And &X11111000
    Ucr=Ucr Or &X100
    @Menu_check
    @Rsconf
  Endif
  '
  If Menu(0)=34                   !Set parity to even
    Menu Off
    Ucr=Ucr And &X11111000
    Ucr=Ucr Or &X110
    @Menu_check
    @Rsconf
  Endif
  '
  If Menu(0)=35                   ! 1 Stop bit
    Menu Off
    Ucr=Ucr And &X11101110
    Ucr=Ucr Or &X1000
    @Menu_check
    @Rsconf
  Endif
  '
  If Menu(0)=36                   ! 2 stop bits
    Menu Off
    Ucr=Ucr And &X11100110
    Ucr=Ucr Or &X11000
    @Menu_check
    @Rsconf
  Endif
  '
  If Menu(0)>10 And Menu(0)<27    ! baud rate
    Menu Off
    @Baud_rate
    @Rsconf
    '
  Endif
  '
  If Strip$(Menu(0))="  Save RS232.CFG  "
    '
    Menu Off
    '
    Rs232_choose:
    Cls
    Print "Save file as:"
    B$="RS232.CFG"
    Fileselect Chr$(65+Drive)+":"+Path_name$+"\*.CFG",B$,Filename$
    @Have_file
    If Is_it=0
      Alrt$="File Exists!"
      Alert 1,Alrt$,1,"Replace|Cancel",B
      If B=2
        Goto Rs232_done
      Endif
    Endif
    '
    If Filename$=""
      Goto Rs232_done
    Endif
    '
    Cls
    Print "Saving file as: ";Filename$
    Defmouse 2
    Open "O",#3,Filename$
    '
    Print #3;Flow
    Print #3;Ucr
    Print #3;Rsr
    Print #3;Tsr
    Print #3;Scr
    Print #3;Duplex
    Print #3;Baud
    '
    Close #3
    Defmouse 0
    Rs232_done:
    Cls
    Print At(1,1)
    @Status
    '
  Endif
  '
  If Strip$(Menu(0))="  Load .CFG file  "
    Menu Off
    Cls
    @Load_cfg
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Quit "
    Menu Off
    If Capture!=-1 And Capture>0
      Alrt$="|Would you like to|Save the Text Buffer?"
      Alert 2,Alrt$,2," Yes | No",B
      If B=1
        B$="capture.txt"
        Fileselect Chr$(Drive+65)+":"+Path_name$+"\*.TXT",B$,Filename$
        @Have_file
        If Is_it=0
          Alrt$="File Exists!"
          Alert 1,Alrt$,2,"Replace|Cancel",B
          If B=1
            Filename$=""
          Endif
        Endif
        If Filename$=""
          Goto No_sav
        Endif
        '
        Defmouse 2
        Open "O",#3,Filename$
        Print #3,Capture$
        Close #3
        Defmouse 0
        '
        No_sav:
      Endif
    Endif
    '
    Alrt$="|Do you really want to Quit?"
    Alert 3,Alrt$,2," OK |Cancel",B
    If B=1
      Reserve Fre(0)+32767-255
      @Restore_palette
      Edit
    Endif
  Endif
  '
  If Strip$(Menu(0))="  Full Duplex "
    Menu Off
    Menu Menu(0),1
    Menu (Menu(0)+1),0
    Echo=Full
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Half Duplex "
    Menu Off
    Menu Menu(0),1
    Menu (Menu(0)-1),0
    Echo=Half
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  DL/ASCII  "         !Download ASCII file
    Menu Off
    @Dl_ascii
  Endif
  '
  If Strip$(Menu(0))="  UL/ASCII  "         !Download ASCII file
    Menu Off
    @Ul_ascii
  Endif
  '
  If Strip$(Menu(0))="  DL/Xmodem"
    Menu Off
    @Xmodem_receive
  Endif
  '
  If Strip$(Menu(0))="  UL/Xmodem  "
    Menu Off
    @Xmodem_send
  Endif
  '
  If Strip$(Menu(0))="  Buffer OPEN  "
    Menu Off
    Menu (Menu(0)),1
    Menu Menu(0)+1,0
    Capture!=-1
    Capture=0
    Cls
    Print At(1,1);
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Buffer CLOSED  "
    Menu Off
    Menu (Menu(0)-1),0
    Menu Menu(0),1
    Capture!=0
    Cls
    Print At(1,1);
    @Status
  Endif
  '
  If Strip$(Menu(0))="  SAVE Buffer"
    Menu Off
    B$="MICHTERM.BUF"
    Fileselect Chr$(Drive+65)+":"+Path_name$+"\*.TXT",B$,Filename$
    @Have_file
    If Is_it=0
      Alrt$="File Exists!"
      Alert 1,Alrt$,3,"Replace|Append|Cancel",B
      If B=1
        Filename$=""
      Endif
    Endif
    If Filename$=""
      Goto Skip_sav
    Endif
    '
    Defmouse 2
    If B=2
      Open "A",#3,Filename$
    Endif
    If B=1
      Open "O",#3,Filename$
      Print #3,Capture$
      Close #3
    Endif
    Defmouse 0
    Capture!=0
    Capture$=""
    Capture=0
    '
    Skip_sav:
  Endif
  '
  If Strip$(Menu(0))="  Print Buffer"
    Menu Off
    Check_prn:
    Prnt=Bios(8,0)
    If Prnt=-1
      Lprint Capture$
    Endif
    If Prnt=0
      Alrt$=" |Printer not ready."
      Alert 1,Alrt$,1,"Retry|Cancel",B
      If B=1
        Goto Check_prn
      Endif
    Endif
  Endif
  '
  If Strip$(Menu(0))="  CLEAR Buffer  "
    Menu Off
    Capture$=""
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Execute File"
    Menu Off
    @Runit
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Directory  "
    Menu Off
    Cls
    Files
    @Rt_bttn
  Endif
  '
  If Strip$(Menu(0))="  Free Space  "
    Menu Off
    Drive=Gemdos(&H19)+1           !get active drive
    Disk_space=Dfree(Drive)
    Cls
    Alrt$="Free Disk Space remaining|     on Disk "+Chr$(Drive+64)+": | "+Str$(Disk_space)+" Bytes"
    Alert 1,Alrt$,1,"OK",B
    @Rt_bttn
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Create Folder  "
    Menu Off
    Cls
    Print "Create Folder:"
    Fileselect Chr$(Drive+65)+":\*.*",B$,Folder$
    If Folder$=""
      Goto Fini_folder
    Endif
    Check$=Right$(Folder$,4)
    If Left$(Check$)="."
      Alrt$=" |Not valid name."
      Alert 1,Alrt$,1,"OK",B
      Goto Fini_folder
    Endif
    Mkdir Folder$
    '
    Fini_folder:
    Cls
    Print At(1,1)
    @Status
    @Rt_bttn
    '
  Endif
  '
  If Strip$(Menu(0))="  Erase File  "
    '
    Menu Off
    Cls
    Print "Select File to Delete:"
    Fileselect Chr$(Drive+65)+":\*.*",B$,Filename$
    If Filename$=""
      Goto Fini_erase
    Endif
    '
    @Have_file
    If Is_it=-33
      Alrt$="File doesn't exists!"
      Alert 1,Alrt$,1,"OK",B
      Goto Fini_erase
    Endif
    '
    Cls
    Alrt$="Ready to DELETE|"+Filename$+"|from disk.|Are you sure?"
    Alert 2,Alrt$,2,"OK|Cancel",B
    If B=2
      Goto Fini_erase
    Endif
    Kill Filename$
    '
    Fini_erase:
    Cls
    Print At(1,1)
    @Status
    @Rt_bttn
    '
  Endif
  '
  If Strip$(Menu(0))="  Rename File  "
    '
    Menu Off
    Cls
    Print "Old File name:"
    '
    Fileselect Chr$(Drive+65)+":\*.*",B$,Filename$
    If Filename$=""
      Goto Fini_rename
    Endif
    '
    @Have_file
    If Is_it=-33
      Alrt$="File doesn't exists!"
      Alert 1,Alrt$,1,"OK",B
      Goto Fini_rename
    Endif
    '
    Oldname$=Filename$
    Cls
    Print "New File name:"
    '
    Fileselect Chr$(Drive+65)+":\*.*",B$,Filename$
    If Filename$=""
      Goto Fini_rename
    Endif
    '
    @Have_file
    If Is_it=0
      Alrt$="File already exists!"
      Alert 1,Alrt$,2,"Replace|Cancel",B
      If B=2
        Goto Fini_rename
      Endif
    Endif
    Nuname$=Filename$
    '
    Alrt$="About to rename|"+Oldname$+" as |"+Nuname$
    Alert 1,Alrt$,2,"OK|Cancel",B
    If B=2
      Goto Fini_rename
    Endif
    Name Oldname$ As Newname$
    '
    Fini_rename:
    Cls
    Print At(1,1)
    @Status
    @Rt_bttn
  Endif
  '
  If Strip$(Menu(0))="  Set Time/Date"
    '
    Menu Off
    Cls
    Date:
    '
    Rez=Xbios(4)
    '
    Nu_date$=Date$
    '
    Print At(3,6);"Date presently set to: ";
    Print Nu_date$
    Print At(3,8);"Press <Return> to use this date or"
    Print At(3,10);"Enter new date (mm/dd/yy) ";
    Print At(3,12);"(Be sure to include the '/')"
    Print At(14,20);
    Form Input 8,In_date$
    If In_date$=""
      Goto Time
    Endif
    '
    If Asc(In_date$)<48 Or Asc(In_date$)>57
      Goto Date
    Endif
    Nu_date$=In_date$
    '
    Time:
    '
    Cls
    Print At(3,6);"Present time is ";
    Print Time$
    Print At(3,8);"Press <Return> to accept time or"
    Print At(3,10);"Enter new time (hh:mm:ss) ";
    Print At(3,12);"(Use Military [24 hour] format)"
    Print At(14,20);
    Form Input 8,In_time$
    '
    Nu_time$=In_time$
    If In_time$=""
      Nu_time$=Time$
    Endif
    '
    Settime Nu_time$,Nu_date$
    '
    Cls
    Print At(1,1)
    @Status
    @Rt_bttn
  Endif
  '
  If Strip$(Menu(0))="  Disk A  "
    Menu Off
    @Delete_check
    Menu 65,1
    Chdrive 1
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Disk B  "
    Menu Off
    @Delete_check
    Menu 66,1
    Chdrive 2
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Disk C"
    Menu Off
    @Delete_check
    Menu 67,1
    Chdrive 3
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Disk D  "
    Menu Off
    @Delete_check
    Menu 68,1
    Chdrive 4
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Disk E  "
    Menu Off
    @Delete_check
    Menu 69,1
    Chdrive 5
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Disk F  "
    Menu Off
    @Delete_check
    Menu 70,1
    Chdrive 6
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Printer ON  "
    Menu Off
    If Baud<9
      Alrt$="On Line Printer requires|Print Spooler program|at higher Baud rates."
      Alert 1,Alrt$,2,"OK|Cancel",B
      If B=2
        Goto No_prnt
      Endif
    Endif
    Menu 75,1
    Menu 76,0
    Prnt!=-1
    No_prnt:
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Printer OFF"
    Menu Off
    Menu 75,0
    Menu 76,1
    Prnt!=0
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Auto Dial  "
    '
    Menu Off
    @Auto_dial
    Print At(1,1)
    @Status
    '
  Endif
  '
  If Strip$(Menu(0))="  Define Function Keys"
    Menu Off
    Alrt$="    EasyTerm|  Define Function Keys  "
    Alert 1,Alrt$,3,"F1-F10|SF1-SF10|Cancel",B
    If B=1
      @Edit_function_key
    Endif
    If B=2
      @Edit_shiftfunction_key
    Endif
    If B=3
      Goto Skip_define
    Endif
    Skip_define:
    Cls
    Print At(1,1)
    @Status
    @Rt_bttn
  Endif
  '
  If Strip$(Menu(0))="  Disk Verify ON  "
    Menu Off
    Spoke &H444,0
    Menu 82,1
    Menu 83,0
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Disk Verify OFF"
    Menu Off
    Spoke &H444,1
    Menu 82,0
    Menu 83,1
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Script File"
    Menu Off
    Alrt$="  EasyTerm| Script File| Processor|"
    Alert 1,Alrt$,1,"OK",B
    @Script
    Cls
    Print At(1,1)
    @Status
    @Rt_bttn
  Endif
  '
  If Strip$(Menu(0))="  Help  "
    Menu Off
    @Help
    @Rt_bttn
    Cls
    Print At(1,1)
    @Status
  Endif
  '
  If Strip$(Menu(0))="  Load Buffer  "
    Menu Off
    Cls
    Alrt$="Loading Capture Buffer will|overwrite contents!"
    Alert 1,Alrt$,2,"OK|Cancel",B
    If B=2
      Goto No_load
    Endif
    Print "Select File to Load:"
    Fileselect Chr$(Drive+65)+":"+Path_name$+"\*.*",B$,Filename$
    If Filename$=""
      Goto No_load
    Endif
    '
    @Have_file
    If Is_it=-33
      Alrt$="File doesn't exists!"
      Alert 1,Alrt$,1,"OK",B
      Goto No_load
    Endif
    '
    Cls
    Print "Loading ";Filename$
    Defmouse 2
    Open "I",#3,Filename$
    Capture$=""
    Do
      Exit If Eof(#3)
      Input #3,A$
      Capture$=Capture$+A$+Chr$(10)+Chr$(13)
    Loop
    Close #3
    Defmouse 0
    '
    No_load:
    Cls
    Print At(1,1)
    @Status
    @Rt_bttn
    '
  Endif
  '
  If Strip$(Menu(0))="  View Buffer"
    Menu Off
    Cls
    If Len(Capture$)=0
      Alrt$=" |Buffer is empty"
      Alert 1,Alrt$,1,"OK",B
      Goto Fini_view
    Endif
    For I=1 To Len(Capture$)
      Print Mid$(Capture$,I,1);
      If Inp?(2)<>0
        Key=Inp(2)
        If Key=19
          Repeat
          Until Inp(2)=17
        Endif
        If Key=27
          I=Len(Capture$)
        Endif
      Endif
    Next I
    Fini_view:
    Print
    Print "         <End of Buffer -- Press any key to continue> "
    Repeat
    Until Inkey$<>""
    Cls
    Print At(1,1)
    @Status
    @Rt_bttn
  Endif
  '
Return
'
Procedure Status
  '
  Print
  Print "Terminal Status:"
  Print
  Print "Duplex Mode: ";
  If Echo=Full
    Print "FULL"
  Else
    Print "HALF"
  Endif
  Print "Baud Rate: ";
  If Baud=0
    Print "19200"
  Endif
  If Baud=1
    Print " 9600"
  Endif
  If Baud=2
    Print " 4800"
  Endif
  If Baud=3
    Print " 3600"
  Endif
  If Baud=4
    Print " 2400"
  Endif
  If Baud=5
    Print " 2000"
  Endif
  If Baud=6
    Print " 1800"
  Endif
  If Baud=7
    Print " 1200"
  Endif
  If Baud=8
    Print "  600"
  Endif
  If Baud=9
    Print "  300"
  Endif
  If Baud=10
    Print "  200"
  Endif
  If Baud=11
    Print "  150"
  Endif
  If Baud=12
    Print "  134"
  Endif
  If Baud=13
    Print "  110"
  Endif
  If Baud=14
    Print "   75"
  Endif
  If Baud=15
    Print "   50"
  Endif
  Print "Printer: ";
  If Bios(&H8,0)=-1
    If Prnt!
      Print "ON         "
    Else
      Print "OFF        "
    Endif
  Else
    Print "not available"
  Endif
  Print "Capture Buffer: ";
  If Capture!=-1
    Print "OPEN  "
  Endif
  If Capture!=0
    Print "CLOSED"
  Endif
  Print "Capture ";
  If Len(Capture$)=0
    Print "EMPTY"
  Else
    Size=Int((Len(Capture$)/32767)*100)
    If Size=0
      Print Chr$(27);"p <1%"
    Else
      Print Size;"%"
    Endif
  Endif
  Print "Disk Verify ";
  If Peek(&H444)=1
    Print "OFF"
  Else
    Print "ON "
  Endif
  Drive=Gemdos(&H19)
  Print "Active Disk Drive is: ";Chr$(65+Drive)
  If Path_name$<>""
    Print "Directory: ";Path_name$
  Endif
  Print Date$;" ";Time$
  '
  If Rez=1
    Print Chr$(27);"b";Chr$(3);
  Endif
Return
'
Procedure Delete_check
  '
  For I=65 To 70
    Menu I,0         !not checked
  Next I
  '
Return
'
'
Procedure Dl_ascii
  '
  Fileselect Chr$(Drive+65)+":\*.*",B$,Filename$
  '
  If Filename$=""
    Goto Skip_ascii_dl
  Endif
  @Have_file
  If Is_it=0
    Alrt$="File Exists!"
    Alert 1,Alrt$,2,"Replace|Cancel",B
    If B=1
      Filename$=""
      Goto Skip_ascii_dl
    Endif
  Endif
  '
  Print "Press <Esc> to exit"
  Open "O",#3,Filename$
  '
  Out 1,Xon
  '
  Repeat
    '
    If Inp?(1)
      Byte=Inp(1)
      Out #3,Byte
      Out 2,Byte
    Endif
    '
  Until Asc(Inkey$)=27
  '
  Close #3
  '
  Skip_ascii_dl:
  '
  @Rt_bttn
  '
Return
'
Procedure Ul_ascii
  '
  Fileselect Chr$(Drive+65)+":\*.*",B$,Filename$
  '
  If Filename$=""
    Goto Skip_ascii_ul
  Endif
  @Have_file
  If Is_it=-33
    Alrt$="File doesn't exist!"
    Alert 1,Alrt$,1,"OK",B
    Filename$=""
  Endif
  '
  Open "I",#3,Filename$
  '
  Out 1,Xon
  '
  Do
    '
    Exit If Eof(#3)
    Byte=Inp(#3)
    Out 1,Byte
    Out 2,Byte
    Pause 5
    '
  Loop
  '
  Close #3
  '
  Skip_ascii_ul:
  '
Return
'
Procedure Remove_check
  For I=11 To 26
    Menu I,0
  Next I
Return
'
Procedure Xmodem_receive
  '
  Xcol=Crscol
  Ylin=Crslin
  Sget Screen$
  Fileselect Chr$(Drive+65)+":\*.*",B$,Filename$
  If Filename$=""
    Goto Skip_xmodem_rcv
  Endif
  @Have_file
  If Is_it=0
    Alrt$="File Exists!"
    Alert 1,Alrt$,2,"Replace|Cancel",B
    If B=2
      Filename$=""
      Goto Skip_xmodem_rcv
    Endif
  Endif
  @Do_rcv
  Skip_xmodem_rcv:
  Sput Screen$
  Print At(Xcol,Ylin);
Return
'
Procedure Do_rcv
  Tucr=Ucr
  Ucr=&X10010000          !8 bits, no parity, 1 stop
  @Rsconf
  Void Xbios(21,0)
  Cls
  Y1=180
  Y2=185
  If Rez=2
    Y1=Y1*2
    Y2=Y2*2
  Endif
  Box 45,50,595,Y1
  Box 40,45,600,Y2
  '
  Rcv_start:
  '
  Open "O",#3,Filename$
  Print At(27,10);"XMODEM Protocol (Receive)"
  Place=(80-(Len(Filename$)+12))/2
  Print At(Place,13);"Receiving:  ";Upper$(Filename$)
  Block%=1
  Attempt%=1
  '
  Send_nak:
  '
  @Clear_modem
  Void Bios(3,1,Nak)
  Time_out=0
  '
  Status_line:
  '
  Print At(26,17);"Block--¯ ";Block%
  Print At(46,17);"Attempt--¯";Attempt%
  If Have!=True
    Have!=False
    Out 1,Ack
  Endif
  '
  Rcv_bconstat:
  '
  Stat!=Bios(1,1)
  If Stat!=0 Then
    Goto Rcv_time_out
  Else
    Goto Rcv_byte
  Endif
  '
  Rcv_time_out:
  '
  Inc Time_out
  If (Time_out/128)=(128*10) Then
    Goto Blew_it
  Endif
  If (Time_out/128)=Int(Time_out/128) Then
    Goto Send_nak
  Else
    Goto Rcv_bconstat
  Endif
  '
  Rcv_byte:
  '
  Da%=Bios(2,1)
  If Da%=Soh Then
    Goto Rcv_block_count
  Endif
  If Da%=Eot Then
    Goto Rcvd_it
  Endif
  If Da%=Can Then
    Goto Blew_it
  Endif
  Goto Rcv_time_out
  '
  Rcv_block_count:
  '
  Block_numb%=Bios(2,1)
  Block_comp%=Bios(2,1)
  Check%=0
  Block$=""
  Checksum%=0
  For Byte=1 To 128
    Da%=0
    Byte_data:
    Abort$=Inkey$
    Exit If Abort$=Chr$(24)
    If Bios(1,1) Then
      Da%=Bios(2,1)
      Check%=Check%+Da%
      Block$=Block$+Chr$(Da%)
    Else
      Goto Byte_data
    Endif
  Next Byte
  If Abort$=Chr$(24) Then
    Goto Blew_it
  Else
    Checksum%=Check% And 255
  Endif
  '
  Check:
  '
  Check_sum%=0
  If Bios(1,1)=-1 Then
    Check_sum%=Inp(1)
  Else
    Goto Check
  Endif
  If Checksum%=Check_sum% Then
    Goto Build_block
  Else
    Goto Check_attempts
  Endif
  '
  Build_block:
  '
  Print #3,Block$;
  Attempt%=1
  Inc Block%
  Time_out=0
  B_out=0
  @Clear_modem
  Have!=True
  Goto Status_line
  '
  Check_attempts:
  '
  Inc Attempt%
  If Attempt%>10 Then
    Goto Blew_it
  Else
    @Clear_modem
  Endif
  Goto Send_nak
  '
  Blew_it:
  '
  Out 1,Can
  Out 1,Can
  Out 1,Can
  Close #3
  Kill Filename$
  Print At(10,20);"Status: XMODEM CANCELLED -- ";
  Print Upper$(Filename$);" deleted."
  Goto Xmodem_end
  '
  Rcvd_it:
  '
  Pause 50
  Out 1,Ack
  Close #3
  Print At(10,20);"Status: XMODEM COMPLETE.  ";
  Print Upper$(Filename$);
  Print " received."
  Goto Xmodem_end
  '
  Xmodem_end:
  '
  Flag_it=1
  For I=0 To 2
    Print Chr$(7)               ! Ring bell
    Pause 50
  Next I
  Ucr=Tucr
  @Rsconf
  '
Return
'
Procedure Clear_modem
  '
  Do
    Test!=Bios(1,1)
    Exit If Test!=0
    Void Inp(1)
  Loop
  '
Return
'
'
'
Procedure Baud_rate
  '
  If Menu(0)=11
    @Remove_check
    Menu 11,1
    Baud=0
  Endif
  '
  If Menu(0)=12
    @Remove_check
    Menu 12,1
    Baud=1
  Endif
  '
  If Menu(0)=13
    @Remove_check
    Menu 13,1
    Baud=2
  Endif
  '
  If Menu(0)=14
    @Remove_check
    Baud=3
    Menu 14,1
  Endif
  '
  If Menu(0)=15
    @Remove_check
    Baud=4
    Menu 15,1
  Endif
  '
  If Menu(0)=16
    @Remove_check
    Baud=5
    Menu 16,1
  Endif
  '
  '
  If Menu(0)=17
    @Remove_check
    Baud=6
    Menu 17,1
  Endif
  '
  If Menu(0)=18
    @Remove_check
    Baud=7
    Menu 18,1
  Endif
  '
  If Menu(0)=19
    @Remove_check
    Baud=8
    Menu 19,1
  Endif
  '
  If Menu(0)=20
    @Remove_check
    Baud=9
    Menu 20,1
  Endif
  '
  If Menu(0)=21
    @Remove_check
    Baud=10
    Menu 21,1
  Endif
  '
  If Menu(0)=22
    @Remove_check
    Baud=11
    Menu 22,1
  Endif
  '
  If Menu(0)=23
    @Remove_check
    Baud=12
    Menu 23,1
  Endif
  '
  If Menu(0)=24
    @Remove_check
    Baud=13
    Menu 24,1
  Endif
  '
  If Menu(0)=25
    @Remove_check
    Baud=14
    Menu 25,1
  Endif
  '
  If Menu(0)=26
    @Remove_check
    Baud=15
    Menu 26,1
  Endif
  '
Return
'
Procedure Rsconf
  '
  Void Xbios(Rsconf,Baud,Flow,Ucr,Rsr,Tsr,Scr)
  @Menu_check
  '
Return
'
Procedure Rt_bttn
  '
  Print At(15,20);" --¯  Press Right Mouse Button go on line  ®--"
  '
Return
'
'
Procedure Xmodem_send
  '
  Void Xbios(&H15,0)
  Sget Screen$
  Fileselect Chr$(Drive+65)+":\*.*",B$,Filename$
  If Filename$=""
    Goto Skip_xmodem_send
  Endif
  @Have_file
  If Is_it=-33
    Alrt$="File doesn't exist!"
    Alert 1,Alrt$,1,"OK",B
    Filename$=""
    Goto Skip_xmodem_send
  Endif
  @Do_send
  Skip_xmodem_send:
  Sput Screen$
  Void Xbios(&H15,2)
  Out 1,Xon
Return
'
Procedure Do_send
  Ctr=0
  Tucr=Ucr
  Ucr=&X10010000          !8 bits, no parity, 1 stop
  @Rsconf
  Open "I",#2,Filename$
  File_length=Lof(#2)
  Close #2
  Xblock=Int(File_length/128)+1
  Cls
  Ptr=Varptr(Buf)
  Y1=180
  Y2=185
  If Rez=2
    Y1=Y1*2
    Y2=Y2*2
  Endif
  Box 45,50,595,Y1
  Box 40,45,600,Y2
  Print At(27,10);"XMODEM Protocol (Transmit)"
  Place=(80-(Len(Filename$)+7))/2
  Print At(Place,13);"Sending:  ";Upper$(Filename$)
  Print At(28,15);Xblock;" Xmodem blocks in file."
  '
  E_flag=0
  Block%=0
  Block_no%=0
  Void Bios(3,1,17)       !send XON
  Time_out=Timer
  Open "I",#3,Filename$
  '
  Print At(30,22);"Waiting for response";
  '
  Wait_nak:
  '
  If Int((Timer-Time_out)/100)=100
    Goto Cancel
  Endif
  '
  Stat=Inp?(1)
  If Stat=0
    Goto Wait_nak
  Endif
  Byte%=Bios(2,1)
  If Byte%<>21
    Goto Wait_nak
  Endif
  Send:
  Check%=0
  Try%=0
  '
  Do
    '
    Inc Block%
    Exit If Eof(#3)
    '
    If Block%=256
      Block%=0
      Ctr=1
    Endif
    Block%=Block% And 255
    Block_comp%=255-Block%
    Check%=0
    I=0
    While I<127
      Poke Ptr+I,Inp(#3)
      If Eof(#3)
        Poke Ptr+I,Eot
        E_flag=1
        I=127
      Endif
      Inc I
    Wend
    '
    R_send:
    '
    If Ctr=1
      Dblock%=256+Block%
    Else
      Dblock%=Block%
    Endif
    Print At(21,22);"Sending Block: ";Dblock%;"  Comp: ";Block_comp%;" Try: ";Try%
    Void Bios(3,1,1)       !send SOH
    Void Bios(3,1,Block%)
    Void Bios(3,1,Block_comp%)
    For I=0 To 127
      Void Bios(3,1,Buf(I))
      Check%=(Check%+Buf(I)) And 255
    Next I
    Void Bios(3,1,Check%)
    Time_out=Timer
    '
    Wait_ack:
    If Int((Timer-Time_out)/100)=100
      Goto Cancel
    Endif
    '
    Stat=Inp?(1)
    If Stat=0
      Goto Wait_ack
    Endif
    Byte%=Bios(2,1)
    If Byte%=Ack
      Try%=0
      Goto N_loop
    Endif
    If Byte%=Nak
      Inc Try%
      If Try%=10
        Goto Cancel
      Endif
      Goto R_send
    Endif
    If Byte%=Can
      Goto Cancel
    Endif
    '
    N_loop:
    If E_flag=1
      Goto Dne_it
    Endif
  Loop
  '
  Cancel:
  Out 1,Can
  Out 1,Can
  Out 1,Can
  Print
  Print At(10,20);Space$(12)+"Excessive Errors -- Procedure Aborted!!"
  Print
  Dne_it:
  Print At(10,22);Space$(21)+"Transmission complete!"+Space$(19)
  '
  Count=0
  Send_eot:
  If Count=10
    Goto Enough
  Endif
  Void Bios(3,1,Eot)            !send End Of Transmission
  Stat=Inp?(1)
  If Stat=0
    Goto Send_eot
  Endif
  Byte%=Bios(2,1)
  If Byte%<>Ack
    Goto Send_eot
    Inc Count
  Endif
  '
  Enough:
  Close #3
  For I=0 To 2
    Print Chr$(7);               ! Ring bell
    Pause 50
  Next I
  Pause 100
  @Rt_bttn
  @Clear_modem
  Void Xbios(21,1)
  '
  Ucr=Tucr
  @Rsconf
  '
Return
'
Procedure Have_file
  '
  ' Returns -33 if file does not exist, otherwise expect a 0
  '
  Is_it=Gemdos(&H4E,L:Varptr(Filename$))
  '
Return
'
Procedure Runit
  '
  Menu Off
  Sget Screen$
  Cls
  Print "Select .PRG, .TOS or .TTP application to run:"
  Fileselect Chr$(Drive+65)+":\*.*",B$,Filename$
  '
  @Have_file
  If Is_it=-33
    Alrt$="File doesn't exist!"
    Alert 1,Alrt$,1,"OK",B
    Filename$=""
  Endif
  If Filename$=""
    Goto Skip_runit
  Endif
  '
  Para$=""
  If Upper$(Right$(Filename$,4))=".PRG"
    Goto Rn_it
  Endif
  If Upper$(Right$(Filename$,4))=".TOS"
    Goto Rn_it
  Endif
  If Upper$(Right$(Filename$,4))=".TTP"
    Cls
    Line Input "Enter .TTP parameters: ",Para$
    Para$=Chr$(Len(Para$))+Para$+Chr$(0)
    Goto Rn_it
  Endif
  '
  Alrt$="| |"+Filename$+"| is not an executable file."
  Alert 3,Alrt$,1,"OK",B
  Goto Skip_runit
  '
  Rn_it:
  '
  Cls
  Open "I",#2,Filename$
  File_length=Lof(#2)
  Close #2
  File_length=File_length+32767
  '
  If Fre(0)<File_length
    Alrt$="Not enough free memory!|Reboot without .ACC files|and/or RAM Disk."
    Alert 1,Alrt$,1,"OK",B
    Goto Skip_runit
  Endif
  '
  If Upper$(Right$(Filename$,4))=".TOS"
    Void Xbios(21,1)
  Endif
  If Upper$(Right$(Filename$,4))=".TTP"
    Void Xbios(21,1)
  Endif
  If Upper$(Right$(Filename$,4))=".PRG"
    Showm
  Endif
  '
  Reserve Fre(0)-File_length
  Print At(10,5);"LOADing ";Filename$
  Exec 0,Filename$,Para$,""
  Reserve Fre(0)+(File_length-255)
  '
  Skip_runit:
  Void Xbios(21,0)
  Sput Screen$
  '
Return
'
Procedure Auto_dial
  '
  If Dial_now=1
    Goto Auto_done
  Endif
  Alrt$=Space$(8)+"EasyTerm | |"+Space$(9)+"Auto-Dial"+Space$(7)+"|    Load new phone list?"
  Alert 2,Alrt$,2,"Yes|No",B
  If B=1
    Cls
    Dr_load:
    B$="autodial.phn"
    Fileselect Chr$(Drive+65)+":"+Path_name$+"\*.phn",B$,Filename$
    If Filename$=""
      Goto Dr_fini
    Endif
    @Have_file
    If Is_it=-33
      Alrt$="Can't find that file."
      Alert 1,Alrt$,1,"OK",B
      Goto Dr_load
    Endif
    '
    '
    Defmouse 2
    Open "I",#3,Filename$
    I=0
    For I=0 To 9
      Line Input #3,Service$(I);Number$(I);Pass$(I);Prompt$(I)
    Next I
    Close #3
    Defmouse 0
    Dr_fini:
    B$=""
  Endif
  '
  Alrt$="        EasyTerm | |        Auto-Dial         "
  Alert 2,Alrt$,3,"Edit No.|Dial No.|Cancel",B
  If B=1
    Ed_number:
    Cls
    @Display_auto
    Input "Edit Number (enter 'A' to Abort ";Number$
    If Upper$(Number$)="A"
      Cls
      Goto Auto_done
    Endif
    '
    If Val(Number$)<1 Or Val(Number$)>10
      Print "Invalid number. Select again"
      Pause 200
      Goto Ed_number
    Endif
    Cls
    Number=Val(Number$)-1
    '
    Re_do:
    Print "Presently: ",Service$(Number);Tab(30);Number$(Number)
    Line Input "Change Service name to: ",Service$(Number)
    Line Input "Change modem command and phone number string to: ";Number$(Number)
    Line Input "Enter password sequence or press <Return>: ";Pass$(Number)
    If Pass$(Number)<>""
      Line Input "Enter Prompt from Service for Password; ";Prompt$(Number)
    Endif
    Print "Entry ";Number$;" corrected to"
    Print Service$(Number);"   ";Number$(Number)
    Print "Password is: ";Pass$(Number)
    If Pass$(Number)<>""
      Print "Prompt is: ";Prompt$(Number)
    Endif
    Input "Is this correct? (Y or N) ";Response$
    If Upper$(Response$)<>"Y"
      Goto Re_do
    Endif
    Print
    Input "Edit another entry? (Y or N)";Response$
    If Upper$(Response$)="Y"
      Goto Ed_number
    Endif
    Choose_file:
    Cls
    Print "Save file as:"
    B$="autodial.phn"
    Fileselect Chr$(Drive+65)+":"+Path_name$+"\autodial.phn",B$,Filename$
    If Filename$=""
      Goto Auto_done
    Endif
    @Have_file
    If Is_it=0
      Alrt$="File Exists!"
      Alert 1,Alrt$,1,"Replace|Cancel",B
      If B=2
        Goto Choose_file
        Filename$=""
      Endif
    Endif
    '
    If Filename$=""
      Goto Auto_done
    Endif
    '
    Print "Saving file as: ";Filename$
    Defmouse 2
    Open "O",#3,Filename$
    For I=0 To 9
      Print #3;Service$(I)
      Print #3;Number$(I)
      Print #3;Pass$(I)
      Print #3;Prompt$(I)
    Next I
    Close #3
    Defmouse 0
    Cls
  Endif
  '
  If B=2
    Dial_number:
    Cls
    @Display_auto
    Input "Enter number designator for Service you'd like to call or 'A' to Abort. ";Number$
    If Upper$(Number$)="A"
      Cls
      Goto Auto_done
    Endif
    '
    If Asc(Number$)<49 Or Asc(Number$)>57
      Print "Invalid number. Select again"
      Goto Dial_number
    Endif
    Number=Val(Number$)-1
    Dial_now=1
    Cls
    Print At(1,1)
    @Status
    Print At(15,20);"--¯ Press Right Mouse button to dial ";Service$(Number);" ®--"
  Endif
  '
  If B=3
    Cls
  Endif
  '
  Auto_done:
  '
Return
'
Procedure Display_auto
  '
  Print "Telephone Directory"
  Print "     Service";Tab(28);"Modem command string"
  For I=0 To 9
    Print I+1;". ";Service$(I);Tab(25);Number$(I);" ";
    If Pass$(I)<>""
      Print Tab(50);"*Password*";
    Endif
    Print
  Next I
Return
'
Procedure Dial_it
  Try_again:
  Abort=0
  @Control_c
  If Abort=1
    Print Chr$(27);"p <<Dialing sequence aborted by User request>> ";Chr$(27);"q"
    Goto Fini_dial
  Endif
  Cnnt=0
  Print "Auto Dialing ";Service$(Number)
  For I=1 To Len(Number$(Number))
    Out 1,Asc(Mid$(Number$(Number),I,1)) And 127
    Pause 5
  Next I
  Out 1,13
  Mode$=""
  St_time=Timer
  Do
    @Control_c
    If Abort=1
      Print Chr$(27);"p <<Dialing sequence aborted by User request>> ";Chr$(27);"q"
      Goto Fini_dial
    Endif
    If Inp?(1)<>0
      T=Inp(1)
      Out 2,T
      Mode$=Mode$+Chr$(T)
    Endif
    If Upper$(Right$(Mode$,4))<>"NECT" And Timer-St_time>5000
      ' Pause 1500
      Out 1,13
      Goto Try_again
    Endif
    If Upper$(Right$(Mode$,4))="NECT"
      Cnnt=1
    Endif
    Exit If Cnnt=1
  Loop
  Print
  '
  Out 1,13
  '
  If Pass$(Number)=""
    Goto Fini_dial
  Endif
  '
  Repeat
    If Inp?(1)<>0
      T=Inp(1)
      Out 2,T And 127
      Mode$=Mode$+Chr$(T And 127)
    Endif
  Until Right$(Mode$,Len(Prompt$(Number)))=Prompt$(Number)
  '
  For I=1 To Len(Pass$(Number))
    Out 1,Asc(Mid$(Pass$(Number),I,1))
    Print "*";
    Pause 5
  Next I
  Out 1,13
  Out 1,10
  Fini_dial:
  Dial_now=0
Return
'
Procedure Edit_function_key
  '
  N_key:
  Cls
  Print "Function Keys:"
  For I=0 To 9
    Print "F";I+1;" ";Key$(I)
  Next I
  Print
  Print "Press Function key to edit or 'A' to abort:"
  Check_key:
  A=Inp(2)
  If A=&H41 Or A=&H61
    Goto No_define
  Endif
  If A<187 Or A>196          !check function key
    Goto Check_key
  Endif
  A=A-187
  W_key:
  Cls
  Print "Key now defined as:"
  Print Key$(A)
  Print
  Print "Enter new defination:"
  Line Input Key$(A)
  Print
  Print "F";A+1;" Defined as:"
  Print Key$(A)
  Print
  Input "Is this correct? (Y or N) ",Response$
  If Upper$(Response$)<>"Y"
    Goto W_key
  Endif
  Print
  Input "Edit another entry? (Y or N)",Response$
  If Upper$(Response$)="Y"
    Goto N_key
  Endif
  '
  Cls
  Alrt$=" |Save redefined Function Keys?"
  Alert 2,Alrt$,1,"YES|NO",B
  '
  If B=1
    Cls
    Print "Saving Function Keys....."
    '
    Defmouse 2
    Open "O",#3,"funct.key"
    For I=0 To 19
      Print #3;Key$(I)
    Next I
    Close #3
    Defmouse 0
  Endif
  '
  No_define:
  Cls
  '
Return
'
Procedure Edit_shiftfunction_key
  '
  Sn_key:
  Cls
  Print "Function Keys:"
  For I=0 To 9
    Print "Shifted F";I+1;" ";Key$(I)
  Next I
  Print
  Print "Press Function key to edit or 'A' to abort:"
  Scheck_key:
  A=Inp(2)
  If A=&H41 Or A=&H61
    Goto No_sdefine
  Endif
  If A<212 Or A>221          !check function key
    Goto Scheck_key
  Endif
  A=A-202
  S_key:
  Cls
  Print "Key now defined as:"
  Print Key$(A)
  Print
  Print "Enter new defination:"
  Line Input Key$(A)
  Print
  Print "Shifted F";A+1;" Defined as:"
  Print Key$(A)
  Print
  Input "Is this correct? (Y or N) ",Response$
  If Upper$(Response$)<>"Y"
    Goto S_key
  Endif
  Print
  Input "Edit another entry? (Y or N)",Response$
  If Upper$(Response$)="Y"
    Goto Sn_key
  Endif
  '
  Alrt$=" |Save redefined Function Keys?"
  Alert 2,Alrt$,1,"YES|NO",B
  '
  If B=1
    Cls
    Print "Saving Function Keys....."
    '
    Defmouse 2
    Open "O",#3,"funct.key"
    For I=0 To 19
      Print #3;Key$(I)
    Next I
    Close #3
    Defmouse 0
  Endif
  '
  No_sdefine:
  Cls
  '
Return
'
Procedure Function_key
  '
  C=T-187
  For I=1 To Len(Key$(C))
    If Asc(Mid$(Key$(C),I,1))=&H7E
      Out 2,13
      Out 1,13
      Goto Nw_key
    Endif
    Out 1,Asc(Mid$(Key$(C),I,1))
    Out 2,Asc(Mid$(Key$(C),I,1))
    Nw_key:
    Pause 1
  Next I
  T=0
  '
Return
'
Procedure Sfunction_key
  '
  C=T-201
  For I=1 To Len(Key$(C))
    If Asc(Mid$(Key$(C),I,1))=&H7E
      Out 2,13
      Out 1,13
      Goto Snw_key
    Endif
    Out 1,Asc(Mid$(Key$(C),I,1))
    Out 2,Asc(Mid$(Key$(C),I,1))
    Snw_key:
    Pause 1
  Next I
  T=0
  '
Return
'
Procedure Help
  '
  X%=Crscol
  Y%=Crslin
  Void Xbios(21,0)
  Sget Screen$
  Cls
  Print
  Print "ALT B  -  Toggle Capture Buffer (Open/Closed)"
  Print "ALT C  -  Clear Capture Buffer"
  Print "ALT D  -  Download File"
  Print "ALT F  -  Display Free Space on current disk drive"
  Print "ALT M  -  Change Duplex Mode (Full/Half"
  Print "ALT P  -  Toggle Printer On/OFF (if printer active)"
  Print "ALT S  -  Change Baud rate (2400,1200,300)"
  Print "ALT U  -  Upload File"
  Print "ALT V  -  Disk Verify toggle (On/OFF)"
  Print "ALT X  -  Display EasyTerm Status"
  Print "ALT Z  -  Turn on type ahead buffer"
  Print "CLR    -  Clear on line screen"
  Print "Help   -  Display this screen"
  Print "Left Mouse Button displays alternate screen."
  Print
  Print
  Print "Press Return to continue..."
  Repeat
  Until Inp(2)=13
  Cls
  Print
  Print "         Script File Commands"
  Print
  Print "Command                 Function"
  Print "  AD(name)      Dial service from Autodial file."
  Print "  AL            Alarm (Ring bell)"
  Print "  CA            Toggle Capture buffer On/Off"
  Print "  DA (filename) Download ASCII file."
  Print "  DE (time)     Delay until (time) hh:mm:ss"
  Print "  DX (filename) Download Xmodem file."
  Print "  EC (text)     Echo to screen"
  Print "  PR            Toggle printer On/Off"
  Print "  RS (filename) Load RS-232 .CFG file."
  Print "  SE (text)     Send characters to RS-232 port."
  Print "  SC (filename) Save capture buffer as filename."
  Print "  UA (filename) Upload an ASCII file."
  Print "  UX (filename) Upload an Xmodem file."
  Print "  WA            Wait for prompt."
  Print
  Print
  Print "Press Return to continue..."
  Repeat
  Until Inp(2)=13
  Cls
  Sput Screen$
  Print At(X%,Y%)
  T=13
Return
'
' Save Original Color Palette
'
Procedure Save_palette
  Local I
  For I=0 To 15
    Palette(I)=Xbios(7,W:I,W:-1)
  Next I
Return
'
' Restore Original Color Palette
'
Procedure Restore_palette
  Local I
  For I=0 To 15
    Setcolor I,Palette(I)
  Next I
Return
'
Procedure Script
  Cls
  Print "Select Script File to execute:"
  Filename$=""
  Fileselect Chr$(Drive+65)+":"+Path_name$+"\*.FIL",B$,Filename$
  Defmouse 2
  @Have_file
  If Is_it=-33
    Defmouse 0
    Alrt$="     EasyTerm| |Can't find that file."
    Alert 1,Alrt$,1,"OK",B
    Goto Skip_script
  Endif
  '
  Cls
  If Filename$=""
    Defmouse 0
    Goto Skip_script
  Endif
  '
  Defmouse 2
  Open "I",#3,Filename$
  Li=0
  Do
    Exit If Eof(#3)
    Line Input #3,Script$(Li)
    Inc Li
  Loop
  Close #3
  Defmouse 0
  '
  Abort=0
  Print "Executing ";Filename$
  Print "Script file of ";Li-1;" lines in memory"
  '
  For L_no=0 To Li-1
    @Control_c
    Size=Len(Script$(L_no))-3
    '
    If Left$(Script$(L_no),2)="EC"            !ECHO to screen
      Print Right$(Script$(L_no),Size)
    Endif
    '
    If Left$(Script$(L_no),2)="WA"            !WAit for prompt
      Start_timer=Timer
      Wait$=Space$(Size)
      Wait:
      Command$=Right$(Script$(L_no),(Size))
      If Timer-Start_timer>15000
        Print "TIME OUT -Script File aborted"
        L_no=Li-1
        Goto Timed_out
      Endif
      If Bios(&H1,1)<>0
        A=Inp(1)
      Else
        Goto Wait
      Endif
      Out 2,A And 127
      If Capture!
        @Capture_it
      Endif
      If Prnt!=-1
        Out 0,A
        If T=13
          Inc Prnt_cnt
          If Prnt_cnt=80
            Out 0,13
            Out 0,10
            Prnt_cnt=0
          Endif
        Endif
      Endif
      Wait$=Wait$+Chr$(A And 127)
      Wait$=Right$(Wait$,5)
      If Right$(Wait$,(Size))<>Command$
        Goto Wait
      Endif
    Endif
    '
    If Left$(Script$(L_no),2)="SE"         !SEnd output to modem port
      If Prnt!=-1
        Lprint Right$(Script$(L_no),(Size))
      Endif
      For S=4 To Len(Script$(L_no))
        Out 1,Asc(Mid$(Script$(L_no),S,1))
        Out 2,Asc(Mid$(Script$(L_no),S,1))
      Next S
      Out 1,13
      Out 2,13
      Out 2,10
    Endif
    '
    If Left$(Script$(L_no),2)="DE"         !Wait for time delay
      Time_reached=0
      Print "Start time: ";Time$
      Hold_time$=Right$(Script$(L_no),Size)
      W_hour=Val(Left$(Hold_time$,2))
      W_min=Val(Mid$(Hold_time$,3,2))
      Print "Waiting until: ";W_hour;":";    !Hold_time$
      If W_min<=9
        Print "0";W_min
      Else
        Print W_min
      Endif
      Xcol=Crscol
      Yrow=Crslin
      Do
        Abort=0
        @Control_c
        Print At(Xcol,Yrow);"Time now is: ";Time$
        P_time$=Left$(Time$,5)
        If W_hour=Val(Left$(P_time$,2))
          If W_min=Val(Right$(P_time$,2))
            Time_reached=1
          Endif
        Endif
        Exit If Time_reached=1
        Exit If Abort=1
      Loop
    Endif
    '
    If Left$(Script$(L_no),2)="AL"
      Pause 50
      Print Chr$(7)
    Endif
    '
    If Left$(Script$(L_no),2)="PR"
      If Prnt!=0 And Prnt=-1
        Prnt!=-1
        Print
        Print Chr$(27);"p <PRINTER ON> ";Chr$(27);"q"
        Goto Pr_changed
      Endif
      If Prnt!=-1 And Prnt=-1
        Prnt!=0
        Print
        Print Chr$(27);"p <PRINTER OFF> ";Chr$(27);"q"
      Endif
      Pr_changed:
    Endif
    '
    If Left$(Script$(L_no),2)="CA"
      If Capture!=-1
        Capture!=0
        Print
        Print Chr$(27);"p <CAPTURE BUFFER CLOSED> ";Chr$(27);"q"
        Goto Changed_cap
      Endif
      If Capture!=0
        Capture!=-1
        Capture=0
        Print
        Print Chr$(27);"p <CAPTURE BUFFER OPEN> ";Chr$(27);"q"
      Endif
      Changed_cap:
    Endif
    '
    If Left$(Script$(L_no),2)="SC"
      Filename$=Right$(Script$(L_no),Size)
      Defmouse 2
      Open "O",#3,Filename$
      Print #3,Capture$
      Close #3
      Defmouse 0
    Endif
    '
    If Left$(Script$(L_no),2)="DA"
      Out 1,Xoff
      Filename$=Right$(Script$(L_no),Size)
      Open "O",#3,Filename$
      '
      Out 1,Xon
      '
      Repeat
        '
        If Inp?(1)
          Byte=Inp(1)
          Out #3,Byte
          Out 2,Byte
        Endif
        '
      Until Asc(Inkey$)=27
      '
      Close #3
    Endif
    '
    If Left$(Script$(L_no),2)="DX"          !Xmodem download
      Out 1,Xoff
      Filename$=Right$(Script$(L_no),Size)
      Out 1,Xon
      @Do_rcv
    Endif
    '
    If Left$(Script$(L_no),2)="UA"
      Out 1,Xoff
      Filename$=Right$(Script$(L_no),Size)
      Open "I",#3,Filename$
      '
      Out 1,Xon
      '
      Do
        Exit If Eof(#3)
        Byte=Inp(#3)
        Out 1,Byte
        Out 2,Byte
        Pause 5
      Loop
      '
      Close #3
    Endif
    '
    If Left$(Script$(L_no),2)="UX"
      Out 1,Xoff
      Filename$=Right$(Script$(L_no),Size)
      @Do_send
    Endif
    '
    If Left$(Script$(L_no),2)="AD"
      Number=99
      Nme$=Right$(Script$(L_no),Size)
      For I=0 To 9
        If Nme$=Service$(I)
          Number=I
          I=9
        Endif
      Next I
      If I=9 And Number=99
        Print Chr$(27);"p <FATAL ERROR - Process aborted> ";Chr$(27);"q"
        L_no=Li
        Goto Timed_out
      Endif
      @Dial_it
    Endif
    '
    '
    If Left$(Script$(L_no),2)="RS"
      Filename$=Right$(Script$(L_no),Size)
      Report=0
      @Load_cfg
      If Report=0
        Print Chr$(27);"p <FATAL ERROR - Process aborted> ";Chr$(27);"q"
        L_no=Li
        Goto Timed_out
      Endif
    Endif
    '
    Timed_out:
    If Abort=1
      L_no=Li
      Print "Procedure aborted by User"
      Out 1,Asc("+")
      Out 1,Asc("+")
      Out 1,Asc("+")
      Out 1,13
      Pause 50
      Out 1,Asc("A")
      Out 1,Asc("T")
      Out 1,Asc("H")
      Out 1,Asc("0")
    Endif
  Next L_no
  '
  Script_fini:
  '
  Print
  Print Filename$;" complete"
  Print
  Print "                 --->> Press any key to continue <<---"
  A=Inp(2)
  Skip_script:
  Cls
  '
Return
'
Procedure Control_c
  '
  If Inp?(2)<>0                        !Control C pressed?
    K=Inp(2)
    If K=3
      Abort=1
      L_no=Li-1
    Endif
  Endif
  '
Return
'
Procedure Load_cfg
  '
  Print "Select Configuration File to load:"
  Fileselect Chr$(Drive+65)+":"+Path_name$+"\*.cfg",B$,Filename$
  Defmouse 2
  @Have_file
  If Is_it=-33
    Defmouse 0
    Alrt$="     EasyTerm| |Can't find that file."
    Alert 1,Alrt$,1,"OK",B
    Goto Rs232_fini
  Endif
  '
  Cls
  If Filename$=""
    Goto Rs232_fini
  Endif
  '
  @Do_cfg
  Report=1
  Rs232_fini:
  Cls
Return
'
Procedure Do_cfg
  Defmouse 2
  '
  Open "I",#3,Filename$
  '
  Input #3;Flow
  Input #3;Ucr
  Input #3;Rsr
  Input #3;Tsr
  Input #3;Scr
  Input #3;Duplex
  Input #3;Baud
  '
  Close #3
  Defmouse 0
  @Rsconf
  '
Return
