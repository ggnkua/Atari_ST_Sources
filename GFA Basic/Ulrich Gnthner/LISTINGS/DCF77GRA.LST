CLS
' DCF77GRA.PRG, (c) Ulrich Gnthner,
' Karpfenweg 1, 68789 St. Leon-Rot
SELECT XBIOS(4)
CASE 0
  mix&=220
  miy&=120
  rad&=66
  x_k=1
CASE 1
  mix&=280
  miy&=120
  x_k=2
  rad&=66
CASE 2
  mix&=320
  miy&=200
  rad&=132
  x_k=1
DEFAULT
  mix&=320
  miy&=200
  rad&=132
  x_k=1
ENDSELECT
fl_pr!=FALSE
ti1$="DCF77GRA.PRG, "
ti2$=CHR$(189)+"Ulrich Gnthner"
ti4$="Die Uhr wird nach DCF77 gestellt !"
ti5$="Die Minuten waren fehlerhaft."
ti6$="Die Stunden waren fehlerhaft."
ti7$="Das Datum war fehlerhaft.    "
ti8$="Die Uhr wurde falsch gestellt.    "
ti9$="Der Programmlauf wird wiederholt !"
ti10$="Die Uhr wird gestellt.         "
ti11$="Telegramm lesen und dekodieren."
ti12$="Auf Minutenbeginn warten."
ti13$="Die Uhr wurde korrekt gestellt."
PRINT AT(2,1);ti1$+ti2$;
PRINT AT(2,2);ti4$;
DIM bi|(60)
DIM w$(7)
DIM zon$(2)
w$(1)="Montag"
w$(2)="Dienstag"
w$(3)="Mittwoch"
w$(4)="Donnerstag"
w$(5)="Freitag"
w$(6)="Samstag"
w$(7)="Sonntag"
zon$(1)="MEZ"
zon$(0)="MESZ"
DIM i$(60)
DIM te$(4)
te$(0)="Es werden keine Taktimpulse empfangen !"
te$(1)="Empf„nger und Kabelverbindung prfen !"
te$(2)="Evtl. den Empf„nger drehen !"
te$(3)="Rote LED muž im Sekundenrythmus blinken!"
fl!=FALSE
at%=TIMER
REPEAT
  IF SUB(TIMER,at%)>600
    FOR l|=0 TO 3
      PRINT AT(2,3+l|);te$(l|);
    NEXT l|
    PAUSE 500
    STICK 0
    EDIT
  ENDIF
UNTIL STICK(1)=1
REPEAT
  IF SUB(TIMER,at%)>600
    FOR l|=0 TO 3
      PRINT AT(2,3+l|);te$(l|);
    NEXT l|
    PAUSE 500
    STICK 0
    EDIT
  ENDIF
UNTIL STICK(1)=0
vk|=2
DEFFILL 1,2,8
LINE mix&,miy&-5,mix&,miy&+5
LINE mix&-5*x_k,miy&,mix&+5*x_k,miy&
c|=0
FOR i=PI TO -PI STEP -2*PI/60
  SELECT c|
  CASE 0,5,10,15,20,25,30,35,40,45,50,55
    r|=2
  DEFAULT
    r|=1
  ENDSELECT
  x=x_k*rad&*SIN(i)+mix&
  y=rad&*COS(i)+miy&
  PCIRCLE x,y,r|
  INC c|
NEXT i
DEFFILL 0
zeit$=""
tag$=""
ot%=TIMER
DO
  REPEAT
    t%=TIMER
  UNTIL STICK(1)=1
  PRINT AT(2,4);"P";
  zy|=ADD(DIV(SUB(TIMER,ot%),190),2)
  ot%=TIMER
  REPEAT
  UNTIL STICK(1)=0
  pu|=DIV(SUB(TIMER,t%),30)
  PRINT AT(2,4);" ";
  PRINT AT(3,4);pu|;
  ent(zy|,pu|)
  IF ASC(INKEY$)=27
    fl_esc!=TRUE
  ENDIF
  EXIT IF fl_esc!
LOOP
IF fl_esc!
  STICK 0
  EDIT
ENDIF
PROCEDURE ent(zy|,pu|)
  IF fl!
    INC sek|
    bi|(sek|)=pu|
    bitset(sek|,pu|)
    kontr(pu|,sek|)
    IF zy|=4
      PRINT AT(2,3);ti10$;
      stell
      IF fl_pr!
        RUN
      ENDIF
    ENDIF
  ELSE
    IF zy|=4
      bitset(0,0)
      PRINT AT(2,3);ti11$;
      fl!=TRUE
      sek|=0
    ELSE
      PRINT AT(2,3);ti12$;
    ENDIF
  ENDIF
  raus_ent:
RETURN
PROCEDURE parity(anf|,en|,p|)
  quer|=0
  FOR k|=anf| TO en|
    IF bi|(k|)=1
      INC quer|
    ENDIF
  NEXT k|
  IF EVEN(quer|+bi|(p|))
    pr$="ok"
  ELSE
    SELECT anf|
    CASE 21
      fehler(1)
    CASE 29
      fehler(2)
    CASE 36
      fehler(3)
    ENDSELECT
  ENDIF
RETURN
PROCEDURE kontr(pu|,sek|)
  SELECT sek|
  CASE 24
    wand(21,4)
    zeit$=we$+":00"
  CASE 27
    wand(25,3)
    zeit$=":"+we$+zeit$
  CASE 28
    parity(21,27,28)
    uhr(VAL(MID$(zeit$,2,2)),0)
  CASE 32
    wand(29,4)
    zeit$=we$+zeit$
  CASE 34
    wand(33,2)
    zeit$=we$+zeit$
  CASE 35
    parity(29,34,35)
    uhr(VAL(LEFT$(zeit$,2)),1)
  CASE 39
    wand(36,4)
    uhr(we|,3)
    tag$=we$+"."
  CASE 41
    wand(40,2)
    uhr(we|,4)
    tag$=we$+tag$
  CASE 44
    wand(42,3)
    wo$=w$(we|)
    uhr(0,11)
  CASE 48
    wand(45,4)
    uhr(we|,5)
    mo$=we$+"."
  CASE 49
    wand(49,1)
    uhr(we|,6)
    tag$=tag$+we$+mo$
  CASE 53
    wand(50,4)
    uhr(we|,7)
    ja$=we$
  CASE 57
    wand(54,4)
    uhr(we|,8)
    ja$=we$+ja$
    SELECT VAL(ja$)
    CASE 90 TO 99
      jh1$="1"
      uhr(VAL(jh1$),9)
      jh2$="9"
      uhr(VAL(jh2$),10)
    CASE 0 TO 79
      jh1$="2"
      uhr(VAL(jh1$),9)
      jh2$="0"
      uhr(VAL(jh2$),10)
    ENDSELECT
    tag$=tag$+jh1$+jh2$+ja$
  CASE 58
    parity(36,57,58)
  ENDSELECT
RETURN
PROCEDURE bitset(sek|,pu|)
  pos=sek|*(-2*PI/60)+PI
  DEFTEXT 1,0,0,4
  SELECT pu|
  CASE 1
    pu$="1"
  CASE 0
    pu$="O"
  ENDSELECT
  xpu=x_k*(rad&+8)*SIN(pos)+mix&-2
  ypu=(rad&+8)*COS(pos)+miy&+2
  TEXT xpu,ypu,pu$
  DEFTEXT 1,0,0,13
RETURN
PROCEDURE zeichn(b|,la|)
  DEFLINE 1,b|,0,1
  wert=wert*(-2*PI/60)+PI
  xz=x_k*la|*SIN(wert)+mix&
  yz=la|*COS(wert)+miy&
  LINE mix&,miy&,xz,yz
  DEFLINE 1,1,0,0
RETURN
PROCEDURE uhr(wert,mod|)
  pos|=LEN(zon$(bi|(18)))
  h_10$=CHR$(VAL(LEFT$(zeit$,1))+16)
  h_1$=CHR$(VAL(MID$(zeit$,2,1))+16)+":"
  m_10$=CHR$(VAL(MID$(zeit$,4,1))+16)
  m_1$=CHR$(VAL(MID$(zeit$,5,1))+16)
  SELECT mod|
  CASE 0
    b|=3
    la|=rad&-5
    zeichn(b|,la|)
  CASE 1
    IF wert>12
      wert=wert-12
    ENDIF
    zus=VAL(MID$(zeit$,4,2))/12
    wert=wert*5+zus
    b|=5
    la|=rad&-10
    zeichn(b|,la|)
  CASE 3
    DEFFILL 0
    PBOX 60,miy&-40,100,miy&-20
    BOX 60,miy&-40,100,miy&-20
    PBOX 30,miy&-20,130,miy&
    BOX 30,miy&-20,130,miy&
    PBOX 30,miy&,130,miy&+20
    BOX 30,miy&,130,miy&+20
    PBOX 50,miy&+20,110,miy&+40
    BOX 50,miy&+20,110,miy&+40
    TEXT 48,miy&+16,CHR$(wert+16)+"."
    TEXT 80-4*pos|,miy&-24,zon$(bi|(18))
    TEXT 60,miy&+36,h_10$
    TEXT 68,miy&+36,h_1$
    TEXT 84,miy&+36,m_10$
    TEXT 92,miy&+36,m_1$
  CASE 4
    TEXT 40,miy&+16,CHR$(wert+16)
  CASE 5
    TEXT 72,miy&+16,CHR$(wert+16)+"."
  CASE 11
    TEXT 80-4*LEN(wo$),miy&-4,wo$
  CASE 6
    TEXT 64,miy&+16,CHR$(wert+16)
  CASE 7
    TEXT 112,miy&+16,CHR$(wert+16)
  CASE 8
    TEXT 104,miy&+16,CHR$(wert+16)
  CASE 9
    TEXT 88,miy&+16,CHR$(wert+16)
  CASE 10
    TEXT 96,miy&+16,CHR$(wert+16)
  ENDSELECT
RETURN
PROCEDURE stell
  DO
    s$=RIGHT$(TIME$,2)
    PAUSE 2
    EXIT IF s$<>RIGHT$(TIME$,2)
  LOOP
  SETTIME zeit$,tag$
  IF zeit$<>TIME$ OR DATE$<>tag$
    fehler(4)
  ELSE
    PRINT AT(2,3);ti13$;
    PAUSE 80
  ENDIF
  STICK 0
  EDIT
RETURN
PROCEDURE fehler(obj|)
  SELECT obj|
  CASE 1
    PRINT AT(2,3);ti5$;
  CASE 2
    PRINT AT(2,3);ti6$;
  CASE 3
    PRINT AT(2,3);ti7$;
  CASE 4
    PRINT AT(2,3);ti8$;
  ENDSELECT
  PRINT AT(2,4);ti9$;
  PAUSE 150
  RUN
RETURN
PROCEDURE wand(a|,z|)
  we|=0
  FOR k|=0 TO z|-1
    we|=we|+(2^(k|))*bi|(k|+a|)
  NEXT k|
  we$=STR$(we|)
RETURN
