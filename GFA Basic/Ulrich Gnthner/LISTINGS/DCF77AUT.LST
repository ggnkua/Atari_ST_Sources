' DCF77AUT.GFA
' (c) Ulrich Gnthner,
' Karpfenweg 1,
' 68789 St.Leon-Rot
CLS
fl_esc!=FALSE
fl_no!=FALSE
cht%=TIMER
REPEAT
  zt|=INT((TIMER-cht%)/200)
  IF zt|>3
    STICK 0
    EDIT
  ENDIF
UNTIL STICK(1)=0
REPEAT
  zt|=INT((TIMER-cht%)/200)
  IF zt|>3
    STICK 0
    EDIT
  ENDIF
UNTIL STICK(1)=1
fl_pr!=FALSE
ti1$="DCF77AUT.PRG, "+CHR$(189)
ti2$=" Ulrich Gnthner"
PRINT AT(3,1);ti1$+ti2$;
ti3$="Die Uhr wird "
ti4$="nach DCF77 gestellt !"
PRINT AT(3,2);ti3$+ti4$;
DIM bi|(60)
DIM w$(7)
DIM zon$(2)
DIM er$(2)
w$(1)="Montag"
w$(2)="Dienstag"
w$(3)="Mittwoch"
w$(4)="Donnerstag"
w$(5)="Freitag"
w$(6)="Samstag"
w$(7)="Sonntag"
zon$(1)="MEZ"
zon$(0)="MESZ"
er$(0)="nein"
er$(1)="ja"
DIM i$(60)
DIM te$(4)
te0$="Keine "
te1$="Taktimpulse !"
te$(0)=te0$+te1$
te2$="Empf„ngermodul "
te3$="und Kabel prfen !"
te$(1)=te2$+te3$
te4$="Lage des "
te5$="Empf„ngers ver„ndern !"
te$(2)=te4$+te5$
te6$="Rote LED muž im "
te7$="1s-Rythmus blinken !"
te$(3)=te6$+te7$
fl!=FALSE
at%=TIMER
REPEAT
  IF SUB(TIMER,at%)>600
    FOR l|=0 TO 3
      PRINT AT(3,3+l|);te$(l|);
    NEXT l|
    PAUSE 200
    STICK 0
    EDIT
  ENDIF
UNTIL STICK(1)=1
REPEAT
  IF SUB(TIMER,at%)>600
    FOR l|=0 TO 3
      PRINT AT(3,3+l|);te$(l|);
    NEXT l|
    PAUSE 200
    STICK 0
    EDIT
  ENDIF
UNTIL STICK(1)=0
PRINT AT(3,5);"Status >>";
PRINT AT(3,8);"               1 2 4 8";
PRINT AT(3,9);"Sommer/Winter:";
PRINT AT(3,10);"Wochentag    :";
PRINT AT(3,11);"1Oer-Tage    :";
PRINT AT(3,12);" 1er-Tage    :";
PRINT AT(3,13);"1Oer-Monate  :";
PRINT AT(3,14);" 1er-Monate  :";
PRINT AT(3,15);"1Oer-Jahre   :";
PRINT AT(3,16);" 1er-Jahre   :";
PRINT AT(3,17);"1Oer-Stunden :";
PRINT AT(3,18);" 1er-Stunden :";
PRINT AT(3,19);"1Oer-Minuten :";
PRINT AT(3,20);" 1er-Minuten :";
zeit$=""
tag$=""
te8$="Uhrzeit >   :   h  , "
te9$="Datum >   .  ."
PRINT AT(3,4);te8$;te9$;
ot%=TIMER
DO
  REPEAT
    t%=TIMER
  UNTIL STICK(1)=1
  PRINT AT(2+2,6);"P";
  zy|=ADD(DIV(SUB(TIMER,ot%),190),2)
  ot%=TIMER
  REPEAT
  UNTIL STICK(1)=0
  pu|=DIV(SUB(TIMER,t%),30)
  PRINT AT(2+2,6);" ";
  PRINT AT(2+4,6);pu|;
  ent(zy|,pu|)
  IF ASC(INKEY$)=27
    fl_esc!=TRUE
  ENDIF
  EXIT IF fl_esc!
LOOP
IF fl_esc!
  STICK 0
  EDIT
ENDIF
PROCEDURE ent(zy|,pu|)
  IF fl!
    INC sek|
    bi|(sek|)=pu|
    kontr(pu|,sek|)
    IF zy|=4
      PRINT AT(3,3);"Die Uhr wird gestellt.";
      PAUSE 50
      stell
      IF fl_pr!
        RUN
      ENDIF
    ENDIF
  ELSE
    IF zy|=4
      PRINT AT(3,3);"Telegramm dekodieren. ";
      fl!=TRUE
      sek|=0
    ELSE
      PRINT AT(3,3);"Erwarte Minutenbeginn.";
    ENDIF
  ENDIF
  raus_ent:
RETURN
PROCEDURE parity(anf|,en|,p|)
  quer|=0
  FOR k|=anf| TO en|
    IF bi|(k|)=1
      INC quer|
    ENDIF
  NEXT k|
  IF EVEN(quer|+bi|(p|))
    pr$="ok"
  ELSE
    SELECT anf|
    CASE 21
      fehler(1)
    CASE 29
      fehler(2)
    CASE 36
      fehler(3)
    ENDSELECT
  ENDIF
RETURN
PROCEDURE kontr(pu|,sek|)
  SELECT sek|
  CASE 18
    PRINT AT(18,7+2);pu|;
    PRINT AT(26,7+2);"-> ";zon$(bi|(18));
  CASE 21
    PRINT AT(18,20);pu|;
  CASE 22
    PRINT AT(20,20);pu|;
  CASE 23
    PRINT AT(22,20);pu|;
  CASE 24
    PRINT AT(24,20);pu|;
    wand(21,4)
    PRINT AT(17,4);we|;
    zeit$=we$+":00"
  CASE 25
    PRINT AT(18,19);pu|;
  CASE 26
    PRINT AT(20,19);pu|;
  CASE 27
    PRINT AT(22,19);pu|;
    wand(25,3)
    PRINT AT(16,4);we|;
    zeit$=":"+we$+zeit$
  CASE 28
    parity(21,27,28)
    PRINT AT(16,5);pr$;
  CASE 29
    PRINT AT(18,18);pu|;
  CASE 30
    PRINT AT(20,18);pu|;
  CASE 31
    PRINT AT(22,18);pu|;
  CASE 32
    PRINT AT(24,18);pu|;
    wand(29,4)
    PRINT AT(14,4);we|;
    zeit$=we$+zeit$
  CASE 33
    PRINT AT(18,17);pu|;
  CASE 34
    PRINT AT(20,17);pu|;
    wand(33,2)
    PRINT AT(13,4);we|;
    zeit$=we$+zeit$
  CASE 35
    parity(29,34,35)
    PRINT AT(13,5);pr$;
  CASE 36
    PRINT AT(18,12);pu|;
  CASE 37
    PRINT AT(20,12);pu|;
  CASE 38
    PRINT AT(22,12);pu|;
  CASE 39
    PRINT AT(24,12);pu|;
    wand(36,4)
    PRINT AT(33,4);we|;
    tag$=we$+"."
  CASE 40
    PRINT AT(18,11);pu|;
  CASE 41
    PRINT AT(20,11);pu|;
    wand(40,2)
    PRINT AT(32,4);we|;
    tag$=we$+tag$
  CASE 42
    PRINT AT(18,8+2);pu|;
  CASE 43
    PRINT AT(20,8+2);pu|;
  CASE 44
    PRINT AT(22,8+2);pu|;
    wand(42,3)
    PRINT AT(24,8+2);"-> ";w$(we|);
    wo$=w$(we|)
  CASE 45
    PRINT AT(18,14);pu|;
  CASE 46
    PRINT AT(20,14);pu|;
  CASE 47
    PRINT AT(22,14);pu|;
  CASE 48
    PRINT AT(24,14);pu|;
    wand(45,4)
    PRINT AT(36,4);we|;
    mo$=we$+"."
  CASE 49
    PRINT AT(18,13);pu|;
    wand(49,1)
    PRINT AT(35,4);we|;
    tag$=tag$+we$+mo$
  CASE 50
    PRINT AT(18,16);pu|;
  CASE 51
    PRINT AT(20,16);pu|;
  CASE 52
    PRINT AT(22,16);pu|;
  CASE 53
    PRINT AT(24,16);pu|;
    wand(50,4)
    PRINT AT(41,4);we|;
    ja$=we$
  CASE 54
    PRINT AT(18,15);pu|;
  CASE 55
    PRINT AT(20,15);pu|;
  CASE 56
    PRINT AT(22,15);pu|;
  CASE 57
    PRINT AT(24,15);pu|;
    wand(54,4)
    ja$=we$+ja$
    SELECT VAL(ja$)
    CASE 90 TO 99
      jh$="19"
    CASE 0 TO 79
      jh$="20"
    ENDSELECT
    PRINT AT(38,4);jh$;we|;
    tag$=tag$+jh$+ja$
  CASE 58
    parity(36,57,58)
    PRINT AT(32,5);"____";pr$;"____";
  ENDSELECT
RETURN
PROCEDURE stell
  DO
    s$=RIGHT$(TIME$,2)
    PAUSE 2
    EXIT IF s$<>RIGHT$(TIME$,2)
  LOOP
  SETTIME zeit$,tag$
  IF zeit$<>TIME$ OR DATE$<>tag$
    fehler(4)
  ELSE
    PRINT AT(3,3);"Die Uhr wurde gestellt.";
    PAUSE 80
  ENDIF
  STICK 0
  EDIT
RETURN
PROCEDURE fehler(obj|)
  me1$="Fehler bei Minuten.     "
  me2$="Fehler bei Stunden.     "
  me3$="Fehler bei Datum.       "
  me4$="Fehler beim Stellen.    "
  me5$="Der Programmlauf "
  me6$="wird wiederholt !           "
  me7$=me5$+me6$
  SELECT obj|
  CASE 1
    PRINT AT(3,3);me1$;
  CASE 2
    PRINT AT(3,3);me2$;
  CASE 3
    PRINT AT(3,3);me3$;
  CASE 4
    PRINT AT(3,3);me4$;
  ENDSELECT
  PRINT AT(3,4);me7$;
  PAUSE 150
  RUN
RETURN
PROCEDURE wand(a|,z|)
  we|=0
  FOR k|=0 TO z|-1
    we|=we|+(2^(k|))*bi|(k|+a|)
  NEXT k|
  we$=STR$(we|)
RETURN
