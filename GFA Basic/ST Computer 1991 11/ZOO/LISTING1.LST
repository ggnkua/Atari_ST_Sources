DIM a(3)
byte=256
word=65535
c|=5
CLR zoo_start
CLR zoo_next
'
OPEN "i",#1,"A:demo.zoo"
length=LOF(#1)
'
' Lesen des Header Textes
'
header$=INPUT$(20,#1)
PRINT header$
'
' Zeiger auf Start der Archivdaten
'
SEEK #1,24
zoo_start$=INPUT$(4,#1)
@convert(zoo_start$,zoo_start)
SEEK #1,zoo_start
@zoo_list
'
~INP(2)
END
'
PROCEDURE zoo_list
  DO
    '
    ' Zeiger auf n„chsten Directory Eintrag
    '
    SEEK #1,zoo_start+6
    in$=INPUT$(4,#1)
    @convert(in$,zoo_next)
    ' -------------------------
    '
    IF zoo_next>0
      '
      ' ===> DATUM
      RELSEEK #1,4
      in$=INPUT$(2,#1)
      ' @bit
      '
      ' ===> ZEIT
      RELSEEK #1,0
      in$=INPUT$(2,#1)
      ' @bit
      '
      ' ===>CRC PRšFSUMME
      RELSEEK #1,0
      in$=INPUT$(2,#1)
      @convert(in$,crc)
      '
      ' ===> ORIGINALGR™žE
      RELSEEK #1,0
      org_size$=INPUT$(4,#1)
      @convert(org_size$,org_size)
      '
      ' ===> GEPACKTE GR™žE
      RELSEEK #1,0
      now_size$=INPUT$(4,#1)
      @convert(now_size$,pac_size)
      '
      ' ===> FILE GEL™SCHT
      RELSEEK #1,2
      delet=INP(#1)
      '
      ' ===> ZEIGER AUF KOMMENTAR
      RELSEEK #1,1
      com_pointer$=INPUT$(4,#1)
      @convert(com_pointer$,com_pointer)
      '
      ' ===> LŽNGE DES KOMMENTARS
      RELSEEK #1,0
      com_len$=INPUT$(2,#1)
      @convert(com_len$,com_len)
      '
      ' ===> FILENAME
      RELSEEK #1,0
      fname$=INPUT$(13,#1)
      '
      ' ===> PFAD DES FILES
      '      SEEK #1,zoo_start+57
      RELSEEK #1,6
      pfad_length=INP(#1)
      RELSEEK #1,0
      pfad$=INPUT$(pfad_length,#1)
      '
      ' Das auslesen des Kommentars sollte hier
      ' geschehen, da ein SEEK Befehl hier besser
      ' ist als ein RELSEEK.
      '
      @ausgabe
      '
      SWAP zoo_start,zoo_next
    ENDIF
  LOOP UNTIL zoo_start=0 OR zoo_next=0
  CLOSE #1
RETURN
PROCEDURE ausgabe
  PRINT AT(1,3);" CRC | Orig Size | Pack Size";
  PRINT " | Del | Filename    | Pfad";
  PRINT AT(1,4);STRING$(80,"-");
  PRINT AT(1,c|);HEX$(crc,4);
  PRINT AT(9,c|);org_size;
  PRINT AT(23,c|);pac_size;
  IF delet<>0
    PRINT AT(32,c|);"Yes";
  ELSE
    PRINT AT(32,c|);"No";
  ENDIF
  PRINT AT(38,c|);fname$;
  PRINT AT(52,c|);pfad$;
  INC c|
RETURN
PROCEDURE convert(VAR convert$,convert)
  ' -------------------------------------------
  ' variable:
  ' a()   = Array frs richtige Format
  ' byte    = Faktor 256 fr Word Format
  ' Word    = Faktor 65535 fr Longword Format
  '
  ' Rckgabe
  ' convert = wert im gewandelten Format
  ' -------------------------------------------
  lg=LEN(convert$)
  CLR i|
  REPEAT
    ' ==> String in Bytes aufteilen
    a(i|)=ASC(MID$(convert$,i|+1))
    INC i|
  UNTIL i|=lg
  '
  ' ==> errechnen des Wertes
  h_byte=a(0)+byte*a(1)
  l_byte=a(2)+byte*a(3)
  convert=h_byte+word*l_byte+l_byte
RETURN

