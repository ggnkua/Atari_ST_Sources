' ***********************************************
' * zwei Routinen zum Laden und Sichern         *
' * von Stringfeld-Bl”cken als ASCII-Datei      *
' * Programmautor: Andreas Hollmann             *
' * Sprache:       GFA-Basic                    *
' * Copyright:     MAXON Computer GmbH          *
' ***********************************************
RESERVE 128000   !sonst kein GEMDOS-Speicher frei
DIM txt$(1000)
'
FOR i&=0 TO 999                      !Feld fllen
  txt$(i&)=STRING$(80,"A")
NEXT i&
'
' Aufrufe zum Laden/Sichern:
' block_load("a:\test.txt",0,1000,txt$())
block_save("a:\test.txt",0,1000,txt$())
'
ERASE txt$()
RESERVE
END
'
PROCEDURE block_save(path$,ln1%,lns%,VAR txt$())
  ' Stringfeld-Block als ASCII-Datei sichern
  ' ---------------------------------------------
  ' Parameter:
  '  path$   = Pfad und Dateiname der ASCII-Datei
  '  ln1%  = erste Zeile des Blocks
  '  lns%  = L„nge des Blocks
  '  txt$() = Stringfeld
  ' ---------------------------------------------
  LOCAL p_dsc%,ln%,buf_size%,p_buf%,len%
  ' Zeilenanzahl > max. Feldindex?
  IF ln1%+lns%>DIM?(txt$())
    lns%=DIM?(txt$())-ln1%
  ENDIF
  ' Adresse der String-Descriptoren berechnen:
  p_dsc%=LONG{*txt$()}+4
  ' erforderliche L„nge des Puffers berechnen:
  FOR ln%=ln1% TO ln1%+lns%-1
    ADD buf_size%,LEN(txt$(ln%))
  NEXT ln%
  ADD buf_size%,(lns%)*2   !+2 Byte fr CR LF
  '
  ' Feld in GEMDOS-Block kopieren und sichern:
  IF MALLOC(-1)>=buf_size%   !Speicher frei
    p_buf%=MALLOC(buf_size%) !her damit
    p_bufstart%=p_buf%      !Adresse sichern
    FOR ln%=ln1% TO ln1%+lns%-1
      len%=WORD{ADD(p_dsc%,4)} !Stringl„nge lesen
      BMOVE {p_dsc%},p_buf%,len% !String kopieren
      ADD p_dsc%,6             !6 Byte weiter
      ADD p_buf%,len%          !+ Stringl„nge
      BYTE{p_buf%}=13          !ASCII 10 = LF
      INC p_buf%
      BYTE{p_buf%}=10          !ASCII 13 = CR
      INC p_buf%
    NEXT ln%          !Alle Zeilen sind kopiert
    ' Block sichern und Speicher freigeben:
    BSAVE path$,p_bufstart%,buf_size%
    ~MFREE(p_bufstart%)
  ELSE                      !nicht genug Speicher
    OPEN "o",#1,path$       !normal sichern
    FOR ln%=ln1% TO ln1%+lns%-1
      PRINT #1,txt$(ln%)
    NEXT ln%
    CLOSE #1
  ENDIF
RETURN
'
PROCEDURE block_load(path$,ln1%,lns%,VAR txt$())
  ' ASCII-Datei als Stringfeld-Block laden
  ' ---------------------------------------------
  ' Parameter:
  '  path$   = Pfad und Dateiname der ASCII-Datei
  '  ln1%  = erste Zeile des Blocks
  '  lns%  = L„nge des Blocks
  '  txt$() = Stringfeld
  ' ---------------------------------------------
  '
  LOCAL buf_size%,p_buf%,p_bufstart%,ln%,p_str%
  '
  OPEN "i",#1,path$
  buf_size%=LOF(#1)          !Dateil„nge abfragen
  CLOSE #1
  '
  IF MALLOC(-1)>=buf_size%   !genug Speicher frei
    p_buf%=MALLOC(buf_size%) !her damit!
    p_bufstart%=p_buf%
    '
    BLOAD path$,p_buf%       !Datei laden
    '
    FOR ln%=ln1% TO ln1%+lns%-1
      p_str%=p_buf%          !Stringanfang merken
      ' Stringende suchen (compiliert ca. 22mal
      ' so schnell wie im Interpreter):
      DO UNTIL BYTE{p_buf%}=13        !Stringende
        INC p_buf%
      LOOP
      BYTE{p_buf%}=0      !Ende fr CHAR{} setzen
      txt$(ln%)=CHAR{p_str%}  !String kopieren
      ADD p_buf%,2      !ASCII 13,10 berspringen
      ' falls Ende des GEMDOS-Blocks erreicht:
      EXIT IF p_buf%>=p_bufstart%+buf_size%
    NEXT ln%
    ~MFREE(p_bufstart%)   !GEMDOS-Block freigeben
  ELSE        !nicht genug Speicher, normal laden
    OPEN "i",#1,path$
    DO UNTIL EOF(#1)
      LINE INPUT #1,txt$(ln%)
      INC ln%
    LOOP UNTIL ln%>ln1%+lns%
    CLOSE #1
  ENDIF
  '
RETURN

