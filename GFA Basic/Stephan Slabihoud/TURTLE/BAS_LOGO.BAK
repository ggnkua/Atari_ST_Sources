'
' ******************************************************************************
' *                                                                            *
' *   Turtle-Grafik fr Gfa-Basic                (fr Gfa-Systemtechnik)       *
' *                                                                            *
' *  ½  Stephan Slabihoud, Gustav-Ohm-Str.72, 4250 Bottrop, Tel.: 02041/23488  *
' *                                                                            *
' ******************************************************************************
'
Procedure Init_logo
  Xmove=180
  If Xbios(4)>0 Then
    Faktorx%=2
  Else
    Faktorx%=1
  Endif
  If Xbios(4)>1 Then
    Faktory%=2
  Else
    Faktory%=1
  Endif
  If Xbios(4)=1 Then
    Faktorz%=2
  Else
    Faktorz%=1
  Endif
  Xplot=Fn Xback(0)
  Yplot=Fn Yback(0)
  Penflag%=True
Return
'
Procedure Home
  Xplot=Fn Xback(0)
  Yplot=Fn Yback(0)
Return
'
Procedure Lt(Wert)
  Xmove=Xmove+Wert
Return
'
Procedure Rt(Wert)
  Xmove=Xmove-Wert
Return
'
Procedure Fd(Wert)
  Local X1%,Y1%
  X1=Wert*Sin(Xmove/180*Pi)*Faktorz%
  Y1=Wert*Cos(Xmove/180*Pi)
  If Penflag%=True Then
    Line Xplot,Yplot,Xplot+X1,Yplot+Y1
  Endif
  Xplot=Xplot+X1
  Yplot=Yplot+Y1
Return
'
Procedure Bk(Wert)
  @Fd(-Wert)
Return
'
Procedure Clearscreen
  Cls
  @Home
Return
'
Procedure Setheading(Wert)
  Xmove=180-Wert
Return
'
Procedure Pd
  Penflag%=True
Return
'
Procedure Pu
  Penflag%=False
Return
'
Procedure Setpos(Wertx,Werty)
  Xplot=Fn Xback(Wertx*Faktorz%)
  Yplot=Fn Yback(-Werty)
Return
'
Procedure Setx(Wert)
  Xplot=Fn Xback(Wert*Faktorz%)
Return
Procedure Sety(Wert)
  Yplot=Fn Yback(-Wert)
Return
'
Procedure Plot(Wertx,Werty)
  Xplot=Fn Xback(Wertx*Faktorz%)
  Yplot=Fn Yback(-Werty)
  If Penflag%=True Then
    Plot Xplot,Yplot
  Endif
Return
'
Procedure Drawto(Wertx,Werty)
  If Penflag%=True Then
    Line Xplot,Yplot,Fn Xback(Wertx*Faktorz%),Fn Yback(-Werty)
  Endif
  @Setpos(Wertx*Faktorz%,Werty)
Return
'
Procedure Draw(Wertx,Werty)
  If Penflag%=True Then
    Line Xplot,Yplot,Xplot+Wertx*Faktorz%,Yplot-Werty
  Endif
  Xplot=Xplot+Wertx*Faktorz%
  Yplot=Yplot-Werty
Return
'
Deffn Xback(Xwert)=160*Faktorx%+Xwert
Deffn Yback(Ywert)=100*Faktory%+Ywert
