\*************************************
\** Program to record and playback  **
\**  a midi sequence.               ** 
\**  by K.Adams                     **
\*************************************

PROCinit

GRAB 0,0,SCREENWIDTH,SCREENHEIGHT

msg$="[1][Select Internal for single|midi keyboard,or thru|to play "
msg$=msg$+"external midi device]"
msg$=msg$+"[Internal|Thru]"

thru%= ALERT(msg$,1)

REPEAT
PROCselect
        IF button%=1 THEN PROCrecord
        IF button%=2 THEN PROCplayback
UNTIL button%=3

PUT 0,0,3
END


DEF PROCrecord

CLS
PRINT TAB(1,1)"Ready to start recording"
PRINT TAB(1,2)"Press return to end."
\Clear keyboard buffer
	REPEAT
	key$=INKEY$
	UNTIL key$=""

\Clear midi buffer
	WHILE INPSTAT(3) <> 0
		midi%=INP(3)
	WEND

	n%=0
 
	REPEAT
	clock%=0:midi%=0
	PROCnote
	note%(n%,0)=midi%
\If thru is selected then send midi event to midi out
	IF thru%=2 THEN OUT 3,midi%
	note%(n%,1)=clock%
	n%=n%+1
	UNTIL key%=13 OR n%=1000

\Send all notes off when buffer is full or return is pressed
\Find midi channel from the first status byte
	channel%=note%(0,0) AND $F
	OUT 3,$B0+channel%,123,0

ENDPROC

DEF PROCnote
chan_press%=0
        REPEAT
        act%=0
        IF chan_press% > 0 THEN chan_press%=chan_press% - 1
                REPEAT
                clock%=clock%+1
                key%=INKEY
                UNTIL INPSTAT(3) OR key%=13
        midi%=INP(3) AND $FF
\Dont start recording until first note on message
        IF n%=0 AND midi%<>144 THEN clock%=0
\Check for active sensing
        IF midi%=$FE THEN act%=1
\Check for channel pressure 
        IF midi% >=$D0 AND midi% <=$DF THEN chan_press%=2
        UNTIL (clock% >0 AND act%=0 AND chan_press%=0) OR key%=13
ENDPROC


DEF PROCselect
        LOCAL msg$
        msg$="[2][  Which option next ?  ]"
        msg$=msg$+"[RECORD|PLAYBACK|QUIT]"
        button%= ALERT(msg$,1)
ENDPROC

DEF PROCplayback
        LOCAL msg$
        msg$="[2][Do you wish to alter |"
        msg$=msg$+"the playback tempo.]"
        msg$=msg$+"[ YES | NO ]"
tempo_change%=ALERT(msg$,2)

        IF tempo_change%=1 THEN
			 REPEAT
                CLS
                PRINT " Enter tempo from 1 - 10"
                PRINT "               slow - fast.   ";
                INPUT tempo%
                UNTIL tempo% >0 AND tempo% < 11
                tempo%=11-tempo%
        ELSE
                tempo%=5
        ENDIF
        
        FOR a%=0 TO n%
        IF note%(a%,1)=0 THEN
                OUT 3,note%(a%,0)
        ELSE
                FOR t%=0 TO note%(a%,1)*tempo%*const
                NEXT t%
                OUT 3,note%(a%,0)
        ENDIF
        NEXT a%
        
        OUT 3,$B0+channel%,123,0
ENDPROC

DEF PROCinit
        DIM note%(5000,1)
        TXTRECT 0,0,SCREENWIDTH,SCREENHEIGHT
        TXTSIZE 13
        GRAFRECT 0,0,SCREENWIDTH,SCREENHEIGHT
\Set constant so that playback speed is the same as recorded
        const=2.5
ENDPROC


