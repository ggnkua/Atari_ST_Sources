'
' Deux fenˆtre … la fois avec fonctions
' GFA-BASIC       WIND3.GFA
'
DEFINT "a-z"    ! Toutes les variables sont des entiers -> nous ‚conomise
' de nombreux %s
'
DIM tampon&(7)  ! Place pour les 8 mots d'un message (evnt_mesag)
DIM whandle(1),wx(1),wy(1),ww(1),wh(1),opened(1)
'
' Initialisation du tableau:
'
wx(0)=20    ! coordonn‚es fenˆtre 0
wy(0)=20
ww(0)=200
wh(0)=120
'
wx(1)=50    ! coordonn‚es fenˆtre 1
wy(1)=50
ww(1)=200
wh(1)=120
'
VOID APPL_INIT()
'
' D‚tecter la taille du bureau (zone de travail)
'
VOID WIND_GET(0,4,x_desk,y_desk,w_desk,h_desk)
'
' Annoncer la fenˆtre avec taille maximale et tous les organes de commande (63)
' sauf les flŠches et les ascenseurs
'
IF FN create_windows
  VOID FORM_ALERT(1,"[3][Plus d'identificateur disponible!][OK]")
ELSE
  '
  ' Param‚trer les lignes de titre et d'information:
  ' Les chaŒnes sont copi‚es dans une zone m‚moire s–re
  ' car leur adresse ne doit pas changer
  '
  t1$="Fenˆtre 1"+CHR$(0)
  i1$="Veuillez noter:"+CHR$(0)
  t2$="Fenˆtre 2"+CHR$(0)
  i2$="Veuillez ‚galement noter:"+CHR$(0)
  '
  s=MALLOC(100)
  '
  FOR i=1 TO LEN(t1$)                ! Ligne de titre 1
    POKE s-1+i,ASC(MID$(t1$,i,1))
  NEXT i
  '
  FOR i=1 TO LEN(i1$)                ! Ligne d'information 1
    POKE s+24+i,ASC(MID$(i1$,i,1))
  NEXT i
  '
  FOR i=1 TO LEN(t2$)                ! Ligne de titre 2
    POKE s+49+i,ASC(MID$(t2$,i,1))
  NEXT i
  '
  FOR i=1 TO LEN(i2$)                ! Ligne d'information 2
    POKE s+74+i,ASC(MID$(i2$,i,1))
  NEXT i
  '
  ' Nous avons besoin des mots Low et High (par DIV 2^16 et MOD 2^16)
  ' Ligne de titre: Sous-fonction 2  /  Ligne d'info: Sous-fonction 3
  '
  VOID WIND_SET(whandle(0),2,s DIV 65536,s MOD 65536,0,0)
  VOID WIND_SET(whandle(0),3,(s+25) DIV 65536,(s+25) MOD 65536,0,0)
  VOID WIND_SET(whandle(1),2,(s+50) DIV 65536,(s+50) MOD 65536,0,0)
  VOID WIND_SET(whandle(1),3,(s+75) DIV 65536,(s+75) MOD 65536,0,0)
  '
  ' Ouvrir fenˆtre:
  '
  FOR i=0 TO 1
    VOID WIND_OPEN(whandle(i),wx(i),wy(i),ww(i),wh(i))
    opened(i)=1
    output_text(i)
  NEXT i
  '
  REPEAT
    '
    ' Attendre un ‚vŠnement:
    '
    VOID EVNT_MESAG(LPEEK(*tampon&())+4) ! Adresse du premier organe de commande
    '
    ' Passer l'index de la fenˆtre d'o— vient le message:
    ' (L'identificateur de fenˆtre est dans le tampon&(3))
    '
    w=0
    WHILE NOT (tampon&(3)=whandle(w) OR w=2)
      INC w
    WEND
    '
    IF window<2           ! tait-ce bien notre fenˆtre (1 ou 2)?
      IF tampon&(0)=20    ! Message Redraw
        GOSUB redraw(w)
      ENDIF
      '
      IF tampon&(0)=21        ! Window-Topped
        VOID WIND_SET(whandle(w),10,0,0,0,0)
      ENDIF
      '
      IF tampon&(0)=23        ! La boŒte plein ‚cran a ‚t‚ cliqu‚e
        wx(w)=x_desk+2        ! Amener la fenˆtre … sa taille maximale
        wy(w)=y_desk+2
        ww(w)=w_desk-6
        wh(w)=h_desk-6
        VOID WIND_SET(whandle(w),5,wx(w),wy(w),ww(w),wh(w))
      ENDIF
      '
      IF tampon&(0)=27        ! BoŒte de taille
        ww(w)=tampon&(6)      ! Lire la taille dans le message
        wh(w)=tampon&(7)
        VOID WIND_SET(whandle(w),5,wx(w),wy(w),ww(w),wh(w))
      ENDIF
      '
      IF tampon&(0)=28        ! BoŒte de d‚placement
        wx(w)=tampon&(4)      ! x et y sont inchang‚s
        wy(w)=tampon&(5)
        VOID WIND_SET(whandle(w),5,wx(w),wy(w),ww(w),wh(w))
      ENDIF
      '
      IF tampon&(0)=22        ! BoŒte de fermeture
        VOID WIND_CLOSE(whandle(w))
        VOID WIND_DELETE(whandle(w))
        opened(w)=0
      ENDIF
      '
    ENDIF
  UNTIL NOT ((opened(0)=1) OR (opened(1)=1)) ! tant que fenˆtre ouverte ...
  '
  VOID MFREE(s)
ENDIF
'
VOID APPL_EXIT()
'
END
'
'
PROCEDURE output_text(w)    !    Afficher le contenu de la fenˆtre
  '                              dŠs le lancement du programme
  ' Passer la taille de la zone de travail:
  '
  VOID WIND_CALC(1,63,wx(w),wy(w),ww(w),wh(w),x,y,w,h)
  x2=x+w-1
  y2=y+h-1
  CLIP x,y TO x2,y2
  draw_text(x,y,x2,y2,x,y)
  CLIP OFF
RETURN
'
'
PROCEDURE draw_text(x1,y1,x2,y2,x,y)
  '
  ' Parameter:
  '   x1,y1,x2,y1 est le rectangle qui doit ˆtre redessin‚,
  '   donc effac‚. x,y est l'angle sup‚rieur gauche du texte.
  '   x,y ne sont pas forc‚ment identiques … x1,y1 !!!
  '
  HIDEM    ! D‚sactiver la souris, qui ne sert qu'… nous gˆner
  '
  ' Effacer la zone de travail:
  '
  BOUNDARY 0
  DEFFILL 0
  PBOX x1,y1,x2,y2
  BOUNDARY 1
  '
  ' Afficher le texte:
  '
  TEXT x+8,y+14,"Bonjour! Ceci est une d‚mo de fenˆtre!"
  TEXT x+8,y+30,"Vous pouvez d‚placer la fenˆtre et modifier sa taille."
  TEXT x+8,y+46,"Quitter: un clic sur la boŒte de fermeture!"
  '
  SHOWM
RETURN
'
'
FUNCTION create_windows
  FOR i=0 TO 1
    whandle(i)=WIND_CREATE(63,x_desk,y_desk,w_desk,h_desk)
  NEXT i
  RETURN (whandle(0)<0) AND (whandle(1)<0)
ENDFUNC
'
'
PROCEDURE redraw(w)
  '
  ' Recalculer la zone de travail:
  ' (nous en avons besoin pour le coin sup‚rieur gauche du texte)
  '
  VOID WIND_CALC(1,63,wx(w),wy(w),ww(w),wh(w),ax,ay,aw,ah)
  '
  VOID WIND_UPDATE(1)   ! Bloquer toutes les fonctions de la souris pour ˆtre
  '             tranquille / Bloquer la liste des rectangles
  '
  ' Traitement de la liste des rectangles
  '
  VOID WIND_GET(whandle(w),11,rx,ry,rw,rh)  ! 11->premier rectangle
  '
  WHILE rw>0    ! tant que le rectangle a une largeur
    IF RC_INTERSECT(tampon&(4),tampon&(5),tampon&(6),tampon&(7),rx,ry,rw,rh)
      rx2=rx+rw-1
      ry2=ry+rh-1
      CLIP rx,ry TO rx2,ry2
      GOSUB draw_text(rx,ry,rx2,ry2,ax,ay)
    ENDIF
    '
    VOID WIND_GET(whandle(w),12,rx,ry,rw,rh) ! 12->rectangle suivant
  WEND
  '
  CLIP OFF
  VOID WIND_UPDATE(0)   ! R‚afficher le pointeur de la souris
RETURN
'
