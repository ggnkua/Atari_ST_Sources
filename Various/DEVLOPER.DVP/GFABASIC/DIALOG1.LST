'
' Charger, afficher et traiter la premiŠre boŒte de dialogue
' GFA-BASIC                      DIALOG1.GFA
'
DEFINT "a-z"   ! Toutes les variables sont des entiers de 4 octets
'
dialog=0       ! Constantes dans DIALOG1.H2
bouton3=4
bouton1=5
bouton2=6
'
VOID APPL_INIT()
'
IF RSRC_LOAD("DIALOG1.RSC")=0
  VOID FORM_ALERT(1,"[3][Pas de fichier RSC!][Fin]")
ELSE
  '
  ' Chercher l'adresse de l'arbre (0 =) "dialog":
  '
  VOID RSRC_GADDR(0,dialog,arbre_adr)
  '
  ' Faire afficher la boŒte de dialogue:
  '
  GOSUB show_dialog(arbre_adr)
  '
  ' Faire travailler la boŒte de dialogue: attendre appui bouton
  '
  bouton=FORM_DO(arbre_adr,0)      ! 0 = pas de champ Edit
  '
  ' Inhiber l'‚tat "selected" du bouton s‚lectionn‚:
  '
  GOSUB deselect(arbre_adr,bouton)
  '
  ' Faire disparaŒtre la boŒte de dialogue:
  '
  GOSUB hide_dialog(arbre_adr)
  '
  ' Signaler … l'utilisateur quel bouton a ‚t‚ appuy‚:
  '
  SELECT bouton
  CASE bouton1
    nr=1
  CASE bouton2
    nr=2
  CASE bouton3
    nr=3
  ENDSELECT
  '
  a$="[1][Vous avez appuy‚ le bouton no "+STR$(nr)+"!][Exact!]"
  VOID FORM_ALERT(1,a$)
  '
  ' Effacer le fichier ressource de la m‚moire
  '
  VOID RSRC_FREE()
  '
ENDIF
'
VOID APPL_EXIT()
'
END
'
'
PROCEDURE select(arbre,index)
  ' Mettre le bit 0 … 1:
  OB_STATE(arbre,index)=OB_STATE(arbre,index) OR 1
RETURN
'
'
PROCEDURE deselect(arbre,index)
  ' Effacer le bit 0:
  OB_STATE(arbre,index)=OB_STATE(arbre,index) AND -2
RETURN
'
'
PROCEDURE show_dialog(arbre)
  LOCAL x,y,w,h
  '
  ' Centrer le formulaire … l'‚cran
  ' (n'est pas encore dessin‚)
  '
  VOID FORM_CENTER(arbre,x,y,w,h)
  '
  ' Stocker les cadres de fenˆtres, etc:
  '
  VOID FORM_DIAL(0,x,y,w,h,x,y,w,h)
  '
  ' Dessiner un rectangle "zoom":
  '
  VOID FORM_DIAL(1,x,y,1,1,x,y,w,h)
  '
  ' Dessiner le formulaire:
  ' Commencer par l'objet no 0 (objet racine, boŒte/cadre ext‚rieur)
  ' Profondeur: 12 niveau maxi (valeur arbitraire)
  '
  VOID OBJC_DRAW(arbre,0,12,x,y,w,h)
  '
RETURN
'
'
PROCEDURE hide_dialog(arbre)
  LOCAL x,y,w,h
  '
  ' Redemander les coordonn‚es:
  '
  VOID FORM_CENTER(arbre,x,y,w,h)
  '
  ' Dessiner un rectangle diminuant:
  '
  VOID FORM_DIAL(2,x,y,1,1,x,y,w,h)
  '
  ' Redessiner les cadres des fenˆtres et envoyer des messages Redraw
  ' … toutes les fenˆtres d‚truites:
  '
  VOID FORM_DIAL(3,x,y,w,h,x,y,w,h)
  '
RETURN
'
