'
' Fenˆtre avec fonctions (agrandir, ...)
' GFA-BASIC        WIND2.GFA
'
DEFINT "a-z"    ! Toutes les variables sont des entiers -> nous ‚conomise
' beaucoup de %s
'
DIM tampon&(7)  ! Place pour les 8 mots d'un message (evnt_mesag)
'
VOID APPL_INIT()
'
' D‚tecter la r‚solution de l'‚cran
'
rez=XBIOS(4)     ! Getrez donne 0=basse, 1=moyenne, 2=haute r‚solution
x_max=640+320*(rez=0)
y_max=200-200*(rez=2)
'
' Param‚trer la taille initiale de la fenˆtre
'
wx=20    ! Coordonn‚es x
wy=20    ! y
ww=280   ! w = width  (largeur)
wh=150   ! h = height (hauteur)
'
' Annoncer la fenˆtre avec taille maximale et tous les organes de commande (63)
' sauf les flŠches et les ascenseurs
'
whandle=WIND_CREATE(63,0,0,x_max,y_max)
'
IF whandle<0     ! Si c'est le cas, une erreur s'est produite
  VOID FORM_ALERT(1,"[3][Plus d'identificateur disponible!][OK]")
ELSE
  '
  ' Param‚trer les lignes de titre et d'information:
  ' Les chaŒnes sont copi‚es dans une zone m‚moire s–re
  ' car leur adresse ne doit pas changer
  '
  title$="WIND2.PRG"+CHR$(0)
  info$="Veuillez noter:"+CHR$(0)
  '
  s=MALLOC(40)
  '
  FOR i=1 TO LEN(title$)              ! Ligne titre
    POKE s-1+i,ASC(MID$(title$,i,1))
  NEXT i
  '
  FOR i=1 TO LEN(info$)               ! Ligne information
    POKE s+19+i,ASC(MID$(info$,i,1))
  NEXT i
  '
  ' Nous avons besoin des mots Low et High (par DIV 2^16 et MOD 2^16)
  ' Ligne de titre: Sous-fonction 2  /  Ligne d'info: Sous-fonction 3
  '
  VOID WIND_SET(whandle,2,s DIV 65536,s MOD 65536,0,0)
  VOID WIND_SET(whandle,3,(s+20) DIV 65536,(s+20) MOD 65536,0,0)
  '
  VOID WIND_OPEN(whandle,wx,wy,ww,wh)
  '
  GOSUB output_text   ! Remplit la fenˆtre de son contenu
  '
  REPEAT
    '
    ' Attendre un ‚vŠnement:
    '
    VOID EVNT_MESAG(LPEEK(*tampon&())+4) ! Adresse du premier organe de commande
    '
    IF tampon&(0)=20     ! Message Redraw
      GOSUB output_text
    ENDIF
    '
    IF tampon&(0)=23     ! La boŒte plein ‚cran a ‚t‚ cliqu‚e
      wx=2               ! Amener la fenˆtre … sa taille maximale
      wy=20              ! (seule la barre des menus et 2 points sur
      ww=x_max-5         ! les cadres seront stock‚s)
      wh=y_max-25
      VOID WIND_SET(whandle,5,wx,wy,ww,wh)  ! R‚gler la taille
    ENDIF
    '
    IF tampon&(0)=27     ! BoŒte de taille
      ww=tampon&(6)      ! Lire la taille dans le message
      wh=tampon&(7)
      VOID WIND_SET(whandle,5,wx,wy,ww,wh)
    ENDIF
    '
    IF tampon&(0)=28     ! BoŒte de d‚placement
      wx=tampon&(4)      ! x et y restent inchang‚s
      wy=tampon&(5)
      VOID WIND_SET(whandle,5,wx,wy,ww,wh)
    ENDIF
    '
  UNTIL tampon&(0)=22   !  ... jusqu'… un clic sur la boŒte de fermeture
  '
  VOID WIND_CLOSE(whandle)
  VOID WIND_DELETE(whandle)
  '
  VOID MFREE(s)
ENDIF
'
VOID APPL_EXIT()
'
END
'
'
PROCEDURE output_text
  HIDEM    ! D‚sactiver la souris, qui ne sert qu'… nous gˆner
  '
  ' Copier la taille de la zone de travail:
  '
  VOID WIND_CALC(1,63,wx,wy,ww,wh,x,y,w,h)
  '
  ' Convertir en x1/y1/x2/y2:
  '
  x1=x
  y1=y
  x2=x+w-1
  y2=y+h-1
  '
  ' Effacer la zone de travail:
  '
  BOUNDARY 0
  DEFFILL 0
  PBOX x1,y1,x2,y2
  BOUNDARY 1
  '
  ' Activer le clipping (pour l'affichage du texte):
  '
  CLIP x1,y1 TO x2,y2
  '
  ' Afficher le texte:
  '
  TEXT x1+8,y1+14,"Bonjour! Ceci est une d‚mo de fenˆtre!"
  TEXT x1+8,y1+30,"Vous pouvez d‚placer la fenˆtre et modifier sa taille."
  TEXT x1+8,y1+46,"Quitter: un clic sur la boŒte de fermeture!"
  '
  ' D‚sactiver le clipping:
  '
  CLIP OFF
  '
  SHOWM
RETURN
