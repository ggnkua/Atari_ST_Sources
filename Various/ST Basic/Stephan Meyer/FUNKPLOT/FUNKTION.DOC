0660002010175
1
2page #
9[...........................................................]
€10    rem ###################### FUNKTION.BAS ########################
20    on ERROR goto 1120:out 4,18:dim s(10):e=2.7183:Pi=3.1416
30    color 1,1,1,1,1:fullw 2:clearw 2:gosub INFO:gosub INIT:goto 80
40    INIT:gotoxy 1,0:print"Funktions-Graph"chr$(191)
      " erstellt auf 260ST ";
50    print chr$(14);chr$(15);"  ";
60    print chr$(189);" by Martin Hachenberg April 1986"
70    linef 0,18,616,18:return
80    if peek(systab)<>1 then clearw 2:out 4,8:end
90    gotoxy 13,5:print" Funktion eingegeben ?"
100   gotoxy 12,7:print"Ansonsten F(x) in der Form"
110   gotoxy 12,8:print"F(x)=[F(x)]  zB. F(x)=2*x+1"
120   gotoxy 12,9:print" als y=2*x+1 in Zeile 1100"
130   gotoxy 16,10:input"eingeben.  ",a$
140   if a$="N" or a$="n" then out 4,8:end
150   gotoxy 10,5:print spc(45):gotoxy 12,7:print spc(30)
160   gotoxy 12,8:print spc(30)
170   gotoxy 12,9:print spc(30):gotoxy 16,10:print spc(20)
180   linef 500,100,500,200 ' Eingabe Grenzen + Skalierung -----------
190   linef 450,150,550,150
200   linef 550,150,545,155:linef 550,150,545,145
210   linef 500,100,505,105:linef 500,100,495,105
220   gotoxy 30,6:print"Y":gotoxy 31,9:print" X"
230   gotoxy 8,3:print"Eingabe der Grenzen :"
240   gotoxy 9,5:input"X-max : ",xo
250   gotoxy 9,6:input"X-min : ",xu
260   if xo<=xu then gotoxy 9,5:print spc(20):gotoxy 9,6:
print spc(20):goto 240
270   ax=abs(xo-xu)
280   gotoxy 12,9:print" * -> Gleiche Grenzen"
290   gotoxy 9,8:input"Y-max : ",yo$
300   gotoxy 12,9:print spc(30)
310   if yo$="*" then yo=xo:yu=xu:goto 360
320   yo=val(yo$)
330   gotoxy 9,9:input"Y-min : ",yu
340   if yo<=yu then gotoxy 9,8:print spc(20):gotoxy 9,9:
print spc(20):goto 280
350   goto 380
360   gotoxy 9,8:print"Y-max :";yo;"       "
370   gotoxy 9,9:print"Y-min :";yu;"           "
380   yy=(yo-yu):gotoxy 8,11:print"Schrittweite"
390   xx=(xo-xu):gotoxy 9,12:print"1 - Standart"
400   gotoxy 9,13:print"2 - Mastab 1:1"
410   gotoxy 9,14:print"3 - Mastab Variabel"
420   gotoxy 9,15:input"Mastab Nr.  ",va
430   if va<1 or va>3 then gotoxy 9,15:print spc(20):goto 420
440   on va gosub STANDART,MAST11,MVARIABEL ' Grenzwert + Skalierung
450   goto 590 ' Sprung zum Plott-Programm --------------------------
460   STANDART:dx=xx/400:dy=yy/300:gx=400:gy=300:vy=(3*xx)/(4*yy):vx=1
470   if vy<1 then vy=1:vx=(4*yy)/(3*xx)
480   return
490   MAST11:vx=1:vy=1
500   if xx*3>yy*4 then dx=xx/400:dy=dx:gx=400:gy=abs(yy/dx):return
510   dx=yy/300:dy=dx:gx=abs(xx/dx):gy=300
520   return
530   MVARIABEL:gotoxy 9,12:print"Verhaeltnis X*A - Y*B":
gotoxy 9,15:print spc(20)
540   gotoxy 9,13:print spc(20):gotoxy 9,14:print spc(20)
550   gotoxy 9,14:input"X ",vx:gotoxy 11,14:input":  Y ",vy:vv=vx/vy
560   if xx*3*vv>yy*4 then goto 570 else goto 580
570   dx=xx/400:dy=dx/vv:gy=abs(yy/dx)/vv:gx=400:return
580   dx=(yy/300)/vv:dy=dx:gx=abs(xx/dx):gy=300:return
590   ' ##############################################################
600   clearw 2:gosub INIT ' Zeichnen X und Y Achse--------------------
610   if yo>0 and yu<0 then ny=gy-abs((0-yu)/yy)*gy:
linef 5,ny+20,gx+5,ny+20 ' xA
620   if yo<0 or yu>=0 then ny=gy
630   if xo>0 and xu<0 then nx=abs((0-xu)/xx)*gx:linef nx+5,20,nx+5,gy+20 'yA
640   if xo<0 or xu>0 then nx=0
650   linef 5,20,5,gy+20:linef gx+5,20,gx+5,gy+20
660   linef 5,20,gx+5,20:linef 5,gy+20,gx+5,gy+20
670   s(1)=.1:s(2)=.25:s(3)=.5:s(4)=1:s(5)=2:s(6)=5 ' Skalierung --------
680   s(7)=10:s(8)=20:s(9)=50:s(10)=100
690   for i=1 to 10:skx=xx/s(i):sky=yy/s(i)
700   skx=gx/skx:sky=gy/sky:if skx<=40 then kx=i
710   if sky<=40 then ky=i
720   next:if xu<=0 then v=0 else v=1
730   skx=int(xu/s(kx)):skx=s(kx)*(skx+v):x=skx
740   while x<=xo ' X - Achse skalieren ------------------------------
750   snx=abs((x-xu)/xx)*gx:linef 5+snx,ny+18,5+snx,ny+22:x=x+s(kx)
760   wend
770   if yu<=0 then v=0 else v=1
780   sky=int(yu/s(ky)):sky=s(ky)*(sky+v):y=sky
790   while y<=yo ' Y - Achse skalieren -------------------------------
800   sny=gy-abs((y-yu)/yy)*gy:linef nx+3,sny+20,nx+7,sny+20:y=y+s(ky)
810   wend
820   gotoxy 25,4:print"Grenzen des Graphen :" ' ----------------------
830   gotoxy 27,5:print"X-max : ";xo:
gotoxy 25,9:print"Skalierung der Achsen :"
840   gotoxy 27,6:print"X-min : ";xu:
gotoxy 27,10:print chr$(127);" X   : ";s(kx)
850   gotoxy 27,7:print"Y-max : ";yo:
gotoxy 27,11:print chr$(127);" Y   : ";s(ky)
860   gotoxy 27,8:print"Y-min : ";yu:linef 430,63,590,63
870   gotoxy 29,2:print"F(x)":linef 430,249,590,249
880   gotoxy 25,12:print"Mastab :":gotoxy 25,13:print"X * ";
890   print using "###.##";vx;:print" ~ Y * ";:print using "###.##";vy
900   px=5:x=xu:px=4:ay=ny:ax=5 ' Funktions Plott --------------
910   gosub FUNKTION:py=gy-abs((y-yu)/yy)*gy+20:px=px+1
920   x=x+dx:if x-dx>=xo then 1000
930   if y<yu then py=gy+20
940   if y>yo then py=20
950   if ay=20 and py>=gy+20 then ay=py
960   if ay=gy+20 and py<=20 then ay=py
970   linef ax,ay,px,py:ay=py:ax=px
980   goto 910
990   ' ######################################################
1000  gotoxy 25,15:print"1 - Neue Funktion"
1010  gotoxy 25,16:print"2 - Neue Grenzen"
1020  gotoxy 25,17:print"3 - Gitter"
1030  gotoxy 25,18:input"4 - Ende        ",ad
1040  if ad<1 or ad>4 then goto 1000
1050  if ad=1 then ad=4
1060  if ad=2 then clearw 2:gosub INIT:goto 180
1070  if ad=3 then gosub GITTER:goto 990
1080  if ad=4 then out 4,8:end
1090  FUNKTION:
1100  ' y=
1110  return
1120  ' SBR ERROR -----------------------------
1130  if err=11 then resume next
1140  if err=5 then resume 1170
1150  if err=108 then resume
1160  out 4,8:end
1170  clearw 2:color 1,1,1,2,2:linef 0,0,620,0
1180  linef 0,0,0,340:linef 620,0,620,340
1190  linef 0,340,620,340:fill 100,100
1200  color 1,1,1,3,2:pellipse 320,160,230,40
1210  gotoxy 11,9:input"  Funktionsaufruf nicht erlaubt  ",x$
1220  goto 30
1230  GITTER:if xu<=0 then v=0 else v=1 ' Gitter ---------------------
1240  skx=int(xu/s(kx)):skx=s(kx)*(skx+v):x=skx
1250  while x<=xo ' X - Achse rastern --------------------------------
1260  snx=abs((x-xu)/xx)*gx:linef 5+snx,20,5+snx,gy+20:x=x+s(kx)
1270  wend
1280  if yu<=0 then v=0 else v=1
1290  sky=int(yu/s(ky)):sky=s(ky)*(sky+v):y=sky
1300  while y<=yo ' Y - Achse rastern ---------------------------------
1310  sny=gy-abs((y-yu)/yy)*gy:linef 5,sny+20,gx+5,sny+20:y=y+s(ky)
1320  wend
1330  linef 5,ny+21,gx+5,ny+21:linef nx+4,20,nx+4,gy+20
1340  gotoxy 25,18:print spc(20):return
1350  INFO: ' ---- INFO--------------------------------
1360  fullw 2:clearw 2:gosub INIT
1370  linef 5,20,610,20:linef 5,320,610,320
1380  linef 5,20,5,320:linef 610,20,610,320
1390  linef 15,30,600,30:linef 15,310,600,310
1400  linef 15,30,15,310:linef 600,30,600,310
1410  color 1,1,1,2,2:fill 305,319
1420  gotoxy 2,2:print string$(31,chr$(3))" Info ";string$(31,chr$(4))
1430  linef 37,58,574,58
1440  gotoxy 3,4
1450  print"Dieses Programm ist ein einfacher Funktions-Drucker. Die Ausgabe"
1460  gotoxy 3,5
1470  print"erfolgt auf dem Bilschirm. Zun„chst  muž die Funktion eingegeben"
1480  gotoxy 3,6
1490  print"werden. Die Eingabe erfolgt manuel. Eine Abfrage unterbricht das"
1500  gotoxy 3,7
1510  print"Programm wenn Sie >N< eingeben, ansonsten nur >Return< eingeben."
1520  gotoxy 3,8
1530  print"Das Programm  hat noch Besonderheiten. So ist es nur in Hochauf-"
1540  gotoxy 3,9
1550  print"loesender Graphik lauffaehig. Zudem wird die Maus ausgeschaltet,"
1560  gotoxy 3,10
1570  print"kann aber falls Sie einen BREAK [Contrl-G] machen mit dem Befehl"
1580  gotoxy 3,11
1590  print">out 4,8< wieder eingeschaltet werden. Sollten Fehler auftreten,"
1600  gotoxy 3,12
1610  print"in Bezug auf den Plottvorgang, zuerst die Funktion  ueberprfen."
1620  gotoxy 3,13
1630  print"Die Konstanten  Pi(->3.1416)  und  e(->2.7183) sind  im Programm"
1640  gotoxy 3,14
1650  print"definiert und koennen  als Funktionsparameter  verwendet werden."
1660  gotoxy 24,16:input"Viel Erfolg     ",a$
1670  color 1,1,1,1,1:clearw 2:return
