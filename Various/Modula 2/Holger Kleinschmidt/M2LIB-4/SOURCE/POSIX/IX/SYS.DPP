DEFINITION MODULE sys;
__DEF_SWITCHES__
#ifdef HM2
#ifdef __LONG_WHOLE__
(*$!i+: Modul muss mit $i- uebersetzt werden! *)
(*$!w+: Modul muss mit $w- uebersetzt werden! *)
#else
(*$!i-: Modul muss mit $i+ uebersetzt werden! *)
(*$!w-: Modul muss mit $w+ uebersetzt werden! *)
#endif
#endif
(*****************************************************************************)
(* Systeminformationen und -einstellungen.                                   *)
(*                                                                           *)
(* Ein Fehler ist immer dann aufgetreten, wenn bei Funktionen ein negativer  *)
(* Wert (-1) bzw. NULL zurueckgegeben wird. Die genaue Fehlerursache kann bei*)
(* Bedarf ueber "e.errno" und die entsprechenden Konstanten ermittelt werden.*)
(* Die Funktionen veraendern "errno" nur dann, wenn ein Fehler aufgetreten   *)
(* ist, bei erfolgreicher Ausfuehrung wird "errno" nicht veraendert.         *)
(*                                                                           *)
(* GEMDOS/MiNT:                                                              *)
(* Die Eigenschaften der Funktionen unter dem ``blanken'' GEMDOS sind z.T.   *)
(* stark eingeschraenkt; unter dem Stichwort GEMDOS ist deswegen immer       *)
(* angegeben, in wieweit die Funktion emuliert werden kann.                  *)
(* Unter der Betriebssystemerweiterung MiNT bzw. einer anderen Erweiterung,  *)
(* die ebenfalls zumindest teilweise die neuen MiNT-Funktionen zur Verfuegung*)
(* stellt, ist eine bessere Unterstuetzung der Funktionen zu erwarten; wie   *)
(* weit diese geht, ist unter dem Stichwort MiNT angegeben.                  *)
(* --------------------------------------------------------------------------*)
(* 10-Aug-96, Holger Kleinschmidt                                            *)
(*****************************************************************************)

FROM SYSTEM IMPORT
(* TYPE *) ADDRESS;

FROM PORTAB IMPORT
(* CONST*) MAXSIGNEDLONG;

FROM types IMPORT
(* TYPE *) StrPtr, StrArray, long, int, uidT, gidT, timeT, sizeT, ssizeT,
           short, ushort, TimevalRec, TimevalPtr;

(*========================== "statfs()" =====================================*)

(* Nicht POSIX: *)
TYPE
  fsidT = RECORD
    val : ARRAY [0..1] OF long;
  END;

(* Nicht POSIX: *)
TYPE
  StatfsRec = RECORD
    (* Unbekannte Werte werden auf -1 gesetzt *)
    fType   : long;
    (* Typ des Dateisystems:
     *   0: TOS-Dateisystem oder unbekannt
     *   1: Minix V1-Dateisystem
     *   2: Minix V2-Dateisystem
     *)
    fBsize  : long; (* Blockgroesse des Dateisystems *)
    fBlocks : long; (* Gesamtzahl von Bloecken *)
    fBfree  : long; (* Zahl der freien Bloecke *)
    fBavail : long; (* Zahl der fuer Benutzer freien Bloecke *)
    fFiles  : long; (* Gesamtzahl von Inodes im Dateisystem *)
    fFfree  : long; (* Anzahl freier Inodes im Dateisystem *)
    fFsid   : fsidT;
    fSpare  : ARRAY [0..6] OF long; (* reserviert *)
  END;

(*======================== "gethostname()" ==================================*)

CONST (* Nicht POSIX: *)
  MAXHOSTNAMELEN = 16;

(*=========================== "uname()" =====================================*)

TYPE
  UtsnameRec = RECORD
    (* Laenge und Inhalt der einzelnen Strings sind systemspezifisch *)
    sysname  : ARRAY [0..19] OF CHAR;
    nodename : ARRAY [0..MAXHOSTNAMELEN-1] OF CHAR;
    release  : ARRAY [0..19] OF CHAR;
    version  : ARRAY [0..19] OF CHAR;
    machine  : ARRAY [0..23] OF CHAR;
  END;

(*==================== "pathconf()", "sysconf()" ============================*)

CONST (* Nicht POSIX: *)
  UNLIMITED = MAXSIGNEDLONG; (* <=> Wert nur durch Speicher o.ae. begrenzt *)

TYPE
  PConfVal = (
    pcVdisable,
      (* Wenn ein Terminal-Spezialzeichen (siehe 'term') diesen Wert hat,
         wird die entsprechende Funktion ignoriert. *)
    pcLinkMax,
      (* Maximale Anzahl von (Hard)Links *)
    pcPathMax,
      (* Maximale Laenge einer kompletten Pfadangabe *)
    pcNameMax,
      (* Maximale Laenge eines Dateinamens ohne Pfad *)
    pcPipeBuf,
      (* Anzahl Bytes, die ``atomar'' in eine Pipe geschrieben werden *)
    pcNoTrunc,
      (* Werden Pfadangaben bei Bedarf automatisch gekuerzt? *)
    pcMaxInput,
      (* Maximale Anzahl von Zeichen, die vom System fuer die Eingabe
         gepuffert werden koennen *)
    pcMaxCanon,
      (* Maximale Anzahl von Zeichen, die vom System fuer die Eingabe
         im kanonischen Modus gepuffert werden koennen *)
    pcChownRestricted
      (* Kann der Benutzer seine Dateien fuer andere freigeben? *)
  );

TYPE
  SConfVal = (
    scClkTck,
      (* = CLKTCK, Anzahl Intervalle/Sekunde bei Typ 'clockT' *)
    scArgMax,
      (* Maximale Groesse fuer Argumente und Environment bei "exec*()" *)
    scOpenMax,
      (* Maximale Anzahl gleichzeitig offener Dateien *)
    scNGroupsMax,
      (* Maximale Anzahl der zusaetzlichen Gruppenkennungen *)
    scChildMax,
      (* Maximale Anzahl gleichzeitig existierender Prozesse pro Benutzer *)
    scJobControl,
      (* Wird ``Job Control'' unterstuetzt? *)
    scSavedIds,
      (* Wird die bei Ausfuehrung eines set-user/group-ID Programms
         geaenderte effektive Kennung gespeichert? *)
    scVersion
      (* POSIX-Version, 6 dez. Digits, z.B. 198808 oder 199009 *)
  );

(*===========================================================================*)

TYPE
  PasswdPtr = POINTER TO PasswdRec;
  PasswdRec = RECORD
    pwName   : StrPtr; (* Benutzername, login-Name *)
    pwPasswd : StrPtr; (* Nicht POSIX: verschluesseltes Passwort *)
    pwUid    : uidT;   (* Benutzerkennung *)
    pwGid    : gidT;   (* Gruppenkennung des Benutzers *)
    pwGecos  : StrPtr; (* Nicht POSIX: Kommentar, voller Name des Benutzers *)
    pwDir    : StrPtr; (* HOME-Verzeichnis *)
    pwShell  : StrPtr; (* login-Shell *)
  END;


TYPE
  GroupPtr = POINTER TO GroupRec;
  GroupRec = RECORD
    grName   : StrPtr;   (* Gruppenname *)
    grPasswd : StrPtr;   (* Nicht POSIX: verschluesseltes Passwort *)
    grGid    : gidT;     (* Gruppenkennung *)
    grMem    : StrArray; (* Liste von Benutzernamen, die zur Gruppe
                            gehoeren. Die Liste ist mit einem NULL-Pointer
                            abgeschlossen. *)
  END;

(*===========================================================================*)

TYPE
  TimebRec = RECORD
    time     : timeT;
    millitm  : ushort;
    timezone : short;
    dstflag  : short;
  END;

  TimezonePtr = POINTER TO TimezoneRec;
  TimezoneRec = RECORD
    tzMinuteswest : int;
    tzDsttime     : int;
  END;

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

PROCEDURE getpagesize ( ): int;

(* Liefert die Groesse der Einheit, in der das System Speicher vergibt.

   GEMDOS: Konstante
     MiNT: Der Wert wird mit Hilfe von Dree(u:\proc) ermittelt.
 *)


PROCEDURE gethostname ((* EIN/ -- *) buf  : StrPtr;
                       (* EIN/ -- *) blen : sizeT  ): int;

(* In <buf>^ wird der Name des Rechners in einem Netzwerk abgelegt.
   <blen> bezeichnet den Platz in <buf>. Das Ergebnis ist nur dann mit einem
   Nullbyte abgeschlossen, falls der Platz ausreicht. Das Ergebnis ist
   maximal MAXHOSTNAMELEN Zeichen lang.

   MiNT/GEMDOS: Zuerst wird versucht, den Namen aus der Environmentvariable
                'HOSTNAME'zu ermitteln; falls diese nicht existiert oder
                leer ist, wird der Name der Datei '/etc/hostname' entnommen;
                falls diese Datei nicht existiert oder leer ist, wird als
                Name "?" geliefert.
 *)


PROCEDURE uname ((* --/AUS *) VAR info : UtsnameRec ): int;

(* Liefert systemspezifische Informationen ueber das System in Form von
   Strings.

   GEMDOS/MiNT:
    'sysname': "TOS/GEMDOS", "TOS/MiNT" oder "MagiC!".
   'nodename': Ergebnis von "gethostname()".
    'release': TOS-Datum im lokalen Datumsformat.
    'version': TOS/GEMDOS-Version, TOS/MiNT-Version oder TOS/MagiC-Version.
    'machine': Der Inhalt des Cookies MNAM oder ein String entsprechend dem
               Ergebnis von "Machine()".
 *)


PROCEDURE pathconf ((* EIN/ -- *) REF path  : ARRAY OF CHAR;
                    (* EIN/ -- *)     which : PConfVal      ): long;

(* Ermittelt Maximalwerte (siehe Def. 'PConfVal') fuer das Dateisystem, auf
   dem sich <path> befindet. Fuer <path> gilt:
   pcLinkMax:  Wenn <path> ein Verzeichnis ist, wird der Wert fuer das
               Verzeichnis selbst geliefert. Einzelne Dateien in diesem
               Verzeichnis koennen andere Werte haben.
   pcNameMax,
   pcNoTrunc:  <path> muss ein Verzeichnis bezeichnen und der Wert gilt fuer
               Dateien in diesem Verzeichnis.
   pcMaxInput,
   pcMaxCanon,
   pcVdisable: <path> muss ein Terminal bezeichnen, sonst ist das Ergebnis
               nicht definiert.
   pcPathMax:  <path> muss ein Verzeichnis bezeichnen und der Wert ist die
               max. Pfadlaenge relativ zu <path>.
   pcPipeBuf:  <path> muss ein Verzeichnis oder eine FIFO bezeichnen.
               Bezeichnet <path> ein Verzeichnis, gilt der Wert fuer jede
               FIFO in diesem Verzeichnis.
   pcChownRestricted:
               Wenn <path> ein Verzeichnis bezeichnet, gilt der Wert fuer
               alle Dateien in diesem Verzeichnis, die nicht selbst
               Verzeichnisse sind.

   Wenn ein Wert unbegrenzt ist, wird UNLIMITED zurueckgegeben.

   GEMDOS/MiNT: Bei 'pcMaxCanon' und 'pcMaxInput' wird die Groesse des
                Puffers durch 'Iorec' ermittelt.
                Ist <path> gleich "midi:" oder "/dev/midi", wird Iorec(2)
                verwendet, ist <path> gleich "kbd:" oder "/dev/kbd", wird
                Iorec(1) verwendet, sonst Iorec(0).
                'pcLinkMax', 'pcPathMax', 'pcNameMax', 'pcPipeBuf',
                'pcNoTrunc' werden, falls moeglich, ueber 'Dpathconf'
                ermittelt, sonst werden Konstanten geliefert.
                Wenn <path> ein symbolischer Link auf ein Verzeichnis ist,
                wird versucht, die Werte fuer dieses Verzeichnis zu
                ermitteln, nicht fuer das Verzeichnis, in dem sich der
                Link befindet.
 *)


PROCEDURE sysconf ((* EIN/ -- *) which : SConfVal ): long;

(* Ermittelt Maximalwerte (siehe Def. 'SConfVal') fuer das System.
   Wenn ein Wert unbegrenzt ist, wird UNLIMITED zurueckgegeben.

   GEMDOS/MiNT: 'scClkTck', 'scArgMax' und 'scVersion' sind Konstanten,
                'scOpenMax', 'scChildMax' und 'scNGroupsMax' werden, falls
                moeglich, ueber 'Sysconf' ermittelt, sonst sinds Konstanten,
                'scJobControl' wird ueber das Vorhandensein von 'Pgetpgrp'
                und 'scSavedIds' ueber das Vorhandensein von 'Psetreuid'
                ermittelt.
 *)


(* Nicht POSIX: *)
PROCEDURE statfs ((* EIN/ -- *) REF path : ARRAY OF CHAR;
                  (* -- /AUS *) VAR buf  : StatfsRec     ): int;

(* Ermittelt die Daten (siehe Definition des Typs 'StatfsRec') des
   Dateisystems, auf dem sich die Datei <path> befindet.

   GEMDOS/MiNT: Auf GEMDOS-Dateisystemen haben nicht alle Felder von <buf>
                einen sinnvollen Wert, bzw. sind nicht ermittelbar.
 *)


PROCEDURE time ((* EIN/ -- *) buf : ADDRESS ): timeT;

(* Liefert als Funktionswert das aktuelle Datum/die aktuelle Zeit in
   Sekunden seit dem 1.1.1970 0:00:00 UTC. Falls <buf> ungleich 'NULL',
   wird das Ergebnis ebenfalls in <buf>^ abgelegt.

   GEMDOS: Es wird eine lokale Zeit geliefert.
     MiNT: -""-
 *)


(* Nicht POSIX: *)
PROCEDURE gettimeofday ((* EIN/ -- *) tp  : TimevalPtr;
                        (* EIN/ -- *) tzp : TimezonePtr ): int;

PROCEDURE settimeofday ((* EIN/ -- *) tp  : TimevalPtr;
                        (* EIN/ -- *) tzp : TimezonePtr ): int;

(* Falls <tp> ungleich NULL ist, wird das aktuelle Datum/die aktuelle Zeit in
   Sekunden und Mikrosekunden seit dem 1.1.1970 0:00:00 UTC geliefert bzw.
   gesetzt. Falls <tzp> ungleich NULL ist, wird die Zeitzone (lokale
   Standardzeit) in Minuten westlich von GMT und die Art der
   Sommerzeitkorrektur geliefert bzw. gesetzt.
   Die Zeit(zone) kann nur vom Supervisor gesetzt werden.

   GEMDOS/MiNT: Es handelt sich um lokale Zeiten. Die Sommerzeitkorrektur
                ist nur als boolscher Wert ausgelegt und kann nicht gesetzt
                werden. Die Aufloesung der Systemuhr betraegt 5ms.
 *)


(* Nicht POSIX: *)
PROCEDURE ftime ((* -- /AUS *) VAR buf : TimebRec );

(* Wie "gettimeofday()", nur dass statt Mikrosekunden Millisekunden
   geliefert werden.
 *)


(* Nicht POSIX: *)
PROCEDURE stime ((* EIN/ -- *) time : timeT ): int;

(* Setzt die Systemuhr auf <time>. <time> enthaelt eine Zeit in Sekunden
   seit dem 1.1.1970 0:00:00 UTC. Die Funktion kann nur vom Supervisor
   ausgefuehrt werden.

   GEMDOS: <time> muss eine lokale Zeit enthalten.
     MiNT: -""-
 *)


(* Die folgenden Prozeduren arbeiten mit den Dateien /etc/passwd und
   /etc/group, um Informationen ueber die Benutzer des Systems zu erhalten.
   Beides sind ASCII-Dateien, bei denen jede Zeile einen festgelegten Aufbau
   hat, der aber je nach System variieren kann:
   GEMDOS/MiNT:
     /etc/passwd: Jede Zeile enthaelt mehrere Zeichenketten, die
                  untereinander durch ein ':' getrennt sind. Die einzelnen
                  Felder entsprechen sinngemaess denen des Typs 'PasswdRec',
                  wobei die Kennungen ebenfalls als Zeichenketten mittels
                  Ziffern angegeben werden.

     /etc/group : Wie passwd, aber fuer 'GroupRec'. Das letzte Feld besteht
                  aus einer Liste von Benutzernamen, die jeweils durch ein
                  Komma voneinander getrennt sind.

   POSIX unterstuetzt nur die Funktionen "getpwnam()", "getpwuid()",
   "getgrnam()" und "getgrgid()", und legt auch nicht fest woher die
   Informationen hierfuer stammen (die Informationen muessen nicht aus
   /etc/passwd und /etc/group kommen).
*)

(* Nicht POSIX: *)
PROCEDURE setpwent;

(* Oeffnet die Datei /etc/passwd fuer die nachfolgende Bearbeitung mit den
   anderen Funktionen, falls dies noch nicht geschehen ist.
 *)


(* Nicht POSIX: *)
PROCEDURE endpwent;

(* Schliesst die Datei /etc/passwd, falls sie geoeffnet ist *)


(* Nicht POSIX: *)
PROCEDURE getpwent ( ): PasswdPtr;

(* Liefert einen Zeiger auf den naechsten Eintrag aus /etc/passwd. Falls ein
   Fehler aufgetreten ist oder kein weiterer Eintrag mehr existiert, wird
   NULL zurueckgeliefert. Der Zeiger zeigt auf einen internen Speicherbereich,
   der beim naechsten Aufruf einer der pw-Routinen ueberschrieben
   werden kann; deshalb muss die Information bei Bedarf vorher an einen
   ``sicheren'' Platz kopiert werden. Falls noetig, wird automatisch ein
   "setpwent()" ausgefuehrt.
 *)


PROCEDURE getpwnam ((* EIN/ -- *) REF name : ARRAY OF CHAR ): PasswdPtr;

PROCEDURE getpwuid ((* EIN/ -- *) uid : uidT ): PasswdPtr;

(* Liefern einen Zeiger auf den ersten (!) Eintrag aus /etc/passwd, dessen
   'pwName'-Feld <name> entspricht, bzw. dessen 'pwUid'-Feld <uid> entspricht.
   Falls ein solcher Eintrag nicht existiert oder ein Fehler aufgetreten ist,
   wird NULL zurueckgeliefert.
   Der Zeiger zeigt auf einen internen Speicherbereich, der beim naechsten
   Aufruf einer der pw-Routinen ueberschrieben werden kann; deshalb muss die
   Information bei Bedarf vorher an einen ``sicheren'' Platz kopiert werden.
   Falls /etc/passwd bereits geoeffnet war, bleibt die Datei auch nach
   AusfÅhrung der Funktion geoeffnet. Sonst wird zuerst ein "setpwent()"
   ausgefÅhrt, und zum Schluss ein "endpwent()".
 *)



(* Das Gleiche fuer /etc/group: *)

(* Nicht POSIX: *)
PROCEDURE setgrent;

PROCEDURE endgrent;

PROCEDURE getgrent ( ): GroupPtr;


PROCEDURE getgrnam ((* EIN/ -- *) REF name : ARRAY OF CHAR ): GroupPtr;

PROCEDURE getgrgid ((* EIN/ -- *) gid : gidT ): GroupPtr;

END sys.
