DEFINITION MODULE dir;
__DEF_SWITCHES__
#ifdef HM2
#ifdef __LONG_WHOLE__
(*$!i+: Modul muss mit $i- uebersetzt werden! *)
(*$!w+: Modul muss mit $w- uebersetzt werden! *)
#else
(*$!i-: Modul muss mit $i+ uebersetzt werden! *)
(*$!w-: Modul muss mit $w+ uebersetzt werden! *)
#endif
#endif
(*****************************************************************************)
(* Funktionen, die Verzeichnisse bearbeiten.                                 *)
(*                                                                           *)
(* Ein Fehler ist immer dann aufgetreten, wenn bei Funktionen ein negativer  *)
(* Wert (-1) bzw. NULL zurueckgegeben wird. Die genaue Fehlerursache kann bei*)
(* Bedarf ueber "e.errno" und die entsprechenden Konstanten ermittelt werden.*)
(* Die Funktionen veraendern "errno" nur dann, wenn ein Fehler aufgetreten   *)
(* ist, bei erfolgreicher Ausfuehrung wird "errno" nicht veraendert.         *)
(*                                                                           *)
(* GEMDOS/MiNT:                                                              *)
(* Die Eigenschaften der Funktionen unter dem ``blanken'' GEMDOS sind z.T.   *)
(* stark eingeschraenkt; unter dem Stichwort GEMDOS ist deswegen immer       *)
(* angegeben, in wieweit die Funktion emuliert werden kann.                  *)
(* Unter der Betriebssystemerweiterung MiNT bzw. einer anderen Erweiterung,  *)
(* die ebenfalls zumindest teilweise die neuen MiNT-Funktionen zur Verfuegung*)
(* stellt, ist eine bessere Unterstuetzung der Funktionen zu erwarten; wie   *)
(* weit diese geht, ist unter dem Stichwort MiNT angegeben.                  *)
(* --------------------------------------------------------------------------*)
(* 10-Aug-96, Holger Kleinschmidt                                            *)
(*****************************************************************************)

FROM SYSTEM IMPORT
(* TYPE *) ADDRESS;

FROM PORTAB IMPORT
(* TYPE *) SIGNEDWORD, SIGNEDLONG;

FROM types IMPORT
(* TYPE *) int, unsigned, uidT, gidT, inoT, timeT, offT, sizeT, devT, nlinkT,
           modeT, StrPtr;

FROM DosDir IMPORT
(* TYPE *) FileAttributes;

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

(*========================= "*stat()" =======================================*)

TYPE
  StatRec = RECORD
    stMode    : modeT; (* Dateityp & Zugriffsberechtigung *)
    stIno     : inoT;  (* Inode, zus. mit 'stDev' eindeutiger Index *)
    stDev     : devT;  (* Geraet, auf dem die Datei liegt *)
    stRdev    : devT;  (* Nicht POSIX: Typ des Geraets *)
    stNlink   : nlinkT;(* Anzahl der Links *)
    stUid     : uidT;  (* Benutzerkennung des Besitzers *)
    stGid     : gidT;  (* Gruppenkennung des Besitzers *)
    stSize    : offT;  (* Dateilaenge in Bytes *)
    stBlksize : offT;  (* Nicht POSIX: Optimale Blockgroesse fuer I/O *)
    stBlocks  : offT;  (* Nicht POSIX: Von Datei belegte (512 Byte)-Bloecke *)
    stMtime   : timeT; (* Datum/Zeit der letzten Dateiveraenderung *)
    stAtime   : timeT; (* Datum/Zeit des letzten Dateizugriffs *)
    stCtime   : timeT; (* Datum/Zeit der letzten Inode-Aenderung *)
    (* Die folgenden Felder sind nicht POSIX: *)
    stAttr    : FileAttributes; (* TOS-Dateiattribute *)
    res1      : SIGNEDWORD;
    res2      : ARRAY [0..1] OF SIGNEDLONG;
  END;

(*======================= "access()" ========================================*)

TYPE
#if reverse_set
  AccessMode = (
    Access15,Access14,Access13,Access12,Access11, (* --> keine 8-Bit-Menge *)
    Access10,Access9, Access8, Access7,
    Access6, Access5, Access4, Access3,
    rOK, (* = 0004H, Leseberechtigung *)
    wOK, (* = 0002H, Schreibberechtigung *)
    xOK  (* = 0001H, Ausfuehr/Suchberechtigung *)
  );
#else
  AccessMode = (
    xOK, (* = 0001H, Ausfuehr/Suchberechtigung *)
    wOK, (* = 0002H, Schreibberechtigung *)
    rOK, (* = 0004H, Leseberechtigung *)
    Access3, Access4, Access5, Access6,
    Access7, Access8, Access9, Access10,
    Access11,Access12,Access13,Access14,Access15  (* --> Keine 8-Bit-Menge *)
  );
#endif

  AccessModes = PACKEDSET OF AccessMode;

CONST
  fOK = AccessModes{}; (* = 0000H, Existenz *)


(*======================= "utime()" =========================================*)

TYPE
  UTimBuf = RECORD
    actime  : timeT;
    modtime : timeT;
  END;

  UTimPtr = POINTER TO UTimBuf;

(*=================== "opendir()", "readdir()" ==============================*)

TYPE
  (* Der folgende Typ ist nicht opak, sondern als ADDRESS deklariert, damit
     ein direkter Vergleich mit types.NULL moeglich ist.
   *)
  DIR = ADDRESS;

TYPE
  DirentPtr = POINTER TO DirentRec;

  (* Variablen vom Typ 'DirentRec' werden nicht vom Programm angelegt,
   * sondern es wird nur ueber das Ergebnis von "readdir()" auf eine
   * vom System angelegte Variable zugegriffen.
   *)
  DirentRec = RECORD
    dName   : StrPtr; (* Name der Datei, keine Laengenbegrenzung!! *)

    (* Die folgenden Felder sind nicht POSIX: *)
    dNamlen : sizeT;  (* Laenge von dName^ *)
    dIno    : inoT;   (* Inode der Datei *)
    CASE dStatValid : BOOLEAN OF
      (* Dateiinfos. Nur gueltig, wenn dStatValid = TRUE!
       * Kann nur von "xreaddir()" auf TRUE gesetzt werden.
       *)
      TRUE: dStat : StatRec;
    ELSE
    END;
  END;

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

PROCEDURE mkdir ((* EIN/ -- *) REF dir  : ARRAY OF CHAR;
                 (* EIN/ -- *)     mode : modeT         ): int;

(* Es wird ein neues Verzeichnis mit Namen <dir> und der Zugriffsberechtigung
   <mode> angelegt; die Dateierstellungsmaske wird beruecksichtigt.

   GEMDOS: Das <mode>-Argument wird ignoriert.
     MiNT: Unterstuetzung von <mode> haengt vom Dateisystem ab.
 *)


PROCEDURE rmdir ((* EIN/ -- *) REF dir : ARRAY OF CHAR ): int;

(* Das leere Verzeichnis <dir> wird geloescht.

   GEMDOS: Keine Besonderheiten.
     MiNT: -""-
 *)


PROCEDURE link ((* EIN/ -- *) REF old : ARRAY OF CHAR;
                (* EIN/ -- *) REF new : ARRAY OF CHAR ): int;

(* Es wird ein neuer Dateieintrag <new> fuer die Datei mit dem Eintrag
   <old> angelegt (Hard Link), so dass die entsprechende Datei ueber
   beide Namen angesprochen werden kann. Ob sich <old> und <new> auf
   demselben Dateisystem befinden muessen, ist nicht festgelegt.

   GEMDOS: Liefert immer -1 (errno = 'ENOSYS').
     MiNT: Unterstuetzung haengt vom Dateisystem ab. <old> und <new> muessen
           sich auf demselben Dateisystem/Laufwerk befinden.
 *)


PROCEDURE unlink ((* EIN/ -- *) REF file : ARRAY OF CHAR ): int;

(* Der Dateieintrag <file> wird geloescht. Wenn keine weiteren Links auf die
   entsprechende Datei bestehen, wird die Datei komplett geloescht. Ist die
   Datei zur Zeit geoeffnet, wird die Datei erst nach dem Schliessen
   geloescht.

   GEMDOS: Da es hier keine Links gibt, wird einfach die Datei geloescht.
           Die Datei darf nicht geoeffnet sein.
     MiNT: Unterstuetzung haengt vom Dateisystem ab.
 *)


PROCEDURE rename ((* EIN/ -- *) REF old : ARRAY OF CHAR;
                  (* EIN/ -- *) REF new : ARRAY OF CHAR ): int;

(* Benennt die Datei oder das Verzeichnis <old> in <new> um. Wenn <new>
   existiert, wird es vorher geloescht. Ob sich <old> und <new> auf
   demelben Dateisystem befinden muessen, ist nicht festgelegt.

   GEMDOS: Verzeichnisse koennen erst ab TOS 1.04 umbenannt werden.
           <old> und <new> muessen sich auf demselben Dateisystem/Laufwerk
           befinden.
           Die Datei darf nicht geoeffnet sein.
     MiNT: -""-
 *)


PROCEDURE opendir ((* EIN/ -- *) REF dir : ARRAY OF CHAR ): DIR;

PROCEDURE readdir ((* EIN/ -- *) dirp : DIR ): DirentPtr;

PROCEDURE rewinddir ((* EIN/ -- *) dirp : DIR );

PROCEDURE closedir ((* EIN/AUS *) VAR dirp : DIR ): int;

(* Nicht POSIX: *)
PROCEDURE telldir ((* EIN/ -- *) dirp : DIR ): offT;

(* Nicht POSIX: *)
PROCEDURE seekdir ((* EIN/ -- *) dirp : DIR;
                   (* EIN/ -- *) loc  : offT );

(* "opendir()":
       Oeffnet das Verzeichnis <dirname>, d.h. trifft die Vorbereitungen
       fuer "readdir()". Der Funktionswert ist als Identifikation des
       Verzeichnisses fuer die anderen Verzeichnis-Prozeduren noetig. Konnte
       das Verzeichnis nicht geoeffnet werden, wird NULL zurueckgeliefert.
   "readdir()":
       Ein Verweis auf den physikalisch naechsten Eintrag des Verzeichnisses
       <dirp> wird zurueckgeliefert oder NULL, falls kein weiterer Eintrag
       existiert oder ein Fehler aufgetreten ist ('errno' vorher auf Null
       setzen).
       ACHTUNG: Der Verweis bleibt nur solange gueltig, wie kein weiterer
       Aufruf von "readdir()" mit dem gleichen <dirp> erfolgt!
   "rewinddir()":
       Setzt das Verzeichnis <dirp> wieder auf den Anfang zurueck, so dass
       ein "readdir()" die physikalisch erste Datei (".") liefert.
   "closedir()":
       Nach dem Auslesen des Verzeichnisses muss dieses wieder geschlossen
       werden. Falls das Schliessen erfolgreich war, wird <dirp> auf NULL
       gesetzt.
   "telldir()":
       Liefert die augenblickliche Position im Verzeichnis <dirp>.
       Diese Position wird mit jedem "readdir()" erhoeht, so wie "read" die
       Lese/Schreibposition in einer ``normalen'' Datei erhoeht.
       Um wieviel sich die Position bei jedem "readdir()" erhoeht, ist
       systemabhaengig.
   "seekdir()":
       Setzt die Position im Verzeichnis auf <loc>. <loc> MUSS(!) das
       Ergebnis eines "telldir()"-Aufrufs sein. Ein Aufruf von "readdir()"
       liefert nur dann dieselbe Datei wie zum Zeitpunkt des entsprechenden
       "telldir()", wenn sich zwischendurch nichts am Verzeichnis geaendert
       hat; insbesondere darf <dirp> zwischendurch nicht geschlossen werden.

   GEMDOS/MiNT: Es werden alle Dateien ausser dem Diskettennamen geliefert.
                Diese Routinen sind anstelle von "Fsfirst/Fsnext" zu
                benutzen.
                Bei einer AES-Version < 4.0 duerfen diese Funktionen in
                einem ACC nicht verwendet werden, da u.U. ein 'Malloc'
                ausgefuehrt wird!
 *)

(* Nicht POSIX: *)
PROCEDURE xreaddir ((* EIN/ -- *) dirp : DIR ): DirentPtr;

(* Zusammenfassung von "readdir()" und "lstat()": Zu der Datei wird
   gleichzeitig versucht, die zugehoerigen Daten zu ermitteln. Falls
   die Daten ermittelt werden konnten, wird 'dStatValid' im DirentRec
   auf TRUE gesetzt, und 'dStat' enthaelt die Werte eines Aufrufs von
   "lstat()" fuer die angegebene Datei.
   Wenn 'dStatValid' gleich FALSE ist, muss eben ein separater lstat-Aufruf
   erfolgen.

   GEMDOS/MiNT: Falls moeglich, wird der schnelle 'Dxreaddir'-Aufruf
                verwendet, aber auch, wenn dieser nicht vorhanden ist,
                laeuft das wesentlich schneller ab als ein getrennter
                Aufruf der beiden Prozeduren.
 *)


(* Noch nicht POSIX: *)
PROCEDURE symlink ((* EIN/ -- *) REF old : ARRAY OF CHAR;
                   (* EIN/ -- *) REF new : ARRAY OF CHAR ): int;

(* Es wird ein symbolischer Dateieintrag <new> fuer die Datei mit dem
   Dateieintrag <old> angelegt (Symbolic Link), so dass die entsprechende
   Datei ueber beide Namen angesprochen werden kann. <old> und <new>
   muessen sich nicht auf dem gleichen Dateisystem befinden.
   Die Existenz von <old> wird nicht ueberprueft!

   GEMDOS: Liefert immer -1 (errno = 'ENOSYS').
     MiNT: Unterstuetzung haengt vom Dateisystem ab.
 *)


(* Noch nicht POSIX: *)
PROCEDURE readlink ((* EIN/ -- *) REF lname  : ARRAY OF CHAR;
                    (* EIN/ -- *)     buf    : StrPtr;
                    (* EIN/ -- *)     bufsiz : sizeT         ): int;

(* Wenn <lname> der Name eines ``Symbolischen Links'' ist, wird in <buf>^
   der bei "symlink()" angegebene Name der zugehoerigen Zieldatei abgelegt;
   Der Name ist NICHT mit einem Nullbyte abgeschlossen, deshalb muss der
   Rueckgabewert der Funktion verwendet werden, um den String mit einem
   Nullbyte abzuschliessen (wenn der Platz dafuer noch ausreicht)!!!
   Als Funktionswert wird die Laenge von <buf>^ oder -1 (fuer Fehler)
   zurueckgegeben. <bufsiz> ist der Platz in <buf>^. Es wird KEIN Fehler
   gemeldet, wenn <buf> nicht gross genug war, den gesamten Namen
   aufzunehmen, es wird dann lediglich auf <bufsiz> gekuerzt! Die
   tatsaechliche Laenge eines symbolischen Links kann lediglich ueber
   "lstat(lname, st)" und 'st.stSize' ermittelt werden.

   GEMDOS/MiNT:
   Achtung: Der Stack wird mit mindestens <bufsiz> Bytes belastet, darum
            sollte <bufsiz> nicht zu grož gewaehlt werden (oder die
            Stackgroesse des Programms muss entsprechend hoch sein)!

   GEMDOS: Liefert immer -1 (errno = 'ENOSYS').
     MiNT: Unterstuetzung haengt vom Dateisystem ab.
 *)


PROCEDURE chmod ((* EIN/ -- *) REF file : ARRAY OF CHAR;
                 (* EIN/ -- *)     mode : modeT         ): int;

(* Setzt die Schreib-, Lese- und Ausfuehrungsrechte und die ``set user id''-
   und ``set group id''-Flags der Datei <file>.

   GEMDOS: Es wird nur sIWUSR ausgewertet: wenn das Bit gesetzt ist, wird
           'faRDONLY' im Dateiattribut geloescht und umgekehrt.
           Das Dateiattribut 'faCHANGED' wird nicht geaendert. Attribute
           von Verzeichnissen koennen nicht geaendert werden (ist aber kein
           Fehler).
     MiNT: Unterstuetzung haengt vom Dateisystem ab.
 *)


PROCEDURE chown ((* EIN/ -- *) REF file : ARRAY OF CHAR;
                 (* EIN/ -- *)     uid  : uidT;
                 (* EIN/ -- *)     gid  : gidT          ): int;

(* Setzt die Besitzer- und Gruppenkennung der Datei <file>.
   Wenn _PC_CHOWN_RESTRICTED gilt (siehe "sysconf()"),kann nur der Superuser
   die Kennungen veraendern; stimmt die effektive Benutzerkennung jedoch mit
   der Kennung des Dateibesitzers ueberein, kann die Gruppenkennung auf eine
   der zusaetzlichen Gruppenkennungen gesetzt werden (siehe "getgroups()").
   Symbolische Links werden aufgeloest, d.h. die Kennungen des Links selbst
   werden nicht veraendert.

   GEMDOS: Falls <uid> und <gid> ungleich Null sind, wird -1 geliefert
           (errno = 'EINVAL').
     MiNT: Unterstuetzung haengt vom Dateisystem ab.
           Symbolische Links werden nicht aufgeloest.
 *)


PROCEDURE utime ((* EIN/ -- *) REF file : ARRAY OF CHAR;
                 (* EIN/ -- *)     time : UTimPtr       ): int;

(* Setzt die Zeit des letzten Dateizugriffs (st.stAtime) der Datei <file>
   auf <time^.actime> und die Zeit der letzten Dateiveraenderung(st.stMtime)
   auf <time^.modtime>. Die Zeit der letzten Statusaenderung (st.stCtime)
   wird automatisch auf das aktuelle Datum gesetzt. Ist <time> gleich NULL,
   wird das aktuelle Datum eingesetzt.

   GEMDOS: Es wird nur <time^.modtime> ausgewertet. Verzeichnisse koennen
           nicht beruecksichtigt werden, es wird aber kein Fehler gemeldet.
     MiNT: Bei geeignetem Dateisystem werden 'modtime' und 'actime'
           ausgewertet. Falls moeglich,werden auch Verzeichnisse
           beruecksichtigt.
 *)


PROCEDURE stat ((* EIN/ -- *) REF file : ARRAY OF CHAR;
                (* -- /AUS *) VAR st   : StatRec       ): int;

PROCEDURE fstat ((* EIN/ -- *)     hndl : int;
                 (* -- /AUS *) VAR st   : StatRec ): int;

(* Noch nicht POSIX: *)
PROCEDURE lstat ((* EIN/ -- *) REF file : ARRAY OF CHAR;
                 (* -- /AUS *) VAR st   : StatRec       ): int;

(* Die Prozeduren liefern zum Namen <file> einer Datei bzw. einer
   Dateikennung <hndl> die Informationen aus dem INODE (siehe Definition
   von 'modeT' und 'StatRec'.
   Bei einem 'Symbolischen Link' liefert "stat()" Informationen ueber die
   physikalisch referenzierte Datei, "lstat()" Informationen ueber den Link
   selber, ansonsten sind die beiden Funktionen identisch.

   GEMDOS:  o 'stMode': Die Berechtigungen fuer Besitzer, Gruppe und Andere
              sind identisch; Leseberechtigung ist vorhanden, wenn das
              faHIDDEN-Attrinbut nicht gesetzt ist, Schreibberechtigung ist
              vorhanden, falls 'faRDONLY' im Dateiattribut nicht gesetzt
              ist, die Ausfuehrberechtigung bei regulaeren Dateien wird mit
              'DosFile.IsExec' festgestellt, Verzeichnisse haben immer
              Suchberechtigung. Geraete werden mit 'DosFile.IsDosDevice'
              ermittelt, Verzeichnisse durch ein gesetztes 'faSUBDIR' im
              Dateiattribut, alles andere sind regulaere Dateien.
            o 'stIno': ein fortlaufender Wert, der keine Beziehung zur Datei
              hat.
            o 'stDev': die (aktuelle) Laufwerksnummer (A = 0, B = 1...).
            o 'stNlink': bei Verzeichnissen Zwei (wegen ".."), sonst Eins.
            o 'stSize': bei Verzeichnissen konstant 1024.
            o 'stAtime', 'stMtime', 'stCtime': die Zeiten sind identisch.
              Bei Geraeten werden aktuelle Zeit und aktuelles Datum genommen
              Bei Wurzelverzeichnissen ist der Wert Null.

            "lstat()": ohne MiNT ist diese Funktion mit "stat()" identisch.
            Diese Funktion ist noch nicht POSIX-Standard.

            "fstat()": ohne MiNT sind die einzigen zuverlaessigen
            Informationen 'stSize' und 'stMtime', falls <hndl> eine
            gueltige Kennung einer regulaeren Datei ist.

     MiNT: Unterstuetzung haengt vom Dateisystem ab.
 *)


PROCEDURE sISCHR  ((* EIN/ -- *) stMode : modeT ): BOOLEAN;
PROCEDURE sISBLK  ((* EIN/ -- *) stMode : modeT ): BOOLEAN;
PROCEDURE sISDIR  ((* EIN/ -- *) stMode : modeT ): BOOLEAN;
PROCEDURE sISREG  ((* EIN/ -- *) stMode : modeT ): BOOLEAN;
PROCEDURE sISFIFO ((* EIN/ -- *) stMode : modeT ): BOOLEAN;
 (* Noch nicht POSIX: *)
PROCEDURE sISLNK  ((* EIN/ -- *) stMode : modeT ): BOOLEAN;

(* Diese Funktionen sind bei POSIX als Makros definiert, und liefern TRUE,
   falls der im 'stMode'-Feld der StatRec-Struktur kodierte Dateityp dem
   durch den Prozedurnamen angedeuteten entspricht.
   Es sollten nur diese Prozeduren verwendet werden, um den Dateityp zu
   bestimmen.

   GEMDOS: Keine Besonderheiten.
     MiNT: -""-
 *)


PROCEDURE access ((* EIN/ -- *) REF file : ARRAY OF CHAR;
                  (* EIN/ -- *)     acc  : AccessModes   ): int;

(* Es wird die Existenz (<acc> = fOK) oder die Lese-, Schreib oder
   Ausfuehr- bzw. Suchberechtigung einer Datei getestet. Beim Test wird
   beruecksichtigt, ob der ausfuehrende Prozess der Besitzer der Datei ist,
   der Gruppe des Besitzers angehoert oder ein Aussenstehender ist (Es
   werden die realen Kennungen getestet). Es ist nicht festgelegt, ob der
   ``Superuser'' besondere Rechte hat, d.h. alle Zugriffsrechte besitzt.

   GEMDOS: Die bei "stat()" genannten Einschraenkungen gelten.
     MiNT: -""-
 *)


(* Nicht POSIX: *)
PROCEDURE major ((* EIN/ -- *) rdev : devT ): unsigned;

PROCEDURE minor ((* EIN/ -- *) rdev : devT ): unsigned;

PROCEDURE makedev ((* EIN/ -- *) maj : unsigned;
                   (* EIN/ -- *) min : unsigned ): devT;

(* Diese Funktionen sind bei *IX als Makros definiert. Falls "sISCHR()" oder
   "sISBLK()" TRUE liefern, kann mit "major('stRdev')" der Typ des Geraets
   bestimmt werden, waehrend "minor('stRdev')" die Geraetenummer innerhalb
   des Typs liefert. "makedev()" setzt aus den angegebenen Nummern einen
   Wert fuer 'stRdev' zusammen. Die Kodierungen fuer Typ und Geraetenummer
   sind systemspezifisch.

   GEMDOS: Keine Besonderheiten.
     MiNT: -""-
 *)

END dir.
