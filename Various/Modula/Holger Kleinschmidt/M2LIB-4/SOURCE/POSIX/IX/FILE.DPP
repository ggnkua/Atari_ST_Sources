DEFINITION MODULE file;
__DEF_SWITCHES__
#ifdef HM2
#ifdef __LONG_WHOLE__
(*$!i+: Modul muss mit $i- uebersetzt werden! *)
(*$!w+: Modul muss mit $w- uebersetzt werden! *)
#else
(*$!i-: Modul muss mit $i+ uebersetzt werden! *)
(*$!w-: Modul muss mit $w+ uebersetzt werden! *)
#endif
#endif
(*****************************************************************************)
(* Funktionen, die ``normale'' Dateien ueber Dateikennungen bearbeiten.      *)
(*                                                                           *)
(* Ein Fehler ist immer dann aufgetreten, wenn bei Funktionen ein negativer  *)
(* Wert (-1) bzw. NULL zurueckgegeben wird. Die genaue Fehlerursache kann bei*)
(* Bedarf ueber "e.errno" und die entsprechenden Konstanten ermittelt werden.*)
(* Die Funktionen veraendern "errno" nur dann, wenn ein Fehler aufgetreten   *)
(* ist, bei erfolgreicher Ausfuehrung wird "errno" nicht veraendert.         *)
(*                                                                           *)
(* GEMDOS/MiNT:                                                              *)
(* Die Eigenschaften der Funktionen unter dem ``blanken'' GEMDOS sind z.T.   *)
(* stark eingeschraenkt; unter dem Stichwort GEMDOS ist deswegen immer       *)
(* angegeben, in wieweit die Funktion emuliert werden kann.                  *)
(* Unter der Betriebssystemerweiterung MiNT bzw. einer anderen Erweiterung,  *)
(* die ebenfalls zumindest teilweise die neuen MiNT-Funktionen zur Verfuegung*)
(* stellt, ist eine bessere Unterstuetzung der Funktionen zu erwarten; wie   *)
(* weit diese geht, ist unter dem Stichwort MiNT angegeben.                  *)
(* --------------------------------------------------------------------------*)
(* 10-Aug-96, Holger Kleinschmidt                                            *)
(*****************************************************************************)

FROM SYSTEM IMPORT
(* TYPE *) ADDRESS;

FROM types IMPORT
(* TYPE *) int, offT, sizeT, ssizeT, pidT, modeT;

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

(*================== "open()", "open3()" ====================================*)

TYPE
#if reverse_set
  OpenMode = (
    Open15, Open14, Open13,
    oNOCTTY,    (* Falls die zu oeffnende Datei ein (Pseudo)Terminal ist,
                   wird es nicht zum Kontroll-Terminal, wenn der Prozess
                   im Moment kein Kontrollterminal hat. *)
    oEXCL,      (* Neu Anlegen nicht erlaubt, falls schon vorhanden *)
    oTRUNC,     (* Vorhandene Datei auf Laenge 0 kuerzen *)
    oCREAT,     (* Datei anlegen, falls nicht vorhanden *)
    oNONBLOCK,  (* Nicht blockieren beim Lesen/Schreiben/Oeffnen *)
    Open7, Open6, Open5, Open4,
    oAPPEND,    (* Schreiben immer ans Ende der Datei *)
    Open2,
    rw1,
    rw0
  );
#else
  OpenMode = (
    rw0,
    rw1,
    Open2,
    oAPPEND,    (* Schreiben immer ans Ende der Datei *)
    Open4, Open5, Open6, Open7,
    oNONBLOCK,  (* Nicht blockieren beim Lesen/Schreiben/Oeffnen *)
    oCREAT,     (* Datei anlegen, falls nicht vorhanden *)
    oTRUNC,     (* Vorhandene Datei auf Laenge 0 kuerzen *)
    oEXCL,      (* Neu Anlegen nicht erlaubt, falls schon vorhanden *)
    oNOCTTY,    (* Falls die zu oeffnende Datei ein (Pseudo)Terminal ist,
                   wird es nicht zum Kontroll-Terminal, wenn der Prozess
                   im Moment kein Kontrollterminal hat. *)
    Open13, Open14, Open15
  );
#endif

  OpenModes = PACKEDSET OF OpenMode;

CONST
  oACCMODE = OpenModes{rw0, rw1}; (* Maske fuer erlaubte Operationen *)

  oRDONLY  = OpenModes{ };   (* 0000H, Nur Lesen erlaubt *)
  oWRONLY  = OpenModes{rw0}; (* 0001H, Nur Schreiben erlaubt *)
  oRDWR    = OpenModes{rw1}; (* 0002H, Lesen und Schreiben erlaubt *)

(*======================= "lseek()" =========================================*)

TYPE
  SeekMode = (
    SeekSet, (* = 0, Absolute Positionsangabe *)
    SeekCur, (* = 1, Positionsangabe relativ zur augenblicklichen Position *)
    SeekEnd  (* = 2, Positionsangabe relativ zum Dateiende *)
  );

(*======================== "fcntl()" ========================================*)

TYPE
#if reverse_set
  FDFlag = (
    FD15, FD14, FD13, FD12, FD11, FD10, FD9,
    FD8,  FD7,  FD6,  FD5,  FD4,  FD3,  FD2, FD1,
    FdCloExec  (* Bei "exec*()" Dateikennung im neuen Prozess schliessen *)
  );
#else
  FDFlag = (
    FdCloExec, (* Bei "exec*()" Dateikennung im neuen Prozess schliessen *)
    FD1,  FD2,  FD3,  FD4,  FD5,  FD6,  FD7, FD8,
    FD9,  FD10, FD11, FD12, FD13, FD14, FD15
  );
#endif
  FDFlags = PACKEDSET OF FDFlag;

TYPE
  FcntlCmd = (
    fDUPFD, (* Neue Dateikennung liefern (-> "dup()") *)
    fGETFD, (* Flags der Dateikennung ermitteln (-> FDFlags) *)
    fSETFD, (* Flags der Dateikennung setzen (-> FDFlags ) *)
    fGETFL, (* Flags des Dateibeschreibungsblocks liefern *)
    fSETFL, (* Flags des Beschreibungsblocks setzen,ausser Lese/Schreibmodus *)
    fGETLK, (* Informationen ueber einen ``Lock'' liefern *)
    fSETLK, (* ``Lock'' setzen, sofort zurueck, falls nicht moeglich *)
    fSETLKW (* ``Lock'' setzen, blockieren, bis moeglich *)
  );

TYPE
  LockType = (
    fRDLCK, (* (set) read/shared lock. Andere Prozesse duerfen ebenfalls
               einen read-lock auf einem Bereich haben oder setzen, der
               sich mit dem so geschuetzten Bereich ueberschneidet, aber
               keinen write-lock. *)
    fWRLCK, (* (set) write/exclusive lock. Kein anderer Prozess darf
               irgendeinen Lock auf einem Bereich haben oder setzen, der sich
               mit dem so geschuetzten Bereich ueberschneidet. *)
    fUNLCK  (* unlock/unlocked. Einen existierenden Lock loeschen *)
  );

TYPE
  FlockRec = RECORD
    lType   : LockType; (* Typ des ``Locks'' *)
    lWhence : SeekMode; (* Offset, von dem 'lStart' gerechnet wird *)
    lStart  : offT;     (* Start des ``Locks'' *)
    lLen    : offT;     (* Groesse des ``Locks'' (0 = bis zum Dateiende) *)
    lPid    : pidT;     (* Prozess-Kennung des ``Lock-Besitzers'' *)
  END;

TYPE
  FcntlArg = RECORD
    CASE TAG_COLON FcntlCmd OF
      fDUPFD  : handle  : int;      (* in & out *)
     |fGETFD,                       (* out *)
      fSETFD  : fdflags : FDFlags;  (* in *)
     |fGETFL,                       (* out *)
      fSETFL  : mode    : OpenModes;(* in *)
     |fGETLK,                       (* in & out *)
      fSETLK,                       (* in *)
      fSETLKW : flock   : FlockRec; (* in *)
    END;
  END;

(*======================= "pipe()" ==========================================*)

TYPE
  PipeBuf = ARRAY [0..1] OF int;

(*===================== "open()", "creat()" =================================*)

CONST (* Kleinstes gueltiges Handle, nicht POSIX: *)
#ifdef __GEMDOS__
  MINHANDLE = -5; (* Wegen MIDI-in/out *)
#else
  MINHANDLE = 0;
#endif

(*===========================================================================*)

CONST (* Die immer geoeffneten Standardkanaele *)
  StdinFileNo  = 0;
  StdoutFileNo = 1;
  StderrFileNo = 2; (* "GEMDOS": Durch 'DosFile' sichergestellt *)

(*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*)

PROCEDURE open3 ((* EIN/ -- *) REF file   : ARRAY OF CHAR;
                 (* EIN/ -- *)     access : OpenModes;
                 (* EIN/ -- *)     mode   : modeT         ): int;

PROCEDURE open ((* EIN/ -- *) REF file   : ARRAY OF CHAR;
                (* EIN/ -- *)     access : OpenModes     ): int;

PROCEDURE creat ((* EIN/ -- *) REF file : ARRAY OF CHAR;
                 (* EIN/ -- *)     mode : modeT         ): int;

(* "open3()" oeffnet eine bereits vorhandene Datei namens <file> in dem
   durch <access> festgelegten Modus (Funktionalitaet siehe Def. von
   'OpenModes'), bzw. erzeugt eine neue mit den Attributen aus <mode>,
   falls in <access> 'oCREAT' gesetzt ist. Die Datei ist ueber die als
   Funktionswert zurueckgelieferte Kennung referenzierbar. Es wird die
   kleinste Kennung geliefert, die frei ist.

   "open()"  entspricht: "open3(file,access,STDPERM)"
   "creat()" entspricht: "open3(file,oWRONLY+OpenModes{oCREAT,oTRUNC},mode)"

   Oeffnen einer Datei bedeutet folgendes: Es wird ein Dateibeschreibungsblock
   fuer die Datei <file> angelegt, und eine Kennung geliefert, ueber die
   dieser Block angesprochen werden kann. Die gleiche Datei kann mehrfach
   geoeffnet sein und damit mehrere Dateibeschreibungsbloecke besitzen.
   Jeder Beschreibungsblock kann wiederum mehrere Dateikennungen erhalten.
   Die Statusflags und der Zugriffsmodus aus <access> sind Attribute des
   Beschreibungsblocks, ebenso wie die Position des Lese/Schreibzeigers. Die
   Statusflags koennen mit "fcntl(fGETFL/fSETFL)" abgefragt und veraendert
   (ausser dem Zugriffsmodus) werden, der Lese/Schreibzeiger wird durch
   "read()", "write()" und "lseek()" veraendert. Eine neue Kennung fuer
   denselben Beschreibungsblock kann mit "dup()" oder "fcntl(fDUPFD)"
   erzeugt werden. Mit "dup2()" laesst sich eine Dateikennung auf einen
   anderen Beschreibungsblock umleiten.
   Mit "fcntl(fGETFD/fSETFD)" laesst sich das ``Schliessen bei exec''-Flag
   abfragen und setzen, das ein Attribut der Dateikennung ist; dieses Flag
   ist nach dem Oeffnen einer Datei geloescht. Nur falls eine neue Datei
   angelegt wird, wird <mode> verwendet, das ein Attribut der Datei ist.

   GEMDOS: <access>: oNOCTTY und oNONBLOCK und oAPPEND werden ignoriert.
           <mode>: Die Datei kann lediglich schreibgeschuetzt werden.
           Die kleinste Kennung fuer eine normale Datei ist 6.
           Eine Datei kann nur zum Lesen mehrfach geoeffnet werden.
     MiNT: oNOCTTY und oAPPEND werden unterstuetzt, fuer oNONBLOCK wird
           oNDELAY verwendet. Die Unterstuetzung von <mode> haengt vom
           Dateisystem ab.

   Es koennen auch Geraete geoeffnet werden, so dass auch negative Werte
   auftreten koennen. Ein Fehler ist erst aufgetreten, falls der
   Funktionswert 'MINHANDLE' unterschreitet.
 *)


PROCEDURE fcntl ((* EIN/ -- *)     h   : int;
                 (* EIN/ -- *)     cmd : FcntlCmd;
                 (* EIN/AUS *) VAR arg : FcntlArg ): int;

(* Fuehrt je nach <cmd> eine Funktion auf der ueber <h> erreichbaren Datei
   aus. Argumente und Ergebnisse werden ueber <arg> mitgeteilt:

    cmd = fDUPFD : zweite Dateikennung zu <h> liefern ( -> "dup()").
      arg.handle:
        Eingabe: kleinster Wert fuer neue Kennung
        Ausgabe: neue Kennung

    cmd = fGETFD : Flags der Dateikennung ermitteln (Dateikennung schliessen
                   im neuen Prozess bei Ausfuehren von ``exec'').
      arg.fdflags:
        Eingabe: --------
        Ausgabe: Flags

    cmd = fSETFD : Flags der Dateikennung setzen.
      arg.fdflags:
        Eingabe: Flags
        Ausgabe: --------

    cmd = fGETFL : Flags des Dateibeschreibungsblocks liefern.
      arg.mode:
        Eingabe: --------
        Ausgabe: Modus-Flags

    cmd = fSETFL : Flags des Dateibeschreibungsblocks setzen, bis auf den
                   Lese/Schreibmodus, der nicht veraendert werden kann.
      arg.mode:
        Eingabe: Modus-Flags
        Ausgabe: --------

    cmd = fSETLK : Hiermit kann ein bestimmter Bereich der Datei fuer den
                   konkurrierenden Zugriff anderer Prozesse gesperrt oder
                   ein vorhandener ``Lock'' freigegeben werden.
                   Ein Lock verhindert nicht, dass mit dem solchermassen
                   geschuetzten Bereich etwas getan wird, ausser dass bei
                   einem Lock von einem anderen Prozess eine entsprechende
                   Fehlermeldung erzeugt wird. Es handelt sich um ein sog.
                   ``advisory locking'' im Gegensatz zum ``mandatory
                   locking'', bei dem ein Zugriff auf den geschuetzten
                   Bereich verhindert wird.
      arg.flock:
        Eingabe: Typ, Position und Laenge des in Frage kommenden ``Locks''.
        Ausgabe: --------

    cmd = fSETLKW : wie 'fSETLK', blockiert aber den aufrufenden Prozess,
                    bis ``Lock'' gesetzt werden kann (Vorsicht!).

    cmd = fGETLK : Liefert Informationen, ob ein bestimmter Bereich der
                   Datei fuer konkurrierenden Zugriff gesperrt ist.
      arg.flock:
        Eingabe: Typ, Position und Laenge des in Frage kommenden ``Locks''.
        Ausgabe: Typ, Position und Laenge eines ``Locks'', der sich
                 (teilweise) mit dem angegeben Bereich ueberschneidet, oder
                 Information, dass der Bereich frei ist ('lType' = fUNLCK).


   GEMDOS: Bisher wird nur 'fDUPFD' unterstuetzt. Falls der GEMDOS-Aufruf
           "Flock()" existiert, wird auch 'fSETLK' (teilweise) unterstÅtzt.
     MiNT: Die Unterstuetzung von ``Locks'' haengt vom Dateisystem ab.
           Bei 'fGETFL'/'fSETFL' wird fuer 'oNONBLOCK' 'oNDELAY' verwendet.
 *)


PROCEDURE close ((* EIN/ -- *) h : int ): int;

(* Die Datei mit der Kennung <h> wird geschlossen. Danach kann nicht mehr
   ueber <h> auf die Datei zugegriffen werden. Die Kennung <h> ist jetzt
   ``frei'', so dass sie wieder mit einer anderen Datei verbunden werden
   kann.

   GEMDOS: Keine Besonderheiten.
     MiNT: -""-
 *)


PROCEDURE read ((* EIN/ -- *) h   : int;
                (* EIN/ -- *) buf : ADDRESS;
                (* EIN/ -- *) len : sizeT   ): ssizeT;

PROCEDURE write ((* EIN/ -- *) h   : int;
                 (* EIN/ -- *) buf : ADDRESS;
                 (* EIN/ -- *) len : sizeT   ): ssizeT;

(* "read()" liest <len> Bytes aus der Datei mit der Kennung <h> in den Puffer
   ab der Adresse <buf>. Als Funktionswert wird die Anzahl der gelesenen
   Bytes zurueckgeliefert (0 = Dateiende, -1 = Fehler).
   "write()" schreibt <len> Bytes aus dem Puffer mit der Startadresse <buf>
   in die Datei mit der Kennung <h>. Als Funktionswert wird die Anzahl der
   geschriebenen Bytes zurueckgeliefert (<> len = Fehler).
   Aus Portabilitaetsgruenden ('ssizeT' = 16 Bit ist moeglich) sollte <len>
   nicht groesser als 32767 sein.
   Wenn <h> ein Terminal kennzeichnet, werden vor allem beim Lesen bestimmte
   Zeichen speziell behandelt. Dies ist z.T. auch ueber "term.tcsetattr()"
   einstellbar.

   GEMDOS: Keine Besonderheiten.
     MiNT: -""-
 *)


PROCEDURE lseek ((* EIN/ -- *) h    : int;
                 (* EIN/ -- *) off  : offT;
                 (* EIN/ -- *) mode : SeekMode ): offT;

(* Die aktuelle Lese/Schreibposition der Datei mit der Kennung <h> wird, je
   nach <mode>, Beschreibung siehe dort, um <off> Bytes verschoben, bzw.
   auf Position <off> gesetzt. Hierbei kann auch ueber das Ende der Datei
   positioniert werden, die Datei wird aber erst dann (virtuell) verlaengert
   (-> ``gap'') wenn ein Schreibvorgang hinter dem tatsaechlichen bisherigen
   Dateiende stattfindet.
   Als Funktionswert wird die aktuelle Schreib/Leseposition geliefert.

   GEMDOS: Es ist kein Verlaengern der Datei durch ein "Fseek" hinter das
           augenblickliche Dateiende moeglich, daher wird bei "lseek" das
           Verlaengern durch blockweises (jeweils 1kB) Schreiben mit
           Nullbytes realisiert; das kann natuerlich je nach Umfang der
           Erweiterung ein bisschen dauern.
     MiNT: Unterstuetzung der Dateiverlaengerung haengt vom Dateisystem ab.
 *)


(* Noch nicht POSIX: *)
PROCEDURE ftruncate ((* EIN/ -- *) h   : int;
                     (* EIN/ -- *) len : offT ): int;

(* Wenn <h> eine geoffnete regulaere Datei kennzeichnet, die groesser als
   <len> Bytes ist, wird die Datei auf <len> Bytes gekuerzt. Wenn die Datei
   kuerzer als <len> ist, ist das Verhalten systemspezifisch.

   GEMDOS: Nicht implementiert.
     MiNT: Unterstuetzung haengt vom Dateisystem ab.
 *)



PROCEDURE dup ((* EIN/ -- *) h : int ): int;

PROCEDURE dup2 ((* EIN/ -- *) oldh : int;
                (* EIN/ -- *) newh : int ): int;

(* "dup()" liefert zur Dateikennung <h> eine zweite Kennung (die kleinste
   freie Kennung), so dass die entsprechende Datei ueber beide Kennungen
   angesprochen werden kann. Das 'FdCloExec'-Flag der neuen Dateikennung ist
   geloescht.
   Bei "dup2()" kann die neue Kennung (<newh>) vorgegeben werden; wenn
   <newh> nicht frei ist, wird die entsprechende Datei zuerst geschlossen.
   Falls <newh> gleich <oldh> ist, wird nichts getan.
   Als Funktionswert wird <newh> zurueckgeliefert.

   GEMDOS: Hier funktioniert die Umlenkung erst ab TOS 1.04 einigermassen,
           aber auch nicht voellig korrekt (z.B. keine mehrfache Umlenkung,
           kein Pexec nach "dup()" auf bereits umgelenkten Standardkanal).
           "dup()" : <h> muss die Nummer eines Standardkanals sein, also
           0 <= h <= 5. Die kleinstmoegliche neue Kennung ist 6.
           "dup2()": <newh> muss die Nummer eines Standardkanals sein, also
           0 <= <newh> <= 5; weiterhin muss <oldh> ein Nicht-Standard-Handle
           (normales Datei- oder Geraetehandle) sein.
     MiNT: Keine Besonderheiten.
 *)


PROCEDURE pipe ((* -- /AUS *) VAR ph : PipeBuf ): int;

(* Generiert einen Kommunikationskanal zwischen zwei (Unter)Prozessen. Der
   eine Prozess kann ueber <ph[1]> etwas schreiben, das der andere
   ueber <ph[0]> lesen kann. Die Groesse einer ``Pipe'' ist intern
   begrenzt (z.B. auf 2kB oder 4kB), so dass der schreibende Prozess
   zeitweise gestoppt werden kann, bis wieder etwas Platz in der Pipe
   vorhanden ist.

   GEMDOS: Liefert immer -1 (errno = 'ENOSYS').
     MiNT: Keine Besonderheiten.
 *)

END file.
