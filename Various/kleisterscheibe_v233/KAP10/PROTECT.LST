' THE TRACK 41 PROTECTOR
' (C) 1987,1988 by Claus Brod, Am Felsenkeller 2,
'             D-8772 Marktheidenfeld, 09391-3206
'
' Installiert Kopierschutz auf Spur 41
' (siehe "SCHEIBENKLEISTER", Kapitel 10)
'
ron$=CHR$(27)+"p"
roff$=CHR$(27)+"q"
init
'
' ************
' ****dispatch: Zeigt Men an und wartet auf Auswahl
' ************
REPEAT
  CLS
  PRINT AT(28,1);ron$;"   THE TRACK 41 PROTECTOR   ";roff$
  PRINT AT(28,2);"  ½ 1987-1991 by Claus Brod"
  PRINT AT(28,3);"  Aus SCHEIBENKLEISTER, dem"
  PRINT AT(28,4);"  etwas anderen Floppybuch"
  PRINT AT(28,7);"(0) Exit"
  PRINT AT(28,8);"(1) Track 41 lesen und prfen"
  PRINT AT(28,9);"(2) Track 41 schreiben"
  PRINT AT(28,10);"(3) Trackpuffer anzeigen"
  REPEAT
    a$=INKEY$
  UNTIL a$>="0" AND a$<"4"
  wahl%=VAL(a$)
  ON wahl% GOSUB read,write,showtrk
UNTIL wahl%=0
END
'
' ***************
' ****** read: inputs password, reads track 41 into buffer
' ******       and looks for password
' ***************
PROCEDURE read
  PRINT
  PRINT ron$;"Read Track 41";roff$
  LINE INPUT "½ string";copy$
  buf$=STRING$(20000,0)
  rdtrk(VARPTR(buf$))
  a%=INSTR(buf$,copy$)
  IF a%
    PRINT "½ message found at offset ";a%
  ELSE
    PRINT "½ message not found"
  ENDIF
  ~INP(2)
RETURN
'
' **************
' ***** rdtrk: Reads track 41 from disk into buffer
' *****        (pointed to by pointer%)
' **************
PROCEDURE rdtrk(pointer%)
  LPOKE prg%+2,0                    ! mode 0 (read track)
  LPOKE prg%+6,pointer%             ! pointer to buffer (at least 7K please!)
  CALL prg%                         ! call mcode
RETURN
'
' *************
' ***** write: creates & writes track 41 from buffer including a password
' *************
PROCEDURE write
  PRINT
  PRINT ron$;"Write Track 41";roff$
  LINE INPUT "½ string";copy$
  mktrk(copy$)
  wrtrk(VARPTR(buf$))
RETURN
'
' **************
' ****** wrtrk: writes track 41 from buffer pointed to by
' ******        pointer%
' **************
PROCEDURE wrtrk(pointer%)
  LPOKE prg%+2,1                         ! mode 1 (write track)
  LPOKE prg%+6,pointer%                  ! start of buffer containing track data
  CALL prg%                              ! call mcode
RETURN
'
' ***************
' ****** mktrk: constructs 9-sector track 41 with password c$
' ***************
PROCEDURE mktrk(c$)
  sync$=CHR$(&HF5)+CHR$(&HF5)+CHR$(&HF5)  ! sync bytes
  sec$=STRING$(512,203)                   ! sector data
  gap1$=STRING$(60,&H4E)                  ! gap1
  gap2$=STRING$(12,0)                     ! gap2
  gap31$=STRING$(22,&H4E)                 ! gap31
  gap32$=gap2$                            ! gap32
  gap4$=STRING$(40,&H4E)                  ! gap4
  gap5$=STRING$(1401,&H4E)                ! gap5
  MID$(gap1$,3)=CHR$(&H29)+CHR$(&HF5)     ! syncs into gap1
  PRINT "Password: ";c$                   ! print password again
  MID$(gap1$,5)=c$                        ! password to gap1
  '
  buf$=gap1$                              ! start of buf$
  FOR t%=1 TO 9
    buf$=buf$+gap2$+sync$+CHR$(&HFE)+CHR$(41)+CHR$(0)+CHR$(t%)+CHR$(2)
    ' start of sector, address header
    buf$=buf$+CHR$(&HF7)
    ' checksum
    buf$=buf$+gap31$+gap32$+sync$+CHR$(&HFB)
    ' gap before data and data mark
    buf$=buf$+sec$+CHR$(&HF7)+gap4$
    ' sector data and checksum and end gap
  NEXT t%
  buf$=buf$+gap5$                          ! gaps to track end
RETURN
'
' **********************
' ***** init: inits machine code
' **********************
PROCEDURE init
  INLINE prg%,500
RETURN
'
' **********************
' ****** showtrk: Shows buf$ in hex and ASCII
' ******          press any key to stop then 'X' to exit
' ******          any other key continues
' **********************
PROCEDURE showtrk
  PRINT
  PRINT ron$;"Show Track Buffer - press any key to stop, then X to exit";roff$
  FOR t=1 TO LEN(buf$) STEP 16
    z$=""
    a$=HEX$(t-1)
    WHILE LEN(a$)<4
      a$="0"+a$
    WEND
    PRINT "$";a$;"  ";
    FOR i=0 TO 15
      a$=HEX$(ASC(MID$(buf$,t+i,1)))
      IF LEN(a$)=1
        a$="0"+a$
      ENDIF
      PRINT a$'
      z=VAL("&h"+a$)
      IF z>31 AND z<128
        z$=z$+CHR$(VAL("&h"+a$))
      ELSE
        z$=z$+"."
      ENDIF
    NEXT i
    PRINT "  ";z$
    k$=""
    IF INKEY$>""
      k$=INPUT$(1)
    ENDIF
    EXIT IF UPPER$(k$)="X"
  NEXT t
RETURN
