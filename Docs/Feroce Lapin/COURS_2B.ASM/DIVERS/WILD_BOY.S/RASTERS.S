* SOURCE CODE TO DO RASTERS, BY POWER MAN OF THE WILD BOYS *

* INTO SUPERVISOR MODE

	CLR.L -(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	MOVE.L D0,STACK
	MOVE.L #MY,A7

* SAVE VECTORS AND OTHER SHIT

	LEA STORAGE,A0
	MOVE.L $70.W,(A0)+
	MOVE.L $120.W,(A0)+
	MOVE.B $FFFFFA1B.W,(A0)+
	MOVE.B $FFFFFA21.W,(A0)+
	MOVE.B $FFFFFA07.W,(A0)+
	MOVE.B $FFFFFA09.W,(A0)+
	MOVE.B $FFFFFA13.W,(A0)+
	ADDQ.L #1,A0			MAKE ADDRESS EVEN
	MOVE.W $FFFF8240.W,(A0)+
	MOVE.W $FFFF8242.W,(A0)+

	MOVE.W #2,-(A7)
	TRAP #14
	ADDQ.L #2,A7
	MOVE.L D0,PHYSIC

	MOVE.W #3,-(A7)
	TRAP #14
	ADDQ.L #2,A7
	MOVE.L D0,LOGIC

	MOVE.W #4,-(A7)
	TRAP #14
	ADDQ.L #2,A7
	MOVE.W D0,RES

* HIDE MOUSE

	DC.W $A000
	DC.W $A00A

* SET SCREEN ADDRESS, LOW RES

	CLR.W -(A7)			LOW RES
	PEA $78000			PHYSICAL SCREEN ADDRESS
	PEA $78000			LOGICAL SCREEN ADDRESS
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7

* FILL SCREEN WITH COLOUR 1

	LEA $78000,A0
	MOVE.W #32000/8-1,D0
FILL	MOVE.W #-1,(A0)
	ADDQ.W #8,A0
	DBF D0,FILL		
	CLR.W $FFFF8240.W
	CLR.W $FFFF8242.W
	
* SET UP NEW VECTORS

	MOVE.W #$2700,SR		STOP INTERRUPTS
	MOVE.L #VBL,$70.W		NEW VBL VECTOR
	MOVE.L #TIMER_B,$120.W		NEW TIMER B INTERRUPT HANDLER
	CLR.B $FFFFFA07.W		MFP RESET
	CLR.B $FFFFFA09.W
	ORI.B #1,$FFFFFA07.W		ENABLE TIMER B
	ORI.B #1,$FFFFFA13.W
	CLR.B $FFFFFA1B.W		STOP TIMER B
	MOVE.W #$2300,SR		ENABLE INTERRUPTS
WAIT	CMPI.B #57,$FFFFFC02.W		SPACE BAR?
	BNE WAIT
	
* RESTORE VECTORS AND SHIT

	MOVE.W #$2700,SR
	LEA STORAGE,A0
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$120.W
	MOVE.B (A0)+,$FFFFFA1B.W
	MOVE.B (A0)+,$FFFFFA21.W
	MOVE.B (A0)+,$FFFFFA07.W
	MOVE.B (A0)+,$FFFFFA09.W
	MOVE.B (A0)+,$FFFFFA13.W
	ADDQ.L #1,A0
	MOVE.W (A0)+,$FFFF8240.W
	MOVE.W (A0)+,$FFFF8242.W

* SHOW MOUSE

	DC.W $A000
	DC.W $A009
	MOVE.W #$2300,SR

* SET SCREEN ADDRESS, INTO USER MODE AND EXIT

	MOVE.W RES,-(A7)
	MOVE.L PHYSIC,-(A7)
	MOVE.L LOGIC,-(A7)
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
		
	MOVE.L STACK,-(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	CLR.L -(A7)
	TRAP #1

* VBL INTERRUPT HANDLER

VBL	CLR.B $FFFFFA1B.W		STOP TIMER B
	MOVE.W PAL1,$FFFF8242.W		SET COLOUR 1 FOR TOP SCANLINE
	MOVE.B #1,$FFFFFA21		OCCUR EVERY SCANLINE
	MOVE.L #PAL1+2,PAL		SET PALETTE POINTER
	MOVE.B #8,$FFFFFA1B.W		START TIMER B
	RTE				PISS OFF

TIMER_B	MOVE.L A0,-(A7)
	MOVEA.L PAL,A0			ADDRESS OF NEXT COLOUR IN TABLE
	MOVE.W (A0)+,$FFFF8242.W	SET COLOUR 1 AND INCREMENT POINTER 
	MOVE.L A0,PAL			REPLACE NEW POINTER ADDRESS
	MOVEA.L (A7)+,A0
	BCLR #0,$FFFFFA0F.W		ACKNOWLEDGE
	RTE				BUGGER OFF
		
STORAGE	DS.B 18
	DS.L 63
MY	DS.L 1
RES	DS.W 1
LOGIC	DS.L 1
PHYSIC	DS.L 1
STACK	DS.L 1
PAL	DC.L PAL1+2
PAL1	DC.W $300,$400,$500,$600,$700,$700,$600,$500,$400,$300
	DC.W $330,$440,$550,$660,$770,$770,$660,$550,$440,$330
	DC.W $303,$404,$505,$606,$707,$707,$606,$505,$404,$303
	DC.W $033,$044,$055,$066,$077,$077,$066,$055,$044,$033
	DC.W $003,$004,$005,$006,$007,$007,$006,$005,$004,$003
	DC.W $333,$444,$555,$666,$777,$777,$666,$555,$444,$333
	DC.W $030,$040,$050,$060,$070,$070,$060,$050,$040,$030
	DC.W $003,$004,$005,$006,$007,$007,$006,$005,$004,$003
	DC.W $333,$444,$555,$666,$777,$777,$666,$555,$444,$333
	DC.W $030,$040,$050,$060,$070,$070,$060,$050,$040,$030
	DC.W $300,$400,$500,$600,$700,$700,$600,$500,$400,$300
	DC.W $330,$440,$550,$660,$770,$770,$660,$550,$440,$330
	DC.W $303,$404,$505,$606,$707,$707,$606,$505,$404,$303
	DC.W $033,$044,$055,$066,$077,$077,$066,$055,$044,$033
	DC.W $003,$004,$005,$006,$007,$007,$006,$005,$004,$003
	DC.W $333,$444,$555,$666,$777,$777,$666,$555,$444,$333
	DC.W $030,$040,$050,$060,$070,$070,$060,$050,$040,$030
	DC.W $003,$004,$005,$006,$007,$007,$006,$005,$004,$003
	DC.W $333,$444,$555,$666,$777,$777,$666,$555,$444,$333
	DC.W $030,$040,$050,$060,$070,$070,$060,$050,$040,$030		