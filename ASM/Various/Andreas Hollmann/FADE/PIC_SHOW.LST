' -----------------------------------------------------------------------------
' Programm: šberblenden von monochromen Bildschirmgrafiken
' Sprache:  GFA-Basic + Assembler
' Autor:    Andreas Hollmann
' (c) MAXON Computer GmbH
' -----------------------------------------------------------------------------
RESERVE 2^16
DIM text$(100)                        ! Feld fr Datei 'PIC_SHOW.INF'
path$=FN get_path$                    ! GEMDOS-Pfad ermitteln (Compilat!!!)
get_inlines                           ! Adressen der INLINE-Bl”cke holen
IF MALLOC(-1)>32000 THEN              ! genug Speicher fr Puffer?
  p_buffer%=MALLOC(32000)             ! dann her damit
  inf_load                            ! Datei 'PIC_SHOW.INF' laden
  ~MFREE(p_buffer%)                   ! Puffer-Speicher wieder freigeben
ELSE
  PRINT AT(1,1),"Nicht genug Hauptspeicher frei !"
  ~INP(2)
ENDIF
ERASE text$()
RESERVE
END
' =============================================================================
> PROCEDURE get_inlines
  INLINE p_add1fade_inl%,76
  INLINE p_add8fade_inl%,62
  INLINE p_rnd1fade_inl%,96
  INLINE p_rnd8fade_inl%,80
RETURN
> PROCEDURE inf_load
  IF EXIST(path$+"pic_show.inf")      ! 'PIC_SHOW.INF' existiert
    OPEN "i",#1,path$+"pic_show.inf"
    RECALL #1,text$(),-1,lines%       ! in Stringfeld laden
    CLOSE #1
    HIDEM
    start_show                        ! ab geht's
    SHOWM
  ELSE
    PRINT AT(1,1);"Datei 'PIC_SHOW.INF' existiert nicht!"
    ~INP(2)
  ENDIF
RETURN
> PROCEDURE start_show
  LOCAL show_time%,offset&,repeats%
  show_time%=VAL(text$(0))              ! Zeit pro Bild
  FOR repeats%=0 TO VAL(text$(1))       ! Anzahl Wiederholungen
    offset&=2
    DO
      IF EXIST(text$(offset&))          ! nur wenn Bild existiert
        BLOAD text$(offset&),p_buffer%  ! neues Bild laden
        INC offset&
        IF text$(offset&)="effect 1"    ! šberblendeffekt lesen
          ~C:p_rnd8fade_inl%(L:p_buffer%,L:XBIOS(2))
        ELSE IF text$(offset&)="effect 2"
          ~C:p_rnd1fade_inl%(L:p_buffer%,L:XBIOS(2))
        ELSE IF text$(offset&)="effect 3"
          ~C:p_add8fade_inl%(L:p_buffer%,L:XBIOS(2),W:117)
        ELSE IF text$(offset&)="effect 4"
          ~C:p_add8fade_inl%(L:p_buffer%,L:XBIOS(2),W:177)
        ELSE IF text$(offset&)="effect 5"
          ~C:p_add8fade_inl%(L:p_buffer%,L:XBIOS(2),W:19)
        ELSE IF text$(offset&)="effect 6"
          ~C:p_add8fade_inl%(L:p_buffer%,L:XBIOS(2),W:15999)
        ELSE IF text$(offset&)="effect 7"
          ~C:p_add1fade_inl%(L:p_buffer%,L:XBIOS(2),L:801)
        ELSE IF text$(offset&)="effect 8"
          ~C:p_add1fade_inl%(L:p_buffer%,L:XBIOS(2),L:123457)
        ELSE IF text$(offset&)="effect 9"
          ~C:p_add1fade_inl%(L:p_buffer%,L:XBIOS(2),L:127999)
        ELSE IF text$(offset&)="effect 10"
          ~C:p_add1fade_inl%(L:p_buffer%,L:XBIOS(2),L:3789)
        ELSE                                ! unbekannter Effektname
          BMOVE p_buffer%,XBIOS(2),32000    ! Bild einfach kopieren
        ENDIF
        INC offset&
        ~EVNT_TIMER(show_time%*1000)        ! warte noch ein Weilchen...
      ELSE
        ADD offset&,2                       ! n„chstes Bild
      ENDIF
    LOOP UNTIL text$(offset&)="end"
  NEXT repeats%                             ! Bildsequenz wiederholen
RETURN
> FUNCTION get_path$                          ! aktuellen GEMDOS-Pfad feststellen
LOCAL drive&,p_path%
drive&=GEMDOS(25)
path$=SPACE$(128)
p_path%=LONG{ARRPTR(path$)}
~GEMDOS(71,L:p_path%,0)                    ! 0=Driveno=aktuelles Laufwerk
path$=CHR$(65+drive&)+":"+CHAR{p_path%}+"\"
RETURN path$
ENDFUNC
