	MC68030
	MC68881

START
	output	d:\centinel\dsp\dsp_bug.o
	incdir	d:\centinel\dsp

	include	d:\centinel\both\define.s
hard_video	equ	0
	jsr	INITS
	bsr	REC_REGISTERS
	
;**************************************
;* routine principale du debugeur  ****
;**************************************
MAIN	xref	AFFICHE
	jsr	AFFICHE
TOUCHE	
	xref	get_key
	jsr	get_key		
	lea	LIST_TOUCHES,a0
.LOOP	cmp	(a0),d0
	beq.s	.FOUND
	addq	#6,a0
	tst	(a0)
	beq	TOUCHE
	bra.s	.LOOP
.FOUND	jsr	([2,a0])
	bra	MAIN
OLD_TOUCHE	dc.l	0

LIST_TOUCHES	xref	CYCLE_W,SCROLL_UP,SCROLL_DOWN,EDITOR,SCROLL_UP_VITE,SCROLL_DOWN_VITE,SCROLL_LEFT,SCROLL_RIGHT
		xref	adresse,GERE_MENU,r_type
	dc.w	$214
	dc.l	r_type
	dc	$10
	dc.l	adresse
	dc	$44
	dc.l	GERE_MENU
	dc	$0212
	dc.l	EDITOR
	dc	$0111
	dc.l	TRACE
	dc	$0110
	dc.l	RUN_AND_BREAK	;ctrl A
	dc	$011f
	dc.l	SKIP
	dc	$000f
	dc.l	CYCLE_W
	dc	$0126
	dc.l	LOAD_LOD
	dc	$012e
	dc.l	FIN
	dc	$0130
	dc.l	SET_BKPT
	dc	$050
	dc.l	SCROLL_DOWN
	dc	$48
	dc.l	SCROLL_UP
	dc	$04b
	dc.l	SCROLL_LEFT
	dc	$4d
	dc.l	SCROLL_RIGHT
	dc	$0450
	dc.l	SCROLL_DOWN_VITE
	dc	$0448
	dc.l	SCROLL_UP_VITE
	dc	$010a
	dc.l	CTRL9
	dc	$0124
	dc.l	CTRLJ
	dc	$0113
	dc.l	RUN
	dc	$348
	dc.l	MOVE_UP
	dc	$350
	dc.l	MOVE_DOWN
	dc	$34b
	dc.l	MOVE_LEFT
	dc	$34d
	dc.l	MOVE_RIGHT

	dc	$148
	dc.l	SIZE_Y_DEC
	dc	$150
	dc.l	SIZE_Y_INC
	dc	$14b
	dc.l	SIZE_X_DEC
	dc	$14d
	dc.l	SIZE_X_INC
	dc	0

SIZE_Y_DEC
	move.l	ACTIVE_WINDOW,a0
	move	Hauteur(a0),d0
	subq	#1,d0
	cmp.w	#2,d0
	ble	.FIN
	move	d0,Hauteur(a0)	
	jsr	set_all_flags
	bsr	CLS_SCREEN
.FIN	rts

SIZE_X_DEC
	move.l	ACTIVE_WINDOW,a0
	move	Largeur(a0),d0
	subq	#1,d0
	cmp.w	#3,d0
	ble	.FIN
	move	d0,Largeur(a0)	
	jsr	set_all_flags
	bsr	CLS_SCREEN
.FIN	rts



SIZE_Y_INC	xref	set_all_flags
	;illegal
	move.l	ACTIVE_WINDOW,a0
	move	W_X1(a0),d0
	move	W_Y1(a0),d1
	move	d0,d2
	move	d1,d3
	add	Largeur(a0),d2
	add	Hauteur(a0),d3
	subq	#1,d2
	addq	#1,d3
	cmp	RESO_Y,d3
	bge	.FIN
	subq	#1,d3
	bsr	CHECK_OVERRIDE
	tst.b	d7
	bne.s	.FIN		
	addq	#1,Hauteur(a0)
	jsr	set_all_flags
	bsr	CLS_SCREEN
.FIN	rts

SIZE_X_INC	xref	set_all_flags
	move.l	ACTIVE_WINDOW,a0
	move	W_X1(a0),d0
	move	W_Y1(a0),d1
	move	d0,d2
	move	d1,d3
	add	Largeur(a0),d2
	add	Hauteur(a0),d3
	subq	#1,d3
	addq	#1,d2
	cmp	RESO_X,d2
	bge	.FIN
	subq	#1,d2
	bsr	CHECK_OVERRIDE
	tst.b	d7
	bne.s	.FIN		
	addq	#1,Largeur(a0)
	jsr	set_all_flags
	bsr	CLS_SCREEN
.FIN	rts



MOVE_UP		xref	set_all_flags
	move.l	ACTIVE_WINDOW,a0
	move	W_X1(a0),d0
	move	W_Y1(a0),d1
	subq	#1,d1
	beq.s	.FIN
	move	d0,d2
	move	d1,d3
	add	Largeur(a0),d2
	add	Hauteur(a0),d3
	subq	#1,d2
	subq	#1,d3
	bsr	CHECK_OVERRIDE
	tst.b	d7
	bne.s	.FIN		
	move	d1,W_Y1(a0)		
	jsr	set_all_flags
	bsr	CLS_SCREEN
.FIN	rts

MOVE_DOWN		xref	set_all_flags
	move.l	ACTIVE_WINDOW,a0
	move	W_X1(a0),d0
	move	W_Y1(a0),d1
	addq	#1,d1
	move	d1,d2
	add	Hauteur(a0),d2
	cmp	RESO_Y,d2
	bge	.FIN
	move	d0,d2
	move	d1,d3
	add	Largeur(a0),d2
	add	Hauteur(a0),d3
	subq	#1,d2
	subq	#1,d3
	bsr	CHECK_OVERRIDE
	tst.b	d7
	bne.s	.FIN		
	move	d1,W_Y1(a0)		
	jsr	set_all_flags
	bsr	CLS_SCREEN
		
.FIN	rts
MOVE_LEFT		xref	set_all_flags
	move.l	ACTIVE_WINDOW,a0
	move	W_X1(a0),d0
	move	W_Y1(a0),d1
	subq	#1,d0
	bmi.s	.FIN
	move	d0,d2
	move	d1,d3
	add	Largeur(a0),d2
	add	Hauteur(a0),d3
	subq	#1,d2
	subq	#1,d3
	bsr	CHECK_OVERRIDE
	tst.b	d7
	bne.s	.FIN		
	move	d0,W_X1(a0)		
		
	jsr	set_all_flags
	bsr	CLS_SCREEN
		
.FIN	rts

MOVE_RIGHT		xref	set_all_flags
	move.l	ACTIVE_WINDOW,a0
	move	W_X1(a0),d0
	move	W_Y1(a0),d1
	addq	#1,d0
	move	d0,d2
	add	Largeur(a0),d2
	cmp	RESO_X,d2
	bge	.FIN
	move	d0,d2
	move	d1,d3
	add	Largeur(a0),d2
	add	Hauteur(a0),d3
	subq	#1,d2
	subq	#1,d3
	bsr	CHECK_OVERRIDE
	tst.b	d7
	bne.s	.FIN		
	move	d0,W_X1(a0)		
		
	jsr	set_all_flags
	bsr	CLS_SCREEN
		
.FIN	rts


CHECK_OVERRIDE
;d0=X1 d1=y1 d2=x2 d3,y2
	
	lea	WINDOW_LIST,a1
.LOOP	move.l	(a1)+,d4
	beq.s	.FIN
	cmp.l	d4,a0
	beq.s	.LOOP
	move.l	d4,a2
	move	W_X1(a2),d4
	move	W_Y1(a2),d5
	cmp	d4,d2	
	blt	.LOOP	;x2<x1
	cmp	d5,d3
	blt	.LOOP	;y2<y1
	add	Largeur(a2),d4
	add	Hauteur(a2),d5
	subq	#1,d4
	subq	#1,d5
	cmp	d4,d0
	bgt	.LOOP	;x1>x2
	cmp	d5,d1
	bgt	.LOOP	;y1>y2
	st	d7
	rts
.FIN	sf	d7
	rts	
	
	
;**************************************
RUN
	envl	#'CMD'
	envl	#DSP_RUN
	bsr	REC_REGISTERS
; quelle est la cause de l'arret ?
	

	lea	WINDOW_LIST,a0	;met tt les flags
.LOOP	movE.l	(a0)+,d0
	beq.s	.FIN2
	move.l	d0,a1
	st	flag_aff(a1)
	bra.s	.LOOP
.FIN2
	
	rts
;**************************************
CTRLJ		xref	MESSAGE_ADR,ACTIVE_WINDOW
	move.l	ACTIVE_WINDOW,a0
	cmp.w	#T_disas,type(a0)	;Wactive= desas ?
	bne.S	.FIN			;OK
	
	move.l	adr_debut(a0),REG_PC


	bsr	SET_REGISTERS
	move.l	#MES_JMP,MESSAGE_ADR
	;bsr	REC_REGISTERS
	bsr	PC_IN_WINDOW

	lea	WINDOW_LIST,a0	;met tt les flags
.LOOP	movE.l	(a0)+,d0
	beq.s	.FIN2
	move.l	d0,a1
	st	flag_aff(a1)
	bra.s	.LOOP
.FIN2
	
.FIN	rts	
;**************************************
LOAD_LOD
	xref	READ_LOD
	lea	LOD_NAME,a0
	lea	WORK_BUF,a1
.LOOP0	move.b	(a0)+,(a1)+
	bne.s	.LOOP0
	
	lea	WORK_BUF,a0
	lea	SYMBOLS,a1
	bsr	READ_LOD
	envl	#'CMD'
	envl	#DSP_RECLOD
	recl	d0
	lea	WORK_BUF,a0
	move.l	(a0)+,d7	;nb de DATA
	envl	d7
	subq	#1,d7
	bmi.s	.FIN
.CMD	envl	(a0)+		;MEMOIRE
	envl	(a0)+		;debut
	move.l	(a0)+,d6	
	beq.s	.FIMEM
	envl	d6		;nombre
	subq	#1,d6
.SEND	envl	(a0)+
	dbra	d6,.SEND		
.FIMEM	dbra	d7,.CMD		
.FIN	bsr	REC_REGISTERS

	jsr	set_all_flags

	rts

;**************************************
TRACE		xref	MESSAGE_ADR,WINDOW_LIST
	envl	#'CMD'
	envl	#DSP_TRACE	
	move.l	#MES_TRACED,MESSAGE_ADR
 	bsr	REC_REGISTERS
	bsr	PC_IN_WINDOW
;place tt les FLAGS
	lea	WINDOW_LIST,a0
.LOOP	movE.l	(a0)+,d0
	beq.s	.FIN
	move.l	d0,a1
	st	flag_aff(a1)
	bra.s	.LOOP
.FIN	rts


PC_IN_WINDOW	xref	ACTIVE_WINDOW
	;move.l	ACTIVE_WINDOW,a1
	;cmp.w	#T_disas,type(a1)	;Wactive= desas ?
	;bne.S	.ok			;OK
	lea	W_DISAS,a1
	move.l	REG_PC,d0
	move.l	adr_debut(a1),d1
	cmp.l	d1,d0
	blt	.update
	cmp.l	adr_fin(a1),d0
	blt	.ok	
.update
	move.l	d0,adr_debut(a1)	
	st	flag_aff(a1)
.ok	
	rts
;**************************************
CTRL9		xref	ACTIVE_WINDOW
	
	move.l	ACTIVE_WINDOW,a0
	move	type(a0),d0
	cmp	#T_disas,d0
	beq.s	.OK
	cmp	#T_H24,d0
	beq.s	.OK
	cmp	#T_H48,d0
	beq.s	.OK
	cmp	#T_FRAC24,d0
	beq.s	.OK
	cmp	#T_HEXASCII24,d0
	beq.s	.OK
	rts
.OK	move.l	REG_PC,adr_debut(a0)
	st	flag_aff(a0)
	rts

SET_BKPT	xref	ACTIVE_WINDOW
	move.l	ACTIVE_WINDOW,a0
	cmp.w	#T_disas,type(a0)
	bne	.FIN
	st	flag_aff(a0)

	move.l	adr_debut(a0),d0
	clr	d1
	bsr	PUT_BKPT	

	bsr	SET_REGISTERS

	;envl	#'CMD'
	;envl	#DSP_BREAK
	;envl	adr_debut(a0)
	;recl	d0
	;tst	d0
	;bmi.s	.ERREUR
	;nop
.ERREUR	
	;	bsr	REC_REGISTERS
.FIN	rts	
 ;**************************************
RUN_AND_BREAK	
	xref	P_COUNT,DESAS_ONE,ACTIVE_WINDOW
	xref	ASCII_BUF,MASK_BUF
	
	move.l	ACTIVE_WINDOW,a0
	cmp.w	#T_disas,type(a0)
	bne	.FIN
	st	flag_aff(a0)

	envl	#'CMD'
	envl	#DSP_PDUMP
	move.l	REG_PC,d0
	move.l	d0,P_COUNT
	envl	d0
	envl	#2
	lea	WORK_BUF,a0
	recl	(a0)+
	recl	(a0)+
	subq	#8,a0
	lea	ASCII_BUF,a6	
	bsr	DESAS_ONE
;le break doit etre en P_COUNT
	move.l	P_COUNT,d0
	move	#-1,d1	;force remplaement BKP
	bsr	PUT_BKPT
	bsr	SET_REGISTERS
	

	;envl	#'CMD'
	;envl	#DSP_BREAK
	;envl	P_COUNT
	;recl	d0
	;tst	d0
	;bmi.s	.FIN
	;move	d0,RB_NB
	;bsr	REC_REGISTERS
	envl	#'CMD'
	envl	#DSP_RUN
	bsr	REC_REGISTERS
	move.l	#MES_RB,MESSAGE_ADR
	
.FIN	
	rts
RB_NB	dc	-1	;no du bkpt pour R&B
;**************************************
PUT_BKPT
;d0.l=adresse du bkpt	
;d1.w=flag force 	(remplace un bkp existant)
;		(sinon efface bkp existant)
;d'abord on recherche si il existe deja
	movem.l	a0/d7,-(sp)
	lea	TAB_BKPT,a0
	move	#16-1,d7
.LOOP	tst.l	(a0)
	beq.s	.OK0
	cmp.l	4(a0),d0
	beq.s	.FOUND
.OK0	add	#16,a0
	dbra	d7,.LOOP
;not found on s'installe au premier slot libre
	lea	TAB_BKPT,a0
	move	#16-1,d7
.LOOP2	tst.l	(a0)
	beq.s	.LIBRE
	add	#16,a0
	dbra	d7,.LOOP2
;plus de bkpt dispo !!!!

.FIN
	movem.l	(sp)+,a0/d7
	rts
.LIBRE	move.l	#-1,(a0)
	move.l	d0,4(a0)
	bra	.FIN
.FOUND	tst	d1		;le bkpt existe dÇja
	beq	.CLR
	move.l	d0,4(a0)	;remplace le BKPT
	bra	.FIN
.CLR	clr.l	(a0)		;efface le BKPT
	bra	.FIN
	

	


;**************************************
SKIP	
	xref	P_COUNT,DESAS_ONE,MESSAGE_ADR,set_all_flags

	envl	#'CMD'
	envl	#DSP_PDUMP
	envl	REG_PC
	envl	#2
	lea	WORK_BUF,a0
	recl	(a0)
	recl	4(a0)
	move.l	REG_PC,P_COUNT
	lea	ASCII_BUF,a6	
	bsr	DESAS_ONE
	move.l	P_COUNT,REG_PC
	bsr	SET_REGISTERS	
	jsr	set_all_flags
	move.l	#MES_SKIPED,MESSAGE_ADR
	rts
;**************************************
SCROLL_U_DUMP_VITE::
	move	nb_colonnes(a0),d0
	move	Hauteur(a0),d1
	mulu	d1,d0
	sub.l	d0,adr_debut(a0)
	rts
;**************************************
SCROLL_D_DUMP_VITE::
	move	nb_colonnes(a0),d0
	move	Hauteur(a0),d1
	mulu	d1,d0
	add.l	d0,adr_debut(a0)
	rts
;**************************************
;**************************************
SCROLL_U_DUMP::
	move.l	d0,-(sp)
	move	nb_colonnes(a0),d0
	ext.l	d0
	sub.l	d0,adr_debut(a0)
	move.l	(sp)+,d0
	rts
;**************************************
SCROLL_D_DUMP::
	move.l	d0,-(sp)
	move	nb_colonnes(a0),d0
	ext.l	d0
	add.l	d0,adr_debut(a0)
	move.l	(sp)+,d0
	rts
;**************************************
SCROLL_L_DUMP::
	subq.l	#1,adr_debut(a0)
	rts
;**************************************
SCROLL_R_DUMP::
	addq.l	#1,adr_debut(a0)
	rts
;**************************************


;**************************************
SCROLL_U_DISAS::	xref	P_COUNT,DESAS_ONE
	movem.l	d0-a6,-(sp)
	lea	WORK_BUF,a6
	envl	#'CMD'
	envl	#DSP_PDUMP
	subq.l	#2,adr_debut(a0)
	move.l	adr_debut(a0),d0
	envl	d0
	envl	#2
	recl	(a6)+
	recl	(a6)+
	move.l	a0,a1
	lea	-8(a6),a0
	lea	WORK_BUF+8,a6
	clr.l	P_COUNT	
	bsr	DESAS_ONE
	move.l	a1,a0
	cmp.l	#1,P_COUNT
	bne.s	.OK
	addq.l	#1,adr_debut(a0)
.OK	movem.l	(sp)+,d0-a6
	rts
;**************************************
SCROLL_U_DISAS_VITE::	
	move.l	d7,-(sp)
	move	Hauteur(a0),d7
	subq	#3,d7
	bmi.s	.FIN
.LOOP	bsr	SCROLL_U_DISAS
	dbra	d7,.LOOP	
.FIN	move.l	(sp)+,d7
	rts
;**************************************
SCROLL_D_DISAS::	xref	P_COUNT,DESAS_ONE
	movem.l	d0-a6,-(sp)
	lea	WORK_BUF,a6
	envl	#'CMD'
	envl	#DSP_PDUMP
	move.l	adr_debut(a0),d0
	envl	d0
	envl	#2	;2 mots Ö recevoir
	recl	(a6)+
	recl	(a6)+
	move.l	a0,a1
	lea	-8(a6),a0
	lea	WORK_BUF+8,a6
	clr.l	P_COUNT
	bsr	DESAS_ONE
	move.l	a1,a0
	addq.l	#1,adr_debut(a0)
	cmp.l	#1,P_COUNT
	beq.s	.OK
	addq.l	#1,adr_debut(a0)
.OK	movem.l	(sp)+,d0-a6
	rts
;**************************************
SCROLL_D_DISAS_VITE::	xref	P_COUNT,DESAS_ONE
	move.l	d7,-(sp)
	move	Hauteur(a0),d7
	subq	#3,d7
	bmi.s	.FIN
.LOOP	bsr	SCROLL_D_DISAS
	dbra	d7,.LOOP	
.FIN	
	move.l	(sp)+,d7
	rts
;**************************************
SCROLL_ASCII_LEFT::
	addq.l	#1,adr_debut(a0)
	rts
SCROLL_ASCII_RIGHT::
	subq.l	#1,adr_debut(a0)
	rts
SCROLL_ASCII_DOWN::
	movem.l	a1/d0,-(sp)
	move.l	adr_debut(a0),a1
.LOOP	move.b	(a1)+,d0
	beq.s	.OK
	cmp.b	#$d,d0
	beq.s	.OK
	cmp.b	#$a,d0
	beq.s	.OK
	bra.s	.LOOP		
.OK	move.b	(a1)+,d0
	beq.S	.OK	
	cmp.b	#$d,d0
	beq.S	.OK	
	cmp.b	#$a,d0
	beq.S	.OK	
	subq	#1,a1
	move.l	a1,adr_debut(a0)
	movem.l	(sp)+,a1/d0
	rts
SCROLL_ASCII_UP::
	movem.l	a1/d0,-(sp)
	move.l	adr_debut(a0),a1
.LOOP	move.b	-(a1),d0
	beq.s	.LOOP
	cmp.b	#$d,d0
	beq.s	.LOOP
	cmp.b	#$a,d0
	beq.s	.LOOP

.LOOP2	move.b	-(a1),d0
	beq.S	.OK	
	cmp.b	#$d,d0
	beq.S	.OK	
	cmp.b	#$a,d0
	beq.S	.OK	
	bra.s	.LOOP2
.OK
	addq	#1,a1	
	move.l	a1,adr_debut(a0)
	movem.l	(sp)+,a1/d0
	rts
;**************************************
	
	
	
	
	
	
;********************************************************************
; Voici les routines de gÇnÇration de texte
;elles remplissent le buffer ASCII
;********************************************************************
GEN_DISAS::	xref	DESAS
	movem.l	d0-a6,-(sp)
.AGAIN	
	lea	WORK_BUF,a6
	envl	#'CMD'
	envl	#DSP_PDUMP
	move.l	adr_debut(a0),d0
	envl	d0
	move.l	d0,(a6)+	;ADR DêBUT

	move	Hauteur(a0),d0
	subq	#2,d0
	move	d0,(a6)+	;NB de lignes Ö desassembler
	add	d0,d0		;2*nb_instr mots Ö recevoir
	ext.l	d0
	envl	d0
	subq	#1,d0
.LOOP	recl	(a6)+
	dbra	d0,.LOOP	
	lea	ASCII_BUF,a6

	move.l	a0,-(sp)
	lea	WORK_BUF,a0
	move.l	a0,a1
	bsr	DESAS	
	move.l	a1,a0
	move.l	(sp)+,a0
	move.l	P_COUNT,adr_fin(a0)
.OK	movem.l	(sp)+,d0-a6
	rts
;**************************************
;**************************************
GEN_H24::	
	xref	COL_NOR,COL_ADR
	movem.l	d0-a6,-(sp)
	envl	#'CMD'
	move	mem_type(a0),d0
	cmp.w	#'P',d0
	beq.s	.MP
	cmp.w	#'X',d0
	beq.s	.MX
	cmp.w	#'Y',d0
	beq.s	.MY
	bra	.FIN	
.MP	envl	#DSP_PDUMP
	bra	.SEND_OK
.MX	envl	#DSP_XDUMP
	bra	.SEND_OK
.MY	envl	#DSP_YDUMP
.SEND_OK
;*** calcul du nombre de colonnes
	envl	adr_debut(a0)
	moveq	#0,d6
	move	Largeur(a0),d6
	subq	#2+6,d6		;-2(bords) -6(adresse)
	bpl.s	.POS
	moveq	#0,d6
.POS	
	divu	#7,d6		;nb colonnes	_hhhhhh
	move	d6,nb_colonnes(a0)
	move	Hauteur(a0),d7
	subq	#2,d7		;-2(titre+bottom)
	ext.l	d7
	move	d7,d5
	mulu	d6,d5		;nb_word = nb_ligne*nb_colonnes
	envl	d5
	subq	#1,d7
	move.l	adr_debut(a0),d0
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5

.LOOP	move	COL_ADR,ATTRIBUTE
	move.l	d0,d1
	bsr	HEXA0_24
	move	COL_NOR,ATTRIBUTE
	move.l	#'    ',(a5)+
	move	#'  ',(a5)+
	move	d6,d5
	subq	#1,d5
	bmi.S	.GARG		;en thÇorie ca se peut pas, mais on assure
.LOOPVAL	
	recl	d1
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	bsr	HEXA0_24
	move.l	#'HHHH',(a5)+
	move	#'HH',(a5)+
	dbra	d5,.LOOPVAL
.GARG	move.b	#$d,(a5)+
	move	#$d,(a6)+
	moveq	#0,d1
	move	nb_colonnes(a0),d1
	add.l	d1,d0
	dbra	d7,.LOOP	
	move	#$8000,(a6)+
	clr.b	(a5)+
	move.l	d0,adr_fin(a0)
.FIN	movem.l	(sp)+,d0-a6
	rts
;****************************************


;****************************************
GEN_H48::	
	xref	COL_NOR,COL_ADR
	movem.l	d0-a6,-(sp)
	envl	#'CMD'
	move	mem_type(a0),d0
	;cmp.w	#'L',d0
	;bne	.FIN		;L obligÇ
	envl	#DSP_LDUMP
;*** calcul du nombre de colonnes
	envl	adr_debut(a0)
	moveq	#0,d6
	move	Largeur(a0),d6
	subq	#2+6,d6		;-2(bords) -6(adresse)
	bpl.s	.POS
	moveq	#0,d6
.POS	divu	#14,d6		;nb colonnes	_hhhhhh.hhhhhh
	move	d6,nb_colonnes(a0)
	move	Hauteur(a0),d7
	subq	#2,d7		;-2(titre+bottom)
	ext.l	d7
	move	d7,d5
	mulu	d6,d5		;nb_word = nb_ligne*nb_colonnes
	envl	d5
	subq	#1,d7
	move.l	adr_debut(a0),d0
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5

.LOOP	
	move	COL_ADR,ATTRIBUTE
	move.l	d0,d1
	bsr	HEXA0_24
	move	COL_NOR,ATTRIBUTE
	move.l	#'    ',(a5)+
	move	#'  ',(a5)+
	move	d6,d5
	subq	#1,d5
	bmi.S	.GARG		;en thÇorie ca se peut pas, mais on assure
.LOOPVAL	
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	recl	d1
	bsr	HEXA0_24
	move.l	#'HHHH',(a5)+
	move	#'HH',(a5)+
	move.b	#' ',(a5)+
	move.b	ATTRIBUTE,(a6)+
	move.b	#'.',(a6)+
	recl	d1
	bsr	HEXA0_24
	move.l	#'HHHH',(a5)+
	move	#'HH',(a5)+
	dbra	d5,.LOOPVAL
.GARG	move.b	#$d,(a5)+
	move	#$d,(a6)+
	moveq	#0,d1
	move	nb_colonnes(a0),d1
	add.l	d1,d0
	dbra	d7,.LOOP	
	move	#$8000,(a6)+
	clr.b	(a5)+
	move.l	d0,adr_fin(a0)
.FIN	movem.l	(sp)+,d0-a6
	rts
;**************************************
GEN_FRAC24::	
	xref	COL_NOR,COL_ADR
	xref	FRAC_24
	movem.l	d0-a6,-(sp)
	envl	#'CMD'
	move	mem_type(a0),d0
	cmp.w	#'P',d0
	beq.s	.MP
	cmp.w	#'X',d0
	beq.s	.MX
	cmp.w	#'Y',d0
	beq.s	.MY
	bra	.FIN	
.MP	envl	#DSP_PDUMP
	bra	.SEND_OK
.MX	envl	#DSP_XDUMP
	bra	.SEND_OK
.MY	envl	#DSP_YDUMP
.SEND_OK
;*** calcul du nombre de colonnes
	envl	adr_debut(a0)
	moveq	#0,d6
	move	Largeur(a0),d6
	subq	#2+6,d6		;-2(bords) -6(adresse)
	bpl.s	.POS
	moveq	#0,d6
.POS	divu	#14,d6		;nb colonnes 	_+0.0000000E+0	
	move	d6,nb_colonnes(a0)
	move	Hauteur(a0),d7
	subq	#2,d7		;-2(titre+bottom)
	ext.l	d7
	move	d7,d5
	mulu	d6,d5		;nb_word = nb_ligne*nb_colonnes
	envl	d5
	subq	#1,d7
	move.l	adr_debut(a0),d0
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5

.LOOP	
	move	COL_ADR,ATTRIBUTE
	move.l	d0,d1
	bsr	HEXA0_24
	move	COL_NOR,ATTRIBUTE
	move.l	#'    ',(a5)+
	move	#'  ',(a5)+
	move	d6,d5
	subq	#1,d5
	bmi.S	.GARG		;en thÇorie ca se peut pas, mais on assure
.LOOPVAL	
	recl	d1
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	bsr	FRAC_24
	move.l	#'S9.9',(a5)+
	move.l	#'9999',(a5)+
	move.w	#'9 ',(a5)+
	move	#'S9',(a5)+
	dbra	d5,.LOOPVAL
.GARG	move.b	#$d,(a5)+
	move	#$d,(a6)+
	moveq	#0,d1
	move	nb_colonnes(a0),d1
	add.l	d1,d0
	dbra	d7,.LOOP	
	move	#$8000,(a6)+
	clr.b	(a5)+
	move.l	d0,adr_fin(a0)
.FIN	movem.l	(sp)+,d0-a6
	rts
;****************************************
GEN_HEXASCII24::	xref	COL_NOR,COL_ADR,ATTRIBUTE
			xref	HEXA0_24
	movem.l	d0-a6,-(sp)
	envl	#'CMD'
	move	mem_type(a0),d0
	cmp.w	#'P',d0
	beq.s	.MP
	cmp.w	#'X',d0
	beq.s	.MX
	cmp.w	#'Y',d0
	beq.s	.MY
	bra	.FIN	
.MP	envl	#DSP_PDUMP
	bra	.SEND_OK
.MX	envl	#DSP_XDUMP
	bra	.SEND_OK
.MY	envl	#DSP_YDUMP
.SEND_OK
;*** calcul du nombre de colonnes
	envl	adr_debut(a0)
	moveq	#0,d6
	move	Largeur(a0),d6
	sub	#2+6,d6	;-2(bords) -6(adresse)
	bpl.s	.POS
	moveq	#0,d6
.POS	divu	#11,d6		;nb colonnes	_hhhhhh AAA
	move	d6,nb_colonnes(a0)
	move	Hauteur(a0),d7
	subq	#2,d7		;-2(titre+bottom)
	ext.l	d7
	move	d7,d5
	mulu	d6,d5		;nb_word = nb_ligne*nb_colonnes
	envl	d5
	subq	#1,d7
	move.l	adr_debut(a0),d0
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5
.LOOP	move	COL_ADR,ATTRIBUTE
	move.l	d0,d1
	bsr	HEXA0_24
	move	COL_NOR,ATTRIBUTE
	move.l	#'    ',(a5)+
	move	#'  ',(a5)+
	move	d6,d5
	subq	#1,d5
	bmi.S	.GARG		;en thÇorie ca se peut pas, mais on assure
	move	nb_colonnes(a0),d1
	mulu	#7,d1
	lea	(a5,d1.w),a3
	lea	(a6,d1.w*2),a4
.LOOPVAL	
	recl	d1
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	bsr	HEXA0_24
	move.l	#'HHHH',(a5)+
	move	#'HH',(a5)+
	move.l	#' AAA',(a3)+
	move	#' ',(a4)+
	bset	#5,ATTRIBUTE	;affiche binaire
	swap	d1
	move.b	ATTRIBUTE,(a4)+
	move.b	d1,(a4)+
	rol.l	#8,d1	
	move.b	ATTRIBUTE,(a4)+
	move.b	d1,(a4)+
	rol.l	#8,d1	
	move.b	ATTRIBUTE,(a4)+
	move.b	d1,(a4)+
	bclr	#5,ATTRIBUTE	;affiche normal
	dbra	d5,.LOOPVAL
	move.l	a4,a6
	move.l	a3,a5
.GARG	move.b	#$d,(a5)+
	move	#$d,(a6)+
	add	nb_colonnes(a0),d0
	dbra	d7,.LOOP	
	move	#$8000,(a6)+
	clr.b	(a5)+
	move.l	d0,adr_fin(a0)
.FIN	movem.l	(sp)+,d0-a6
	rts
;****************************************
;****************************************


GEN_SS::
	xref	HEXA0_5,HEXA0_16,COL_NOR,COL_ADR
	movem.l	a0/a6/d1/d6/d7,-(sp)
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5
	lea	REG_SS,a0
	move	#15-1,d7
	move	#0,d6
.LOOP	
	move	COL_ADR,ATTRIBUTE
	move	d6,d1
	bsr	HEXA0_5
	move.l	#'    ',(a5)+
	move	#'  ',(a5)+
	move	COL_NOR,ATTRIBUTE	
	
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	move.l	(a0)+,d1
	bsr	HEXA0_24
	move.b	ATTRIBUTE,(a6)+
	move.b	#':',(a6)+
	move.l	(a0)+,d1
	bsr	HEXA0_24
	move.l	#'HHHH',(a5)+
	move.l	#'HH:H',(a5)+
	move.l	#'HHHH',(a5)+
	move.b	#'H',(a5)+
	move.b	#$d,(a5)+
	move	#$d,(a6)+
	addq	#1,d6
	dbra	d7,.LOOP
	clr.b	(a5)+
	move	#$8000,(a6)+
	movem.l	(sp)+,a0/a6/d1/d6/d7
	rts
;**************************************
GEN_REG::
	xref	HEXA0_16,HEXA0_8,HEXA0_24,COL_CHG,HEXA0_4
	xref	ATTRIBUTE
	
	movem.l	d0-a6,-(sp)
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5
	move	COL_NOR,ATTRIBUTE

	lea	REG_R0,a0
	lea	OLD_REG_R0,a1
	move.b	#'0',d7
	bsr	PUTRNM
	cpy	<'  SP:'>,a6
	lea	REG_SP,a0
	lea	OLD_REG_SP,a1
	bsr	C_HEXA0_16
	cpy.b	<'         '>,a5
	
	move	#$d,(a6)+	;R0:hhhhhh N0:hhhhhh M0:hhhhhh  SP:hhhhhh
	move.b	#$d,(a5)+
	
	lea	REG_R1,a0
	lea	OLD_REG_R1,a1
	move.b	#'1',d7
	bsr	PUTRNM
	cpy	<' SSH:'>,a6
	lea	REG_SSH,a0
	lea	OLD_REG_SSH,a1
	bsr	C_HEXA0_24
	cpy.b	<'           '>,a5

	cpy	<' SSL:'>,a6
	lea	REG_SSL,a0
	lea	OLD_REG_SSL,a1
	bsr	C_HEXA0_24
	cpy.b	<'           '>,a5
	move	#$d,(a6)+	;R1:hhhhhh N1:hhhhhh M1:hhhhhh SSH:hhhhhh SSL:hhhhhh
	move.b	#$d,(a5)+

	lea	REG_R2,a0
	lea	OLD_REG_R2,a1
	move.b	#'2',d7
	bsr	PUTRNM
	cpy	<'  LA:'>,a6
	lea	REG_LA,a0
	lea	OLD_REG_LA,a1
	bsr	C_HEXA0_24
	cpy.b	<'           '>,a5
	cpy	<'  LC:'>,a6
	lea	REG_LC,a0
	lea	OLD_REG_LC,a1
	bsr	C_HEXA0_24
	cpy.b	<'           '>,a5
	move	#$d,(a6)+	;R2:hhhhhh N2:hhhhhh M2:hhhhhh  LA:hhhhhh  LC:hhhhhh
	move.b	#$d,(a5)+

	lea	REG_R3,a0
	lea	OLD_REG_R3,a1
	move.b	#'3',d7
	bsr	PUTRNM
	move	#$d,(a6)+	;R3:hhhhhh N3:hhhhhh M3:hhhhhh
	move.b	#$d,(a5)+

	lea	REG_R4,a0
	lea	OLD_REG_R4,a1
	move.b	#'4',d7
	bsr	PUTRNM
	cpy	<'   A:'>,a6
	lea	REG_A2,a0
	lea	OLD_REG_A2,a1
	bsr	C_HEXA0_8
	cpy	':',a6
	lea	REG_A1,a0
	lea	OLD_REG_A1,a1
	bsr	C_HEXA0_24
	cpy	':',a6
	lea	REG_A0,a0
	lea	OLD_REG_A0,a1
	bsr	C_HEXA0_24
	cpy.b	<'      HH HHHHHH HHHHHH'>,a5
	move	#$d,(a6)+	;R4:hhhhhh N4:hhhhhh M4:hhhhhh A=hh:hhhhhh:hhhhhh
	move.b	#$d,(a5)+

	lea	REG_R5,a0
	lea	OLD_REG_R5,a1
	move.b	#'5',d7
	bsr	PUTRNM
	cpy	<'   A:'>,a6
	lea	REG_B2,a0
	lea	OLD_REG_B2,a1
	bsr	C_HEXA0_8
	cpy	':',a6
	lea	REG_B1,a0
	lea	OLD_REG_B1,a1
	bsr	C_HEXA0_24
	cpy	':',a6
	lea	REG_B0,a0
	lea	OLD_REG_B0,a1
	bsr	C_HEXA0_24
	cpy.b	<'      HH HHHHHH HHHHHH'>,a5
	move	#$d,(a6)+	;R5:hhhhhh N5:hhhhhh M5:hhhhhh B=hh:hhhhhh:hhhhhh
	move.b	#$d,(a5)+

	lea	REG_R6,a0
	lea	OLD_REG_R6,a1
	move.b	#'6',d7
	bsr	PUTRNM
	cpy	<'   X:   '>,a6
	lea	REG_X1,a0
	lea	OLD_REG_X1,a1
	bsr	C_HEXA0_24
	cpy	':',a6
	lea	REG_X0,a0
	lea	OLD_REG_X0,a1
	bsr	C_HEXA0_24
	cpy.b	<'        HHHHHH HHHHHH'>,a5
	move	#$d,(a6)+	;R6:hhhhhh N6:hhhhhh M6:hhhhhh X= hhhhhh:hhhhhh
	move.b	#$d,(a5)+

	lea	REG_R7,a0
	lea	OLD_REG_R7,a1
	move.b	#'6',d7
	bsr	PUTRNM
	cpy	<'   Y:   '>,a6
	lea	REG_Y1,a0
	lea	OLD_REG_Y1,a1
	bsr	C_HEXA0_24
	cpy	':',a6
	lea	REG_Y0,a0
	lea	OLD_REG_Y0,a1
	bsr	C_HEXA0_24
	cpy.b	<'        HHHHHH HHHHHH'>,a5
	move	#$d,(a6)+	;R7:hhhhhh N7:hhhhhh M7:hhhhhh Y= hhhhhh:hhhhhh
	move.b	#$d,(a5)+
	
	cpy	'PC:',a6
	lea	REG_PC,a0
	lea	OLD_REG_PC,a1
	bsr	C_HEXA0_24
	cpy.b	<'   HHHHHH'>,a5
	
	cpy	<' ( '>,a6
	lea	WORK_BUF,a1
	envl	#'CMD'
	envl	#DSP_PDUMP
	envl	REG_PC
	envl	#2			;2 mots Ö recevoir
	recl	(a1)+
	recl	(a1)+
	move.l	a0,a1
	lea	WORK_BUF,a0
	move.l	REG_PC,P_COUNT
	bsr	DESAS_ONE
	cpy	<' )'>,a6		;PC:hhhhhh ( JMP $40 )
	move	#$d,(a6)+
	move.b	#$d,(a5)+
	
	cpy	'SR:',a6
	lea	REG_SR,a0
	lea	OLD_REG_SR,a1
	bsr	C_HEXA0_24

	bsr	PUTCCR
	bsr	PUTEMR

	move	#$d,(a6)+		;SR:hhhhhh CCR:SLEUNZVC
	move.b	#$d,(a5)+
.FIN	move	#$8000,(a6)+
	clr.b	(a5)+
	movem.l	(sp)+,d0-a6
	rts

PUTEMR	move.l	REG_SR,d0
	move	COL_NOR,ATTRIBUTE
	cpy	<' EMR: CP'>,a6
	bfextu	d0{8:2},d1
	bsr	HEXA0_4		;core priority
	
	move.b	#grisF,ATTRIBUTE
	btst	#21,d0
	beq.s	.OK0
	move	COL_NOR,ATTRIBUTE
.OK0	cpy	<' RM'>,a6	;Rounding Mode



	move.b	#grisF,ATTRIBUTE
	btst	#20,d0
	beq.s	.OK1
	move	COL_NOR,ATTRIBUTE
.OK1	cpy	<' SM'>,a6	;arithemtic Saturation

	move.b	#grisF,ATTRIBUTE
	btst	#19,d0
	beq.s	.OK2
	move	COL_NOR,ATTRIBUTE
.OK2	cpy	<' CE'>,a6	;Cache Enable

	move.b	#grisF,ATTRIBUTE
	btst	#17,d0
	beq.s	.OK3
	move	COL_NOR,ATTRIBUTE
.OK3	cpy	<' SA'>,a6	;16 bi Arithmetic

	move.b	#grisF,ATTRIBUTE
	btst	#16,d0
	beq.s	.OK4
	move	COL_NOR,ATTRIBUTE
.OK4	cpy	<' FV'>,a6	;do forever flag

	move.b	#grisF,ATTRIBUTE
	btst	#15,d0
	beq.s	.OK5
	move	COL_NOR,ATTRIBUTE
.OK5	cpy	<' LV'>,a6	;do loop flag

	move.b	#grisF,ATTRIBUTE
	btst	#14,d0
	beq.s	.OK6
	move	COL_NOR,ATTRIBUTE
.OK6	cpy	<' DM'>,a6	;double precision mode

	move.b	#grisF,ATTRIBUTE
	btst	#13,d0
	beq.s	.OK7
	move	COL_NOR,ATTRIBUTE
.OK7	cpy	<' SC'>,a6	;16 bit compatibility
	
	move	COL_NOR,ATTRIBUTE
	cpy	<' S'>,a6
	move.b	COL_NOR,(a6)+	
	bfextu	d0{20:2},d1
	cmp.b	#0,d1
	beq.s	.DNO
	cmp.b	#1,d1
	beq.s	.DNO0
	move.b	#'',(a6)+
	bra.s	.OK8
.DNO0	move.b	#'',(a6)+
	bra.s	.OK8
.DNO	move.b	#'-',(a6)+

.OK8	move	COL_NOR,ATTRIBUTE
	cpy	<' I'>,a6
	bfextu	d0{22:2},d1
	bsr	HEXA0_4
	rts
PUTCCR
	cpy	<' CCR:'>,a6
	move.l	REG_SR,d0
	lea	.LISTCCR,a0
	move	#7,d7
.LOOPCCR
	move	COL_NOR,d1
	btst	d7,d0
	bne.s	.S1
	move	#grisF*256,d1
.S1	move.b	(a0)+,d1
	move	d1,(a6)+
	dbra	d7,.LOOPCCR
	rts
	
.LISTCCR	dc.b	'SLEUNZVC'	

PUTRNM	xref	ATTRIBUTE
	move	COL_NOR,ATTRIBUTE
	cpy	<'R0:'>,a6
	move.b	d7,-3(a6)
	bsr	C_HEXA0_24	;r0:hhhhhh
	move.l	#'    ',(a5)+
	subq	#1,a5
	move.l	#'HHHH',(a5)+	
	move	#'HH',(a5)+	
	
	cpy	<' N0:'>,a6	
	move.b	d7,-3(a6)
	bsr	C_HEXA0_24	;n0:hhhhhh
	move.l	#'    ',(a5)+
	move.l	#'HHHH',(a5)+	
	move	#'HH',(a5)+	

	cpy	<' M0:'>,a6	
	move.b	d7,-3(a6)
	bsr	C_HEXA0_24	;m0:hhhhhh
	move.l	#'    ',(a5)+
	move.l	#'HHHH',(a5)+	
	move	#'HH',(a5)+	
	
	rts
	
C_HEXA0_24
	move	COL_NOR,ATTRIBUTE
	move.l	(a0)+,d1
	cmp.l	(a1)+,d1
	beq.s	.SAME0
	move	COL_CHG,ATTRIBUTE
.SAME0	bsr	HEXA0_24	
	move	COL_NOR,ATTRIBUTE
	rts
C_HEXA0_16
	move	COL_NOR,ATTRIBUTE
	move.l	(a0)+,d1
	cmp.l	(a1)+,d1
	beq.s	.SAME0
	move	COL_CHG,ATTRIBUTE
.SAME0	bsr	HEXA0_16	
	move	COL_NOR,ATTRIBUTE
	rts
C_HEXA0_8
	move	COL_NOR,ATTRIBUTE
	move.l	(a0)+,d1
	cmp.l	(a1)+,d1
	beq.s	.SAME0
	move	COL_CHG,ATTRIBUTE
.SAME0	bsr	HEXA0_8
	move	COL_NOR,ATTRIBUTE
	rts

	
			

;***********************************
GEN_HEXASCII16::	
	xref	COL_NOR,COL_ADR,HEXA0_16,HEXA0_32
	movem.l	d0-a6,-(sp)
;*** calcul du nombre de colonnes
	move.l	adr_debut(a0),a1
	moveq	#0,d6
	move	Largeur(a0),d6
	sub	#2+8+1+1,d6	;-2(bords) -8(adresse) -1 espace bin/ascii +1 (espace impaire)
	bpl.s	.POS
	moveq	#0,d6
.POS	divu	#7,d6		;nb colonnes	'_hhhh' +'AA'
	move	d6,nb_colonnes(a0)
	move	Hauteur(a0),d7
	subq	#2,d7		;-2(titre+bottom)
	ext.l	d7
	move	d7,d5
	mulu	d6,d5		;nb_word = nb_ligne*nb_colonnes
	subq	#1,d7
	move.l	adr_debut(a0),d0
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5
.LOOP	
	move	nb_colonnes(a0),d1
	mulu	#5,d1		;pointe sur l'ascii
	lea	10(a5,d1.w),a3
	lea	20(a6,d1.w*2),a4

	move	COL_ADR,ATTRIBUTE
	move.l	a1,d1
	bsr	HEXA0_32
	move	COL_NOR,ATTRIBUTE
	move.l	#'    ',(a5)+
	move.l	#'    ',(a5)+
	move	d6,d5
	subq	#1,d5
	bmi.S	.GARG		;en thÇorie ca se peut pas, mais on assure
	move	d1,d0
	btst	#0,d0
	bne.s	.IMPAIRE
.LOOPVAL	
	move	(a1),d1
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	
	bsr	HEXA0_16	;space+binaire
	move.l	#'HHHH',(a5)+
		
	bset	#5,ATTRIBUTE	;affiche binaire
	
	rept	2
	move.b	ATTRIBUTE,(a4)+
	move.b	(a1)+,(a4)+
	endr
	move	#'AA',(a3)+
	bclr	#5,ATTRIBUTE	;affiche normal
	dbra	d5,.LOOPVAL
	move.l	#'    ',(a6)+
	move	#'  ',(a5)+	
	move.l	a4,a6
	move.l	a3,a5
.GARG	move.b	#$d,(a5)+
	move	#$d,(a6)+
	add	nb_colonnes(a0),d0
	dbra	d7,.LOOP	
	move	#$8000,(a6)+
	clr.b	(a5)+
	move.l	d0,adr_fin(a0)
.FIN	movem.l	(sp)+,d0-a6
	rts
.IMPAIRE
	move.b	(a1),d1
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	bsr	HEXA0_8	;space+binaire
	move	#'HH',(a5)+
	bset	#5,ATTRIBUTE	;affiche binaire
	move.b	ATTRIBUTE,(a4)+
	move.b	(a1)+,(a4)+
	move.b	#'A',(a3)+
	bclr	#5,ATTRIBUTE	;affiche normal
	subq	#1,d5
	bmi.s	.GARG
.LOOPVAL2	
	move	(a1),d1
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	bsr	HEXA0_16	;space+binaire
	move.l	#'HHHH',(a5)+
	bset	#5,ATTRIBUTE	;affiche binaire
	rept	2
	move.b	ATTRIBUTE,(a4)+
	move.b	(a1)+,(a4)+
	endr
	move	#'AA',(a3)+
	bclr	#5,ATTRIBUTE	;affiche normal
	dbra	d5,.LOOPVAL2
	move.b	(a1),d1
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	bsr	HEXA0_8	;space+binaire
	move	#'HH',(a5)+
	bset	#5,ATTRIBUTE	;affiche binaire
	move.b	ATTRIBUTE,(a4)+
	move.b	(a1)+,(a4)+
	move.b	#'A',(a3)+
	bclr	#5,ATTRIBUTE	;affiche normal
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	move.l	a4,a6
	move.l	a3,a5

	bra	.GARG	


;**************************************************
GEN_HEXASCII16_INV::	
;transfert de la fenetre => RAM
	movem.l	d0-a6,-(sp)
	tst	key_valid	
	beq	.FIN		;y a t il eu saisie ?
	
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5
	move.l	adr_debut(a0),a1
	move	Hauteur(a0),d7
	subq	#3,d7
	move	a1,d1
	btst	#0,d1
	bne	HA_INV_IMPAIRE
	cmp.b	#'A',MODE_EDIT
	beq	.ASCC
.LOOP	add	#8*2,a6	;adresse
	move	nb_colonnes(a0),d5
	subq	#1,d5	;
.COLONNE
	addq	#2,a6	;'_'	
	moveq	#0,d1
	move	#4-1,d6
.CONV	lsl.l	#4,d1
.ADD	move.w	(a6)+,d0
	and	#$ff,d0
	cmp.b	#'9',d0
	ble	.NUM
	sub.b	#'A'-10,d0
	bra	.GO
.NUM	sub.b	#'0',d0
.GO	or.b	d0,d1
	dbra	d6,.CONV
	move	d1,(a1)+
	dbra	d5,.COLONNE	
.SC	cmp	#$d,(a6)+
	bne.s	.SC
	dbra	d7,.LOOP	
	bra	.FIN
.ASCC	move	nb_colonnes(a0),d0
	mulu	#5*2,d0
	add	d0,a6
	add	#20,a6	;adresse+space
	move	nb_colonnes(a0),d5
	subq	#1,d5	;
.COLONNE2
	rept	2
	addq	#1,a6
	move.b	(a6)+,(a1)+
	endr
	dbra	d5,.COLONNE2
.SC2	cmp	#$d,(a6)+
	bne.s	.SC2
	dbra	d7,.ASCC		

.FIN	
.END	movem.l	(sp)+,d0-a6
	rts

HA_INV_IMPAIRE
	cmp.b	#'A',MODE_EDIT
	beq	.ASCC
.LOOP	add	#8*2,a6	;adresse
	move	nb_colonnes(a0),d5

	addq	#2,a6	;'_'	
	move	#2-1,d6
	BSR	CONV_A2H
	MOVE.B	D1,(A1)+		
	subq	#2,d5	;
	BMI.S	.OK0
.COLONNE
	addq	#2,a6	;'_'	
	move	#4-1,d6
	BSR	CONV_A2H
	move	d1,(a1)+
	dbra	d5,.COLONNE	
.OK0	addq	#2,a6	;'_'	
	move	#2-1,d6
	BSR	CONV_A2H
	MOVE.B	D1,(A1)+		
.SC	cmp	#$d,(a6)+
	bne.s	.SC
	dbra	d7,.LOOP	
	bra	.FIN

.ASCC	move	nb_colonnes(a0),d0
	mulu	#5*2,d0
	add	d0,a6
	add	#20,a6	;adresse+space
	move	nb_colonnes(a0),d5

	addq	#1,a6
	move.b	(a6)+,(a1)+
	subq	#2,d5	;
	BMI.S	.OK1
.COLONNE2
	rept	2
	addq	#1,a6
	move.b	(a6)+,(a1)+
	endr
	dbra	d5,.COLONNE2
.OK1	addq	#1,a6
	move.b	(a6)+,(a1)+
.SC2	cmp	#$d,(a6)+
	bne.s	.SC2
	dbra	d7,.ASCC		
.FIN	
.END	movem.l	(sp)+,d0-a6
	rts
;**************************************************
;****************************************

GEN_ASCII::	
;affiche en ASCII cr,lf et 0 font mise Ö la ligne
	xref	COL_NOR,COL_ADR
	movem.l	d0-a6,-(sp)
	move.l	adr_debut(a0),a1
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5
	move	COL_NOR,d0
	;bset	#5+8,d0
	move	Hauteur(a0),d7
	subq	#3,d7
.LOOP	
	move	Largeur(a0),d6
	subq	#3,d6
.XLOOP		
	move.b	(a1)+,d0
	beq.s	.CR
	cmp.b	#$d,d0
	beq.s	.CR
	cmp.b	#$a,d0
	beq.s	.CR
	move	d0,(a6)+
	move.b	#'A',(a5)+
	dbra	d6,.XLOOP
	dbra	d7,.LOOP	
	move	#$8000,(a6)+
	clr.b	(a5)+
.FIN	move.l	a1,adr_fin(a0)
	movem.l	(sp)+,d0-a6
	rts
.CR	cmp.b	#$d,-1(a6)	;un seul retour chario Ö la suite...
	beq.s	.XLOOP
	cmp.b	#$a,-1(a6)
	beq.s	.XLOOP
	tst	-1(a6)
	beq.s	.XLOOP
	move	#$d,(a6)+
	move.b	#$d,(a5)+
	bra	.XLOOP	


	
;**************************************
;**************************************
;*** LES ROUTINES INVERSES !!!!!   ****
;**************************************
;**************************************


;****************************************
GEN_H24_INV::	
	movem.l	d0-a6,-(sp)
	lea	ASCII_BUF,a6
	envl	#'CMD'
	move	mem_type(a0),d0
	cmp.w	#'P',d0
	beq.s	.MP
	cmp.w	#'X',d0
	beq.s	.MX
	cmp.w	#'Y',d0
	beq.s	.MY
	bra	.FIN	
.MP	envl	#DSP_P2DSP
	bra	.SEND_OK
.MX	envl	#DSP_X2DSP
	bra	.SEND_OK
.MY	envl	#DSP_Y2DSP
.SEND_OK
	move.l	adr_debut(a0),d0
	envl	d0		;ADR_DEBUT

	move	Hauteur(a0),d7
	subq	#2,d7
	ext.l	d7
	move	d7,d6
	mulu	nb_colonnes(a0),d6
	envl	d6
	subq	#1,d7
.LOOP	add	#6*2,a6	;adresse
	move	nb_colonnes(a0),d5
	subq	#1,d5	;;
.COLONNE
	addq	#2,a6	;espace	
	moveq	#0,d1
	move	#6-1,d6
.CONV	lsl.l	#4,d1
.ADD	move.w	(a6)+,d0
	and	#$ff,d0
	cmp.b	#'9',d0
	ble	.NUM
	sub.b	#'A'-10,d0
	bra	.GO
.NUM	sub.b	#'0',d0
.GO	or.b	d0,d1
	dbra	d6,.CONV
	envl	d1
	dbra	d5,.COLONNE	
.SC	cmp	#$d,(a6)+
	bne.s	.SC
	dbra	d7,.LOOP	
.FIN	
	movem.l	(sp)+,d0-a6
	rts
;**************************************************
GEN_H48_INV::	
	
	xref	key_valid,MODE_EDIT
	movem.l	d0-a6,-(sp)
	tst	key_valid
	beq	.FIN
	lea	ASCII_BUF,a6
	envl	#'CMD'
	envl	#DSP_L2DSP
	move.l	adr_debut(a0),d0
	envl	d0		;ADR_DEBUT

	move	Hauteur(a0),d7
	subq	#2,d7
	ext.l	d7
	move	d7,d6
	mulu	nb_colonnes(a0),d6
	envl	d6
	subq	#1,d7
.LOOP	add	#6*2,a6	;adresse
	move	nb_colonnes(a0),d5
	add	d5,d5
	subq	#1,d5	;;
.COLONNE
	addq	#2,a6	;espace	
	moveq	#0,d1
	move	#6-1,d6
.CONV	lsl.l	#4,d1
.ADD	move.w	(a6)+,d0
	and	#$ff,d0
	cmp.b	#'9',d0
	ble	.NUM
	sub.b	#'A'-10,d0
	bra	.GO
.NUM	sub.b	#'0',d0
.GO	or.b	d0,d1
	dbra	d6,.CONV
	envl	d1
	dbra	d5,.COLONNE	
.SC	cmp	#$d,(a6)+
	bne.s	.SC
	dbra	d7,.LOOP	
.FIN	
	movem.l	(sp)+,d0-a6
	rts
;**************************************************
GEN_HEXASCII24_INV::	
;transfert de la fenetre => RAM
;et recalcul de POSX POSY si le mode_edite Ö changÇ ( HEXA => ASCII ou l'inverse )
	movem.l	d0-a6,-(sp)
	tst	key_valid	
	beq	.FIN		;y a t il eu saisie ?
	
	envl	#'CMD'
	move	mem_type(a0),d0
	cmp.w	#'P',d0
	beq.s	.MP
	cmp.w	#'X',d0
	beq.s	.MX
	cmp.w	#'Y',d0
	beq.s	.MY
	bra	.FIN	
.MP	envl	#DSP_P2DSP
	bra	.SEND_OK
.MX	envl	#DSP_X2DSP
	bra	.SEND_OK
.MY	envl	#DSP_Y2DSP
.SEND_OK
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5
	move.l	adr_debut(a0),d0
	envl	d0		;ADR_DEBUT

	move	Hauteur(a0),d7
	subq	#2,d7
	ext.l	d7
	move	d7,d6
	mulu	nb_colonnes(a0),d6
	envl	d6
	subq	#1,d7
	cmp.b	#'A',MODE_EDIT
	beq	.ASCC
.LOOP	add	#6*2,a6	;adresse
	move	nb_colonnes(a0),d5
	subq	#1,d5	;
.COLONNE
	addq	#2,a6	;'_'	
	moveq	#0,d1
	move	#6-1,d6
.CONV	lsl.l	#4,d1
.ADD	move.w	(a6)+,d0
	and	#$ff,d0
	cmp.b	#'9',d0
	ble	.NUM
	sub.b	#'A'-10,d0
	bra	.GO
.NUM	sub.b	#'0',d0
.GO	or.b	d0,d1
	dbra	d6,.CONV
	envl	d1
	dbra	d5,.COLONNE	
.SC	cmp	#$d,(a6)+
	bne.s	.SC
	dbra	d7,.LOOP	
	bra	.FIN
.ASCC	move	nb_colonnes(a0),d0
	mulu	#7*2,d0
	add	d0,a6
	add	#12,a6
	move	nb_colonnes(a0),d5
	subq	#1,d5	;
.COLONNE2
	addq	#2,a6	;'_'
	move.b	1(a6),d1
	lsl.l	#8,d1
	move.b	3(a6),d1
	lsl.l	#8,d1
	move.b	5(a6),d1
	addq	#6,a6
	envl	d1
	dbra	d5,.COLONNE2
.SC2	cmp	#$d,(a6)+
	bne.s	.SC2
	dbra	d7,.ASCC		

.FIN	

.END	movem.l	(sp)+,d0-a6
	rts
;**************************************************
GEN_FRAC24_INV::	
	rts
;****************************************
GEN_REG_INV::	
	movem.l	d0-a6,-(sp)
	lea	ASCII_BUF,a6

	move	Hauteur(a0),d7
	subq	#2,d7
	cmp	#8,d7
	ble	.OK00
	move	#8,d7
.OK00	subq	#1,d7
	lea	REG_R0,a1
.LOOP	add	#6,a6
	bsr	.A2REG	
	add	#8,a6
	bsr	.A2REG	
	add	#8,a6
	bsr	.A2REG	
.CR	cmp	#$d,(a6)+
	bne.s	.CR
	dbra	d7,.LOOP	
.FIN	
	movem.l	(sp)+,d0-a6
	rts
.A2REG
	moveq	#0,d1
	move	#6-1,d6
.CONV	lsl.l	#4,d1
.ADD	move.w	(a6)+,d0
	and	#$ff,d0
	cmp.b	#'9',d0
	ble	.NUM
	sub.b	#'A'-10,d0
	bra	.GO
.NUM	sub.b	#'0',d0
.GO	or.b	d0,d1
	dbra	d6,.CONV
	move.l	d1,(a1)+
	rts
			
	
;**************************************************
GEN_H16::
	xref	COL_NOR,COL_ADR,ATTRIBUTE,HEXA0_32,HEXA0_16,HEXA0_8
	movem.l	d0-a6,-(sp)
;*** calcul du nombre de colonnes
	move.l	adr_debut(a0),a1
	moveq	#0,d6
	move	Largeur(a0),d6
	sub	#2+8+1,d6		;-2(bords) -8(adresse)-1(space)
	bpl.s	.POS
	moveq	#0,d6
.POS	divu	#5,d6		;nb colonnes	_hhhh
	move	d6,nb_colonnes(a0)
	move	Hauteur(a0),d7
	subq	#3,d7		;-2(titre+bottom)
	lea	ASCII_BUF,a6
	lea	MASK_BUF,a5

.LOOP	move	COL_ADR,ATTRIBUTE
	move.l	a1,d1
	bsr	HEXA0_32
	move	COL_NOR,ATTRIBUTE
	move.l	#'    ',(a5)+
	move.l	#'    ',(a5)+
	move	d6,d5
	subq	#1,d5
	bmi.S	.GARG		;en thÇorie ca se peut pas, mais on assure
	move	a1,d0
	btst	#0,d0
	bne.s	.IMPAIRE
.LOOPVAL	
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	move	(a1)+,d1
	bsr	HEXA0_16
	move.l	#'HHHH',(a5)+
	dbra	d5,.LOOPVAL
.GARG	move.b	#' ',(a5)+
	move.b	#$d,(a5)+
	move	#' ',(a6)+
	move	#$d,(a6)+
	add	nb_colonnes(a0),d0
	dbra	d7,.LOOP	
	move	#$8000,(a6)+
	clr.b	(a5)+
	move.l	a1,adr_fin(a0)
.FIN	movem.l	(sp)+,d0-a6
	rts
.IMPAIRE
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	move.b	(a1)+,d1
	bsr	HEXA0_8
	move	#'HH',(a5)+
	subq	#1,d5
	bmi.s	.GARG
.LOOPVAL2
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	move	(a1)+,d1
	bsr	HEXA0_16
	move.l	#'HHHH',(a5)+
	dbra	d5,.LOOPVAL2
	move	#' ',(a6)+
	move.b	#' ',(a5)+
	move.b	(a1)+,d1
	bsr	HEXA0_8
	move	#'HH',(a5)+
	bra.s	.GARG
;**************************************
GEN_H16_INV::	
	xref	ASCII_BUF
	movem.l	d0-a6,-(sp)
	lea	ASCII_BUF,a6
	move.l	adr_debut(a0),a1
	move	Hauteur(a0),d7
	subq	#3,d7
.LOOP	add	#8*2,a6	;adresse
	move	nb_colonnes(a0),d5
	subq	#1,d5	;;
	move.l	a1,d0
	btst	#0,d0
	bne.s	.IMPAIRE
.COLONNE
	addq	#2,a6	;espace	
	move	#4-1,d6
	bsr	CONV_A2H
	move	d1,(a1)+
	dbra	d5,.COLONNE	
.SC	cmp	#$d,(a6)+
	bne.s	.SC
	dbra	d7,.LOOP	
.FIN	
	movem.l	(sp)+,d0-a6
	rts
.IMPAIRE	
	addq	#2,a6	;espace	
	move	#2-1,d6
	bsr	CONV_A2H
	move.b	d1,(a1)+
	subq	#1,d5
	bmi.s	.SC0
.COLONNE2	
	addq	#2,a6	;espace	
	move	#4-1,d6
	bsr	CONV_A2H
	move.w	d1,(a1)+
	dbra	d5,.COLONNE2	
.SC0	addq	#2,a6	;espace	
	move	#2-1,d6
	bsr	CONV_A2H
	move.b	d1,(a1)+
	bra.s	.SC

CONV_A2H::
;a6=ascii
;d6=nb de digits
	moveq	#0,d1
.CONV	lsl.l	#4,d1
.ADD	move.w	(a6)+,d0
	and	#$ff,d0
	cmp.b	#'9',d0
	ble	.NUM
	sub.b	#'A'-10,d0
	bra	.GO
.NUM	sub.b	#'0',d0
.GO	or.b	d0,d1
	dbra	d6,.CONV
	rts	
		
;**************************************************

;**************************************************





;**************************************
;**************************************




	
;**************************************	
REC_REGISTERS
	recl	d0
	lea	REG_R0,a6
	lea	OLD_REG_R0,a1
	move	#(24+10+5+30+2)-1,d7
.LOOP	
	move.l	(a6),(a1)+
	recl	(a6)+
	dbra	d7,.LOOP

	lea	TAB_BKPT,a6
	move	#16-1,d7
.LOOP2	
	recl	(a6)+
	recl	(a6)+
	recl	(a6)+
	clr.l	(a6)+
	dbra	d7,.LOOP2
	rts
;**************************************
SET_REGISTERS
	movem.l	a0/d7,-(sp)
	envl	#'CMD'
	envl	#DSP_SETREG
	lea	REG_R0,a0
	move	#(24+10+5+30+2)-1,d7
.LOOP	envl	(a0)+
	dbra	d7,.LOOP
	
	lea	TAB_BKPT,a0
	move	#16-1,d7
.LOOP2	envl	(a0)+
	envl	(a0)+
	envl	(a0)+
	addq	#4,a0
	dbra	d7,.LOOP2
	
	recl	d7
	movem.l	(sp)+,a0/d7
	rts
;**************************************
	


FIN	
	ifne	hard_video
	lea	NORMAL,a0
	bsr	SET_VIDEO	;restore video
	endc

	lea	SAVE_PAL,a0
	lea	$ffff9800.w,a1
	move	#15,d7
.COUL	move.l	(a0)+,(a1)+
	dbra	d7,.COUL
	
	move.l	OLD_SSP,-(sp)
	move	#32,-(sp)
	trap	#1
	addq	#6,sp	
	
	clr	-(sp)
	trap	#1
	

INITS
	move.L	8(sp),a0
	move.l	12(a0),a1
	add.l	20(a0),a1
	add.l	28(a0),a1
	pea	256(a1)
	pea	(a0)
	clr.w	-(sp)
	move.w	#$4a,-(sp)
	trap	#1
	add.l	#12,sp


	move.l	(sp),PILE
	clr.l	-(sp)
	move	#$20,-(sp)
	trap	#1
	addq	#6,sp
	move.l	d0,OLD_SSP	
	lea	PILE,sp
	

	xref	INIT_RESOL
	jsr	INIT_RESOL
	bsr	load_dsp_prog
	bsr	INIT_LOG
	bsr	INIT_COUL
	xref	INIT_TABY
	jsr	INIT_TABY

	rts
CLS_SCREEN	xref	RESO_X,RESO_Y,LOG
	
	move.l	LOG,a0
	move	RESO_Y,d0
	mulu	#size_font,d0
	moveq	#0,d1
	move	RESO_X,d1
	asl.l	#2,d1
	mulu	d1,d0
	asr.l	#6,d0
	subq	#1,d0
.LOOP	
	rept	16
	clr.l	(a0)+
	endr
	dbra	d0,.LOOP
	rts
	
;**************************************	
INIT_COUL	xref	PALETTE	
	lea	SAVE_PAL,a2
	lea	PALETTE,a0
	lea	$ffff9800.w,a1
	move	#15,d0
.C	move.l	(a1),(a2)+
	move.l	(a0)+,(a1)+
	dbra	d0,.C
	rts
;**************************************	
INIT_LOG
	xref	LOG
	ifne	hard_video
	lea	NORMAL,a0
	bsr	SAVE_VIDEO
	endc
	move	#3,-(sp)
	trap	#14
	addq	#2,sp
	move.l	d0,LOG
	
	;move	#%11010,-(sp)		;MONO VGA
	;move	#88,-(sp)
	;trap	#14
	;addq	#4,sp			;il faudra installer le LOG en PLUS...

	ifne	hard_video
	lea	$ffff8200.w,a0
	move.l	LOG,d0
	move.b	d0,d1
	lsr.l	#8,d0
	movep.w	d0,1(a0)
	move.b	d1,$d(a0)
	lea	MONO,a0
	bsr	SAVE_VIDEO
	endc

	rts		
;**************************************	
load_dsp_prog
	move.l	#END_P56,d0
	sub.l	#P56,d0
	divu	#3,d0
	ext.l	d0
	move.l	d0,-(sp)
	pea	P56
	move.w	#$6d,-(sp)	
	trap	#14		
	lea	10(sp),sp	
	rts
;**************************************	
	ifne	hard_video
	include	video.S
	endc
;**************************************	



	DATA
LOD_NAME	dc.b	'D:\A.LOD',0
	even

;**************************************	
W_REG::	
	dc	T_reg		;type
	dc	0,1,80,14	;X1,Y1,largeur,hauteur
	dc.l	0		;ptr expr
	dc	0		;mem type
	dc.l	0,0		;adr debut,fin
	dc	0		;nb colonnes
	dc	10,0		;start col,ligne
	dc	1		;flag aff
;**************************************	
W_DISAS::	
	dc	T_disas		;type
	dc	0,15,40,14	;X1,Y1,largeur,hauteur
	dc.l	0		;ptr expr
	dc	'P'		;mem type
	dc.l	$40,0		;adr debut,fin
	dc	0		;nb colonnes
	dc	10,0		;start col,ligne
	dc	1		;flag aff
;**************************************	

W_DUMPX::	
	dc	T_H24		;type
	dc	60,25,4,4	;X1,Y1,largeur,hauteur
	dc.l	0		;ptr expression
	dc	'X'		;mem type
	dc.l	SOURCE,0		;adr debut,fin
	dc	0		;nb colonnes
	dc	0,0		;start col,ligne
	dc	1		;flag aff
SOURCE	incbin	DSP_REG.S
	even
;**************************************	
;**************************************	
P56	incbin	DSP.P56
END_P56
	even
MES_TRACED	dc.b	'Traced',0
MES_SKIPED	dc.b	'Skiped',0
MES_RB		dc.b	'Run and Break',0
MES_JMP		dc.b	'Jump',0
MES_SPACE	dcb.b	80,' '
		dc.b	0	
		even
	even


	BSS
SAVE_PAL ds.l	16
OLD_SSP	ds.l	1
	ifne	hard_video
NORMAL	ds.l	300
MONO	ds.l	300
	endc
;**************************************
	include	DSP_REG.S
;**************************************
TAB_BKPT::	ds.l	16*4
SYMBOLS::	ds.l	10000
WORK_BUF	ds.b	2000	;pour mettre du binaire etc...
;**************************************
	ds.l	1000
PILE	ds.l	1

