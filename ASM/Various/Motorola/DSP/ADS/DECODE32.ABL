module decode32k

title '32K X 24 address decoder for DSP56000
       MOTOROLA DSP OPERATIONS 8-19-87'

"Description

"Declarations
		decode32	   device      'P16L8';

		a9,a10,a11,a12	   pin	       1,2,3,4;
		a13,a14,a15,xy	   pin	       5,6,7,8;
		ds,ps		   pin	       9,11;

		o7,o6,o5,o4	   pin	       12,13,14,15;
		o3,o2,o1,o0	   pin	       16,17,18,19;

	H,L,X = 1,0,.X.;

		p = !ps & !a15 & !a14;
		x = !ds & xy & !a15;
		xry= !ds & !a15 & !a14 & !a13;
		y = !ds & !xy & !a15;

equations
		!o0 = !ps & !a15;

		!o1 = (p) # (x & !a14);

		!o2 = (p) # (y & !a14);

		!o3 = (p) # (xry);

		!o4 = (p & !a13) # (x & !a14) # (y & !a14 & !a13);

		!o5 = (p & !a13) # (x & !a14 & !a13) # (y & !a14);

		!o6 = (p & !a13) # (x & a14) # (y & a14 & !a13);

		!o7 = (p & !a13) # (x & a14 & !a13) # (y & a14);

test_vectors

([a15,a14,a13,a12,a11,a10,a9,ps,ds,xy] -> [o0,o1,o2,o3,o4,o5,o6,o7])
" no output test
 [ H , X , X , X , X , X , X,X ,X ,X ] -> [H ,H ,H ,H ,H ,H ,H ,H ];
" p mem test up to 32k
 [ L , H , X , X , X , X , X,L ,H ,X ] -> [L ,H ,H ,H ,H ,H ,H ,H ];
" p mem test up to 16k
 [ L , L , H , X , X , X , X,L ,H ,X ] -> [L ,L ,L ,L ,H ,H ,H ,H ];
" p mem test up to 8k
 [ L , L , L , H , X , X , X,L ,H ,X ] -> [L ,L ,L ,L ,L ,L ,L ,L ];
" x mem test up to 16k
 [ L , L , H , X , X , X , X,H ,L ,H ] -> [H ,L ,H ,H ,L ,H ,H ,H ];
" x mem test up to 8k
 [ L , L , L , H , X , X , X,H ,L ,H ] -> [H ,L ,H ,L ,L ,L ,H ,H ];
" x mem test up to 16k at 4000h
 [ L , H , H , H , X , X , X,H ,L ,H ] -> [H ,H ,H ,H ,H ,H ,L ,H ];
" x mem test up to 8k at 4000h
 [ L , H , L , H , X , X , X,H ,L ,H ] -> [H ,H ,H ,H ,H ,H ,L ,L ];
" y mem test up to 16k
 [ L , L , H , X , X , X , X,H ,L ,L ] -> [H ,H ,L ,H ,H ,L ,H ,H ];
" y mem test up to 8k
 [ L , L , L , H , X , X , X,H ,L ,L ] -> [H ,H ,L ,L ,L ,L ,H ,H ];
" y mem test up to 8k at 4000h
 [ L , H , L , H , X , X , X,H ,L ,L ] -> [H ,H ,H ,H ,H ,H ,L ,L ];
" y mem test up to 16k at 4000h
 [ L , H , H , H , X , X , X,H ,L ,L ] -> [H ,H ,H ,H ,H ,H ,H ,L ];
" test of x memory greater than 16k
 [ H , H , X , X , X , X , X,H ,L ,H ] -> [H ,H ,H ,H ,H ,H ,H ,H ];
" test of p mem greater than 32k
 [ H , X , X , X , X , X , X,L ,H ,X ] -> [H ,H ,H ,H ,H ,H ,H ,H ];

end decode32k

