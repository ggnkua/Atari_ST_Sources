' Listing 1 : OFFSET
' (c)1992 by MAXON-Computer
' Autor: Torsten Thiel
' - erm”glicht das kontinuierliche Messen
'   und Anzeigen der Eingangsspannung
' - ohne Eingangssignal kann Offsetabgleich
'   auf 640mV erfolgen
'
RESERVE 10000  ! Speicher fr Variablen,
'              ! und Assemblerprogramm reservieren
'
a%=EXEC(3,"SAM_ASM.PRG","","") ! ausfhrbares Programmr
'                              ! in den Speicher laden
IF a%>0
  samtext%=LPEEK(a%+8) ! Adresse des Textsegment
  samdata%=LPEEK(a%+16)! Adresse des Datensegment
  samstart%=samdata%
  samanzahl%=samdata%+4
  samzaehl%=samdata%+8
  samvorteil%=samdata%+9
  '
  puffer&=0
  puffer%=VARPTR(puffer&)
  '
  CLS
  PRINT AT(20,4);"Die Spannungen an den Analogeing„ngen sind"
  @mausaus
  REPEAT
    POKE samzaehl%,20    ! Z„hlerstand des TimerD
    POKE samvorteil%,1   ! Vorteiler des TimerD
    LPOKE samstart%,puffer%  ! Samplestartadresse
    LPOKE samanzahl%,2   ! Anzahl des Messungen (*2)
    CALL samtext%
    k1|=PEEK(puffer%)
    k2|=PEEK(puffer%+1)
    k1|=XOR(128,k1|)
    k2|=XOR(128,k2|)
    PRINT AT(10,10);"Kanal1 = ";5*k1|;" mV  "
    PRINT AT(50,10);"Kanal2 = ";5*k2|;" mV  "
    a|=PEEK(&HFFFFFC02)
  UNTIL a|=1
  @mausein
ELSE
  PRINT "Systemdatei konnte nicht nachgeladen werden"
ENDIF
RESERVE
END
'
PROCEDURE mausaus
  a|=18
  ~XBIOS(25,1,L:VARPTR(a|))
RETURN
PROCEDURE mausein
  a|=8
  ~XBIOS(25,1,L:VARPTR(a|))
RETURN
