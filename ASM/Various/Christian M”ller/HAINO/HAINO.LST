$m100000
'
'                HAINO, der Harddisk-Rekorder  (c)1992 by MAXON-Computer GmbH
'
'                Autor: Christian Mîller
'
'                Datum  : 04.10.1992  19:51:00 Uhr
'
ver_nr$="1.11"
ver_datum$="04.10.1992"
'
' RESERVE 100000
'
main
'
' RESERVE
'
' ********* Haupt-PROCEDUREN             **********
'
> PROCEDURE main
  '
  appl_init                     !RSC laden; Variablen init. usw.
  do_dialog
  appl_exit                     !Speicher aufrÑumen und raus
  '
RETURN
> PROCEDURE info_box
  '
  LOCAL dummy%
  '
  box_draw(TRUE,infobox_adr%,MOUSEX,MOUSEY,5,5)
  dummy%=FORM_DO(infobox_adr%,0)
  box_undraw(TRUE,infobox_adr%,0,0,0,0)
  undo_objc(infobox_adr%,dummy%)
  '
RETURN
> PROCEDURE do_dialog
  LOCAL ex_obj%,dummy%,ex_button%,gr_slreturn%
  LOCAL frq_count&
  frq_count&=2
  CHAR{{OB_SPEC(main_adr%,smp_frq&)}}=smp_frq$(frq_count&)
  vorteiler&=smp_vorteiler&(frq_count&)
  box_draw(TRUE,main_adr%,MOUSEX,MOUSEY,5,5)
  DO
    ex_obj%=FORM_DO(main_adr%,0)
    SELECT ex_obj%
    CASE frq_dwn&
      INC frq_count&
      frq_count&=MIN(frq_count&,8)
      CHAR{{OB_SPEC(main_adr%,smp_frq&)}}=smp_frq$(frq_count&)
      vorteiler&=smp_vorteiler&(frq_count&)
      objc_update(main_adr%,smp_frq&)
      undo_objc(main_adr%,frq_dwn&)
      objc_update(main_adr%,frq_dwn&)
    CASE frq_up&
      DEC frq_count&
      frq_count&=MAX(frq_count&,1)
      CHAR{{OB_SPEC(main_adr%,smp_frq&)}}=smp_frq$(frq_count&)
      vorteiler&=smp_vorteiler&(frq_count&)
      objc_update(main_adr%,smp_frq&)
      undo_objc(main_adr%,frq_up&)
      objc_update(main_adr%,frq_up&)
    CASE outl_dwn&
      INC output_level&
      output_level&=MIN(output_level&,15)
      CHAR{{OB_SPEC(main_adr%,out_lvl&)}}=STR$(output_level&*1.5*-1)+" dB"
      objc_update(main_adr%,out_lvl&)
      undo_objc(main_adr%,outl_dwn&)
      objc_update(main_adr%,outl_dwn&)
    CASE outl_up&
      DEC output_level&
      output_level&=MAX(output_level&,0)
      CHAR{{OB_SPEC(main_adr%,out_lvl&)}}=STR$(output_level&*1.5*-1)+" dB"
      objc_update(main_adr%,out_lvl&)
      undo_objc(main_adr%,outl_up&)
      objc_update(main_adr%,outl_up&)
    CASE inl_up&
      INC input_level&
      input_level&=MIN(input_level&,15)
      CHAR{{OB_SPEC(main_adr%,inp_lvl&)}}=STR$((15-input_level&)*1.5*-1)+" dB"
      objc_update(main_adr%,inp_lvl&)
      undo_objc(main_adr%,inl_up&)
      objc_update(main_adr%,inl_up&)
    CASE inl_dwn&
      DEC input_level&
      input_level&=MAX(input_level&,0)
      CHAR{{OB_SPEC(main_adr%,inp_lvl&)}}=STR$((15-input_level&)*1.5*-1)+" dB"
      objc_update(main_adr%,inp_lvl&)
      undo_objc(main_adr%,inl_dwn&)
      objc_update(main_adr%,inl_dwn&)
    CASE hd_play&
      hd_play_start
      hd_rec_play_stop
      undo_objc(main_adr%,hd_play&)
      objc_update(main_adr%,hd_play&)
    CASE hd_rec&
      hd_record_start
      hd_rec_play_stop
      undo_objc(main_adr%,hd_rec&)
      objc_update(main_adr%,hd_rec&)
    CASE infobut&
      undo_objc(main_adr%,ex_obj%)
      box_undraw(TRUE,main_adr%,0,0,0,0)
      info_box
      box_draw(TRUE,main_adr%,MOUSEX,MOUSEY,5,5)
    CASE main_ex&
      hd_rec_play_stop
      undo_objc(main_adr%,ex_obj%)
      EXIT IF TRUE
    DEFAULT
      ALERT 2,"|no function yet !|Objekt: "+STR$(ex_obj%),1,"oops",dummy%
      undo_objc(main_adr%,ex_obj%)
      objc_update(main_adr%,ex_obj%)
    ENDSELECT
  LOOP
  box_undraw(TRUE,main_adr%,0,0,0,0)
RETURN
> PROCEDURE hd_rec_play_stop
  ~@buffoper(0)                      ! Schluû mit dem Krach
  restore_codec_data
RETURN
> PROCEDURE hd_play_start
  LOCAL datei$,handle&,half_buffer%,smp_len%
  LOCAL anz_buffer%,buffer_count%
  half_buffer%=sound_buffer_len% DIV 2
  '
  DEFMOUSE 2
  v_hide_c
  OPEN "i",#1,"HAINO.SMP"
  smp_len%=LOF(#1)
  CLOSE #1
  balken_faktor=balken_len%/(smp_len% DIV 32768)
  anz_buffer%=smp_len% DIV half_buffer%
  '
  do_balken(0)
  objc_update(main_adr%,tmp_bak&)
  '
  datei$="HAINO.SMP"+CHR$(0)
  handle&=GEMDOS(61,L:V:datei$,WORD(0)) ! Datei îffnen
  ~GEMDOS(63,WORD(handle&),L:sound_buffer_len%,L:sound_buffer_adr%)
  buffer_count%=1
  '
  ~@soundcmd(ltatten_&,SHL(output_level&,4))   ! AusgabeabschwÑchung 0 = voll Power
  ~@soundcmd(rtatten_&,SHL(output_level&,4))
  ~@setbuffer(0,sound_buffer_adr%,sound_buffer_adr%+sound_buffer_len%+1)! Abspielpuffer setzen
  ~@setsndmode(1)                    ! 16 Bit Stereo
  ~@settrack(0,0)                    ! Kanal 0 setzen
  ~@setmontrack(0)                   ! Interner Lautsprecher soll auch was machen
  ~@devconnect(dmaplay_&,dac_&,0,vorteiler&,1) ! Parameter zum Abspielen setzen
  ~@buffoper(3)                      ! und los gehts mit dem Krach
  '
  '     Diese Routine holt wÑhrend das Sample spielt schon die nÑchsten Daten
  '
  start_timer%=TIMER
  DO
    REPEAT
      ~@buffptr(V:buffpointer%(0))
      do_peak(buffpointer%(0))
      do_time(TIMER-start_timer%)
      do_balken(buffer_count%)
    UNTIL buffpointer%(0)>=sound_buffer_adr%+half_buffer%
    ~GEMDOS(63,WORD(handle&),L:half_buffer%,L:sound_buffer_adr%)
    INC buffer_count%
    REPEAT
      ~@buffptr(V:buffpointer%(0))
      do_peak(buffpointer%(0))
      do_time(TIMER-start_timer%)
      do_balken(buffer_count%)
    UNTIL buffpointer%(0)<sound_buffer_adr%+half_buffer%
    ~GEMDOS(63,WORD(handle&),L:half_buffer%,L:sound_buffer_adr%+half_buffer%)
    INC buffer_count%
    IF INP?(2)
      ~INP(2)
      EXIT IF TRUE
    ENDIF
    EXIT IF buffer_count%>=anz_buffer%
  LOOP
  ~GEMDOS(62,WORD(handle&))
  DEFMOUSE 0
  v_show_c
RETURN
> PROCEDURE hd_record_start
  LOCAL datei$,handle&,half_buffer%
  LOCAL tme_ptr%,balken_faktor,blk_cnt%
  LOCAL frei%
  half_buffer%=sound_buffer_len% DIV 2
  CLR tme_ptr%
  CLR balken_faktor
  CLR blk_cnt%
  '
  do_balken(0)
  objc_update(main_adr%,tmp_bak&)
  '
  DEFMOUSE 2
  v_hide_c
  datei$="HAINO.SMP"+CHR$(0)
  IF EXIST(datei$)
    KILL datei$
  ENDIF
  frei%=DFREE(GEMDOS(25)+1)
  balken_faktor=balken_len%/(frei% DIV 32768)
  handle&=GEMDOS(60,L:V:datei$,WORD(0)) ! Datei îffnen
  '
  ~@soundcmd(ltatten_&,SHL(output_level&,4))            ! AusgabeabschwÑchung 0 = voll Power
  ~@soundcmd(rtatten_&,SHL(output_level&,4))
  ~@soundcmd(ltgain_&,SHL(input_level&,4))              ! Input-VerstÑrkung
  ~@soundcmd(rtgain_&,SHL(input_level&,4))
  ~@soundcmd(adderin_&,1)
  ~@soundcmd(adcinput_&,0)
  ~@setbuffer(1,sound_buffer_adr%,sound_buffer_adr%+sound_buffer_len%+1)! Abspielpuffer setzen
  ~@setsndmode(1)                    ! 16 Bit Stereo
  ~@settrack(0,0)                    ! Kanal 0 setzen
  ~@setmontrack(0)                   ! Interner Lautsprecher soll auch was machen
  ~@devconnect(adc_&,dmarec_&,0,vorteiler&,1) ! Parameter zur Aufnahme
  ~@buffoper(12)                      ! und los gehts mit der Aufnahme
  start_timer%=TIMER
  DO
    REPEAT
      ~@buffptr(V:buffpointer%(0))
      do_peak(buffpointer%(1))
      do_time(TIMER-start_timer%)
      do_balken(blk_cnt%)
    UNTIL buffpointer%(1)>=sound_buffer_adr%+half_buffer%
    ~GEMDOS(64,WORD(handle&),L:half_buffer%,L:sound_buffer_adr%)
    INC blk_cnt%
    REPEAT
      ~@buffptr(V:buffpointer%(0))
      do_peak(buffpointer%(1))
      do_time(TIMER-start_timer%)
      do_balken(blk_cnt%)
    UNTIL buffpointer%(1)<sound_buffer_adr%+half_buffer%
    ~GEMDOS(64,WORD(handle&),L:half_buffer%,L:sound_buffer_adr%+half_buffer%)
    INC blk_cnt%
    EXIT IF INP?(2)
  LOOP
  ~INP(2)
  ~GEMDOS(62,WORD(handle&))
  DEFMOUSE 0
  v_show_c
RETURN
> PROCEDURE do_peak(zeiger%)
  LOCAL links_neu%,rechts_neu%
  links_neu%=ABS(WORD{zeiger%}-daempfung&)
  IF links_neu%<links%
    SUB links%,512
    links_neu%=links%
  ELSE
    links%=links_neu%
  ENDIF
  rechts_neu%=ABS(WORD{zeiger%+2}-daempfung&)
  IF rechts_neu%<rechts%
    SUB rechts%,512
    rechts_neu%=rechts%
  ELSE
    rechts%=rechts_neu%
  ENDIF
  OB_W(main_adr%,peakl&)=peak_faktor*links_neu%
  OB_W(main_adr%,peakr&)=peak_faktor*rechts_neu%
  objc_update(main_adr%,peaks&)
RETURN
> PROCEDURE do_time(zeit%)
  CHAR{{OB_SPEC(main_adr%,tme&)}}=@tme$(zeit%)
  objc_update(main_adr%,tme&)
RETURN
> PROCEDURE do_balken(len%)
  LOCAL act_len%
  act_len%=balken_faktor*len%
  act_len%=MAX(act_len%,1)
  OB_W(main_adr%,tme_rest&)=act_len%
  objc_update(main_adr%,tme_rest&)
RETURN
> FUNCTION tme$(t%)
LOCAL std&,min&,sek&,ztl&
std&=((t% DIV 200) DIV 60) DIV 60
t%=t%-(((std&*60)*60)*200)
min&=(t% DIV 200) DIV 60
t%=t%-((min&*60)*200)
sec&=t% DIV 200
t%=t%-(sec&*200)
ztl&=t% DIV 20
RETURN STR$(std&)+":"+STR$(min&)+":"+STR$(sec&)+"."+STR$(ztl&)
ENDFUNC
'
' ********* GEM-AES PROCEDUREN           **********
'
> PROCEDURE appl_init
LOCAL fehler%,dummy%
'
objc_def                      ! RSC-Objekte definieren
'
' ********** Globale Variablen
'
vorteiler&=2                  !Samplefrequenz Default
output_level&=0              !Output-Level Default
input_level&=15                !Input-Level Default
daempfung&=2048
'
links%=0
rechts%=0
'
x_res_max&=WORK_OUT(0)
y_res_max&=WORK_OUT(1)
DIM smp_vorteiler&(8)
DIM smp_frq$(8)
RESTORE freq
FOR n&=1 TO 8
  READ smp_vorteiler&(n&),smp_frq$(n&)
NEXT n&
'
DEFMOUSE 2
fehler%=RSRC_LOAD("HAINO.RSC")
DEFMOUSE 0
IF fehler%=0 THEN
  ALERT 3,"|Fehler beim Resource-File !",1,"Abbruch",dummy%
  appl_exit
  END
ENDIF
'
~RSRC_GADDR(0,main&,main_adr%)
~RSRC_GADDR(0,infobox&,infobox_adr%)
'
CHAR{{OB_SPEC(main_adr%,version&)}}="V"+ver_nr$+" "+ver_datum$
CHAR{{OB_SPEC(main_adr%,out_lvl&)}}=STR$(output_level&*1.5*-1)+" dB"
CHAR{{OB_SPEC(main_adr%,inp_lvl&)}}=STR$((15-input_level&)*1.5*-1)+" dB"
'
peak_faktor=OB_W(main_adr%,peakl&)/(32768-daempfung&)
OB_W(main_adr%,peakl&)=1
OB_W(main_adr%,peakr&)=1
balken_len%=OB_W(main_adr%,tme_rest&)
OB_W(main_adr%,tme_rest&)=1
'
'
init_sound_data
get_codec_data
dummy%=@locksnd
IF dummy%<>1
  ALERT 3,"|Sound-Subsystem ist|nicht verfÅgbar!",1,"Abbruch",dummy%
  appl_exit
  END
ENDIF
sound_buffer_len%=65536
sound_buffer_adr%=MALLOC(sound_buffer_len%)
IF sound_buffer_adr%<=0
  ALERT 3,"|zuwenig freier Speicher!",1,"Abbruch",dummy%
  appl_exit
  END
ENDIF
'
freq:
DATA 1,49170 Hz
DATA 2,33880 Hz
DATA 3,24585 Hz
DATA 4,20770 Hz
DATA 5,16490 Hz
DATA 7,12292 Hz
DATA 9,9834 Hz
DATA 11,8195 Hz
RETURN
> PROCEDURE appl_exit
~RSRC_FREE()
restore_codec_data
~@unlocksnd
~MFREE(sound_buffer_adr%)
RETURN
> PROCEDURE do_fsel(info$,ext$,VAR path$,datei$,ex_button%)
LOCAL path_adr%,pos%,n%,dgret%,path_alt$
IF ext$=""
  ext$="*.*"
ENDIF
path_alt$=path$
IF path$=""
  path$=STRING$(64,CHR$(0))
  path_adr%=V:path$
  dgret%=GEMDOS(71,L:path_adr%,CARD(0))
  pos%=INSTR(path$,CHR$(0))
  path$=LEFT$(path$,pos%-1)
  path$=CHR$(65+GEMDOS(25))+":"+path$+"\"
ENDIF
path$=path$+ext$
~WIND_UPDATE(1)
FILESELECT #info$,path$,datei$,name$
~WIND_UPDATE(0)
IF name$=""
  ex_button%=FALSE
  path$=path_alt$
ELSE
  ex_button%=TRUE
  pos%=RINSTR(name$,"\")
  path$=LEFT$(name$,pos%)
  datei$=RIGHT$(name$,LEN(name$)-pos%)
ENDIF
RETURN
> PROCEDURE box_draw(mode!,tree_adr%,x%,y%,w%,h%)
~FORM_CENTER(tree_adr%,fx%,fy%,fw%,fh%)
IF mode!
  ~WIND_UPDATE(1)
  ~FORM_DIAL(0,x%,y%,w%,h%,fx%,fy%,fw%,fh%)
ENDIF
~OBJC_DRAW(tree_adr%,0,8,fx%,fy%,fw%,fh%)
RETURN
> PROCEDURE box_undraw(mode!,tree_adr%,x%,y%,w%,h%)
~FORM_CENTER(tree_adr%,fx%,fy%,fw%,fh%)
IF mode!
  ~FORM_DIAL(3,x%,y%,w%,h%,fx%,fy%,fw%,fh%)
  ~WIND_UPDATE(0)
ENDIF
RETURN
> PROCEDURE undo_objc(tree_adr%,index&)
OB_STATE(tree_adr%,index&)=BCLR(OB_STATE(tree_adr%,index&),0)
RETURN
> PROCEDURE do_objc(tree_adr%,index&)
OB_STATE(tree_adr%,index&)=BSET(OB_STATE(tree_adr%,index&),0)
RETURN
> PROCEDURE undo_sel_objc(tree_adr%,index&)
OB_STATE(tree_adr%,index&)=BSET(OB_STATE(tree_adr%,index&),3)
RETURN
> PROCEDURE do_sel_objc(tree_adr%,index&)
OB_STATE(tree_adr%,index&)=BCLR(OB_STATE(tree_adr%,index&),3)
RETURN
> PROCEDURE do_ed_objc(tree_adr%,index&)
OB_FLAGS(tree_adr%,index&)=BSET(OB_FLAGS(tree_adr%,index&),3)
RETURN
> PROCEDURE undo_ed_objc(tree_adr%,index&)
OB_FLAGS(tree_adr%,index&)=BCLR(OB_FLAGS(tree_adr%,index&),3)
RETURN
> PROCEDURE hide_objc(tree_adr%,index&)
OB_FLAGS(tree_adr%,index&)=BSET(OB_FLAGS(tree_adr%,index&),7)
RETURN
> PROCEDURE unhide_objc(tree_adr%,index&)
OB_FLAGS(tree_adr%,index&)=BCLR(OB_FLAGS(tree_adr%,index&),7)
RETURN
> PROCEDURE get_objc_xywh(tree_adr%,index&,VAR x%,y%,width%,height%)
'
LOCAL obj_adresse%
'
~OBJC_OFFSET(tree_adr%,index&,x%,y%)
obj_adresse%=tree_adr%+24*index&
width%=DPEEK(obj_adresse%+20)
height%=DPEEK(obj_adresse%+22)
'
RETURN
> PROCEDURE objc_update(tree_adr%,index&)
get_objc_xywh(tree_adr%,index&,x%,y%,w%,h%)
~OBJC_DRAW(tree_adr%,index&,8,x%,y%,w%,h%)
RETURN
> PROCEDURE objc_def
'
main&=0    !/* Formular/Dialog */
version&=2    !/* TEXT in Baum MAIN */
peaks&=10   !/* BOX in Baum MAIN */
peakr&=11   !/* BOX in Baum MAIN */
peakl&=12   !/* BOX in Baum MAIN */
tmp_bak&=15   !/* BOX in Baum MAIN */
tme_rest&=16   !/* BOX in Baum MAIN */
tme&=18   !/* BOXTEXT in Baum MAIN */
inl_dwn&=20   !/* BOXCHAR in Baum MAIN */
inp_lvl&=21   !/* BOXTEXT in Baum MAIN */
inl_up&=22   !/* BOXCHAR in Baum MAIN */
outl_dwn&=24   !/* BOXCHAR in Baum MAIN */
out_lvl&=25   !/* BOXTEXT in Baum MAIN */
outl_up&=26   !/* BOXCHAR in Baum MAIN */
frq_dwn&=28   !/* BOXCHAR in Baum MAIN */
smp_frq&=29   !/* BOXTEXT in Baum MAIN */
frq_up&=30   !/* BOXCHAR in Baum MAIN */
infobut&=32   !/* BOXTEXT in Baum MAIN */
main_ex&=36   !/* BOXTEXT in Baum MAIN */
hd_rec&=38   !/* BOXTEXT in Baum MAIN */
hd_play&=39   !/* BOXTEXT in Baum MAIN */
infobox&=1    !/* Formular/Dialog */
info_ok&=14   !/* BUTTON in Baum INFOBOX */
'
RETURN
'
> PROCEDURE v_show_c
~GRAF_MOUSE(257,0)
RETURN
> PROCEDURE v_hide_c
~GRAF_MOUSE(256,0)
RETURN
'
' GFA-BASIC Library fÅr den FALCON 030
' (c)1992 by MAXON/Computer
' Autor: Christian Mîller
'
' *** Sound-Subsystem
'
> PROCEDURE init_sound_data
ltatten_&=0
rtatten_&=1
ltgain_&=2
rtgain_&=3
adderin_&=4
adcinput_&=5
setprescale_&=6
dmaplay_&=0
dspxmit_&=1
extinp_&=2
adc_&=3
dmarec_&=1
dsprec_&=2
extout_&=4
dac_&=8
DIM codec_data&(6)
DIM buffpointer%(3)
RETURN
> PROCEDURE get_codec_data
LOCAL n&
FOR n&=0 TO 6
  codec_data&(n&)=@soundcmd(n&,-1)
NEXT n&
RETURN
> PROCEDURE restore_codec_data
LOCAL n&
FOR n&=0 TO 6
  ~@soundcmd(n&,codec_data&(n&))
NEXT n&
RETURN
> FUNCTION locksnd
RETURN XBIOS(&H80)
ENDFUNC
> FUNCTION unlocksnd
RETURN XBIOS(&H81)
ENDFUNC
> FUNCTION soundcmd(mode&,data&)
RETURN XBIOS(&H82,mode&,data&)
ENDFUNC
> FUNCTION setbuffer(reg&,begaddr%,endaddr%)
RETURN XBIOS(&H83,reg&,L:begaddr%,L:endaddr%)
ENDFUNC
> FUNCTION setsndmode(mode&)
RETURN XBIOS(&H84,mode&)
ENDFUNC
> FUNCTION settrack(playtracks&,rectracks&)
RETURN XBIOS(&H85,playtracks&,rectracks&)
ENDFUNC
> FUNCTION setmontrack(montrack&)
RETURN XBIOS(&H86,montrack&)
ENDFUNC
> FUNCTION setinterrupt(scr_inter&,cause&)
RETURN XBIOS(&H87,scr_inter&,cause&)
ENDFUNC
> FUNCTION buffoper(mode&)
RETURN XBIOS(&H88,mode&)
ENDFUNC
> FUNCTION dsptristate(dspxmit&,dsprec&)
RETURN XBIOS(&H89,dspxmit&,dsprec&)
ENDFUNC
> FUNCTION gpio(mode&,data&)
RETURN XBIOS(&H8A,mode&,data&)
ENDFUNC
> FUNCTION devconnect(src&,dst&,srcclk&,prescale&,protocol&)
RETURN XBIOS(&H8B,src&,dst&,srcclk&,prescale&,protocol&)
ENDFUNC
> FUNCTION sndstatus(reset&)
RETURN XBIOS(&H8C,reset&)
ENDFUNC
> FUNCTION buffptr(pointer%)
RETURN XBIOS(&H8D,L:pointer%)
ENDFUNC
