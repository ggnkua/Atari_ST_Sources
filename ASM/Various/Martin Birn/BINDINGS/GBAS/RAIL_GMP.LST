' RAIL_MAN 2.00 D GFA-BASIC-Musterprogramm
' ========================================
'
' Letzte énderung: 07.07.1990
'
' (C) 1990 by Martin Birn
' Knoblochstraûe 51, 7100 Heilbronn
' =============================================================================
'
' Funktionsgruppe A
'
Deffn A_start=Gemdos(253,0)
'
' der RÅckgabewert ist der globalen Integervariablen
' Handle% zuzuweisen.
' Hierzu bitte das Unterprogramm Rail_init benutzen.
'
Deffn A_end(Handle%)=Gemdos(253,1,Handle%)
Deffn A_state(Handle%,Mode%)=Gemdos(253,2,Handle%,Mode%)
Deffn A_wait(Handle%)=Gemdos(253,3,Handle%)
Deffn A_reset(Handle%)=Gemdos(253,5,Handle%)
Deffn A_update(Handle%)=Gemdos(253,6,Handle%)
'
' Funktionsgruppe E (Engine = Loksteuerung)
'
Deffn E_speed(Handle%,Index%,Mode%,Speed%)=Gemdos(253,7,Handle%,Index%,Mode%,Speed%)
Deffn E_direction(Handle%,Index%)=Gemdos(253,8,Handle%,Index%)
Deffn E_mode(Handle%,Index%,Mode%)=Gemdos(253,9,Handle%,Index%,Mode%)
Deffn E_function(Handle%,Index%,Number%,Mode%)=Gemdos(253,10,Handle%,Index%,Number%,Mode%)
'
' Funktionsgruppe S (Switch = Weiche)
'
Deffn S_setgroup(Handle%,Index%,Mode%)=Gemdos(253,11,Handle%,Index%,Mode%)
Deffn S_delgroup(Handle%,Index%)=Gemdos(253,12,Handle%,Index%,Mode%)
Deffn S_switch(Handle%,Index%,Mode%,Direction%)=Gemdos(253,13,Handle%,Index%,Mode%,Direction%)
Deffn S_bswitch(Handle%,Index%)=Gemdos(253,21,Handle%,Index%)
'
' Funktionsgruppe R (RÅckmeldungen)
'
Deffn R_start(Handle%)=Gemdos(253,14,Handle%)
Deffn R_stop(Handle%)=Gemdos(253,15,Handle%)
'
' Funktionsgruppe Q (reQuest = Nachfragefunktionen)
'
Deffn Q_version(Handle%)=Gemdos(253,16,Handle%)
Deffn Q_array(Handle%,Addr%)=Gemdos(253,17,Handle%,L:Addr%)
Deffn Q_file(Handle%)=Gemdos(253,18,Handle%)
'
' Funktionsgruppe X (information eXchange = Infoaustausch)
'
Deffn X_search(Handle%)=Gemdos(253,19,Handle%)
Deffn X_updated(Handle%)=Gemdos(253,20,Handle%)
Deffn X_done(Handle%,Index%)=Gemdos(253,22,Handle%,Index%)
'
' =============================================================================
' PROGRAMM
'
Data RAIL_MAN ,  Information ,----------------------,a,a,a,a,a,a,""
Data Datei ,  Fahrplan 1 ,  Fahrplan 2 ,  Fahrplan 3 ,  ----------,  Quit ,""
Data "","",*
'
Fullw 1   !fÅr den schînen grauen Hintergrung im Interpreter
Closew 1  !(sonst mÅûten wir die Koordinaten Åber's AES ermitteln...
Dim Men$(50)
Clr I%
Do
  Read A$
  Exit If A$="*"
  Men$(I%)=A$
  Inc I%
Loop
Defmouse 0
Menu Men$()
On Menu  Gosub Menu_selected
@Rail_init
Do
  On Menu
Loop
Procedure Menu_selected
  Local T$,X%
  T$=Men$(Menu(0))
  Menu Kill
  If T$="  Information "
    Alert 1,"RAIL_MAN 2.00 D Basic-Binding|(C) 1990 by Martin Birn|            Knoblochstraûe 51|            7100 Heilbronn",1," OK ",X%
  Endif
  If T$="  Fahrplan 1 "
    Gosub Fahrplan1
  Endif
  If T$="  Fahrplan 2 "
    Gosub Fahrplan2
  Endif
  If T$="  Fahrplan 3 "
    Gosub Fahrplan3
  Endif
  If T$="  Quit "
    Void Fn A_end(Handle%)
    Closew 0
    End
  Endif
  Menu Off
  Menu Men$()
  On Menu  Gosub Menu_selected
  Defmouse 0
Return
'
Procedure Fahrplan1
  ' hier ist Platz fÅr jede Menge Befehle
  ' wird beim Aufruf des entspr. MenÅs gestartet.
  @Rail_err(@A_end(1000),0) !ein provozierter Fehler -1020 als Demo
Return
'
Procedure Fahrplan2
  ' hier ist Platz fÅr jede Menge Befehle
  ' wird beim Aufruf des entspr. MenÅs gestartet.
Return
'
Procedure Fahrplan3
  ' hier ist Platz fÅr jede Menge Befehle
  ' wird beim Aufruf des entspr. MenÅs gestartet.
Return
'
' =============================================================================
' FUNKTIONEN
'
' -----------------------------------------------------------------------------
' Rail_init
'
' Globale Variablen:
'
' Handle%     RAIL_MAN-Handle (bei weiteren Funktionsaufrufen angeben!)
' Rail_eflg%  Flag zur internen Verwendung
' Rail_arr%() Feld mit den RÅckgabewerten von q_array
'
Procedure Rail_init
  Local Rret%,Adr%
  Rret%=@A_start
  If Rret%=-32          !nicht installiert
    Clr Handle%
    Print Chr$(7);
    Alert 3,"RAIL_MAN nicht installiert.",1,"Abbruch",X%
  Else
    @Rail_err(Rret%,*Handle%)   !Handle berechnen
    Dim Rail_arr%(25)           !Feld anlegen
    Adr%=Varptr(Rail_arr%(0))   !Adresse des ersten Elements holen
    Void @Q_array(Handle%,Adr%) !ab hier Daten ablegen
    Clr Rail_eflg%              !internes Flag zurÅcksetzen
  Endif
Return
'
' -----------------------------------------------------------------------------
' Fehlerbehandlungsprocedur
'
Procedure Rail_err(Code%,Return%)
  Local Ercd%,E%
  Ercd%=Code% And &HFFFF
  If Ercd%>&HEFFF
    Ercd%=-(&H10000-Ercd%) !vorzeichenbehaftet, daher umrechnen
  Endif
  If Return%<>0      !RÅckgabewert gewÅnscht?
    *Return%=Ercd%   !ja
  Endif
  '
  If Ercd%<>-1020 And Rail_eflg%=0  !nicht bei falschem Handle oder laufendem Redraw
    '
    ' DER FOLGENDE BEFEHL IST EXTREM WICHTIG!!
    '
    Rail_eflg%=1 !weitere Redraw-Messages sperren
    '
    If Code% And 2^16
      Gosub Redraw
    Endif
    If Code% And 2^17
      Gosub Rm_update
    Endif
    If Code% And 2^18
      Gosub Newfile
    Endif
    If Code% And 2^19
      Gosub Lok_update
    Endif
    '
    Clr Rail_eflg% !Redraw-Messages wieder zulassen
  Endif
  E%=-(Ercd%+1000)      !Fehlerindex feststellen
  If Dim?(Rail_arr%())  !Feld angelegt?
    If E%>0 And E%<=Lpeek(Rail_arr%(22))  !Fehlermeldung im angegebenen Bereich?
      Print Chr$(7);
      Dpoke Gintin,1    !Defaultbutton 1
      Lpoke Addrin,Lpeek(Rail_arr%(22)+4*E%)   !Adresse des Alert-Strings Åbergeben
      Gemsys 52         !FORM_ALERT des AES aufrufen
    Endif
  Endif
Return
'
' -----------------------------------------------------------------------------
' Redraw-UP
'
Procedure Redraw
  Local Rret%,Index%,Rh%
  Repeat
  Until @X_search(Handle%)=0
Return
Procedure Rm_update
  Void Fn X_done(Handle%,1)
Return
Procedure Newfile
  Void Fn X_done(Handle%,2)
Return
Procedure Lok_update
  Void Fn X_done(Handle%,3)
Return
