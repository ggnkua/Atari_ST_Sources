10    REM *** PLAYBACK VI        ***
20    REM *** ST BASIC DEMO      ***
30    REM *** BY TRISTAN HUNTING ***
40    REM
50    REM ***LOAD CODE***
60    CODE%=300000
70    BLOAD "A:\PLAYBACK\PLAYBACK.C",CODE%
80    REM
90    REM ***SKIP HEADER***
100   CODE%=CODE%+28
110   REM
120   REM ***LOAD SAMPLES***
130   PLBK%=CODE%+1004
140   BLOAD "A:\PLAYBACK\PLAYBACK.SND",PLBK%
150   MGUN%=CODE%+1004+2799
160   BLOAD "A:\PLAYBACK\MGUN.SND",MGUN%
170   REM
180   REM ***ADDRESS OF VARIABLES***
190   SAMPLE_ADDRESS%=CODE%+984
200   SAMPLE_LENGTH%=CODE%+988
210   SAMPLE_RATE%=CODE%+992
220   REM
230   REM ***INTRO***
240   FULLW 2
250   CLEARW 2
260   PRINT "Welcome to..."
270   DL%=30:GOSUB 570
280   RATE%=7
290   REM
300   REM ***PLAY SOUND***
310   GOSUB 670
320   GOSUB 410
330   K$=INPUT$(1)
340   IF K$="1" THEN GOSUB 670
350   IF K$="2" THEN GOSUB 730
360   IF K$="3" THEN GOSUB 620
370   IF K$="0" THEN END
380   GOTO 330
390   REM
400   REM ***SET UP SCREEN***
410   CLEARW 2
420   PRINT "PLAYBACK - VI"
430   PRINT
440   PRINT "RATE - ";RATE%
450   PRINT
460   PRINT "PRESS REPEATEDLY TO STUTTER"
470   PRINT
480   PRINT "PRESS KEY"
490   PRINT
500   PRINT "1 - TO HEAR PLAYBACK"
510   PRINT "2 - TO HEAR MACHINE GUN"
520   PRINT "3 - TO CHANGE RATE"
530   PRINT "0 - TO EXIT"
540   RETURN
550   REM
560   REM ***DELAY IN 50ths OF A SECOND***
570   FOR X=1 TO DL%
580   XBIOS 37
590   NEXT
600   RETURN
610   REM ***CHANGE RATE***
620   RATE%=RATE%+1
630   IF RATE%=17 THEN RATE%=1
640   GOTOXY 0,7:PRINT            "RATE - ";RATE%;" "
650   RETURN
660   REM ***PLAY PLAYBACK***
670   POKE_L SAMPLE_ADDRESS%,PLBK%
680   POKE_L SAMPLE_LENGTH%,2799
690   POKE_L SAMPLE_RATE%,RATE%
700   CALL CODE%
710   RETURN
720   REM ***PLAY MACHINE GUN***
730   POKE_L SAMPLE_ADDRESS%,MGUN%
740   POKE_L SAMPLE_LENGTH%,4707
750   POKE_L SAMPLE_RATE%,RATE%
760   CALL CODE%
770   RETURN
