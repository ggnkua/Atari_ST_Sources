; ###############################################
; ##  DATE'N TIME                              ##
; ##  PROGRAMM ZUM SETZEN VON DATUM UND ZEIT   ##
; ###############################################

         .TEXT 
START:   bra     MAIN       ;
; >> WEGEN RESIDENT-BLEIBEN... <<

; ** WIRD PER RESETVEKTOR ANGESPRUNGEN...***+
; KOPIERT "RESET-ROUTINE" AN EINGETRAGENE ADRESSE
; (SIEHE OBEN), SETZT PHYSTOP HERAB  UND KLINKT
; SICH DANN AUS EINER EVT. XBRA-KETTE WIEDER AUS.

         .DC.b "XBRA"       ; XBRA-PROTOKOLL
         .DC.b "RNDT"       ; PROGRAMMKENNUNG
OLDRV:   .DC.l 0 ;          ; ALTE ADRESSE
RESVEC:  movem.l d0-d2/a0-a2/a7,SAVREGS
         lea.l   MAIN+1052,sp ; STACK INIT.
         movea.l $42e,a2    ; PHYSTOP HOLEN
         suba.w  #((ERT-SRT+$1ff)&$200),a2
;                           ; VERSCHIEBUNG
         lea.l   SRT,a0     ; STARTADRESSE
         movea.l a2,a1      ; COPY-ADRESSE
         move.l  a1,$42e    ; NEUES PHYSTOP
         move.w  #(ERT-SRT)/4,d0
.COPY:   move.l  (a0)+,(a1)+ ; KOPIERE DATEN
         dbra    d0,.COPY   ; NéCHSTEN...

; >> ADRESSE IM HANDLER EINTRAGEN
         lea.l   $680,a0    ; SPRUNGLISTE
         moveq.l #32-1,d0   ; MAX. 32 POS.
.TSTPOS: tst.l   (a0)+      ; IST 0?
         beq     .OKPOS     ; JA: OK!
         dbra    d0,.TSTPOS ; WEITERSUCHEN

         tst.w   d0         ; D0 NEGATIV?
         bmi     .FAILIT    ; JA: AUS.

.OKPOS:  move.l  a2,-4(a0)  ; ROUT. EINHéNGEN
         bsr     CHCKSM     ; PRöFSUMME KORR.

.FAILIT: lea.l   $42a,a1    ; BASISADRESSE
.TST:    cmpi.l  #RESVEC,(a1) ; BIN DAS ICH?
         beq     .CPY       ; JA: KOPIERE...
         movea.l (a1),a2    ; ADR. HOLEN
         cmpi.l  #"XBRA",-12(a2) ; XBRA?
         bne     .REMOVE    ; NEIN: DANN CLR!!
         lea.l   -4(a2),a1  ; FöR NéCHSTEN LAUF
         tst.l   (a1)       ; ADRESSE 0?
         bne     .TST       ; NEIN: WEITER
         bra     .EXIT      ; JA: RAUS (??!)
.CPY:    move.l  OLDRV,(a1) ; SONST SETZEN

         tst.l   $42a       ; RES_VECTOR=0?
         bne     .EXIT      ; NEIN: WEITER

.REMOVE: clr.l   $426       ; RESVALID=INVALID

.EXIT:   tst.l   OLDRV      ; NOCH EINE ROUTINE?
         beq     .CONT      ; NEIN: WEITER
         movem.l SAVREGS,d0-d2/a0-a2
         move.l  OLDRV,-(sp) ; ALTE ROUTINE
         rts                ; UND ZURöCK

.CONT:   cmpi.w  #$9bcd,(a6) ; FEHLERHAFTES TOS?
         bne     .GOON      ; NEIN: NORMAL WEITER
         lea.l   $24(a6),a6 ; SONST $24 ADDIEREN
.GOON:   movem.l SAVREGS,d0-d2/a0-a2/a7
;                           ; REGISTER RETTEN
         jmp     (a6)       ; UND WEITERMACHEN

;*** HANDLER ***
SHANDLER:        
         .DC.l $12123456    ; MAGIC NUMBER
         .DC.l $600         ; ZEIGER
         movem.l d0-d7/a0-a6,-(sp)
         movea.w #$680,a0   ; STARTADRESSE
         move.w  #16-1,d0   ; MAXIMAL 16 ROUTINEN
.EXEC:   tst.l   (a0)+      ; IST ADRESSE=0?
         beq     .CONT      ; JA: WEITER
         movea.l -4(a0),a1  ; SONST ADRESSE HOLEN
         movem.l d0/a0,-(sp) ; SAVE REGISTER
         jsr     (a1)       ; UND AUSFöHREN
         movem.l (sp)+,d0/a0 ; RESTORE REG.
.CONT:   dbra    d0,.EXEC   ; NéCHSTE ADR
         movem.l (sp)+,d0-d7/a0-a6
         rts                ; ZURöCK
EHANDLER:        

; *** EIGENTLICHE ROUTINE, UM DIE ES GEHT... ***
SRT:     bra.s   .JMP       ; öBERSPRINGEN
         .DC.l "RNDT"       ; KENNUNG...
.JMP:    move.w  #23,-(sp)  ; F#23
         trap    #14        ; XBIOS: GETTIME
         addq.l  #2,sp      ; STACKKORREKTUR
         move.l  d0,d4      ; DATEN MERKEN
         move.w  d4,-(sp)   ; ZEIT ANGEBEN
         move.w  #45,-(sp)  ; F#45
         trap    #1         ; GEMDOS: TSETTIME
         addq.l  #4,sp      ; STACKKORREKTUR
         swap.w  d4         ; JETZT AN ZEIT
         move.w  d4,-(sp)   ; DATUM PUSHEN
         move.w  #43,-(sp)  ; F#43
         trap    #1         ; GEMDOS: TSETDATE
         addq.l  #4,sp      ; STACKKORREKTUR
         pea     RESTXT     ; TEXT
         move.w  #9,-(sp)   ; F#9 : CCONWS
         trap    #1         ; GEMDOS
         addq.l  #6,sp      ; STACKKORREKTUR
         rts                ; ZURöCK
RESTXT:  .DC.b 27,"p Date'n Time v3.2 ",27,"q"
         .DC.b 13,10,"Datum und Uhrzeit wurden "
         .DC.b "aktualisiert.",13,10,0
         .EVEN 
ERT:             ; ENDE DER ROUTINE

; *** ROUTINE KORRIGIERT PRöFSUMME ***
CHCKSM:  moveq.l #0,d0      ; D0 LôSCHEN
         movea.w #$600,a0   ; SEITE LADEN
         move.w  #256-2,d1  ; 255 WôRTER TESTEN
.ADD:    add.w   (a0)+,d0   ; UND ADDIEREN
         dbra    d1,.ADD    ; NéCHSTES WORT

         move.w  #$5678,d2  ; PRöFSUMME
         sub.w   d0,d2      ; AKT. PRöFSUMME -
         move.w  d2,(a0)+   ; NEUE EINTRAGEN

         rts                ; UND TSCHöSS..

;*** DIESER TEIL BLEIBT NICHT RESIDENT! ***
SAVREGS: 
MAIN:    clr.l   -(sp)      ; MODUS: SV
         move.w  #32,-(sp)  ; F# 32
         trap    #1         ; GEMDOS: SUPER
         addq.l  #6,sp      ; STACKKORREKTUR
         move.l  d0,OLDSP   ; ALTEN SSP MERKEN

         move.w  #4,-(sp)   ; F#=4
         trap    #14        ; XBIOS(4): GETREZ
         addq.l  #2,sp      ; STACKKORREKTUR
         tst.b   d0         ; LOW-RESOLUTION?
         sne     OLDRES     ; ZUSTAND MERKEN
         bne     .WEITER    ; AUFLSG. N. éNDERN

         clr.w   -(sp)      ; SCHWARZ= NEUE FARBE
         move.w  #3,-(sp)   ; COLOR-REGISTER #3
         move.w  #7,-(sp)   ; F#= 7
         trap    #14        ; XBIOS(7): SETCOLOR
         addq.l  #6,sp      ; STACKKORREKTUR
         move.w  d0,OLDCOL  ; ALTEN WERT MERKEN

         move.w  #1,-(sp)   ; MITTEL-AUFLôSUNG
         moveq.l #-1,d0     ; -1 VORLADEN
         move.l  d0,-(sp)   ; PHYSLOC UNVERéNDERT
         move.l  d0,-(sp)   ; LOGLOC UNVERéNDERT
         move.w  #5,-(sp)   ; F#=5
         trap    #14        ; XBIOS(5): SETSCREEN
         lea.l   $c(sp),sp  ; STACKKORREKTUR

.WEITER: bsr     GETIKBD    ; ZEIT VON IKBD HOLEN
         move.l  d0,IKTD    ; UND SPEICHERN

         lea.l   INSRTTM,a0 ; DAHIN SCHREIBEN

         bsr     TIME2ASC   ; ZEIT->ASCII-TEXT
         move.b  #" ",(a0)+ ; ZWISCHENRAUM EINFG.
         swap.w  d0         ; DATUM IN D0.W LOW
         bsr     DATE2ASC   ; DATUM->ASCII-TEXT

         pea     TEXT       ; AUSGABE ZEIT/DATUM
         move.w  #9,-(sp)   ; F#9
         trap    #1         ; GEMDOS: CCONWS
         addq.l  #6,sp      ; STACKKORREKTUR

         movea.l $4f2,a0    ; HARDWARE-DATUM
         move.w  30(a0),HWDATE ;  HOLEN

         move.w  #-1,-(sp)  ; PAR.: NUR-ABFRAGE
         move.w  #11,-(sp)  ; F#11
         trap    #13        ; BIOS-CALL: KBSHIFT
         addq.l  #2,sp      ; STACKKORREKTUR
         btst    #3,d0      ; BIT 3 (ALTERNATE)?
         bne     .REQ       ; JA: IMMER ABFRAGEN

         move.w  IKDATE,d0  ; IKBD-DATUM HOLEN
         andi.w  #%111111111,d0 ; JAHRE MASKIEREN
         beq     .REQ       ; MONATE UND TAGE =0?

         move.w  HWDATE,d0  ; HOLE HARDWARE-DATUM
         cmp.w   IKDATE,d0  ; DASSELBE?
         bne     .CONT      ; <>: USERDATUM AKTIV

.REQ:    pea     REQDATE    ; "NEW DATE:"
         move.w  #9,-(sp)   ; F#9
         trap    #1         ; GEMDOS-TRAP: CCONWS
         addq.l  #6,sp      ; STACKKORREKTUR

         move.b  #10,BLENG  ; "INPUT" DATE
         clr.b   BREAD      ; GELESENE ZEICHEN=0
         pea     BUF        ; DAHIN SCHREIBEN
         move.w  #10,-(sp)  ; F#10
         trap    #1         ; GEMDOS: CCONRS
         addq.l  #6,sp      ; STACKKORREKTUR

         bsr     GETUSD     ; CALC_DATE

         pea     REQTIME    ; "NEW TIME:"
         move.w  #9,-(sp)   ; F#9
         trap    #1         ; GEMDOS-TRAP: CCONWS
         addq.l  #6,sp      ; STACKKORREKTUR

         move.b  #8,BLENG   ; INPUT TIME
         clr.b   BREAD      ; NULLSETZEN
         pea     BUF        ; DAHIN SCHREIBEN
         move.w  #10,-(sp)  ; F#10
         trap    #1         ; GEMDOS: CCONRS
         addq.l  #6,sp      ; STACKKORREKTUR

         bsr     GETUST     ; CALC_TIME
         bsr     SETIKBD    ; IKBD-UHR SETZEN

         pea     OKAY       ; "ALLES KLAR!"
         move.w  #9,-(sp)   ; F#9
         trap    #1         ; GEMDOS-CALL:CCONRS
         addq.l  #6,sp      ; STACKKORREKTUR

.CONT:   bsr     SETDOST    ; GEMDOS-UHR SETZEN
         bsr     SETDOSD    ; GEMDOS-DATUM AUCH

         lea.l   $600,a0    ; BASISADRESSE
         cmpi.l  #$12123456,(a0) ; SCHON INST.?
         bne     .INST      ; NEIN: INSTALLIEREN
         cmpa.l  4(a0),a0   ; STIMMT DAS AUCH?
         bne     .INST      ; NEIN: INSTALLIEREN
         moveq.l #0,d1      ; LôSCHEN (PRöFSUMME)
         move.w  #$100-1,d0 ; 256 WôRTER ADDIEREN
.TSTCHS: add.w   (a0)+,d1   ; ADDIEREN
         dbra    d0,.TSTCHS ; NéCHSTE
         cmpi.w  #$5678,d1  ; PRöFSUMME KORREKT?
         beq     .JUMP      ; JA: WEITER

.INST:   bsr     H_INST     ; < EIGENER HANDLER >
         bsr     CHCKSM     ; CHECKSUM-KORR.
         bra     .NOTFND    ; KANN NOCH NICHT
;                           ; INSTALLIERT SEIN

.JUMP:           ; TESTET, OB PRG SCHON RESIDENT
         lea.l   $680,a0    ; BASISADRESSE
         moveq.l #32-1,d0   ; 32 SLOTS TESTEN
.TST:    tst.l   (a0)+      ; WERT=0?
         beq     .CONT1     ; öBERGEHEN
         movea.l -4(a0),a1  ; SONST ADR HOLEN
         cmpi.l  #"RNDT",2(a1) ; STIMMT KENNUNG?
         beq     .FOUND     ; JA: GEFUNDEN
.CONT1:  dbra    d0,.TST    ; SONST: WEITERSUCHEN

         cmpi.l  #$31415926,$426 ; RESVALID?
         bne     .NOTFND    ; NICHT GEFUNDEN!
         movea.l $42a,a0    ; SONST ADR HOLEN
.TST2:   cmpi.l  #"XBRA",-12(a0) ; XBRA?
         bne     .NOTFND    ; NEIN: >RAUS
         cmpi.l  #"RNDT",-8(a0) ; KENNUNG ?
         beq     .FOUND     ; STIMMT: GEFUNDEN
         tst.l   -4(a0)     ; IST ADR =0?
         beq     .NOTFND    ; => LETZTE ROUTINE
         movea.l -4(a0),a0  ; SONST ADR HOLEN
         bra     .TST2      ; UND WEITERSUCHEN

.NOTFND: cmpi.l  #$31415926,$426 ; RESVALID?
         bne     .NORM      ; NEIN: NORMAL WEITER
         move.l  $42a,OLDRV ; SONST: ADR. MERKEN
.NORM:   move.l  #RESVEC,$42a ; NEUE ROUT. INST.
         move.l  #$31415926,$426 ; RESVALID!

.K_RES:  pea     R_OK       ; TXT:"PRG INST."
         move.w  #9,-(sp)   ; F#=9
         trap    #1         ; GEMDOS: CCONWS
         addq.l  #6,sp      ; STACKKORREKTUR
         bsr     RES_REST   ; AUFLôSUNG REST.
         bsr     SUPOFF     ; SV-MODUS AUS
         clr.w   -(sp)      ; STATUS: 0=OK
         move.l  #MAIN-START+1308,-(sp)
;        SOVIELE BYTES RESIDENT HALTEN...
         move.w  #49,-(sp)  ; F# 49
         trap    #1         ; GEMDOS

.FOUND:  pea     R_NI       ; TXT:"SCHON INST."
         move.w  #9,-(sp)   ; F#=9
         trap    #1         ; GEMDOS: CCONWS
         addq.l  #6,sp      ; STACKKORREKTUR
         bsr     RES_REST   ; AUFLôSUNG REST.
         bsr     SUPOFF     ; SV-MODUS AUS
         clr.w   -(sp)      ; F#0
         trap    #1         ; GEMDOS: PTERM


; *** SUPERVISOR-MODUS AUS ***
SUPOFF:  movea.l (sp)+,a3   ; RöCKSPRUNGADR.
         move.l  OLDSP,-(sp) ; ALTER STACK
         move.w  #32,-(sp)  ; F# 32
         trap    #1         ; GEMDOS: SUPER
         addq.l  #6,sp      ; STACKKORREKTUR
         jmp     (a3)       ; UND ZURöCK...

; *** INSTALLIERT HANDLER ***
H_INST:  move.w  #(EHANDLER-SHANDLER)/4,d0
;                            ; LéNGE IN LONGS
         movea.w #$600,a1   ; KOPIERZIEL
         lea.l   SHANDLER,a0 ; STARTADR. QUELLE
.COPY1:  move.l  (a0)+,(a1)+ ; KOPIERE...
         dbra    d0,.COPY1  ; SOVIELE LANGWORTE

.CLR:    cmpa.l  #$800,a1   ; SCHON BEI  $800?
         beq     .EXIT      ; JA: EXIT
         clr.l   (a1)+      ; SONST LôSCHEN
         bra     .CLR       ; UND NOCH EINMAL
.EXIT:   rts                ; CIAO...


; *** STELLT ALTE AUFLôSUNG WIEDER HER ***
RES_REST:                   ; RESTAURIERE AUFLSG
         tst.b   OLDRES     ; ALTE AUFLSG. TESTEN
         bne     .K_RSRT    ; KEINE RESTAURATION

         move.l  $4ba,d7    ; STARTWERT MERKEN
.WAIT:   move.l  $4ba,d0    ; NéCHSTEN HOLEN...
         sub.l   d7,d0      ; VERSTRICHENE ZEIT
         cmpi.l  #400,d0    ; SCHON 2 SEKUNDEN ?
         bls     .WAIT      ; NEIN: WEITERWARTEN

         move.w  OLDCOL,-(sp) ; ALTE FARBE
         move.w  #3,-(sp)   ; COLOR-REGISTER #3
         move.w  #7,-(sp)   ; F#= 7
         trap    #14        ; XBIOS(7): SETCOLOR
         addq.l  #6,sp      ; STACKKORREKTUR

         clr.w   -(sp)      ; AUFLôSUNG: NIEDRIG
         moveq.l #-1,d0     ; VORLADEN
         move.l  d0,-(sp)   ; PHYSLOC BLEIBT
         move.l  d0,-(sp)   ; LOGLOC AUCH
         move.w  #5,-(sp)   ; F#=5
         trap    #14        ; XBIOS(5): SETSCREEN
         lea.l   $c(sp),sp  ; STACKKORREKTUR

.K_RSRT: rts     


; *** HOLT ZEIT & DATUM AUS IKBD-UHR ***
GETIKBD: move.w  #23,-(sp)  ; F#23
         trap    #14        ; XBIOS: GETTIME
         addq.l  #2,sp      ; STACKKORREKTUR
         rts                ; UND TSCHöSS...

; *** SETZT ZEIT & DATUM FöR IKBD-UHR ***
SETIKBD: move.l  IKTD,-(sp) ; ZEIT/DATUM ANGEBEN
         move.w  #22,-(sp)  ; F#22
         trap    #14        ; XBIOS: SETTIME
         addq.l  #6,sp      ; STACKKORREKTUR
         rts                ; CIAO.

; *** DOS-DATUM SETZEN ***
SETDOSD: move.w  IKDATE,-(sp) ; DATUM PUSHEN
         move.w  #43,-(sp)  ; F#43
         trap    #1         ; GEMDOS: TSETDATE
         addq.l  #4,sp      ; STACKKORREKTUR
         rts                ; UND ZURöCK

; *** DOS-ZEIT SETZEN ***
SETDOST: move.w  IKTIME,-(sp) ; ZEIT ANGEBEN
         move.w  #45,-(sp)  ; F#45
         trap    #1         ; GEMDOS: TSETTIME
         addq.l  #4,sp      ; STACKKORREKTUR
         rts                ; RETURN

; *** WANDELT DATUM-STR IN NUMERISCHEN WERT ***
GETUSD:  cmpi.b  #0,BREAD   ; USER-DATUM
         beq     .EXIT      ;  AUSWERTEN
         lea.l   BSTRG,a0   ; STR-ADR LADEN
         moveq.l #2-1,d1    ; 2 STELLEN WANDELN
         bsr     A0ITOBIN   ; STR(A0) -> NUM
         andi.b  #%11111,d0 ; AUSMASKIEREN
         or.b    d0,d7      ; UND -> D7
         ror.w   #5,d7      ; NéCHSTE POS.
         cmpi.b  #".",(a0)+ ; "." EINGEGEBEN?
         beq     .C1        ; JA: INKREMENT OK
         subq.l  #1,a0      ; SONST WIEDER ZURöCK
.C1:     bsr     A0ITOBIN   ; MONAT WANDELN
         andi.b  #%1111,d0  ; UNDIEREN
         or.b    d0,d7      ; UND SCHREIBEN
         ror.w   #4,d7      ; WEITERSCHIEBEN
         cmpi.b  #".",(a0)+ ; "."?
         beq     .C2        ; JA: KEIN DEKREMENT
         subq.l  #1,a0      ; DEKREMENTIEREN
.C2:     moveq.l #4-1,d1    ; JETZT 4 STELLEN
         bsr     A0ITOBIN   ; WANDELN... (JAHR)
         subi.w  #1980,d0   ; 1980 ABZIEHEN
         andi.b  #%1111111,d0 ; UNDIEREN
         or.b    d0,d7      ; UND SCHREIBEN
         ror.w   #7,d7      ; RICHTIG EINRöCKEN
         move.w  d7,IKDATE  ; DATUM SPEICHERN
.EXIT:   rts                ; ZURöCK AN AUFRUFER

; *** WANDELT ASCII-ZEIT IN NUMERISCHEN WERT ***
GETUST:  cmpi.b  #0,BREAD   ; LéNGE=0 (ABBRUCH)?
         beq     .EXIT      ;, JA: EXIT
         moveq.l #0,d7      ; D7 LôSCHEN
         lea.l   BSTRG,a0   ; STR-ADR LADEN
         moveq.l #2-1,d1    ; 2STELLIGE STUNDEN
         bsr     A0ITOBIN   ; WANDELN->NUMERISCH
         andi.b  #%11111,d0 ; MASKIEREN
         or.b    d0,d7      ; UND SCHREIBEN
         rol.w   #6,d7      ; EINRöCKEN...
         cmpi.b  #":",(a0)+ ; ":" EINGEGEBEN?
         beq     .C1        ; JA: ALLES KLAR
         subq.l  #1,a0      ; SONST WIEDER -1
.C1:     bsr     A0ITOBIN   ; WANDLE MINUTEN
         andi.b  #%111111,d0 ; AUSMASKIEREN
         or.b    d0,d7      ; UND SCHREIBEN
         rol.w   #5,d7      ; PLATZ FöR SEC.
         cmpi.b  #":",(a0)+ ; ":"?
         beq     .C2        ; JA: OK
         subq.l  #1,a0      ; SONST WIEDER -1
.C2:     bsr     A0ITOBIN   ; WANDLE SEKUNDEN
         asr.w   #1,d0      ; IN 2ER-SCHRITTEN
         andi.b  #%11111,d0 ; MASKIEREN
         or.b    d0,d7      ; UND ODERIEREN
         move.w  d7,IKTIME  ; ZEIT SPEICHERN
         rts                ; RETURN
;      * FALLS NUR RETURN GEDRöCKT (LéNGE=0) *
.EXIT:   bsr     GETIKBD    ; ZEIT HOLEN
         move.w  d0,IKTIME  ; UND SCHREIBEN
         rts                ; ZURöCK..

; *** WANDELT DATUM UM IN STRING ***
DATE2ASC:                   ; DATUM(D0)->ASCII
         moveq.l #0,d1      ; D1 LôSCHEN
         moveq.l #2-1,d2    ; 2 STELLEN
         move.b  d0,d1      ;  IN D1 WANDELN
         andi.b  #%00011111,d1 ; TAGE MASKIEREN
         bsr     D0TODEC    ; WANDELN NACH (A0)+
         move.b  #".",(a0)+ ; "." SCHREIBEN
         ror.l   #5,d0      ; MONAT "RUNTERHOLEN"
         move.b  d0,d1      ; UND ANGEBEN IN D1
         andi.b  #%00001111,d1 ; ZUM MASKIEREN
         bsr     D0TODEC    ;  UND WANDELN
         move.b  #".",(a0)+ ; "." SCHREIBEN
         ror.l   #4,d0      ; JAHRE HOLEN
         move.b  d0,d1      ; PARAMETER SETZEN
         andi.b  #%01111111,d1 ; ...MASKIEREN
         addi.w  #1980,d1   ; ..1980 DAZU
         moveq.l #4-1,d2    ;  4STELLIG JETZT
         bsr     D0TODEC    ; WANDELN IN ASCII..
         rts     

; *** WANDELT ZEIT IN EINEN STRING ***
TIME2ASC:                   ; ZEIT(D0)->ASCII
         moveq.l #11,d1     ; HILFSREGISTER
         ror.w   d1,d0      ; ZUM VERSCHIEBEN
         moveq.l #2-1,d2    ; 2STELLIGE STUNDEN
         move.b  d0,d1      ; PARAMETER SETZEN
         andi.b  #%11111,d1 ; UND MASKIEREN
         bsr     D0TODEC    ; SOWIE WANDELN...
         move.b  #":",(a0)+ ; ":" SCHREIBEN
         rol.w   #6,d0      ; MINUTEN EINRöCKEN
         move.b  d0,d1      ; WERT ALS PARAMETER
         andi.w  #%111111,d1 ; ANGEBEN UND MASK.
         bsr     D0TODEC    ; MINUTEN IN ASCII...
         move.b  #":",(a0)+ ; ":" SCHREIBEN
         rol.w   #5,d0      ; JETZT DIE SEKUNDEN
         move.b  d0,d1      ; HOLEN UND DANN
         andi.b  #%11111,d1 ; AUSMASKIERN
         asl.w   #1,d1      ; SOWIE *2 (SYSTEM)
         bsr     D0TODEC    ; JETZT UMWANDELN...
         rts                ; ZURöCK ...

; *** WANDELT ZAHL IN D0 IN ZEICHENKETTE UM ***
D0TODEC: move.w  d2,d3      ; LéNGE DER ZAHL
         lea.l   1(a0,d3.w),a0 ; ZUR BASIS +
         movea.l a0,a1      ; NACH A1 KOPIEREN
.STELLE: divu.w  #10,d1     ; DURCH 10 TEILEN
         swap.w  d1         ; RESTWERT ERMITTELN
         addi.b  #"0",d1    ; "0" ADDIEREN...
         move.b  d1,-(a1)   ; UND SCHREIBEN
         clr.w   d1         ; RESTWERT LôSCHEN
         swap.w  d1         ; HOLE ALTES ERGEBNIS
         dbra    d3,.STELLE ; NéCHSTE STELLE
         rts                ; ZURöCK..

; *** WANDELT ZEICHENKETTE (A0)+ IN WERT ***
A0ITOBIN:                   ; ASCII(A0)->NUM.
         move.w  d1,d2      ; D1 LASSEN
         moveq.l #0,d3      ; D3 LôSCHEN
.GETIT:  rol.l   #8,d3      ; ALTES HOCHSCHIEBEN
         move.b  (a0)+,d3   ; ZEICHEN HOLEN
         subi.b  #"0",d3    ; ASCII->HEXBIN
         dbra    d2,.GETIT  ; NéCHSTES ZEICHEN
         move.w  d1,d2      ; ZéHLER RESTAUR.
         moveq.l #1,d5      ; BASIS =10 HOCH 0
         moveq.l #0,d0      ; LôSCHEN F. ERGEBNIS
.STORE:  moveq.l #0,d4      ; MöLL WEG...
         move.b  d3,d4      ; STELLE HOLEN
         divu.w  #10,d4     ; STELLE= STELLE
         swap.w  d4         ;   MOD 10
         mulu.w  d5,d4      ; STELLE * WERTIGKEIT
         add.l   d4,d0      ; ZUR SUMME ADDIEREN
         mulu.w  #10,d5     ; WERTIGKEIT ERHôHEN
         ror.l   #8,d3      ; NéCHSTE STELLE
         dbra    d2,.STORE  ; S.O.
         rts                ; ZURöCK AN AUFRUF

         .DATA 
TEXT:    .DC.b 27,"E",27,"w",27,"f",27,"p"
         .DCB.b 80,32
         .DC.b 13,10," Date'n Time v3.2"
         .DCB.b 44,32
         .DC.b " 27. November 1991 ",13,10
         .DCB.b 80,32
         .DC.b 27,"q",13,10,13,10
         .DC.b " Geschrieben von Roland Nitz"
         .DC.b " fÅr ST-Computer. Neusetzen:"
         .DC.b " [ALT] gedrÅckt halten.",13,10
         .DC.b " Datum: ttmmyjjjj/tt.mm.jjjj"
         .DC.b "   Zeit: ssmmss/ss:mm:ss  "
         .DC.b " Nur [Return]: öbergehen."
         .DC.b 13,10,13,10
         .DC.b " Systemzeit und -datum ist: "
INSRTTM: .DC.b "00:00:00 00.00.0000"
         .DC.b 13,10,27,"f",0
REQDATE: .DC.b 13,10," Neues Datum: ",27,"e",0
REQTIME: .DC.b 27,"f",13,10,"   Neue Zeit: "
         .DC.b 27,"e",0
OKAY:    .DC.b 27,"f",13,10,13,10
         .DC.b " Zeit & Datum wurden"
         .DC.b " aktualisiert.",13,10,0
R_OK:    .DC.b " Programm ist jetzt resident.",0
R_NI:    .DC.b " Programm war bereits"
         .DC.b " installiert.",0
         .EVEN 
         .BSS 
OLDSP:   .DS.l 1
IKTD:    
IKDATE:  .DS.w 1
IKTIME:  .DS.w 1
HWDATE:  .DS.w 1
BUF:     
BLENG:   .DS.b 1
BREAD:   .DS.b 1
BSTRG:   .DS.b 12
         .EVEN 
OLDCOL:  .DS.w 1
OLDRES:  .DS.b 1

