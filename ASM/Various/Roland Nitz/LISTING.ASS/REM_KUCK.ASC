; ###############################################
; ##  *** R_KUCK! ***                          ##
; ##  ENTFERNT KUCKUCK AUS DEM SPEICHER        ##
; ##  by ROLAND NITZ  (c) 1992 MAXON Computer  ##
; ###############################################
         .ABS
KUCKLEN  equ $200

         .TEXT 
START:   clr.l   -(sp)      ; MODUS: SV
         move.w  #32,-(sp)  ; F# 32
         trap    #1         ; GEMDOS: SUPER
         addq.l  #6,sp      ; STACKKORREKTUR
         move.l  d0,OLDSP   ; ALTEN SSP MERKEN

         lea.l   $600,a0    ; BASISADRESSE
         cmpi.l  #$12123456,(a0) ; SCHON INST.?
         bne     .N_INST    ; NEIN: INSTALLIEREN
         cmpa.l  4(a0),a0   ; STIMMT DAS AUCH?
         bne     .N_INST    ; NEIN: INSTALLIEREN
         moveq.l #0,d1      ; L™SCHEN (PRšFSUMME)
         move.w  #$100-1,d0 ; 256 W™RTER ADDIEREN
.TSTCHS: add.w   (a0)+,d1   ; ADDIEREN
         dbra    d0,.TSTCHS ; NŽCHSTE
         cmpi.w  #$5678,d1  ; PRšFSUMME KORREKT?
         bne     .N_INST    ; JA: WEITER

         lea.l   $680,a0    ; BASISADRESSE
         moveq.l #32-1,d0   ; 32 SLOTS TESTEN
.TST:    tst.l   (a0)+      ; WERT=0?
         beq     .CONT1     ; šBERGEHEN
         movea.l -4(a0),a1  ; SONST ADR HOLEN
         cmpi.l  #"KUCK",2(a1) ; STIMMT KENNUNG?
         beq     .FOUND     ; JA: GEFUNDEN
.CONT1:  dbra    d0,.TST    ; SONST: WEITERSUCHEN

         cmpi.l  #$31415926,$426 ; RESVALID?
         bne     .N_INST    ; NICHT GEFUNDEN!
         lea.l   $42a,a1    ; SONST BASIS HOLEN
.TST2:   movea.l (a1),a0    ; ADR HOLEN
         cmpi.l  #"XBRA",-12(a0) ; XBRA?
         bne     .N_INST    ; NEIN: >RAUS
         cmpi.l  #"KUCK",-8(a0) ; KENNUNG ?
         beq     .RVFOUND   ; STIMMT: GEFUNDEN
         tst.l   -4(a0)     ; IST ADR =0?
         beq     .N_INST    ; => LETZTE ROUTINE
         lea.l   -4(a0),a1  ; SONST ADR HOLEN
         bra     .TST2      ; UND WEITERSUCHEN

.N_INST: pea     NTEXT      ; TXT:"WAR N. INST."
         bra     .PRGEX     ; ->RAUS

.RVFOUND:        
         move.l  -4(a0),(a1) ; IM RESVEC GEFUNDEN
         tst.l   $42a       ; RES_VEC=0?
         bne     .REM       ; NEIN: WEITER
         clr.l   $426       ; SONST RESVALID DEL.
         bra     .REM       ; L™SCHEN UND WEITER

.FOUND:  lea.l   -4(a0),a1  ; ZEIGER AUF ADRESSE
         lea.l   $680,a0    ; BASIS HOLEN
         movea.l (a1),a2    ; ADRESSE HOLEN
         movea.l $42e,a3    ; PHYSTOP HOLEN

         movea.w #-1,a4     ; ADR.
         moveq.l #32-1,d0   ; 32 SLOTS TESTEN
.GMIN:   tst.l   (a0)+      ; IST 0?
         beq     .CONT      ; JA: WEITERSUCHEN
         cmpa.l  -4(a0),a4  ; KLEINER ALS ADR?
         bls     .JMP       ; JA: WEITER
         movea.l -4(a0),a4  ; SONST HOLEN
.JMP:    cmpa.l  -4(a0),a2  ; KLEINER ALS
         bls     .CONT      ; A2? JA: WEITER
         addi.l  #KUCKLEN,-4(a0) ;  ADR. KORR.
.CONT:   dbra    d0,.GMIN   ; WEITERSUCHEN

         clr.l   (a1)       ; EINTRAG L™SCHEN

         cmpa.l  a4,a3      ; KLEINSTE ADR
         bne     .REMERR    ; MUž=PHYSTOP SEIN

         move.l  a2,d0      ; ADRESSDIFFERENZ
         sub.l   a4,d0      ; ERRECHNEN.
         beq     .CNT2      ; IST =0? ->RAUS

         subq.l  #1,d0      ; WG. DBRA
.SHIFT:  move.b  -(a2),KUCKLEN(a2) ; SCHIEBE...
         dbra    d0,.SHIFT  ; UND WEITER...

.CNT2:   moveq.l #0,d0      ; D0 L™SCHEN
         movea.w #$600,a0   ; SEITE LADEN
         move.w  #256-2,d1  ; 255 W™RTER TESTEN
.ADD:    add.w   (a0)+,d0   ; UND ADDIEREN
         dbra    d1,.ADD    ; NŽCHSTES WORT

         move.w  #$5678,d2  ; PRšFSUMME
         sub.w   d0,d2      ; AKT. PRšFSUMME -
         move.w  d2,(a0)+   ; NEUE EINTRAGEN

         addi.l  #KUCKLEN,$42e ; PHYSTOP KORR.
         bra     .REM       ; EXIT ERFOLGSMELDUNG

.REMERR: pea     FEHLER     ; "FEHLER!!"
         bra     .PRGEX     ; ->RAUS

.REM:    pea     REMOVED    ; TXT:"PRG. ENTFERNT"
.PRGEX:  move.w  #9,-(sp)   ; F#:9 CCONWS
         trap    #1         ; GEMDOS-CALL
         addq.l  #6,sp      ; STACKKORREKTUR

         move.w  #7,-(sp)   ; #7: CRAWCIN
         trap    #1         ; (GEMDOS)
         addq.l  #2,sp      ; STACKKORREKTUR

         bsr     SUPOFF     ; SV-MODUS AUS

         clr.w   -(sp)      ; F#0: PTERM
         trap    #1         ; (GEMDOS)

; *** SUPERVISOR-MODUS AUS ***
SUPOFF:  movea.l (sp)+,a3   ; RšCKSPRUNGADR.
         move.l  OLDSP,-(sp) ; ALTER STACK
         move.w  #32,-(sp)  ; F# 32
         trap    #1         ; GEMDOS: SUPER
         addq.l  #6,sp      ; STACKKORREKTUR
         jmp     (a3)       ; UND ZURšCK...

         .DATA 
NTEXT:   .DC.b "E KUCKUCK! konnte nicht"
         .DC.b " gefunden werden.",13,10,0
FEHLER:  .DC.b "E KUCKUCK! nur desaktiviert."
         .DC.b " FEHLER: Speicher ungenutzt!! ",0
REMOVED: .DC.b "E KUCKUCK! wurde aus"
         .DC.b " dem Speicher entfernt.",13,10,0
         .EVEN 

         .BSS 
OLDSP:   .DS.l 1
