
 LEA  AAA(PC),A4
 BSR  OUTABADR

NOCHMAL:
 LEA  ERKL(PC),A4
 BSR  OUTABADR
 MOVEQ #0,D7
 BSR  HOLDIG
 BMI  QUIT
 OR.B D4,D7
 BSR  HOLDIG
 BMI  QUIT
 LSL  #4,D7
 OR.B D4,D7
 BSR  HOLDIG
 BMI  QUIT
 LSL  #4,D7
 OR.B D4,D7
 BSR  HOLDIG
 BMI  QUIT
 LSL  #4,D7
 OR.B D4,D7      nun ist in d4 die hexZahl (zB.7FFF fr ca 360 grad)

 LEA  CRLC(PC),A4
 BSR  OUTABADR
 MOVE D7,D4
 BSR  AMENDE  liefer d0=cosinus d1=sinus (zu d4)
 MOVE D0,D7
 BSR  OUT4HEX    cosinus
 LEA  CRLS(PC),A4
 BSR  OUTABADR
 MOVE D1,D7
 BSR  OUT4HEX    sinus
 BRA  NOCHMAL

OUTABADR:
*subroutine will a4=begin eines ascii-string,
* gibt dessen text aus, bis eine 0 kommt. ruft OUTBYTE.  futsch: d4,a4,evtlmehr
 MOVE.B (A4)+,D4
 BEQ    RTOUTAB
 BSR    OUTBYTE
 BRA    OUTABADR
RTOUTAB:
 RTS

HEX1:
*subroutine will in bits_3_2_1_0 von D7 eine Ziffer,
* macht daraus einen asciiCharacter (0..9 oder A..F) in D4.B
 MOVE.B #$0F,D4
 AND.B    D7,D4 * Ziffer herausmaskiert
 ADD.B  #$30,D4 * erzeug ascii gr/gl '0'
 CMP.B  #$3A,D4 * > '9' ? dann cc (CarryClear) ..
 BCS    OK09
 ADDQ.B #$07,D4 * ..und deshalb mindestens 'A' (=$41) erzeugen
OK09:
 RTS

OUT4HEX:
*subrou will d7.w,gibt 4hexZiff aus, BRA OUT04HEX  futsch: d4,evtlmehr
 ROL.W #4,D7
 BSR  OUT1HEX
 ROL.W #4,D7
 BSR  OUT1HEX
 ROL.W #4,D7
 BSR  OUT1HEX
 ROL.W #4,D7

OUT1HEX:
*subroutine will in bits_3_2_1_0 von D7 eine Ziffer,ruft HEX1,
* somit asciiCharacter in D4.B, gibts auf tv via OUTBYTE.    futsch:d4,evtlmehr
 BSR HEX1
 BSR OUTBYTE
 RTS

HOLDIG:
*subroutine holt eine hexziffer von Tastatur,ruft INBYTE
* gibt entsprechnde Zahl in d4.B, wenns hex war. dann flag PL
 BSR   INBYTE
 CMP.B #'0',D4
 BLO   RMHOLDIG
 CMP.B #'9',D4
 BLS   RRHOLDIG
 AND.B #$DF,D4 *mach Grossbuchstabe
 SUB.B #$47,D4 *dadurch 'F'=$46 zu $FF gemacht
 BCC   RMHOLDIG *denn es war vorher > 'F'
 CMP.B #$FA,D4
 BCC   RRHOLDIG *dann wars vorher > 'A', also hexZiffer
RMHOLDIG:
 MOVE.W #8,CCR  *flag MI
 RTS
RRHOLDIG:
 AND.B #$0F,D4
 RTS

CRLS: DC.B       '  sinus: ',0
CRLC: DC.B 13,10,' cosinus: ',0
ERKL: DC.B 13,10,'tip winkel= vier hexZiffern (0000 bis 7FFF): ',0
AAA: DC.B 13,10,' Autor: Kurt Walz, Friedenstr.23  7180 Craislheim',0

.EVEN


OUTBYTE:
*subroutine will d4.B=ascii, gibts auf tv.
 MOVE D4,-(A7)
 MOVE #2,-(A7)
 TRAP #1
 ADDQ #4, A7
 RTS

INBYTE:
*subroutine wartet auf Tastatur und gibt in d4.B das, was von Tastatur gekommn
*
* macht IMMER GLEICH ECHO auf tv !!
*
 MOVE #7,-(A7)
 TRAP #1
 ADDQ #2, A7
 MOVE.B D0,D4
 BSR  OUTBYTE * hier also ECHO
 RTS

QUIT:
 CLR -(A7)
 TRAP #1

AMENDE:
 DC.W $DADA
.END



