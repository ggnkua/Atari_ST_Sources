module decode8k

title '8K x 24 address decoder for DSP56000 ADS
       MOTOROLA DSP OPERATIONS 8-18-87'

"Description

"Declarations
		decod8		   device      'P16L8';

		a9,a10,a11,a12	   pin	       1,2,3,4;
		a13,a14,a15,xy	   pin	       5,6,7,8;
		ds,ps		   pin	       9,11;

		o7,o6,o5,o4	   pin	       12,13,14,15;
		o3,o2,o1,o0	   pin	       16,17,18,19;

	H,L,X = 1,0,.X.;

		p = !ps & !a15 & !a14 & !a13;
		x1 = !ds & xy & !a15 & !a14 & !a13 & !a12;
		x2 = !ds & xy & !a15 & !a14 & !a13 & a12;
		xry= !ds & !a15 & !a14 & !a13 & !a12;
		y1 = !ds & !xy & !a15 & !a14 & !a13 & !a12;
		y2 = !ds & !xy & !a15 & !a14 & !a13 & a12;

equations
		!o0 = p;

		!o1 = (p & !a12) # (x1);

		!o2 = (p & !a12) # (y1);

		!o3 = (p & !a12) # (xry & !a11);

		!o4 = (p & !a12 & !a11) # x1 # (y1 & !a11);

		!o5 = (p & !a12 & !a11) # (x1 & !a11) # y1;

		!o6 = (p & !a12 & !a11) # x2 # (y2 & !a11);

		!o7 = (p & !a12 & !a11) # (x2 & !a11) # y2;

test_vectors

([a15,a14,a13,a12,a11,a10,a9,ps,ds,xy] -> [o0,o1,o2,o3,o4,o5,o6,o7])
" no output test
 [ L , L , H , X , X , X , X,X ,X ,X ] -> [H ,H ,H ,H ,H ,H ,H ,H ];
" p mem test up to 8k
 [ L , L , L , X , X , X , X,L ,H ,X ] -> [L ,L ,L ,L ,L ,L ,L ,L ];
" p mem test up to 4k
 [ L , L , L , L , X , X , X,L ,H ,X ] -> [L ,L ,L ,L ,L ,L ,L ,L ];
" p mem test up to 2k
 [ L , L , L , L , L , X , X,L ,H ,X ] -> [L ,L ,L ,L ,L ,L ,L ,L ];
" x mem test up to 4k
 [ L , L , L , L , X , X , X,H ,L ,H ] -> [H ,L ,H ,L ,L ,L ,H ,H ];
" x mem test up to 2k
 [ L , L , L , L , L , X , X,H ,L ,H ] -> [H ,L ,H ,L ,L ,L ,H ,H ];
" x mem test up to 4k at 1000h
 [ L , L , L , H , X , X , X,H ,L ,H ] -> [H ,H ,H ,H ,H ,H ,L ,L ];
" x mem test up to 2k at 1000h
 [ L , L , L , H , L , X , X,H ,L ,H ] -> [H ,H ,H ,H ,H ,H ,L ,L ];
" y mem test up to 4k
 [ L , L , L , L , X , X , X,H ,L ,L ] -> [H ,H ,L ,L ,L ,L ,H ,H ];
" y mem test up to 2k
 [ L , L , L , L , L , X , X,H ,L ,L ] -> [H ,H ,L ,L ,L ,L ,H ,H ];
" y mem test up to 2k at 1000h
 [ L , L , L , H , L , X , X,H ,L ,L ] -> [H ,H ,H ,H ,H ,H ,L ,L ];
" y mem test up to 4k at 1000h
 [ L , L , L , H , X , X , X,H ,L ,L ] -> [H ,H ,H ,H ,H ,H ,L ,L ];
" test of x memory greater than 4k
 [ L , L , H , H , X , X , X,H ,L ,H ] -> [H ,H ,H ,H ,H ,H ,H ,H ];
" test of p mem greater than 8k
 [ L , L , H , L , X , X , X,L ,H ,X ] -> [H ,H ,H ,H ,H ,H ,H ,H ];

end decode8k

