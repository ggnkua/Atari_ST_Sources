OUTPUT_ARCH(m68k)
SEARCH_DIR(/lib); SEARCH_DIR(/usr/lib); SEARCH_DIR(/usr/local/lib); SEARCH_DIR(/usr/m68k-atari-mint/lib);
PROVIDE (__stack = 0);
MEMORY
{
  ram (rw) : ORIGIN = 0x21000000, LENGTH = 0x50000 /* also in main pci_bios.h PCI_DRIVERS_OFFSET and PCI_DRIVERS_SIZE */
  flash (rx) : ORIGIN = 0xEC0000, LENGTH = 0x40000
  flash2 (rx) : ORIGIN = 0xFC0000, LENGTH = 0x30000
}
SECTIONS
{
  _d_rezword = 0xA4EA; /* AES, for videocnf.c */
  .text :
  {
    CREATE_OBJECT_SYMBOLS
    *(.text)
    /* The next six sections are for SunOS dynamic linking.  The order
       is important.  */
    *(.dynrel)
    *(.hash)
    *(.dynsym)
    *(.dynstr)
    *(.rules)
    *(.need)
    _etext = .;
    __etext = .;
  } > flash
  . = ALIGN(2);
  .data :
  {
    /* The first three sections are for SunOS dynamic linking.  */
    *(.dynamic)
    *(.got)
    *(.plt)
    *(.data)
    *(.linux-dynamic) /* For Linux dynamic linking.  */
    CONSTRUCTORS
    _edata  =  .;
    __edata  =  .;
  } > flash2
  .bss :
  {
    __bss_start = .;
   *(.bss)
   *(COMMON)
   _end = ALIGN(4) ;
   __end = ALIGN(4) ;
  } > ram
}

