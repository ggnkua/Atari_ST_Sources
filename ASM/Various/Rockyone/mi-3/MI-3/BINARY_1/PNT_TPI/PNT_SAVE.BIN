;rockyone			POUR MI-3 MI-9			28/06/2016
;
;reduction du code						23/02/2017
;
;	save 	PRISM-PAINT v1.1	PNT compress‚ ou non compress‚
;	save 	TRUE-PAINT  v1.03	TPI n'est jamais compress‚
;			------------------------
;
; Source	Buffer Contenant  le fichier image format PI1-3
; Destination  	Bufer 32480 octet
; Op%		0 pas compresser, 1 compresser
;
;
; Utilisez avant :      pc_save( L Source,L Destination,Op%) 
;			
; Return dans	low d0  La taille des data image 
;			taille = lpeek(reserved(0)) 
;
;
;		CALL Pnt_Tpi_Save( L Destination, taille )
;		---------------------------------
; Retour dans d0  la taille du fichier a sauvegarder
;
; + 0 'PNT'+$0	signature
; + 4	1 w	$0100	version
; + 6	1 w 	nombre de couleur
; + 8	1 w	largeur
; +10	1 w	hauteur
; +12	1 w	nombre de plan
; +14	1 w	0  / 1 = compression
; +16	1 l	taille des data images
; +20 	108 bytes $0
; +130  words 	palette vdi valeur rvb 0-1000 ordre vdi
;-------------------------------------------------------------------------	
;a0 source

		movem.l	d1-d7/a0-a6,-(sp)
		movem.l	60(sp),a0
		movea.l	a0,a5			; but = source

;taille de la palette ?
		move.b	1(a0),d1		;  0   1  2 resol source
		subq.l	#2,d1			; -2, -1, 0
		neg.w	d1			;  2,  1, 0
		mulu.w	#10,d1			;  20, 10,0

		lea.l	table(pc),a2
		adda.w	d1,a2			;  table PNT / resol	

;recopier data image source apres entete et palette PNT 
		moveq.l	#0,d4
		move.w	64(sp),d4		; taille du fichier source
		bpl.s	ok_size
		ext.l 	d4
		andi.l	#$ffff,d4 
		
ok_size:	lea.l	0(a0,d4.l),a1		; fin du source
		lea.l	128-34(a1),a4		; copier data image sur 
;						: a0 + taille source - 34
;						;    + taille header
		adda.w	(a2)+,a4		;    + taille palette PNT

		move.l	a4,d0			;R. taille total du 
		sub.l	a0,d0			;   fichier PNT

		subi.w	#34,d4			; nombre de data a recopier
;                                               ; a incrire dans l'entete
;						; - 1 byte compresion
;						; - 1 byte resolution
;						; -32 byte palette		
		move.l	d4,d1				
copy_pic:	move.b	-(a1),-(a4)
		subq.l	#1,d1
		bhi.s	copy_pic		; >0

		lea.l	128+6(a0),a4		; point B de RVB
		addq.l	#2,a0			; star palette source

		move.b	1(a5),d7		; resol source
;........................................
		lea.l	vdi_0(pc),a3		; table conversion resol 0
		tst.b	d7			 
		beq.s	ok_table	
		lea.l	16(a3),a3		; table conversion resol 1
		cmp.b	#1,d7		
		beq.s  ok_table
		addq.l	#4,a3			;table conversion resol  2
ok_table:
	
;.......................................	
	
palette:	move.w	(a2),d2		; nombre de couleurs 
		subq.l	#1,d2		; -1 pour dbra	
		moveq.l	#0,d6

read_color:	moveq.l	#2,d1		; trois valeur a transformer
		move.b	(a3)+,d6	; read index vdi color
		move.w	0(a0,d6.w),d3	; read color source

code_bvr:	moveq.l	#0,d5
		move.b	d3,d5		;B, then V, then R

		andi.b	#$f,d5		
		mulu.w	#1000,d5
		divu.w	#15,d5			

		move.b	d5,d7
		mulu.w	#2,d5
		cmpi.w	#1000,d5
		blt.s	c8_bvr
		subi.w	#1000,d5
c8_bvr:		andi.b	#3,d7
		beq.s	ok_bvr
		addq.b	#1,d5

ok_bvr:		move.w	d5,-(a4)	;write B, then V, then R
		lsr.w	#4,d3
		dbra.w	d1,code_bvr

		lea.l	12(a4),a4
		dbra.w	d2,read_color
	
;.......................................HEADER........................		
		moveq.l	#0,d1
		tst.b	(a5)			; byte compression
		beq.s	not_packed
		moveq.l	#1,d1	

not_packed: 	move.l	#$504e5400,(a5)+	; 'PNT'+$0
		move.w	#$100,(a5)+		; version PNT 1.00 
	
		moveq.l	#3,d2
copy_header:	move.w	(a2)+,(a5)+		; nb color,L , H, nb plan
		dbra.w	d2,copy_header

		move.w	d1,(a5)+		;+14  flag compression 1
		move.l	d4,(a5)+		;+16  taille des data

		moveq.l	#26,d1			;+20  108 bytes nul avant
clean:		move.l	#0,(a5)+		;     la palette RVB
		dbra.w	d1,clean

;.........................................................................
end_prg:	movem.l	(a7)+,d1-d7/a0-a6
		rts
;.........................................................................
;	taille palette, color, largeur, hauteur, nombre de plan
table:	dc.w	12,2,640,400,1
	dc.w	24,4,640,200,2
	dc.w	96,16,320,200,4	

Vdi_0:	dc.b 	0,30,2,4,8,12,6,10,14,16,18,20,24,28,22,26
	dc.b 	0,6,2,4
	dc.b 	0,2

	end
;-------------------------------------------------------------------------