* @@   @@  @@@@@@	@@@@	@@@@   @@@@@@	@@@   @@   @@	@@@@
* @@@ @@@	 @@    @@  @@  @@  @@	 @@    @@ @@  @@@  @@  @@  @@
* @@@@@@@	 @@    @@	   @@		 @@   @@   @@ @@@@ @@  @@
* @@@@@@@	 @@ 	@@@@	@@@@	 @@   @@   @@ @@ @@@@	@@@@
* @@ @ @@	 @@ 	   @@	   @@	 @@   @@   @@ @@  @@@	   @@
* @@   @@	 @@    @@  @@  @@  @@	 @@    @@ @@  @@   @@  @@  @@
* @@   @@  @@@@@@	@@@@	@@@@   @@@@@@	@@@   @@   @@	@@@@

* Routines to display mission related messages.

* Mission #1 - Stolen Constrictor

*	$10 - print message offering mission upon docking
*	$11 - waiting until docked with briefing planet, then display message
*		  informing the player to make a inter-galactic jump
*	$12 - waiting for inter-galactic jump, then initialises number of
*		  planets to visit to 5
*	$13 - waiting for player to visit the five planets. Each time the
*		  player docks a new planet is chosen.
*	$14 - wait until entering system with Constrictor in it.
*	$15 - get attacked by Constrictor in deep space.
*	$16 - give reward at next docking

* Atari ST Version 1.0 13th May 1988 Copyright (c) 1988 Mr. Micro Ltd.
* Designed and coded by Rob Nicholson. Adapted from the MSX version.

; Modification history:

;	V1.0	13/05/88	Original version.
;	V1.1	26/03/90	Spelling mistake. 'the sun is this' changed to
;						'the sun in this' in text1.

	include macros

	xref yes_no,cargo_spare,prepare_text,str_copy,build_number,print_centre
	xref print_string,attract,search,build_name,init_cursor,choose_planet,data
	xref continue,status,launch,buy_cargo,sell_cargo,equip_ship,galactic_chart
	xref local_chart,market_prices,inventory,disk,find_planet,home_cursor
	xref options

* ---- LOCAL CONSTANTS ----

max_mission:	equ 5					max. # of missions

	module missions


* ***************************************
* **								   **
* ** NEW_MISSION - START A NEW MISSION **
* **								   **
* ***************************************

* Starts a new mission - called when JUMP_COUNT = 0

* Entry: None
* Exit:  None

* Regs: ?
* Subr: ?

	subr new_mission,global

	move #mission_rate,jump_count(a6)	reset counter
	move next_mission(a6),d0			no more missions ?
	cmp #max_mission+1,d0
	ret <eq>							yes, return (no mission)
	inc next_mission(a6)				advance to next mission
	asl #4,d0							set state #0 (e.g. $10)
	move d0,mission(a6)

	ret 								return


*************************************
**								   **
** DOCK_CHECK - MISSION DOCK CHECK **
**								   **
*************************************

* Perform checks for special messages on docking.

* Entry:  None
* Exit:   None

* Regs:   ?
* Subr:   ?

	subr dock_check,global

	move mission(a6),d0 				constrictor mission ?
	move d0,d1
	and #240,d1
	cmp #$10,d1
	if <eq> then.s						yes
		cmp #$10,d0 					start of mission ?
		beq offer1						yes, offer mission
		cmp #$11,d0 					check for briefing planet ?
		if <eq> then.s					yes
			move current(a6),d0 		reached required planet ?
			cmp mission_planet(a6),d0
			beq gone1					yes, display message
		endi
		cmp #$13,d0 					check for reaching planet ?
		if <eq> then.s					yes
			move current(a6),d0 		reached required planet ?
			cmp mission_planet(a6),d0
			beq gone2					yes, display message
		endi
		cmp #$16,d0 					give reward ?
		beq thanks2 					yes
		jmp status						display status screen and return
	endi
	cmp #$20,d1 						thargoid documents mission ?
	if <eq> then.s						yes
		cmp #$20,d0 					offer mission ?
		beq offer1						yes
		move current(a6),d0 			at correct planet ?
		cmp mission_planet(a6),d0
		beq thanks1 					yes, display thanks message
		jmp status
	endi
	cmp #$30,d1 						rescue refugees ?
	if <eq> then.s						yes
		cmp #$31,d0 					offer mission ?
		beq offer3						yes
		cmp #$34,d0 					give thanks ?
		beq thanks3 					receive thanks
		jmp status
	endi
	cmp #$40,d0 						display warning ?
	beq warning4						yes
	cmp #$50,d0 						display another warning ?
	beq warning5						yes
	cmp #$53,d0 						thanks for mission ?
	beq thanks5 						yes
	jmp status							display status screen


*********************************************
**										   **
** MISSION_SCREEN - PREPARE MISSION SCREEN **
**										   **
*********************************************

* Prepares the text screen and prints the current mission number.

* Entry:  HL = Pointer to text (AT format)
* Exit:   None

* Regs: ?
* Subr: ?

	subr mission_screen

	push.l a0							save text pointer
	jsr prepare_text					prepare text screen
	lea text15(pc),a0					build title
	lea pad(a6),a1
	jsr str_copy
	move.l a1,a0
	dec.l a0
	move mission(a6),d0
	lsr #4,d0
	hclr d0
	clr d1
	jsr build_number
	st slow_print(a6)					start slow print
	lea pad(a6),a0						print title
	moveq #title_y,d1
	jsr print_centre
	lea text16(pc),a0					display commanders name
	jsr print_string
	lea player_name(a6),a0
	jsr print_string
	pop.l a0							print full text
	jmp print_string


**********************************
**								**
** OFFER1 - OFFER MISSION #1/#2 **
**								**
**********************************

* Offer the player the choice of taking the Constrictor or Thargoid
* documents mission.

* Entry:  None
* Exit:   None

* Regs:   ?
* Subr:   ?

	subr offer1

	cmp #$10,mission(a6)				Mission #1 ?
	call <eq>,attract					display constrictor
	movem splanet+xcoord(a6),d0/d1		find furthest planet
	jsr search
	move d0,mission_planet(a6)
	lea text17(pc),a0					display mission screen
	bsr mission_screen
	cmp #$10,mission(a6)				constrictor mission ?
	if <eq> then.s						yes
		lea text6(pc),a0
	else.s								thargoid documents mission
		lea text11(pc),a0
	endi
	jsr print_string					print 1st section
	push.l a0							save pointer to next section
	move mission_planet(a6),d0			print planet name
	jsr build_name
	lea planet_name(a6),a0
	jsr print_string
	pop.l a0							print 2nd section
	jsr print_string
	clr slow_print(a6)					stop slow print
	lea text19(pc),a0					accept mission ?
	jsr yes_no
	cmp.b #'N',d0
	if <eq> then.s						no
		clr mission(a6) 				mission over
		jmp status
	endi
	inc mission(a6) 					state #1
	cmp #$21,mission(a6)				mission #2 ?
	if <eq> then.s						yes
		move.l #1,hold+documents*4(a6)	install documents
		move #70,equip+fuel(a6) 		fill her up
		move #4,equip+missiles(a6)
		lea text20(pc),a0				display screen
		bsr mission_screen
	else.s
		lea text7(pc),a0				display screen
		bsr mission_screen
		push.l a0
		move mission_planet(a6),d0
		jsr build_name
		lea planet_name(a6),a0
		jsr print_string
		pop.l a0
		jsr print_string
	endi
	clr slow_print(a6)
	lea mission_table(pc),a0			initialise mouse and exit
	jmp init_cursor


* *******************************
* **						   **
* ** OFFER3 - OFFER MISSION #3 **
* **						   **
* *******************************

* Offers the mission of saving the refugees from their exploding sun.

* Entry: None
* Exit:  None

* Regs: ?
* Subr: ?

	subr offer3

	inc mission(a6) 					mission state #2
	lea hold(a6),a0 					empty the hold
	loop 1,max_products
		clr.l (a0)+
	next 1
	lea text1(pc),a0					display mission screen
	bsr mission_screen
	clr slow_print(a6)
	lea text18(pc),a0					input yes/no answer
	jsr yes_no
	cmp.b #'Y',d0						yes ?
	if <ne> then.s						no
		lea text5(pc),a0				display text and exit
	else.s
		jsr cargo_spare 				fill cargo with refugees
		move.l d3,hold+refugees*4(a6)
		inc mission(a6) 				mission state #3
		lea text4(pc),a0
	endi
	bsr mission_screen					print message
	clr slow_print(a6)
	lea mission_table(pc),a0
	jmp init_cursor


* *************************************************
* **											 **
* ** WARNING4 - DISPLAY WARNING ABOUT THE COUGAR **
* **											 **
* *************************************************

* Display a warning screen about the Cougar.

* Entry: None
* Exit:  None

* Regs: ?
* Subr: ?

	subr warning4

	lea text21(pc),a0					print message
	bsr mission_screen
	clr slow_print(a6)
	inc mission(a6) 					move to state #$41
	lea mission_table(pc),a0
	jmp init_cursor


* ************************************************
* **											**
* ** WARNING - DISKPLAY WARNING ABOUT THARGOIDS **
* **											**
* ************************************************

* Display message about the invasion of a local system.

* Entry: None
* Exit:  None

* Regs: ?
* Subr: ?

	subr warning5

	lea text13(pc),a0					print message
	bsr mission_screen
	clr slow_print(a6)
	inc mission(a6) 					move to state #$51
	lea mission_table(pc),a0
	jmp init_cursor


*******************************
**							 **
** GONE1 - DISPLAY A MESSAGE **
**							 **
*******************************

* Display the message informing the player that the Constrictor has
* made a galactic jump.

	subr gone1,global

	lea text8(pc),a0					display message
	bsr mission_screen
	clr slow_print(a6)
	inc mission(a6) 					move to state #$12
	lea mission_table(pc),a0			initialise mouse and return
	jmp init_cursor


*******************************
**							 **
** GONE2 - DISPLAY A MESSAGE **
**							 **
*******************************

* Display the message informing the player that the Constrictor has
* left this system.

	subr gone2,global

	jsr choose_planet					Choose close another planet
	lea text9(pc),a0					Display message
	bsr mission_screen
	move mission_planet(a6),d0			Display planet
	jsr build_name
	lea planet_name(a6),a0
	jsr print_string
	clr slow_print(a6)
	dec mission_count(a6)				Enough planets visited ?
	if <eq> then.s						Yes
		inc mission(a6) 				Mission state #4
	endi
	lea mission_table(pc),a0			initialise mouse and exit
	jmp init_cursor


*****************************************************
**												   **
** THANKS - DISPLAY THANKS FOR CONSTRICTOR MISSION **
**												   **
*****************************************************

* Display thank you message for completing the Constrictor missions. Install
* a military energy unit as a reward.

	subr thanks1

	lea text12(pc),a0					Display message
	bsr mission_screen
	clr slow_print(a6)
	move #2,equip+energy_unit(a6)		Military energy unit
	clr mission(a6) 					mission over
	clr.l hold+documents*4(a6)			remove documents
	lea mission_table(pc),a0
	jmp init_cursor


* *************************************************************
* **														 **
* ** THANKS2 - DISPLAY THANKS FOR THARGOID DOCUMENTS MISSION **
* **														 **
* *************************************************************

* Display thank you message for completing the Thargoid documents missioms. As
* as reward, add 10,000 credits to the players cash.

* Entry: None
* Exit:  None

* Regs: ?
* Subr: ?

	subr thanks2

	lea text10(pc),a0					Display message
	bsr mission_screen
	clr slow_print(a6)
	add.l #100000,cash(a6)				reward for 10,000 credits
	clr mission(a6) 					mission over
	lea mission_table(pc),a0
	jmp init_cursor



* ***************************************************
* **											   **
* ** THANKS3 - DISPLAY THANKS FOR REFUGEES MISSION **
* **											   **
* ***************************************************

* Display thank you message for carrying the refugees to the next space
* station. As a reward, add 1kg of gem-stones to the players hold.

* Entry: None
* Exit:  None

* Regs: ?
* Subr: ?

	subr thanks3

	lea text2(pc),a0					display mission screen
	bsr mission_screen
	clr slow_print(a6)
	clr.l hold+refugees*4(a6)			remove refugees
	move.l #1000,hold+gem_stones*4(a6)	add gem-stones
	clr mission(a6) 					mission over
	lea mission_table(pc),a0
	jmp init_cursor


* ***********************************************************
* **													   **
* ** THANKS5 - DISPLAY THANKS FOR DESTROYING SPACE STATION **
* **													   **
* ***********************************************************

* Display a thank you message and fit the ECM jammer.

* Entry: None
* Exit:  None

* Regs: ?
* Subr: ?

	subr thanks5

	lea text14(pc),a0					display thank you
	bsr mission_screen
	clr slow_print(a6)
	clr mission(a6) 					Mission over
	move #1,equip+ecm_jammer(a6)		Fit ECM jammer
	lea mission_table(pc),a0
	jmp init_cursor


* ******************************************************
* **												  **
* ** WAIT_TILL_READ - WAIT UNTIL SCREEN HAS BEEN READ **
* **												  **
* ******************************************************

* Prompts the player to press any key to continue then jump to status
* screen.

* Entry: None
* Exit:  None

* Regs: ?
* Subr: ?

	subr wait_till_read

	jsr continue						wait for key press
	jmp status							display status screen


* ---- LOCAL DATA ----

* Mission mouse action table.

mission_table:

	include icons
	dc.w -1
	dc.l 0

* Text messages.

text1:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 6*8,'Warning - the sun in this system is'
	db c_cursor,0, 7*8,'going Supernova. You are advised to'
	db c_cursor,0, 8*8,'leave as soon as possible.'

	db c_ink,yellow
	db c_cursor,0,10*8,'Please help us! Everyone else has fled'
	db c_cursor,0,11*8,'the supernova but we were left behind'
	db c_cursor,0,12*8,'when our shuttle was damaged by an'
	db c_cursor,0,13*8,'asteroid. We managed to limp back to the'
	db c_cursor,0,14*8,'space station but face certain death if'
	db c_cursor,0,15*8,'we don''t leave soon.'

	db c_ink,lgt_blue
	db c_cursor,0,17*8,'There are only a few of us - we could'
	db c_cursor,0,18*8,'fit in your cargo hold.',0

text2:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 9*8,'Thank you for saving our lives and'
	db c_cursor,0,10*8,'carrying us to safety. Please accept a'
	db c_cursor,0,11*8,'selection of precious Zanxian gem stones'
	db c_cursor,0,12*8,'as a token of our appreciation.',0

text4:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 8*8,'Thank you - you will be well rewarded by'
	db c_cursor,0, 9*8,'our government.'

	db c_ink,yellow
	db c_cursor,0,11*8,'I''m afraid that we''ve had to dump any'
	db c_cursor,0,12*8,'cargo you had to get us all in. If'
	db c_cursor,0,13*8,'you could just drop us off at the'
	db c_cursor,0,14*8,'nearest system that''ll be fine.',0

text5:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0,10*8,'We hope you die a horribly long and'
	db c_cursor,0,11*8,'lingering death at the hands of a slimey'
	db c_cursor,0,12*8,'green lobstoid, that''s if you''re not'
	db c_cursor,0,13*8,'blown to bits by our sun!',0

text6:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 7*8,'A prototype model of the new top-secret'
	db c_cursor,0, 8*8,'military ship, the '
	db c_ink,pulse,'CONSTRICTOR',c_ink,magenta,', has been'
	db c_cursor,0, 9*8,'been stolen by unknown agents from the'
	db c_cursor,0,10*8,'navy research base at '
	db c_ink,white,0

	db c_ink,yellow
	db c_cursor,0,12*8,'Due to a capabilities of this ship, the'
	db c_cursor,0,13*8,'Galactic Co-operative of Worlds is'
	db c_cursor,0,14*8,'offering a large reward to anyone who'
	db c_cursor,0,15*8,'destroys the Constrictor before it falls'
	db c_cursor,0,16*8,'into enemy hands.',0

text7:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 7*8,'For full mission briefing, please report'
	db c_cursor,0, 8*8,'to the space station at '
	db c_ink,white,0

	db c_ink,yellow
	db c_cursor,0,11*8,'Due to the sensitive nature of this'
	db c_cursor,0,12*8,'assignment, it is vital that you keep'
	db c_cursor,0,13*8,'details of the Constrictor to yourself.'

	db c_ink,lgt_blue
	db c_cursor,0,16*8,'Good luck Commander!',0

text8:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 8*8,'The Galactic Intelligence Agency (GIA)'
	db c_cursor,0, 9*8,'reports that the Constrictor made an'
	db c_cursor,0,10*8,'inter-galactic jump out of this system'
	db c_cursor,0,11*8,'a short while ago.'

	db c_ink,yellow
	db c_cursor,0,13*8,'It is suggested that you make the same'
	db c_cursor,0,14*8,'jump and report to the nearest space'
	db c_cursor,0,15*8,'station for further details.',0

text9:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 8*8,'GIA reports that after causing havoc,'
	db c_cursor,0, 9*8,'the Constrictor made a hyperspace jump'
	db c_cursor,0,10*8,'out of this system.'

	db c_ink,yellow
	db c_cursor,0,13*8,'The deep space tracking station suggests'
	db c_cursor,0,14*8,'that it''s destination was ',c_ink,white,0

text10:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 6*8,'The Galactic Co-opertive of Worlds is'
	db c_cursor,0, 7*8,'forever in your debt, oh mighty trader!'

	db c_ink,yellow
	db c_cursor,0, 9*8,'As a small token of our appreciation,'
	db c_cursor,0,10*8,'please accept a reward of 10,000 credits'
	db c_cursor,0,11*8,'for your trouble.'

	db c_ink,lgt_blue
	db c_cursor,0,13*8,'While I have your attention, the'
	db c_cursor,0,14*8,'Galactic Police would like me to remind'
	db c_cursor,0,15*8,'you about the small matter of several'
	db c_cursor,0,16*8,'hundred unpaid parking tickets!',0

text11:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 6*8,'The navy has managed to obtain rare'
	db c_cursor,0, 7*8,'blueprints of a Thargoid battle ship'
	db c_cursor,0, 8*8,'giving details of the drive and weapon'
	db c_cursor,0, 9*8,'systems.'

	db c_ink,yellow
	db c_cursor,0,11*8,'These documents need to be taken as soon'
	db c_cursor,0,12*8,'as possible to the main naval base in'
	db c_cursor,0,13*8,'orbit around the planet '
	db c_ink,white,0

	db c_ink,lgt_blue
	db c_cursor,0,15*8,'The enemy is unaware that we have these'
	db c_cursor,0,16*8,'documents, so the journey should proceed'
	db c_cursor,0,17*8,'without any trouble.',0

text20:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 9*8,'The documents have been placed in your'
	db c_cursor,0,10*8,'cargo hold. Your ship has been fully'
	db c_cursor,0,11*8,'serviced and is ready for takeoff.'

	db c_ink,yellow
	db c_cursor,0,13*8,'Good luck Commander!',0

text12:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 5*8,'We are (again) forever in your debt'
	db c_cursor,0, 6*8,'for bringing these very important'
	db c_cursor,0, 7*8,'documents to us.'

	db c_ink,yellow
	db c_cursor,0, 9*8,'As a reward we have fitted your Cobra'
	db c_cursor,0,10*8,'with the latest naval energy unit which'
	db c_cursor,0,11*8,'will recharge your energy banks at twice'
	db c_cursor,0,12*8,'the normal rate. In light of the number'
	db c_cursor,0,13*8,'of ''incidents'' you get yourself involved'
	db c_cursor,0,14*8,'in, you will probably find it rather'
	db c_cursor,0,15*8,'useful.'

	db c_ink,lgt_blue
	db c_cursor,0,17*8,'The GIA also wishes to apologise for the'
	db c_cursor,0,18*8,'security leak to the Thargoids. They are'
	db c_cursor,0,19*8,'carrying out intensive investigations'
	db c_cursor,0,20*8,'and the culprit will soon be found.',0

text13:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 7*8,'The Galactic Intelligence Agency reports'
	db c_cursor,0, 8*8,'that a nearby system has been invaded by'
	db c_cursor,0, 9*8,'Thargoid ships and that the space'
	db c_cursor,0,10*8,'station has been captured. All available'
	db c_cursor,0,11*8,'forces are being gathered to destroy the'
	db c_cursor,0,12*8,'invading force.'

	db c_ink,yellow
	db c_cursor,0,14*8,'The space station has become the main'
	db c_cursor,0,15*8,'Thargoid headquarters and it''s from this'
	db c_cursor,0,16*8,'base that the planetary attack is being'
	db c_cursor,0,17*8,'mounted. The space station must be'
	db c_cursor,0,18*8,'detroyed - no matter what the cost!',0

text14:

*						0123456789012345678901234567890123456789'
	db c_ink,magenta
	db c_cursor,0, 8*8,'Thanks for destroying the Thargoid base.'

	db c_ink,yellow
	db c_cursor,0,10*8,'A new device was salvaged from one of'
	db c_cursor,0,11*8,'Thargoid ships which we have fitted to'
	db c_cursor,0,12*8,'your Cobra. The device appears to jam'
	db c_cursor,0,13*8,'ECM broadcasts and is activated by the'
	db c_cursor,0,14*8,'L key on your flight console.',0

text21:

*						0123456789012345678901234567890123456789'
	db c_ink,pulse
	db c_cursor,0, 7*8,'Warning to all Traders:'

	db c_ink,magenta
	db c_cursor,0, 9*8,'Reports having been coming in from'
	db c_cursor,0,10*8,'Traders in this sector of an unknown'
	db c_cursor,0,11*8,'hostile ship with awesome capabilities.'
	db c_cursor,0,12*8,'Rumours suggest that this ship is fitted'
	db c_cursor,0,13*8,'with a device which causes on-board'
	db c_cursor,0,14*8,'computer systems to malfunction.',0

text15: db 'Mission No. ',0
text16: db c_cursor,0,3*8,c_ink,orange,'Attn: '
		db c_ink,lgt_green,'Commander ',0
text17: db 0
text18: db 'Will you help us',0
text19: db 'Accept mission',0
