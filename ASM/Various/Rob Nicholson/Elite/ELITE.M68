;  @@@@   @@@@@@		 @@@@@@@ @@@@	  @@@@@@  @@@@@@ @@@@@@@
; @@  @@  @ @@ @		  @@   @  @@		@@	  @ @@ @  @@   @
; @@		@@			  @@ @	  @@		@@		@@	  @@ @
;  @@@@ 	@@			  @@@@	  @@		@@		@@	  @@@@
;	  @@	@@			  @@ @	  @@   @	@@		@@	  @@ @
; @@  @@	@@			  @@   @  @@  @@	@@		@@	  @@   @
;  @@@@    @@@@ 		 @@@@@@@ @@@@@@@  @@@@@@   @@@@  @@@@@@@

; Welcome aboard this Cobra Mk III trading and combat craft. The ship  has
; been supplied to you by Faulcon deLacy Spaceways, by arrangements with the
; Galactic Co-operative of Worlds whose Space and Interstellar Pilot Exams you
; have just successfully completed. The small flight  manual supplied with the
; craft is designed to	familiarise you  with all the aspects of space flight,
; combat and trading, and we hope that it will be of use to you.

; All routines in Elite are commented and the following conventions are used:

;	1. When an address register is specified as an entry or exit parameter,
;	   it is assumed to be 32 bits unless otherwise specified.
;	2. When a data register is specified as an entry or exit parmameter, it
;	   is assumed to be 16 bits unless otherwise specified.
;	3. A6 always points to the base of the variables (VARS) and is always
;	   preserved.
;	4. Additionally to the registers listed, all subroutines corrupts the
;	   status flags unless specified otherwise.

; Atari ST Version 1.0 15th March 1988

; Copyright (c) 1988 Mr. Micro and Firebird Software.
; Converted from the MSX version by Rob Nicholson.

; Modifcation history:

; V1.02 ??/??/?? 1st production version.
; V1.03 04/10/88 Mission #5 space station bug fixed (took one shot to blow
;				 it up. 2nd production version.
; V1.1	26/03/90 Police record reset to clean after using escape capsule.
;				 Password changed to SUZANNE and encoded in the program.
;				 Spelling mistake in mission text changed.
;				 Rating clamped at Elite.
;				 Reduce_Shields routine debugged to prevent you lossing
;				 equipment you didn't have.
;			     'Cursor reset to xxxx' prompt changed to 'Current system: '
;				 'E-L-I-T-E' message flashes on status screen.
;				 Unable to engage docking computer while hyperspace
;				 countdown is active.
;                Mining lasers now release random # of platlets.
;				 Cash value of Thargoids and Alien items increased.

	include macros
	include bitlist

	xref initialise,attract,check_keys,quiet,game_logic,prepare_text
	xref print_string,end_game,cockpit,read_key,put_bitmap,find_bitmap

	section 0

	global elite

	push.l #1								in user mode ?
	bdos s_mode,6
	tst.l d0
	if <eq> then.s							yes
		clr.l -(sp) 						switch to supervisor mode
		bdos s_mode,6
	endi
	jsr initialise							initialise program
	repeat
		jsr attract 						display attract screen
		repeat
			inc.b loop_ctr(a6)				increment loop counter
			clr frame_count(a6) 			clear the frame counter
			jsr check_keys					scan keys and take action
			tst game_frozen(a6) 			game frozen ?
			if <eq> then.s					no
				tst docked(a6)				docked ?
				call <eq>,game_logic		no, perform game logic
			endi
			tst game_over(a6)				until game over
		until <ne>
		clr condition(a6)					clear up after game end
		clr mission(a6)
		jsr quiet							shutdown sounds
		cmp #player_quits,reason(a6)		did player quit ?
		if <ne> then						no
			jsr end_game					display end game explosion
		endi
		clr game_over(a6)
	endr
