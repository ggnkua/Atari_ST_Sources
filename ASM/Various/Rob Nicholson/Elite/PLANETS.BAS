10 REM BASIC version of Galactic map routine   15 March 1984
12 '
15 REM (C) David Braben and Ian Bell 1984
20 '
22 REM  Tech Level may be wrong here
23 '
30 DIM GOV$(8),ECON$(8),TOK$(32)
40 SEED1=23114:SEED2=584:SEED3=46931!
50 FOR N%=0 TO 7:READ GOV$(N%):NEXT:FOR N%=0 TO 7:READ ECON$(N%):NEXT:FOR N%=0 TO 31:READ TOK$(N%):NEXT
100 FOR SYS=0 TO 255:GOSUB 2000:PRINT XCOORD;",";YCOORD;:PRINT TAB(9);PLANET$;:PRINT TAB(19),ECON$(ECON XOR 7)+" "+GOV$(GOV);:PRINT TAB(60);"Tech.:";TECH+1:NEXT:END
1000 TEMP=SEED1+SEED2:IF TEMP>65535! THEN TEMP=TEMP-65536!
1010 SEED1=SEED2:SEED2=SEED3:SEED3=(TEMP+SEED2)
1020 IF SEED3>65535! THEN SEED3=SEED3-65536!
1030 RETURN
2000 XCOORD=INT(SEED2/256):YCOORD=INT(SEED1/256):GOV=INT(SEED2/256) AND 7:ECON=(INT(SEED1/256) AND 7) OR ((INT(GOV/256)=0) AND 2):TECH=(INT(SEED2/256)) AND (3+(ECON XOR 7)):PLANET$="":FOR I=1 TO 3:GOSUB 3000:PLANET$=PLANET$+HOLD$:NEXT
2010 GOSUB 3000:n=seed1:if n>32767 then n=n-32768
2020 IF n AND 64 THEN PLANET$=PLANET$+HOLD$
2030 RETURN
3000 HOLD$=TOK$(INT(SEED3/256) AND 31):GOSUB 1000:RETURN
10000 DATA Anarchy,Feudal,Multi-Government,Dictatorship,Communist,Confederacy,Democracy,Corporate State
10010 DATA Poor agricultural,Average agricultural,Rich agricultural,Mainly agricultural,Mainly industrial,Poor industrial,Average industrial,Rich industrial
10020 DATA "",LE,XE,GE,ZA,CE,BI,SO,US,ES,AR,MA,IN,DI,RE,A,ER,AT,EN,BE,RA,LA,VE,TI,ED,OR,QU,AN,TE,IS,RI,ON
