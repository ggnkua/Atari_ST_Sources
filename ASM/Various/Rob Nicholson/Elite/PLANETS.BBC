   10 REM BASIC version of Galactic map routine   15 March 1984
   12 
   15 REM (C) David Braben and Ian Bell 1984
   20 
   22REM  Tech Level may be wrong here
   23 
   30 DIM GOV$(8),ECON$(8),tok$(32)
   40 SEED1=&5A4A:SEED2=&248:SEED3=&B753
   45 @%=3
   50 FORN%=0TO7:READGOV$(N%):NEXT:FORN%=0TO7:READECON$(N%):NEXT:FORN%=0TO31:READtok$(N%):NEXT
  100 FORSYSTEM=0TO255:PROCNEWDATA:PRINTXCOORD,","YCOORD;:PRINTTAB(9),NAME$;:PRINTTAB(19),ECON$(ECON EOR7)+" "+GOV$(GOV),;:PRINTTAB(60)"Tech.:"TECH+1:NEXT:END
 1000 DEFPROCRAND:TEMP=(SEED1+SEED2)AND65535:SEED1=SEED2:SEED2=SEED3:SEED3=(TEMP+SEED2)AND65535:ENDPROC
 2000 DEFPROCNEWDATA:XCOORD=SEED2 DIV256:YCOORD=SEED1 DIV256:GOV=SEED2 DIV8 AND7:ECON=(SEED1 DIV256 AND7)OR((GOV DIV2=0)AND2):TECH=(SEED2 DIV256)AND3+(ECON EOR7):NAME$=FNGTOK+FNGTOK+FNGTOK:A$=FNGTOK:IFSEED1 AND64 NAME$=NAME$+A$:ENDPROC:ELSEENDPROC
 3000DEFFNGTOK:HOLD$=tok$(SEED3 DIV256 AND31):PROCRAND:=HOLD$
10000 DATA Anarchy,Feudal,Multi-Government,Dictatorship,Communist,Confederacy,Democracy,Corporate State
10010 DATA Poor agricultural,Average agricultural,Rich agricultural,Mainly agricultural,Mainly industrial,Poor industrial,Average industrial,Rich industrial
10020 DATA "",LE,XE,GE,ZA,CE,BI,SO,US,ES,AR,MA,IN,DI,RE,A,ER,AT,EN,BE,RA,LA,VE,TI,ED,OR,QU,AN,TE,IS,RI,ON
