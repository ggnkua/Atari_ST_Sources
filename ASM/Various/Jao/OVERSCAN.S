	 ************************************************
	*				         *
	*      OVERSCAN - ROZSZERZENIE EKRANU W DOL      *
	*       STEK MAGAZINE No.1, PROGRAM BY JAO       *
	*  	    USTAW TABULATOR NA 10              *
	*				         *
	 ************************************************


START:

	CLR.L -(SP)		*** TRYB SUPERVISOR
	MOVE.W #$20,-(SP)
	TRAP #1
	ADDQ.L #6,SP
	MOVE.L D0,SAVE_SSP
	MOVE.W #2,-(SP)		*** ODCZYT ADRESU PAMIECI
	TRAP #14
	ADDQ.L #2,SP
	MOVE.L D0,-(SP)		*** ADRES NA STOS
	MOVE.W #4,-(SP)		*** JAKA ROZDZIELCZOSC ?
	TRAP #14			*** D0=0 - NISKA D0=1 - SREDNIA
	ADDQ.L #2,SP
	MOVE.W D0,-(SP)		*** ROZDZIELCZOSC NA STOS
	MOVE.L #$70000,D7 		*** PAMIEC EKRANU NA $70000
	MOVE.W #0,-(SP)		*** NISKA ROZDZIELCZOSC
	MOVE.L D7,-(SP)
	MOVE.L D7,-(SP)
	MOVE.W #5,-(SP)
	TRAP #14
	LEA 12(SP),SP
	MOVE.L #$70000,A0 		*** ZAMALOWANIE EKRANU PASKAMI
	MOVE.W #6000,D0
PAINT:	MOVE.L #$FF000000,(A0)+
	CLR.L (A0)+
	DBF D0,PAINT
	CLR.W $FFFF8242.W		*** KOLOR CZARNY DLA PASKOW
	MOVE.L $70.W,-(SP)		*** WEKTOR VBL NA STOS
	MOVE.L #VBL,$70.W		*** INICJALIZACJA PRZERWANIA VBL
	PEA OVERSCAN(PC)		*** INICJALIZACJA ZEGARA B
	MOVE.W #199,-(SP)		*** WYWOLANIE PRZY 200 LINI EKR.
	MOVE.W #8,-(SP)		*** TRYB ZLICZANIA
	MOVE.W #1,-(SP)		*** ZRGAR B
	MOVE.W #$1F,-(SP)
	TRAP #14	
	LEA 12(SP),SP
	BCLR #5,$FFFFFA09.W		*** WYLACZ ZEGAR C
	MOVE #7,-(SP)		*** CZEKAJ NA KLAWISZ
	TRAP #1
	ADDQ.L #2,SP
	MOVE.W #8,-(SP)		*** WYLACZ ZEGAR B
	MOVE.W #$1A,-(SP)
	TRAP #14
	ADDQ.L #4,SP
	MOVE.L (SP)+,$70.W		*** WYLACZ VBL
	MOVE.W (SP)+,D0
	MOVE.L (SP)+,D7
	MOVE.W D0,-(SP)		*** PRZYWROCENIE POPRZEDNIEJ
	MOVE.L D7,-(SP)		*** ROZDZIELCZOSCI I PAMIECI EKRANU
	MOVE.L D7,-(SP)
	MOVE.W #5,-(SP)
	TRAP #14
	LEA 12(SP),SP
	BSET #5,$FFFFFA09.W		*** WLACZ ZEGAR C
	MOVE.L SAVE_SSP,-(SP)	*** POWROT DO TRYBU USER
	MOVE #$20,-(SP)
	TRAP #1
	ADDQ.L #6,SP
	CLR.L -(SP)		*** KONIEC
	TRAP #1


OVERSCAN:

	MOVEM.L D0/A0,-(SP)		*** REJ. NA STOS
	LEA $FFFF8209.W,A0		*** WYROWNANIE SIE Z PROMIENIEM
	MOVE.B (A0),D0		*** RYSUJACYM OBRAZ TV
WAIT:	SUB.B (A0),D0		*** OP = OPUZNIENIE
	BEQ.S WAIT		
	MOVEQ #28,D0		*** OP 4 TAKTY
WHBL:	DBF D0,WHBL		*** OP 29 * 12 TAKTOW
	BCLR #0,$FFFFFA0F.W		*** ODNOW PRZERWANIE,OP 16 TK. 
	CLR.B $FFFF820A.W		*** 60 HZ,OP 16 TAKTOW
	MOVEM.L (SP)+,D0/A0		*** REJ. ZE STOSU,OP 28
	TST.L (SP)		*** OP 12 TAKTOW
	TST.L (SP)		*** OP 12
	TST.L (SP)		*** OP 12
	TST.L (SP)		*** OP 12
	MOVE.B #2,$FFFF820A.W	*** 50 HZ
	RTE

VBL:

	CLR.B $FFFFFA1B.W		*** WYLACZ ZEGAR B
	MOVE.B #$C7,$FFFFFA21.W	*** LICZNIK NA 199
	MOVE.B #$08,$FFFFFA1B.W	*** TRYB ZLICZANIA
	RTE

SAVE_SSP:	
	DC.L 0

