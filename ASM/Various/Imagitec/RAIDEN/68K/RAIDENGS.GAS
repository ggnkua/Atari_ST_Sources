	Include	"D:\Jaguar\Include\Blit.Inc"
	Include	"D:\Jaguar\Include\Jaguar.Inc"

	Org	G_RAM+HARDWARE
	Xdef	GPU_SORT
	Xdef	SORT_PARAMS
	Xdef	STOP_THE_GPU
	Xdef	GPU_DRAW_ALIENS
	Xdef	Olist_Return
	Xdef	Num_Objects_Added
	Xdef	Draw_Aliens_Params
	Xdef	XGpu_Draw_Aliens_Shadow
	Xdef	XDraw_Aliens_Shadow_Params
	Xdef	GET_ATTRIB_RISC
	Xdef	Attrib_Params
	Xdef	Num_Objects_In_List
	Xdef	Draw_Weapons_GPU
	Xdef	Weapon_Params
	Xdef	Link_Params
	Xdef	Build_Link_GPU

	Xdef	Gpu_Quick_Ref
	Xdef	Move_Aliens_GPU
	Xdef	Move_Params
	Xdef	GpuX_Move_Pointer
	Xdef	Weapon_Params
	Xdef	Gpu_Weapon_Collision
	Xdef	WEPCOL_POINTER
	Xdef	Move_Weapon_Params
	Xdef	Gpu_Move_Weapons


Screen_Height		Equ	206+21
Screen_Width		Equ	322
Panel_Width		Equ	100+16
Playfield_Height	Equ	Screen_Height
Playfield_Width		Equ	(Screen_Width-Panel_Width)+10

Max_Aliens		Equ 	160

Spr_data		Equ	0
Spr_Pixel_Width		Equ	4
Spr_SHeight		Equ	6
Spr_BitsperPixel	Equ	8
Spr_Flags		Equ	9
Spr_Yoffset		Equ	10
Spr_Palnum		Equ	12
Spr_Pad			Equ	14

;--------------------------------------------------------
; Routine expects parameters to be laid out as follows - ALL IN LONGWORDS!
; QUICKMUL+DATA 		(L-ARRAY)
; SORT_COUNTS+DATA 		(W-ARRAY)
; ALIEN_SORT_POINTER+DATA	(L-POINTER)
; NUMBER_OF_SPRITES_ON_SCREEN+DATA	(W-VARIABLE)
; DRAW_CHECK_TABLE+DATA		(L-ARRAY)
; Priority size !


Quick_Mul		REGEQU		r1
Sort_Counts	 	REGEQU		r2
Sort_Pointer		REGEQU		r3
Sort_Counter_Loop	REGEQU		r4
Sort_Neg		REGEQU		r5
Sort_Clr		REGEQU		r7
Sort_Temp		REGEQU		r6
Sort_Number_Sprites	REGEQU		r8
Sort_Draw_Check		REGEQU		r9
Sort_Pri		REGEQU		r10
Sort_Spr_Addr		REGEQU		r11
Sort_Spr_Pri		REGEQU		r12
Sort_Incp		REGEQU		r13
Sort_Dec		REGEQU		r16

Gpu_Sort:	
	Nop
	Nop
	Movei	#Sort_Params,r14
	Nop
	Load	(r14+0),Quick_Mul		; Address of quick-mul table
	Load	(r14+1),Sort_Counts		; Address of sort counts
	Load	(r14+2),Sort_Pointer		; Address of sort pointer
	Load	(r14+5),Sort_Pri
		
	Moveq	#16,Sort_Counter_Loop		; Counter for 16 values
	Moveq	#1,Sort_Neg			; 1
	Moveq	#0,Sort_Clr			; 1
	Neg	Sort_Neg			; -1
	Movei	#(Max_Aliens/2)*4,Sort_Temp
	Nop

.Loop0:	Store	Sort_Pointer,(Quick_Mul)
	Storew	Sort_Clr,(Sort_Counts)
	Addq	#4,Quick_Mul
	Addq	#2,Sort_counts
	Add	Sort_Temp,Sort_Pointer
	Subq	#1,Sort_Counter_Loop
	Jr	NE,.Loop0
	Nop
	
	Load	(r14+1),Sort_Counts
	Movei	#8*2,Sort_Temp
	Add	Sort_Temp,Sort_Counts
	Loadw	(Sort_Counts),Sort_Temp
	Addq	#1,Sort_Temp
	Storew	Sort_Temp,(Sort_Counts)

	Load	(r14+0),r15
	Load	(r15+8),Sort_Temp
	Store	Sort_Neg,(Sort_Temp)
	Addq	#4,Sort_Temp
	Store	Sort_Temp,(r15+8)

	Movei	#.Concat,r29
	Load	(r14+3),Sort_Number_Sprites
	Cmpq	#0,Sort_Number_Sprites
	Jump	EQ,(r29)
	Nop

	Load	(r14+1),Sort_Counts	
	Load	(r14+0),Quick_Mul
	Load	(r14+4),Sort_Draw_Check	
	Movei	#.Loop1,r29
.Loop1:	Load	(Sort_Draw_Check),Sort_Spr_Addr
	Add	Sort_Pri,Sort_Spr_Addr
	Loadb	(Sort_Spr_Addr),Sort_Spr_Pri
	Sub	Sort_Pri,Sort_Spr_Addr
	Add	Sort_Spr_Pri,Sort_Counts
	Loadw	(Sort_Counts),Sort_Temp
	Addq	#1,Sort_Temp
	Storew	Sort_Temp,(Sort_Counts)
	Sub	Sort_Spr_Pri,Sort_Counts
	
	Add	Sort_Spr_Pri,Quick_Mul
	Add	Sort_Spr_Pri,Quick_Mul
	Load	(Quick_Mul),Sort_Temp
	Store	Sort_Spr_Addr,(Sort_Temp)
	Addq	#4,Sort_Temp
	Store	Sort_Temp,(Quick_Mul)
	Sub	Sort_Spr_Pri,Quick_Mul
	Sub	Sort_Spr_Pri,Quick_Mul
	Addq	#4,Sort_Draw_Check
	Subq	#1,Sort_Number_Sprites	
	Jump	Ne,(r29)
	Nop
	
.Concat:
	Load	(r14+0),Quick_Mul
	Load	(r14+2),Sort_Pointer		; Address of sort pointer
	Movei	#(Max_Aliens/2)*4,Sort_Temp
	Moveq	#16,Sort_Counter_Loop		; Counter for 16 values

.Loop2:	Store	Sort_Pointer,(Quick_Mul)
	Addq	#4,Quick_Mul
	Add	Sort_Temp,Sort_Pointer
	Subq	#1,Sort_Counter_Loop
	Jr	NE,.Loop2
	Nop

	Load	(r14+2),Sort_Pointer		; Address of sort pointer
	Load	(r14+0),Quick_Mul
	Load	(r14+1),Sort_Counts		; Address of sort counts
	Moveq	#16,Sort_Counter_Loop		; Counter for 16 values

.Loop3:	Load	(Quick_Mul),Sort_Incp
	Loadw	(Sort_Counts),Sort_Dec
	Addq	#4,Quick_Mul
	Addq	#2,Sort_counts
	Cmpq	#0,Sort_Dec
	Jr	EQ,.No_Concat
	Nop
.Loop4:	Load	(Sort_Incp),Sort_Temp
	Store	Sort_Temp,(Sort_Pointer)
	Addq	#4,Sort_Incp
	Addq	#4,Sort_Pointer
	Subq	#1,Sort_Dec
	Jr	NE,.Loop4
	Nop
.No_Concat:
	Subq	#1,Sort_Counter_Loop
	Jr	Ne,.Loop3
	Nop
	Store	Sort_Neg,(Sort_Pointer)
	Nop
	Movei	#Stop_The_Gpu,r0
	Jump	(r0)
	Nop	

;--------------------------------------------------------
; Routine expects parameters to be laid out as follows - ALL IN LONGWORDS!
; NUMBER OF SPRITES ON SCREEN  	(L-VARIABLE)
; ALIEN SORT POINTER	       	(L-POINTER)]
; ENEMY-SPRITE BASE		(L-POINTER)
; PRE-GENERATED ENEMY OBJECTS	(L-ARRAY)
; CURRENT LIST POINTER		(L-POINTER)
; DISTANCE FOR SPRITE		(L-INDEX)
; DISTANCE FOR HIT		(L-INDEX)
; HIT TABLE OF SPRITES		(W-ARRAY)
; DISTANCE FOR DMG		(L-INDEX)
; DMG-TABLE			(W-ARRAY)
; DMG DEC DISTANCE		(B-INDEX)
; X/Y_OFFSETED			(DW-ARRAY)
; X_OFFSET_INTO_MAP		(W-VALUE)
; Flame table			(W-ARRAY)

FLAME_POINTER		REGEQU		R0
NUMBER_ALIENS_ON_SREEN	REGEQU		R1	
ALIEN_DRAW_LIST_POINTER	REGEQU		R2	
DRAW_ALIEN_STRUCT	REGEQU		R3	
DRAW_SPRITE_INDEX	REGEQU		R4	
DRAW_ALIEN_STRUCT_TEMP	REGEQU		R5	
DRAW_SPRITE_NUM		REGEQU		R6	
DRAW_IMID		REGEQU 		R7	
DRAW_HIT_DIST		REGEQU		R8	
DRAW_HIT_TABLE		REGEQU		R9	
DRAW_DMG_INDEX		REGEQU		R10	
DRAW_DMG_TABLE		REGEQU		R11	
DRAW_DMG_DEC		REGEQU		R12	
ALN_X_OFFSET		REGEQU		R13	

MAPX			REGEQU		r15
MESSY1			REGEQU		R16	
MESSY2			REGEQU		R17	
MESSY3			REGEQU		R18	
MESSY4			REGEQU		R19	
DRAW_HEIGHT		REGEQU		R20	
NUM_OBJECTS_DISPLAYED	REGEQU		R21	
DRAW_JUMP		REGEQU		R22	
DRAW_X			REGEQU		R23	
DRAW_Y			REGEQU		R24	
DRAW_THIS_SPRITE	REGEQU		R25	
OLIST_POINTER		REGEQU		R26	
PRE_GENOBS		REGEQU		R27	
SPRITE_BASE		REGEQU		R28	
RETURN_ADDRESS		REGEQU		R29	
DRAW_TEMP		REGEQU		R30	
DRAW_WIDTH		REGEQU		R31	
		Align	LONG
Gpu_Draw_Aliens:
	Nop	
	Nop	
	Movei	#Quick_Draw,DRAW_JUMP
	Nop	
	Movei	#Draw_Aliens_Params,r14
	Nop	
	Moveq	#0,NUM_OBJECTS_DISPLAYED
	Load	(r14+0),NUMBER_ALIENS_ON_SREEN
	Load	(r14+1),ALIEN_DRAW_LIST_POINTER	
	Load	(r14+2),SPRITE_BASE
	Load	(r14+3),PRE_GENOBS
	Load	(r14+4),OLIST_POINTER
	Load	(r14+5),DRAW_SPRITE_INDEX
	Load	(r14+6),DRAW_HIT_DIST
	Load	(r14+7),DRAW_HIT_TABLE
	Load	(r14+8),DRAW_DMG_INDEX
	Load	(r14+9),DRAW_DMG_TABLE
	Load	(r14+10),DRAW_DMG_DEC
	Load	(r14+11),ALN_X_OFFSET
	Load	(r14+12),MAPX

.Loopx:	Load	(ALIEN_DRAW_LIST_POINTER),DRAW_ALIEN_STRUCT
	Addq	#4,ALIEN_DRAW_LIST_POINTER
	Cmpq	#0,DRAW_ALIEN_STRUCT
	Jr	PL,.Positive_Address
	Nop
	Movei	#.Exit_Sprite_Routine,DRAW_IMID
	Jump	(DRAW_IMID)
	Nop

.Positive_Address:
	Move	DRAW_ALIEN_STRUCT,DRAW_ALIEN_STRUCT_TEMP
	Add	DRAW_SPRITE_INDEX,DRAW_ALIEN_STRUCT_TEMP
	Loadw	(DRAW_ALIEN_STRUCT_TEMP),DRAW_SPRITE_NUM
	Shlq	#16,DRAW_SPRITE_NUM
	Sharq	#16,DRAW_SPRITE_NUM
	Cmpq	#0,DRAW_SPRITE_NUM
	Jr	PL,.Positive_Sprite
	Nop
	Movei	#.Dec,DRAW_IMID
	Jump	(DRAW_IMID)
	Nop
.Positive_Sprite:
	Move	DRAW_ALIEN_STRUCT,DRAW_ALIEN_STRUCT_TEMP
	Add	DRAW_HIT_DIST,DRAW_ALIEN_STRUCT_TEMP
	Loadb	(DRAW_ALIEN_STRUCT_TEMP),DRAW_IMID
	Cmpq	#0,DRAW_IMID
	Jr	NE,.Sprite_Hit
	Nop	
	Movei	#.Sprite_Not_Hit,DRAW_IMID
	Jump	(DRAW_IMID)
	Nop
.Sprite_Hit:
	Moveq	#0,DRAW_IMID
	Storeb	DRAW_IMID,(DRAW_ALIEN_STRUCT_TEMP)
	Add	DRAW_SPRITE_NUM,DRAW_SPRITE_NUM
	Add	DRAW_SPRITE_NUM,DRAW_HIT_TABLE
	Loadw	(DRAW_HIT_TABLE),DRAW_THIS_SPRITE
	Shlq	#16,DRAW_THIS_SPRITE
	Sharq	#16,DRAW_THIS_SPRITE
	Sub	DRAW_SPRITE_NUM,DRAW_HIT_TABLE
	Shrq	#1,DRAW_SPRITE_NUM
	Movei	#.Draw_Alien_Sprite,DRAW_IMID
	Cmpq	#0,DRAW_THIS_SPRITE
	Jump	PL,(DRAW_IMID)
	Nop

.Sprite_Not_Hit:
	Move	DRAW_ALIEN_STRUCT,DRAW_ALIEN_STRUCT_TEMP
	Add	DRAW_DMG_INDEX,DRAW_ALIEN_STRUCT_TEMP
	Loadw	(DRAW_ALIEN_STRUCT_TEMP),DRAW_IMID
	Shlq	#16,DRAW_IMID
	Sharq	#16,DRAW_IMID
	Cmpq	#0,DRAW_IMID
	Jr	MI,.Draw_Damaged_Sprite
	Nop
	Movei	#.Draw_Normal_Sprite,DRAW_IMID
	Jump	(DRAW_IMID)
	Nop	
.Draw_Damaged_Sprite:
	Move	DRAW_ALIEN_STRUCT,DRAW_ALIEN_STRUCT_TEMP
	Add	DRAW_DMG_DEC,DRAW_ALIEN_STRUCT_TEMP
	Loadb	(DRAW_ALIEN_STRUCT_TEMP),DRAW_IMID
	Btst	#2,DRAW_IMID
	Jr	EQ,.Draw_Normal_Sprite
	Nop

	Add	DRAW_SPRITE_NUM,DRAW_SPRITE_NUM
	Add	DRAW_SPRITE_NUM,DRAW_DMG_TABLE
	Loadw	(DRAW_DMG_TABLE),DRAW_THIS_SPRITE
	Shlq	#16,DRAW_THIS_SPRITE
	Sharq	#16,DRAW_THIS_SPRITE
	Sub	DRAW_SPRITE_NUM,DRAW_DMG_TABLE
	Shrq	#1,DRAW_SPRITE_NUM
	Cmpq	#0,DRAW_THIS_SPRITE
	Jr	PL,.Draw_Alien_Sprite
	Nop

.Draw_Normal_Sprite:
	Move	DRAW_SPRITE_NUM,DRAW_THIS_SPRITE
.Draw_Alien_Sprite:
	Move	DRAW_ALIEN_STRUCT,DRAW_ALIEN_STRUCT_TEMP
	Add	ALN_X_OFFSET,DRAW_ALIEN_STRUCT_TEMP
	Loadw	(DRAW_ALIEN_STRUCT_TEMP),DRAW_X
	Addq	#2,DRAW_ALIEN_STRUCT_TEMP
	Loadw	(DRAW_ALIEN_STRUCT_TEMP),DRAW_Y
	Shlq	#16,DRAW_Y
	Shlq	#16,DRAW_X
	Sharq	#16,DRAW_X
	Sharq	#16,DRAW_Y
	Sub	MAPX,DRAW_X
	Movei	#.Back_Here,RETURN_ADDRESS
	Jump	(DRAW_JUMP)
	Nop
.Back_Here:
	Move	DRAW_ALIEN_STRUCT,DRAW_ALIEN_STRUCT_TEMP
	Add	DRAW_DMG_INDEX,DRAW_ALIEN_STRUCT_TEMP
	Loadw	(DRAW_ALIEN_STRUCT_TEMP),DRAW_IMID
	Shlq	#16,DRAW_IMID
	Sharq	#16,DRAW_IMID
	Cmpq	#0,DRAW_IMID
	Jr	MI,.Flame_Check
	Nop
	Movei	#.Dec,DRAW_IMID
	Jump	(DRAW_IMID)
	Nop
.Flame_Check:
	Load	(r14+13),FLAME_POINTER	
	Move	DRAW_ALIEN_STRUCT,DRAW_ALIEN_STRUCT_TEMP
	Add	DRAW_SPRITE_INDEX,DRAW_ALIEN_STRUCT_TEMP
	Loadw	(DRAW_ALIEN_STRUCT_TEMP),DRAW_SPRITE_NUM
	Shlq	#2,DRAW_SPRITE_NUM
	Add	DRAW_SPRITE_NUM,FLAME_POINTER
	Load	(FLAME_POINTER),DRAW_SPRITE_NUM
	Cmpq	#0,DRAW_SPRITE_NUM
	Jr	NE,.Flames_On
	Nop
	Movei	#.Dec,DRAW_IMID
	Jump	(DRAW_IMID)
	Nop
.Flames_On:

	Move	DRAW_ALIEN_STRUCT,DRAW_ALIEN_STRUCT_TEMP
	Add	DRAW_DMG_DEC,DRAW_ALIEN_STRUCT_TEMP
	Loadb	(DRAW_ALIEN_STRUCT_TEMP),DRAW_THIS_SPRITE
	Moveq	#1,DRAW_IMID
	And	DRAW_IMID,DRAW_THIS_SPRITE
	Movei	#912,DRAW_IMID
	Add	DRAW_IMID,DRAW_THIS_SPRITE
	Move	DRAW_SPRITE_NUM,FLAME_POINTER
	Loadw	(FLAME_POINTER),DRAW_SPRITE_NUM
	Addq	#2,FLAME_POINTER
	Movei	#.Out_Flame_Loop,RETURN_ADDRESS
	
	Addq	#8,DRAW_ALIEN_STRUCT
	Movei	#.Flame_Loop,DRAW_ALIEN_STRUCT_TEMP
.Flame_Loop:
	Loadw	(FLAME_POINTER),DRAW_X
	Addq	#2,FLAME_POINTER
	Loadw	(FLAME_POINTER),DRAW_Y
	Shlq	#16,DRAW_X
	Shlq	#16,DRAW_Y
	Sharq	#16,DRAW_X
	Sharq	#16,DRAW_Y
	Loadw	(DRAW_ALIEN_STRUCT),DRAW_IMID
	Shlq	#16,DRAW_IMID
	Sharq	#16,DRAW_IMID
	Addq	#2,FLAME_POINTER
	Addq	#2,DRAW_ALIEN_STRUCT
	Add	DRAW_IMID,DRAW_X
	Loadw	(DRAW_ALIEN_STRUCT),DRAW_IMID
	Shlq	#16,DRAW_IMID
	Sharq	#16,DRAW_IMID
	Sub	MAPX,DRAW_X
	Add	DRAW_IMID,DRAW_Y
	Subq	#2,DRAW_ALIEN_STRUCT
	Jump	(Draw_Jump)
	Nop	
.Out_Flame_Loop:
	Subq	#4,DRAW_SPRITE_NUM
	Jump	Ne,(DRAW_ALIEN_STRUCT_TEMP)
	Shrq	#4,DRAW_THIS_SPRITE

.Dec:	Movei	#.Loopx,RETURN_ADDRESS
	Subq	#1,NUMBER_ALIENS_ON_SREEN
	Jump	NE,(RETURN_ADDRESS)
	Nop
.Exit_Sprite_Routine:
	Movei	#Olist_Return,DRAW_IMID
	Store	OLIST_POINTER,(DRAW_IMID)
	Movei	#Num_Objects_Added,DRAW_IMID
	Store	NUM_OBJECTS_DISPLAYED,(DRAW_IMID)

	Movei	#Stop_The_Gpu,RETURN_ADDRESS
	Jump	(RETURN_ADDRESS)
	Nop	

; Does not use x-offset into map !

Quick_Draw:
	Shlq	#4,DRAW_THIS_SPRITE
	Add	DRAW_THIS_SPRITE,SPRITE_BASE
	Moveq	#Spr_SHeight,DRAW_IMID
	Add	DRAW_IMID,SPRITE_BASE
	Loadw	(SPRITE_BASE),DRAW_HEIGHT
	Shlq	#16,DRAW_HEIGHT
	Sharq	#16,DRAW_HEIGHT
	Sub	DRAW_IMID,SPRITE_BASE
	Sub	DRAW_THIS_SPRITE,SPRITE_BASE
	Cmpq	#0,DRAW_HEIGHT
	Jump	MI,(RETURN_ADDRESS)
	Nop
	Jump	EQ,(RETURN_ADDRESS)
	Nop
	Movei	#%111111111111,DRAW_IMID
	And	DRAW_IMID,DRAW_X

	Shlq	#1,DRAW_Y

	Movei	#.Clipped,DRAW_IMID
	Cmpq	#0,DRAW_Y
	Jump	MI,(DRAW_IMID)
	Nop
	Move	PRE_GENOBS,DRAW_IMID
	Add	DRAW_THIS_SPRITE,PRE_GENOBS
	Load	(PRE_GENOBS),MESSY1
	Addq	#4,PRE_GENOBS
	Load	(PRE_GENOBS),MESSY2
	Addq	#4,PRE_GENOBS
	Load	(PRE_GENOBS),MESSY3
	Addq	#4,PRE_GENOBS
	Load	(PRE_GENOBS),MESSY4
	Move	DRAW_IMID,PRE_GENOBS
		
.Back_Here1:
	Move	OLIST_POINTER,DRAW_IMID
	Addq	#16,DRAW_IMID
	Rorq	#11,DRAW_IMID
	Movei	#$ffff,DRAW_TEMP
	And	DRAW_IMID,DRAW_TEMP
	Or	DRAW_TEMP,MESSY1
	Movei	#$ffff0000,DRAW_TEMP
	And	DRAW_TEMP,DRAW_IMID
	Or	DRAW_IMID,MESSY2
	Shlq	#3,DRAW_Y
	Add	DRAW_Y,MESSY2
	Or	DRAW_X,MESSY4
.INTO_LIST:
	Store	MESSY1,(OLIST_POINTER)
	Addq	#4,OLIST_POINTER
	Store	MESSY2,(OLIST_POINTER)
	Addq	#4,OLIST_POINTER
	Store	MESSY3,(OLIST_POINTER)
	Addq	#4,OLIST_POINTER
	Store	MESSY4,(OLIST_POINTER)
	Addq	#4,OLIST_POINTER
	Addq	#1,NUM_OBJECTS_DISPLAYED	
.No_Display_Sprite:
	Jump	(RETURN_ADDRESS)
	Nop
	
.Clipped:
	Sharq	#1,DRAW_Y
	Move	DRAW_Y,MESSY1
	Add	DRAW_THIS_SPRITE,SPRITE_BASE
	Moveq	#Spr_SHeight,DRAW_IMID
	Add	DRAW_IMID,SPRITE_BASE
	Loadw	(SPRITE_BASE),MESSY2
	Sub	DRAW_IMID,SPRITE_BASE
	Sub	DRAW_THIS_SPRITE,SPRITE_BASE
	Add	MESSY1,MESSY2
	Cmpq	#0,MESSY2
	Jump	EQ,(RETURN_ADDRESS)
	Nop
	Jump	MI,(RETURN_ADDRESS)
	Nop
	Move	PRE_GENOBS,DRAW_IMID
	Add	DRAW_THIS_SPRITE,PRE_GENOBS
	Load	(PRE_GENOBS),MESSY1
	Addq	#4,PRE_GENOBS
	Load	(PRE_GENOBS),MESSY2
	Addq	#4,PRE_GENOBS
	Load	(PRE_GENOBS),MESSY3
	Addq	#4,PRE_GENOBS
	Load	(PRE_GENOBS),MESSY4
	Move	DRAW_IMID,PRE_GENOBS
	Neg	DRAW_Y
	Move	DRAW_Y,DRAW_IMID
	Shlq	#14,DRAW_IMID
	Sub	DRAW_IMID,MESSY2
	
	Add	DRAW_THIS_SPRITE,SPRITE_BASE
	Moveq	#Spr_Pixel_Width,DRAW_IMID
	Add	DRAW_IMID,SPRITE_BASE
	Loadw	(SPRITE_BASE),DRAW_WIDTH
	Sub	DRAW_IMID,SPRITE_BASE
	Subq	#1,DRAW_WIDTH
	Moveq	#Spr_Bitsperpixel,DRAW_IMID
	Add	DRAW_IMID,SPRITE_BASE
	Loadb	(SPRITE_BASE),DRAW_TEMP
	Sub	DRAW_IMID,SPRITE_BASE
	Sub	DRAW_THIS_SPRITE,SPRITE_BASE
		
	Neg	DRAW_TEMP	
	Sh	DRAW_TEMP,DRAW_WIDTH
	Movei	#-64,DRAW_TEMP
	And	DRAW_TEMP,DRAW_WIDTH
	Neg	DRAW_TEMP
	Add	DRAW_TEMP,DRAW_WIDTH
	Shrq	#3,DRAW_WIDTH
	Imult	DRAW_Y,DRAW_WIDTH
	Shlq	#8,DRAW_WIDTH
	Add	DRAW_WIDTH,MESSY1
	Movei	#.Back_Here1,DRAW_IMID
	Moveq	#0,DRAW_Y	
	Jump	(DRAW_IMID)
	Nop	

;--------------------------------------------------------
; Routine expects parameters to be laid out as follows - ALL IN LONGWORDS!

; Number of sprites in active list (L-VARIABLE)
; Pointer to active list (L-ARRAY)
; Shadow sprite (into !)
; Object list pointer
; X/Y index
; Left hand screen bound
; Right hand screen bound
; Pointer to pre-generated objects
; Base of sprites
		

SHADOW_COUNT		REGEQU		R1
SHADOW_ACTIVELIST	REGEQU		R2
SHADOW_INDEX		REGEQU		R3
;
SHADOW_TEMP		REGEQU		R5
SHADOW_SPRITE_STRUCT	REGEQU		R6
SHADOW_30		REGEQU		R13
SHADOW_XY_INDEX		REGEQU		R8
SHADOW_LEFT_BOUND	REGEQU		R9
SHADOW_RIGHT_BOUND	REGEQU		R10
SHADOW_WIDTH		REGEQU		R11
SHADOW_SKIP		REGEQU		R12

		Align	LONG
XGpu_Draw_Aliens_Shadow:
	Nop
	Nop	
	Movei	#Quick_Draw,DRAW_JUMP
	Nop
	Movei	#XDraw_Aliens_Shadow_Params,r14
	Nop
	Moveq	#0,NUM_OBJECTS_DISPLAYED
	Moveq	#30,SHADOW_30
	Nop
	Movei	#.Dont_Draw_Shadow,SHADOW_SKIP
	Nop

	Load	(r14+0),SHADOW_COUNT
	Load	(r14+1),SHADOW_ACTIVELIST
	Load	(r14+2),SHADOW_INDEX
	Load	(r14+3),OLIST_POINTER
	Load	(r14+4),SHADOW_XY_INDEX
	Load	(r14+5),SHADOW_LEFT_BOUND
	Load	(r14+6),SHADOW_RIGHT_BOUND
	Load	(r14+7),SPRITE_BASE
	Load	(r14+8),PRE_GENOBS

	Movei	#.Exit_Sprite_Routine,RETURN_ADDRESS
	Cmpq	#0,SHADOW_COUNT
	Jump	EQ,(RETURN_ADDRESS)
	Nop
	Jump	MI,(RETURN_ADDRESS)
	Nop
	
.Shadow_Loop:
	Load	(SHADOW_ACTIVELIST),SHADOW_SPRITE_STRUCT
	Add	SHADOW_INDEX,SHADOW_SPRITE_STRUCT
	Loadw	(SHADOW_SPRITE_STRUCT),DRAW_THIS_SPRITE
	Shlq	#16,DRAW_THIS_SPRITE
	Sharq	#16,DRAW_THIS_SPRITE
	Cmpq	#0,DRAW_THIS_SPRITE
	Jr	PL,.Draw_Shadow
	Nop
.Back_Shadow:
	Addq	#4,SHADOW_ACTIVELIST
	Subq	#1,SHADOW_COUNT
	Jr	NE,.Shadow_Loop
	Nop
	Movei	#.Exit_Sprite_Routine,RETURN_ADDRESS
	Jump	(RETURN_ADDRESS)
	Nop

.Draw_Shadow:
	Sub	SHADOW_INDEX,SHADOW_SPRITE_STRUCT
	Add	SHADOW_XY_INDEX,SHADOW_SPRITE_STRUCT
	Loadw	(SHADOW_SPRITE_STRUCT),DRAW_X
	Addq	#2,SHADOW_SPRITE_STRUCT
	Loadw	(SHADOW_SPRITE_STRUCT),DRAW_Y
	Shlq	#16,DRAW_X
	Shlq	#16,DRAW_Y
	Sharq	#16,DRAW_X
	Sharq	#16,DRAW_Y
	Add	SHADOW_30,DRAW_X
	Add	SHADOW_30,DRAW_Y
	Cmp	SHADOW_RIGHT_BOUND,DRAW_X
	Jump	PL,(SHADOW_SKIP)
	Nop
	Load	(r14+9),SHADOW_TEMP
;	Movei	#230,SHADOW_TEMP
	Cmp	SHADOW_TEMP,DRAW_Y
	Jump	PL,(SHADOW_SKIP)
	Nop
	Shlq	#4,DRAW_THIS_SPRITE
	Add	DRAW_THIS_SPRITE,SPRITE_BASE
	Moveq	#Spr_PIxel_Width,SHADOW_TEMP
	Add	SHADOW_TEMP,SPRITE_BASE
	Loadw	(SPRITE_BASE),SHADOW_WIDTH
	Sub	SHADOW_TEMP,SPRITE_BASE
	Sub	DRAW_THIS_SPRITE,SPRITE_BASE
	Shrq	#4,DRAW_THIS_SPRITE
	Add	SHADOW_WIDTH,DRAW_X
	Cmp	SHADOW_LEFT_BOUND,DRAW_X
	Jump	MI,(SHADOW_SKIP)
	Nop
	Sub	SHADOW_WIDTH,DRAW_X
	Sub	SHADOW_LEFT_BOUND,DRAW_X
	Movei	#.Append_RMW,RETURN_ADDRESS
	Move	OLIST_POINTER,SHADOW_TEMP
	Jump	(DRAW_JUMP)
	Nop
.Append_RMW:
	Cmp	OLIST_POINTER,SHADOW_TEMP
	Jr	EQ,.Dont_Draw_Shadow
	Nop
	Subq	#8,OLIST_POINTER
	Load	(OLIST_POINTER),MESSY3
	Movei	#%10100000000000000,SHADOW_TEMP
	Or	SHADOW_TEMP,MESSY3
	Store	MESSY3,(OLIST_POINTER)
	Addq	#8,OLIST_POINTER
.Dont_Draw_Shadow:
	Movei	#.Back_Shadow,RETURN_ADDRESS
	Jump	(RETURN_ADDRESS)
	Nop	


	
;--------------------------------------------------------
; Routine expects parameters to be laid out as follows - ALL IN LONGWORDS!

; Active list pointer (L-ARRAY)
; Weapon check table  (B-ARRAY)
; Player check table  (B-ARRAY)
; Draw check table    (B-ARRAY)
; Number of sprites in list    (L-VARRIABLE)
; Enemy sprites base  (L-POINTER)
; X_OFFSET_INTO_MAP	(W-VARIABLE)
; Playfield width	(W-VARIABLE)
; Alien offset check	(B-ARRAY)
; Aln_Its_On_Screen	(B)
; Aln_SPRITE	(W)
; Aln_Offsets	(W)
; ATTRIB_WEAPON_INDEX	
; ATTRIB_PLAYER_INDEX	
; ATTRIB_HITOVER_INDEX	
; ATTRIB_XOFF_INDEX	
; ATTRIB_ALNBOXES


ATTRIB_ACTIVE_POINTER	REGEQU		R0
ATTRIB_WEAPON_POINTER	REGEQU		R1
ATTRIB_PLAYER_POINTER	REGEQU		R2
ATTRIB_DRAWER_POINTER	REGEQU		R3
ATTRIB_NUMBER		REGEQU		R4
ATTRIB_SPRITES_BASE	REGEQU		R5
ATTRIB_X		REGEQU		R6
ATTRIB_XMAX		REGEQU		R7
ATTRIB_OFFCHECK		REGEQU		R8
ATTRIB_SPRITE_STRUCT	REGEQU		R9
ATTRIB_SPRITE_TEMP	REGEQU		R10
ATTRIB_SCREEN_INDEX	REGEQU		R11
ATTRIB_CLEAR		REGEQU		R12
ATTRIB_ALNX		REGEQU		R13
ATTRIB_ALNY		REGEQU		R16
ATTRIB_ALNSPRITE	REGEQU		R17
ATTRIB_ALNSPRITE_INDEX	REGEQU		R18
ATTRIB_TEMP		REGEQU		R20
ATTRIB_OFFSETS		REGEQU		R21
ATTRIB_HEIGHT		REGEQU		R22
ATTRIB_TEMP1		REGEQU		R23
ATTRIB_ALNX2		REGEQU		R24
ATTRIB_ALNY2		REGEQU		R25
ATTRIB_OFFSCREENJUMP	REGEQU		R26
ATTRIB_WEAPON_INDEX	REGEQU		R27
ATTRIB_PLAYER_INDEX	REGEQU		R28
ATTRIB_HITOVER_INDEX	REGEQU		R29
ATTRIB_XOFF_INDEX	REGEQU		R30
ATTRIB_COUNT		REGEQU		R15
ATTRIB_ALNBOXES		REGEQU		R14
ATTRIB_COLLISION_FLAG	REGEQU		R31


	Align	LONG
GET_ATTRIB_RISC:
	Nop
	Movei	#Attrib_Params,r14
	Nop
	Moveq	#0,ATTRIB_CLEAR
	Moveq	#0,ATTRIB_COUNT
;	MOvei	#240,ATTRIB_HEIGHT
	Load	(r14+17),ATTRIB_HEIGHT
	Nop
	Movei	#.Off_Screen,ATTRIB_OFFSCREENJUMP
	Nop
	Load	(r14+0),ATTRIB_ACTIVE_POINTER	
	Load	(r14+1),ATTRIB_WEAPON_POINTER	
	Load	(r14+2),ATTRIB_PLAYER_POINTER	
	Load	(r14+3),ATTRIB_DRAWER_POINTER	
	Load	(r14+4),ATTRIB_NUMBER		
	Load	(r14+5),ATTRIB_SPRITES_BASE	
	Load	(r14+6),ATTRIB_X		
	Load	(r14+7),ATTRIB_XMAX
	Shlq	#16,ATTRIB_X
	Shlq	#16,ATTRIB_XMAX
	Sharq	#16,ATTRIB_X
	Sharq	#16,ATTRIB_XMAX
	Add	ATTRIB_X,ATTRIB_XMAX		
	Load	(r14+8),ATTRIB_OFFCHECK
	Load	(r14+9),ATTRIB_SCREEN_INDEX
	Load	(r14+10),ATTRIB_ALNSPRITE_INDEX
	Load	(r14+11),ATTRIB_OFFSETS

	Load	(r14+12),ATTRIB_WEAPON_INDEX
	Load	(r14+13),ATTRIB_PLAYER_INDEX
	Load	(r14+15),ATTRIB_XOFF_INDEX
	Load	(r14+14),ATTRIB_HITOVER_INDEX
	Load	(r14+16),ATTRIB_SPRITE_TEMP
	Move	ATTRIB_SPRITE_TEMP,ATTRIB_ALNBOXES
	Movei	#.Out_Attrib,ATTRIB_SPRITE_TEMP
	Cmpq	#0,ATTRIB_NUMBER
	Jump	EQ,(ATTRIB_SPRITE_TEMP)
	Nop
	Jump	MI,(ATTRIB_SPRITE_TEMP)
	Nop
.Attrib_Loop:
	Load	(ATTRIB_ACTIVE_POINTER),ATTRIB_SPRITE_STRUCT
	Addq	#4,ATTRIB_ACTIVE_POINTER
	
	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_SPRITE_TEMP
	Add	ATTRIB_SCREEN_INDEX,ATTRIB_SPRITE_TEMP
	Storeb	ATTRIB_CLEAR,(ATTRIB_SPRITE_TEMP)				; Clear ALN_ITSONSCREEN
	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_SPRITE_TEMP
	Addq	#8,ATTRIB_SPRITE_TEMP
	Loadw	(ATTRIB_SPRITE_TEMP),ATTRIB_ALNX				; Get x coardinate
	Addq	#2,ATTRIB_SPRITE_TEMP
	Loadw	(ATTRIB_SPRITE_TEMP),ATTRIB_ALNY				; Get y coardinate
	Shlq	#16,ATTRIB_ALNX
	Shlq	#16,ATTRIB_ALNY
	Sharq	#16,ATTRIB_ALNX
	Sharq	#16,ATTRIB_ALNY							; sign extend both
	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_SPRITE_TEMP
	Add	ATTRIB_ALNSPRITE_INDEX,ATTRIB_SPRITE_TEMP
	Loadw	(ATTRIB_SPRITE_TEMP),ATTRIB_ALNSPRITE				; Get alien sprite
	Shlq	#16,ATTRIB_ALNSPRITE
	Sharq	#16,ATTRIB_ALNSPRITE
	Cmpq	#0,ATTRIB_ALNSPRITE
	Jr	PL,.Plus
	Nop
	Movei	#.Desmond_Decer,ATTRIB_ALNSPRITE
	Jump	(ATTRIB_ALNSPRITE)
	Nop

.Plus:	Add	ATTRIB_ALNSPRITE,ATTRIB_OFFCHECK
	Loadb	(ATTRIB_OFFCHECK),ATTRIB_TEMP
	Sub	ATTRIB_ALNSPRITE,ATTRIB_OFFCHECK
	Cmpq	#0,ATTRIB_TEMP							; Check if its offseted
	Jr	NE,.Sprite_Offseted						; Yes it is !
	Nop
	Shlq	#4,ATTRIB_ALNSPRITE						; This gets shifted left 4, due to pipleine (AFTER JR!)
	Jr	.Skip_Offset_Add
	Nop
.Sprite_Offseted:
	Shlq	#2,ATTRIB_ALNSPRITE						; * 4
	Add	ATTRIB_ALNSPRITE,ATTRIB_OFFSETS
	Loadw	(ATTRIB_OFFSETS),ATTRIB_TEMP
	Shlq	#16,ATTRIB_TEMP
	Sharq	#16,ATTRIB_TEMP							; Collect x offset and sign extend
	Add	ATTRIB_TEMP,ATTRIB_ALNX
	Addq	#2,ATTRIB_OFFSETS
	Loadw	(ATTRIB_OFFSETS),ATTRIB_TEMP
	Shlq	#16,ATTRIB_TEMP
	Sharq	#16,ATTRIB_TEMP
	Add	ATTRIB_TEMP,ATTRIB_ALNY						; Add y offset and sign extend
	Subq	#2,ATTRIB_OFFSETS						; Back to normal offset
	Sub	ATTRIB_ALNSPRITE,ATTRIB_OFFSETS					; Base address of offset table gotten !
	Shlq	#2,ATTRIB_ALNSPRITE						; Sprite *4 (shifted 4 in total!)
.Skip_Offset_Add:
	Add	ATTRIB_ALNSPRITE,ATTRIB_SPRITES_BASE				; sprite is always *4 here !
	Moveq	#Spr_Yoffset,ATTRIB_TEMP
	Add	ATTRIB_SPRITES_BASE,ATTRIB_TEMP
	Loadw	(ATTRIB_TEMP),ATTRIB_TEMP1					; Get y- offset 
	Shlq	#16,ATTRIB_TEMP1
	Sharq	#16,ATTRIB_TEMP1
	Add	ATTRIB_TEMP1,ATTRIB_ALNY					; sign extend and add to y
	Move	ATTRIB_ALNX,ATTRIB_ALNX2
	Move	ATTRIB_ALNY,ATTRIB_ALNY2					; Store the x/y
	Movei	#Spr_Sheight,ATTRIB_TEMP
	Add	ATTRIB_SPRITES_BASE,ATTRIB_TEMP
	Loadw	(ATTRIB_TEMP),ATTRIB_TEMP1
	Shlq	#16,ATTRIB_TEMP1
	Sharq	#16,ATTRIB_TEMP1
	Add	ATTRIB_TEMP1,ATTRIB_ALNY2					; Get height of sprite, add to Y2
	Jump	MI,(ATTRIB_OFFSCREENJUMP)					; Negative, bottom y off screen
	Nop
	Moveq	#Spr_Pixel_Width,ATTRIB_TEMP
	Add	ATTRIB_SPRITES_BASE,ATTRIB_TEMP
	Loadw	(ATTRIB_TEMP),ATTRIB_TEMP1
	Shlq	#16,ATTRIB_TEMP1
	Sharq	#16,ATTRIB_TEMP1
	Add	ATTRIB_TEMP1,ATTRIB_ALNX2					; Get X offset, at it to x2
	Cmp	ATTRIB_X,ATTRIB_ALNX2						; Compare x2 with left hand side of screen
	Jump	MI,(ATTRIB_OFFSCREENJUMP)					; Off screen ! (take branch!)
	Nop
	Cmp	ATTRIB_XMAX,ATTRIB_ALNX	
	Jump	PL,(ATTRIB_OFFSCREENJUMP)					; If X1 is off right side of screen
	Nop
	Cmp	ATTRIB_HEIGHT,ATTRIB_ALNY
	Jump	PL,(ATTRIB_OFFSCREENJUMP)					; If Y1 is off bottom of screen !
	Nop

	Sub	ATTRIB_ALNSPRITE,ATTRIB_SPRITES_BASE
	Moveq	#0,ATTRIB_COLLISION_FLAG
	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_TEMP
	Add	ATTRIB_WEAPON_INDEX,ATTRIB_TEMP
	Loadb	(ATTRIB_TEMP),ATTRIB_TEMP1
	Cmpq	#0,ATTRIB_TEMP1
	Jr	EQ,.No_Weapon							; Can you hit this with a weapon ? no take branch
	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_TEMP
	Add	ATTRIB_HITOVER_INDEX,ATTRIB_TEMP
	Loadb	(ATTRIB_TEMP),ATTRIB_TEMP1					; HITOVERIDE flag set
	Cmpq	#0,ATTRIB_TEMP1
	Jr	NE,.No_Weapon							; Yes, take branch
	Cmpq	#10,ATTRIB_ALNY2
	Jr	MI,.No_Weapon							; ALN-Y less than 10, cant hit !
	Nop
	Store	ATTRIB_SPRITE_STRUCT,(ATTRIB_WEAPON_POINTER)			; Else store in weapon hit table
	Addq	#4,ATTRIB_WEAPON_POINTER					; & increment weapon hit pointer
	Moveq	#1,ATTRIB_COLLISION_FLAG

.No_Weapon:
	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_TEMP
	Add	ATTRIB_PLAYER_INDEX,ATTRIB_TEMP
	Loadb	(ATTRIB_TEMP),ATTRIB_TEMP1					; Check to see if this sprite can hit player 
	Cmpq	#0,ATTRIB_TEMP1
	Jr	EQ,.No_Player							; It cant
	Nop
	Store	ATTRIB_SPRITE_STRUCT,(ATTRIB_PLAYER_POINTER)
	Addq	#4,ATTRIB_PLAYER_POINTER					; Store & increment pointer
	Moveq	#1,ATTRIB_COLLISION_FLAG

.No_Player:
	Movei	#.No_Collision,ATTRIB_TEMP
	Cmpq	#0,ATTRIB_COLLISION_FLAG
	Jump	EQ,(ATTRIB_TEMP)
	Nop
.Make_Collison_Box:
	
	Shrq	#1,ATTRIB_ALNSPRITE						; Index on 8 byte table
	Add	ATTRIB_ALNSPRITE,ATTRIB_ALNBOXES				; Add to boxes
	Move	ATTRIB_ALNX,ATTRIB_ALNX2
	Move	ATTRIB_ALNY,ATTRIB_ALNY2					; Save x/y
	
	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_TEMP1
	Move	ATTRIB_XOFF_INDEX,ATTRIB_TEMP
	Add	ATTRIB_TEMP,ATTRIB_TEMP1					; ALN X offseted
	Storew	ATTRIB_ALNX,(ATTRIB_TEMP1)
	Addq	#2,ATTRIB_TEMP1
	Storew	ATTRIB_ALNY,(ATTRIB_TEMP1)					; Store alien x/y offseted
	
	Loadw	(ATTRIB_ALNBOXES),ATTRIB_TEMP1
	Shlq	#16,ATTRIB_TEMP1
	Sharq	#16,ATTRIB_TEMP1
	Add	ATTRIB_TEMP1,ATTRIB_ALNX
	Addq	#2,ATTRIB_ALNBOXES
	Loadw	(ATTRIB_ALNBOXES),ATTRIB_TEMP1
	Shlq	#16,ATTRIB_TEMP1
	Sharq	#16,ATTRIB_TEMP1
	Add	ATTRIB_TEMP1,ATTRIB_ALNY
	Addq	#2,ATTRIB_ALNBOXES
	Loadw	(ATTRIB_ALNBOXES),ATTRIB_TEMP1
	Add	ATTRIB_TEMP1,ATTRIB_ALNX2
	Addq	#2,ATTRIB_ALNBOXES
	Loadw	(ATTRIB_ALNBOXES),ATTRIB_TEMP1
	Add	ATTRIB_TEMP1,ATTRIB_ALNY2
	Subq	#6,ATTRIB_ALNBOXES
	Sub	ATTRIB_ALNSPRITE,ATTRIB_ALNBOXES

	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_TEMP1
	Movei	#40,ATTRIB_TEMP
	Add	ATTRIB_TEMP,ATTRIB_TEMP1
	Storew	ATTRIB_ALNX,(ATTRIB_TEMP1)
	Addq	#2,ATTRIB_TEMP1
	Storew	ATTRIB_ALNY,(ATTRIB_TEMP1)
	Addq	#2,ATTRIB_TEMP1
	Storew	ATTRIB_ALNX2,(ATTRIB_TEMP1)
	Addq	#2,ATTRIB_TEMP1
	Storew	ATTRIB_ALNY2,(ATTRIB_TEMP1)
	Jr	.Add_Sprite_To_List
	Nop
.No_Collision:
	Move	ATTRIB_SPRITE_STRUCT,ATTRIB_TEMP
	Add	ATTRIB_XOFF_INDEX,ATTRIB_TEMP
	Storew	ATTRIB_ALNX,(ATTRIB_TEMP)
	Addq	#2,ATTRIB_TEMP
	Storew	ATTRIB_ALNY,(ATTRIB_TEMP)
.Add_Sprite_To_List:
	Store	ATTRIB_SPRITE_STRUCT,(ATTRIB_DRAWER_POINTER)
	Addq	#4,ATTRIB_DRAWER_POINTER
	Addq	#1,ATTRIB_COUNT
	Jr	.Desmond_Decer
	Nop
.Off_Screen:
	Sub	ATTRIB_ALNSPRITE,ATTRIB_SPRITES_BASE
	Add	ATTRIB_SCREEN_INDEX,ATTRIB_SPRITE_STRUCT
	Movei	#-1,ATTRIB_TEMP
	Storeb	ATTRIB_TEMP,(ATTRIB_SPRITE_STRUCT)
.Desmond_Decer:
	Movei	#.Attrib_Loop,ATTRIB_TEMP
	Subq	#1,ATTRIB_NUMBER
	Jump	NE,(ATTRIB_TEMP)
	Nop
.Out_Attrib:
	Movei	#-1,ATTRIB_TEMP
	Store	ATTRIB_TEMP,(ATTRIB_WEAPON_POINTER)
	Store	ATTRIB_TEMP,(ATTRIB_PLAYER_POINTER)
	Addq	#4,ATTRIB_PLAYER_POINTER
	Store	ATTRIB_TEMP,(ATTRIB_PLAYER_POINTER)
	Store	ATTRIB_TEMP,(ATTRIB_DRAWER_POINTER)
	Movei	#Num_Objects_In_List,r0
	Store	ATTRIB_COUNT,(r0)
	Movei	#Stop_The_Gpu,r0
	Jump	(r0)
	Nop



;--------------------------------------------------------
; Routine expects parameters to be laid out as follows - ALL IN LONGWORDS!

; Active list pointer (L-ARRAY)

; Shots to do (l-array)
; Max weapons
; Player sprite base (l-pointer)
; Pre generated player objects
; Current list pointer
; WepX index
; Wep_sprite_num index
; Mapx
; Wep end



WEAPON_SHOTP	REGEQU		R0
WEAPON_COUNT	REGEQU		R1
WEAPON_XINDEX	REGEQU		R2
WEAPON_MAPX	REGEQU		R3
WEAPON_END	REGEQU		R4
WEAPON_NUMINDEX	REGEQU		R5
WEAPON_TEMP	REGEQU		R6
WEAPON_JUMP	REGEQU		R8
WEAPON_JUMP2	REGEQU		R9

	Align	LONG
Draw_Weapons_GPU:
	Nop
	Movei	#.Exit_Weapon,RETURN_ADDRESS
	Moveq	#0,NUM_OBJECTS_DISPLAYED
	Movei	#Quick_Draw,DRAW_JUMP
	Nop
	Movei	#Weapon_Params,r14
	Nop
	Load	(r14+0),WEAPON_SHOTP
	Load	(r14+1),WEAPON_COUNT
	Load	(r14+2),SPRITE_BASE
	Load	(r14+3),PRE_GENOBS
	Load	(r14+4),OLIST_POINTER

	Load	(r14+5),WEAPON_XINDEX
	Load	(r14+6),WEAPON_NUMINDEX
	Load	(r14+7),WEAPON_MAPX
	Load	(r14+8),WEAPON_END
	Movei	#.Exit_Weapon,WEAPON_JUMP
	Nop
	Movei	#.Weapon_Loop,WEAPON_JUMP2	
.Weapon_Loop:
	Loadb	(WEAPON_SHOTP),WEAPON_TEMP
	Cmpq	#0,WEAPON_TEMP
	Jump	EQ,(WEAPON_JUMP)
	Nop
	Jump	MI,(WEAPON_JUMP)
	Nop
	Move	WEAPON_SHOTP,WEAPON_TEMP
	Add	WEAPON_XINDEX,WEAPON_TEMP
	Loadw	(WEAPON_TEMP),DRAW_X
	Shlq	#16,DRAW_X
	Addq	#2,Weapon_Temp
	Sharq	#16,DRAW_X
	Loadw	(WEAPON_TEMP),DRAW_Y
	Shlq	#16,DRAW_Y
	Sharq	#16,DRAW_Y
	Sub	WEAPON_MAPX,DRAW_X
	Move	WEAPON_SHOTP,WEAPON_TEMP
	Add	WEAPON_NUMINDEX,WEAPON_TEMP
	Loadb	(WEAPON_TEMP),DRAW_THIS_SPRITE
	Jump	(DRAW_JUMP)
	Nop
.Exit_Weapon:
	Subq	#1,WEAPON_COUNT
	Jump	NE,(WEAPON_JUMP2)
	Add	WEAPON_END,WEAPON_SHOTP

	Movei	#.Exit_Sprite_Routine,DRAW_IMID
	Jump	(DRAW_IMID)
	Nop

;--------------------------------------------------------

	If	1
; Routine expects parameters to be laid out as follows - ALL IN LONGWORDS!

; Active list pointer (L-ARRAY)

; Number of sprites in active list
; Active list pointer
; Index into ALN_ITS_A_CHILD
; Index into ALN_CHILDX
; Index into ALN_LINK
; Index into ALN_PREVLINK
; Index into ALN_ANMSET
; Index into ALN_ROTNUMBER


LINKED_COUNT		REGEQU		R0
LINKED_POINTER		REGEQU		R1
LINKED_LINK		REGEQU		R2
LINKED_CHILDX		REGEQU		R3
LINKED_PREVLINK		REGEQU		R4
LINKED_ANMSET		REGEQU		R5
LINKED_ROTNUMBER	REGEQU		R6
LINKED_CHILDIDX 	REGEQU		R7
LINKED_TEMP0		REGEQU		R8
LINKED_SPRITE_STRUCT	REGEQU		R9
LINKED_TEMP_STRUCT	REGEQU		R10
LINKED_JUMP_DEC		REGEQU		R11
LINKED_JUMP_BACK	REGEQU		R12
LINKED_JUMP_LOOP	REGEQU		R13
LINK_X			REGEQU		R16
LINK_Y			REGEQU		R17
LINK_ADDRESS		REGEQU		R18
LINK_NEG		REGEQU		R19
LINK_CHILD_X		REGEQU		R20
LINK_CHILD_Y		REGEQU		R21
LINK_LOOP_REG		REGEQU		R22
LINK_8000		REGEQU		R23
LINK_SPECIAL		REGEQU		R24
LINK_8001		REGEQU		R25
LINKED_TEMP1		REGEQU		R26
LINK_TANK		REGEQU		R27
LINKED_TEMP2		REGEQU		R28

	Align	LONG
Build_Link_GPU:
	Nop
	Movei	#Link_Params,r14
	Nop
	Movei	#.Link_Dec,LINKED_JUMP_DEC
	Movei	#.Linked_Back,LINKED_JUMP_BACK
	Movei	#.Link_Loop,LINK_LOOP_REG
	Movei	#.Build_Loop,LINKED_JUMP_LOOP
	Moveq	#1,LINK_NEG
	Neg	LINK_NEG
	Movei	#$ffff8000,LINK_8000
	Movei	#$ffff8001,LINK_8001
	Load	(r14+0),LINKED_COUNT
	Load	(r14+1),LINKED_POINTER
	Load	(r14+2),LINKED_CHILDIDX
	Load	(r14+3),LINKED_CHILDX
	Load	(r14+4),LINKED_LINK
	Load	(r14+5),LINKED_PREVLINK
	Load	(r14+6),LINKED_ANMSET
	Load	(r14+7),LINKED_ROTNUMBER
	Movei	#.Special_Link,LINK_SPECIAL
	Movei	#.Tank_Link,LINK_TANK
	Movei	#Stop_The_GPU,LINKED_TEMP0
	Cmpq	#0,LINKED_COUNT
	Jump	EQ,(LINKED_TEMP0)
	Nop
	Jump	MI,(LINKED_TEMP0)
	Nop
		
.Build_Loop:
	Load	(LINKED_POINTER),LINKED_SPRITE_STRUCT
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Add	LINKED_CHILDIDX,LINKED_TEMP_STRUCT
	Loadb	(LINKED_TEMP_STRUCT),LINKED_TEMP0
	Cmpq	#0,LINKED_TEMP0
	Jump	NE,(LINKED_JUMP_DEC)

	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Add	LINKED_LINK,LINKED_TEMP_STRUCT
	Load	(LINKED_TEMP_STRUCT),LINKED_TEMP0
	Cmpq	#0,LINKED_TEMP0
	Jump	EQ,(LINKED_JUMP_DEC)
	Nop		
	Jump	MI,(LINKED_JUMP_DEC)
	
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Add	LINKED_PREVLINK,LINKED_TEMP_STRUCT
	Load	(LINKED_TEMP_STRUCT),LINKED_TEMP0
	Cmpq	#0,LINKED_TEMP0
	Jr	EQ,.Skip_Jump_Link
	Nop		
	Jump	PL,(LINKED_JUMP_DEC)

.Skip_Jump_Link:
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Addq	#8,LINKED_TEMP_STRUCT
	Loadw	(LINKED_TEMP_STRUCT),LINK_X
	Addq	#2,LINKED_TEMP_STRUCT
	Loadw	(LINKED_TEMP_STRUCT),LINK_Y
	Shlq	#16,LINK_X
	Shlq	#16,LINK_Y
	Sharq	#16,LINK_X
	Sharq	#16,LINK_Y
	
.Link_Loop:
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Add	LINKED_LINK,LINKED_TEMP_STRUCT
	Load	(LINKED_TEMP_STRUCT),LINK_ADDRESS
	Cmpq	#0,LINK_ADDRESS
	Jump	EQ,(LINKED_JUMP_DEC)
	Nop
	Jump	MI,(LINKED_JUMP_DEC)
	Nop
	Move	LINK_ADDRESS,LINKED_SPRITE_STRUCT
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Add	LINKED_CHILDIDX,LINKED_TEMP_STRUCT
	Storeb	LINK_NEG,(LINKED_TEMP_STRUCT)
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Add	LINKED_CHILDX,LINKED_TEMP_STRUCT
	Loadw	(LINKED_TEMP_STRUCT),LINK_CHILD_X
	Addq	#2,LINKED_TEMP_STRUCT
	Loadw	(LINKED_TEMP_STRUCT),LINK_CHILD_Y
	Shlq	#16,LINK_CHILD_X
	Shlq	#16,LINK_CHILD_Y
	Sharq	#16,LINK_CHILD_X
	Sharq	#16,LINK_CHILD_Y
	Cmp	LINK_8000,LINK_CHILD_X
	Jump	EQ,(LINK_SPECIAL)
	Nop
.Pos_Offset:
	Add	LINK_X,LINK_CHILD_X
	Add	LINK_Y,LINK_CHILD_Y
.Linked_Back:
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Addq	#8,LINKED_TEMP_STRUCT
	Storew	LINK_CHILD_X,(LINKED_TEMP_STRUCT)
	Addq	#2,LINKED_TEMP_STRUCT
	Storew	LINK_CHILD_Y,(LINKED_TEMP_STRUCT)
	Jump	(LINK_LOOP_REG)
	Nop

.Link_Dec:
	Addq	#4,LINKED_POINTER
	Subq	#1,LINKED_COUNT
	Jump	NE,(LINKED_JUMP_LOOP)
	Nop
	Movei	#Stop_The_Gpu,r0
	Jump	(r0)
	Nop

.Special_Link:
	Cmp	LINK_8001,LINK_CHILD_Y
	Jump	NE,(LINK_TANK)
	Nop
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Add	LINKED_PREVLINK,LINKED_TEMP_STRUCT
	Load	(LINKED_TEMP_STRUCT),LINKED_TEMP0
	Add	LINKED_ANMSET,LINKED_TEMP0
	Loadw	(LINKED_TEMP0),LINKED_TEMP1
	Movei	#71,LINKED_TEMP0
	Sub	LINKED_TEMP0,LINKED_TEMP1
	Shlq	#3,LINKED_TEMP1
	Movei	#Linked_Boat_Offsets,LINKED_TEMP0
.Add_To_Child:
	Add	LINKED_TEMP1,LINKED_TEMP0
	Move	LINK_X,LINK_CHILD_X
	Move	LINK_Y,LINK_CHILD_Y
	Load	(LINKED_TEMP0),LINK_X
	Addq	#4,LINKED_TEMP0
	Load	(LINKED_TEMP0),LINK_Y
	Add	LINK_X,LINK_CHILD_X
	Add	LINK_Y,LINK_CHILD_Y
	Jump	(LINKED_JUMP_BACK)
	Nop

.Tank_Link:
	Move	LINKED_SPRITE_STRUCT,LINKED_TEMP_STRUCT
	Add	LINKED_PREVLINK,LINKED_TEMP_STRUCT
	Load	(LINKED_TEMP_STRUCT),LINKED_TEMP0
	Add	LINKED_ROTNUMBER,LINKED_TEMP0
	Loadb	(LINKED_TEMP0),LINKED_TEMP1
	Shlq	#3,LINKED_TEMP1
	Movei	#Tank_Offsets,LINKED_TEMP0
	Movei	#.Add_To_Child,LINKED_TEMP2
	Jump	(LINKED_TEMP2)
	Nop

	Endif


;--------------------------------------------------------
; Routine expects parameters to be laid out as follows - ALL IN LONGWORDS!

; Alien table
; ALN_END INDEX
; Max aliens
; Array for valid animated aliens !
; ALN_HALF_DAMAGE INDEX
; ALN_DMG_DEC INDEX
; ALN_ITS_A_CHILD INDEX
; Aln_AnimFlipFlg INDEX
; Aln_Pause INDEX
; Aln_Delay	INDEX
; Aln_Anmset INDEX
; Aln_Antable INDEX
; Aln_Init INDEX
; Aln_Anim INDEX
; MOVE_ANIMCONT		
; MOVE_ANIMFLAG		
; MOVE_SPRITE
; MOVE_BASESPRITE
; MOVE_ROTNUMBER
; MOVE_PRI
; MOVE_KILLPLAYER
; MOVE_WEAPONEFFECT
; MOVE_SHADOW
; MOVE_TRACK

MOVE_ALIEN_TABLE	REGEQU		R0
MOVE_ALN_END		REGEQU		R1
MOVE_TEMP0		REGEQU		R2
MOVE_MAX_ALIENS		REGEQU		R3
MOVE_ALIENS_ACTIVE	REGEQU		R4
MOVE_HALF		REGEQU		R5
MOVE_DMG_DEC		REGEQU		R6
MOVE_TEMP1		REGEQU		R7
MOVE_CHILDIDX		REGEQU		R8
MOVE_CLR		REGEQU		R9
MOVE_FLIP		REGEQU		R10
MOVE_PAUSE		REGEQU		R11
MOVE_DELAY		REGEQU		R12
MOVE_ANMSET		REGEQU		R13
MOVE_ANMTABLE		REGEQU		R16
MOVE_TEMP2		REGEQU		R17
MOVE_INIT		REGEQU		R18
MOVE_NEG		REGEQU		R19
MOVE_ALIEN_TABLE_TEMP	REGEQU		R20
MOVE_ANIM		REGEQU		R21
MOVE_ANIMCONT		REGEQU		R22
MOVE_ANIMFLAG		REGEQU		R23
MOVE_SPRITE		REGEQU		R24
MOVE_BASESPRITE		REGEQU		R25
MOVE_ROTNUMBER		REGEQU		R26

MOVE_PRI		REGEQU		R27
MOVE_KILLPLAYER		REGEQU		R28
MOVE_WEAPONEFFECT	REGEQU		R29
MOVE_SHADOW		REGEQU		R30
MOVE_TRACK		REGEQU		R31




	Align	LONG

Move_Aliens_GPU:
	Nop
	Movei	#Move_Params,r14
	Nop
	Load	(r14+0),MOVE_ALIEN_TABLE
	Load	(r14+1),MOVE_ALN_END
	Load	(r14+2),MOVE_MAX_ALIENS
	Load	(r14+3),MOVE_ALIENS_ACTIVE
	Load	(r14+4),MOVE_HALF
	Load	(r14+5),MOVE_DMG_DEC
	Load	(r14+6),MOVE_CHILDIDX
	Load	(r14+7),MOVE_FLIP
	Load	(r14+8),MOVE_PAUSE
	Load	(r14+9),MOVE_DELAY
	Load	(r14+10),MOVE_ANMSET
	Load	(r14+11),MOVE_ANMTABLE
	Load	(r14+12),MOVE_INIT
	Load	(r14+13),MOVE_ANIM
	Load	(r14+14),MOVE_ANIMCONT		
	Load	(r14+15),MOVE_ANIMFLAG		
	Load	(r14+16),MOVE_SPRITE
	Load	(r14+17),MOVE_BASESPRITE

	Load	(r14+18),MOVE_ROTNUMBER
	Load	(r14+19),MOVE_PRI
	Load	(r14+20),MOVE_KILLPLAYER
	Load	(r14+21),MOVE_WEAPONEFFECT
	Load	(r14+22),MOVE_SHADOW
	Load	(r14+23),MOVE_TRACK

	Nop

	Movei	#Gpu_Quick_Ref,R15
	Nop

	Moveq	#0,MOVE_CLR
	Moveq	#1,MOVE_NEG
	Neg	MOVE_NEG

.Move_Loop:
	Loadw	(MOVE_ALIEN_TABLE),MOVE_TEMP0
	Shlq	#16,MOVE_TEMP0
	Sharq	#16,MOVE_TEMP0
	Cmpq	#0,MOVE_TEMP0
	Jr	PL,.Move_Valid
	Nop
.No_Alien:
	Add	MOVE_ALN_END,MOVE_ALIEN_TABLE
	Subq	#1,MOVE_MAX_ALIENS
	Jr	Ne,.Move_Loop
	Nop

	Movei	#.Quit_Move,MOVE_TEMP0	
	Jump	(MOVE_TEMP0)
	Nop


.Move_Valid:
	Jr	EQ,.No_Alien
	Nop
	Move	MOVE_ALIEN_TABLE,MOVE_TEMP0
	Add	MOVE_HALF,MOVE_TEMP0
	Loadw	(MOVE_TEMP0),MOVE_TEMP0
	Shlq	#16,MOVE_TEMP0
	Sharq	#16,MOVE_TEMP0
	Cmpq	#0,MOVE_TEMP0
	Jr	Pl,.No_Sub_Dmg_Dec
	Move	MOVE_ALIEN_TABLE,MOVE_TEMP0
	Add	MOVE_DMG_DEC,MOVE_TEMP0
	Loadb	(MOVE_TEMP0),MOVE_TEMP1
	Subq	#1,MOVE_TEMP1
	Storeb	MOVE_TEMP1,(MOVE_TEMP0)
	Move	MOVE_ALIEN_TABLE,MOVE_TEMP0

.No_Sub_Dmg_Dec:
	Add	MOVE_CHILDIDX,MOVE_TEMP0
	Storeb	MOVE_CLR,(MOVE_TEMP0)
	Move	MOVE_ALIEN_TABLE,MOVE_TEMP0
	Add	MOVE_FLIP,MOVE_TEMP0
	Loadb	(MOVE_TEMP0),MOVE_TEMP0
	Cmpq	#0,MOVE_TEMP0
	Jr	EQ,.No_Power_Up
	Nop
	Movei	#.Power_Move,MOVE_TEMP0
	Jump	(MOVE_TEMP0)
	Nop

.No_Power_Up:
	Move	MOVE_ALIEN_TABLE,MOVE_TEMP2
	Add	MOVE_INIT,MOVE_TEMP2
	Loadb	(MOVE_TEMP2),MOVE_TEMP0
	Cmpq	#0,MOVE_TEMP0
	Jr	EQ,.No_Initial_Pause

	Move	MOVE_ALIEN_TABLE,MOVE_TEMP0
	Add	MOVE_PAUSE,MOVE_TEMP0
	Loadw	(MOVE_TEMP0),MOVE_TEMP1
	Move	MOVE_TEMP1,MOVE_TEMP0
	Movei	#.Move_Paused,MOVE_TEMP1
	Cmpq	#0,MOVE_TEMP0
	Jump	NE,(MOVE_TEMP1)

.No_Initial_Pause:
	Move	MOVE_ALIEN_TABLE,MOVE_TEMP0
	Add	MOVE_DELAY,MOVE_TEMP0
	Loadb	(MOVE_TEMP0),MOVE_TEMP1
	Movei	#.Move_No_New_Sprite,MOVE_TEMP2
	Subq	#1,MOVE_TEMP1
	Storeb	MOVE_TEMP1,(MOVE_TEMP0)
	Jump	CC,(MOVE_TEMP2)
	Move	MOVE_ALIEN_TABLE,MOVE_TEMP0
	Add	MOVE_ANMSET,MOVE_TEMP0
	Loadw	(MOVE_TEMP0),MOVE_TEMP1
	Shlq	#16,MOVE_TEMP1
	Sharq	#14,MOVE_TEMP1
	Add	MOVE_ANMTABLE,MOVE_TEMP1
	Load	(MOVE_TEMP1),MOVE_TEMP0
	Move	MOVE_TEMP0,MOVE_TEMP1
	Loadw	(MOVE_TEMP1),MOVE_TEMP0

	Addq	#2,MOVE_TEMP1

	Move	MOVE_ALIEN_TABLE,MOVE_TEMP2
	Add	MOVE_DELAY,MOVE_TEMP2
	Storeb	MOVE_TEMP0,(MOVE_TEMP2)
	Move	MOVE_ALIEN_TABLE,MOVE_TEMP2
	Add	MOVE_INIT,MOVE_TEMP2
	Loadb	(MOVE_TEMP2),MOVE_TEMP0
	Cmpq	#0,MOVE_TEMP0
	Jr	NE,.Move_Already_Initialized
	Nop
	Storeb	MOVE_NEG,(MOVE_TEMP2)	
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_ANIM,MOVE_ALIEN_TABLE_TEMP
	Loadb	(MOVE_ALIEN_TABLE_TEMP),MOVE_TEMP0
	Subq	#1,MOVE_TEMP0
	Storeb	MOVE_TEMP0,(MOVE_ALIEN_TABLE_TEMP)
	
.Move_Already_Initialized:
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_ANIM,MOVE_ALIEN_TABLE_TEMP
	Loadb	(MOVE_ALIEN_TABLE_TEMP),MOVE_TEMP0
	Addq	#1,MOVE_TEMP0
	Storeb	MOVE_TEMP0,(MOVE_ALIEN_TABLE_TEMP)

	Loadb	(MOVE_ALIEN_TABLE_TEMP),MOVE_TEMP0
	Shlq	#1,MOVE_TEMP0
	Add	MOVE_TEMP1,MOVE_TEMP0
	Loadw	(MOVE_TEMP0),MOVE_TEMP2
	Shlq	#16,MOVE_TEMP2
	Sharq	#16,MOVE_TEMP2
	Cmpq	#0,MOVE_TEMP2
	Jr	PL,.Move_Valid_Sprite

	Nop
	Loadw	(MOVE_TEMP1),MOVE_TEMP2
	Shlq	#16,MOVE_TEMP2
	Sharq	#16,MOVE_TEMP2
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_ANIM,MOVE_ALIEN_TABLE_TEMP
	Storeb	MOVE_CLR,(MOVE_ALIEN_TABLE_TEMP)

	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_ANIMCONT,MOVE_ALIEN_TABLE_TEMP
	Loadb	(MOVE_ALIEN_TABLE_TEMP),MOVE_TEMP1
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_ANIMFLAG,MOVE_ALIEN_TABLE_TEMP
	Storeb	MOVE_TEMP1,(MOVE_ALIEN_TABLE_TEMP)

.Move_Valid_Sprite:
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_SPRITE,MOVE_ALIEN_TABLE_TEMP
	Storew	MOVE_TEMP2,(MOVE_ALIEN_TABLE_TEMP)
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_BASESPRITE,MOVE_ALIEN_TABLE_TEMP
	Storew	MOVE_TEMP2,(MOVE_ALIEN_TABLE_TEMP)

	Load	(r15+0),MOVE_TEMP0
	Add	MOVE_TEMP2,MOVE_TEMP0
	Loadb	(MOVE_TEMP0),MOVE_TEMP0
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_PRI,MOVE_ALIEN_TABLE_TEMP
	Storeb	MOVE_TEMP0,(MOVE_ALIEN_TABLE_TEMP)
	
	Load	(r15+1),MOVE_TEMP0
	Add	MOVE_TEMP2,MOVE_TEMP0
	Loadb	(MOVE_TEMP0),MOVE_TEMP0
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_KILLPLAYER,MOVE_ALIEN_TABLE_TEMP
	Storeb	MOVE_TEMP0,(MOVE_ALIEN_TABLE_TEMP)
	
	Load	(r15+2),MOVE_TEMP0
	Add	MOVE_TEMP2,MOVE_TEMP0
	Loadb	(MOVE_TEMP0),MOVE_TEMP0
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_WEAPONEFFECT,MOVE_ALIEN_TABLE_TEMP
	Storeb	MOVE_TEMP0,(MOVE_ALIEN_TABLE_TEMP)
		
	Load	(r15+3),MOVE_TEMP0
	Shlq	#1,MOVE_TEMP2
	Add	MOVE_TEMP2,MOVE_TEMP0
	Loadw	(MOVE_TEMP0),MOVE_TEMP1
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_SHADOW,MOVE_ALIEN_TABLE_TEMP
	Storew	MOVE_TEMP1,(MOVE_ALIEN_TABLE_TEMP)
	Shlq	#16,MOVE_TEMP1
	Sharq	#16,MOVE_TEMP1
	Cmpq	#0,MOVE_TEMP1
	Jr	MI,.Move_No_New_Sprite
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_TRACK,MOVE_ALIEN_TABLE_TEMP
	Loadb	(MOVE_ALIEN_TABLE_TEMP),MOVE_TEMP0
	Cmpq	#0,MOVE_TEMP0
	Jr	EQ,.Move_No_New_Sprite
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_ROTNUMBER,MOVE_ALIEN_TABLE_TEMP
	Loadb	(MOVE_ALIEN_TABLE_TEMP),MOVE_TEMP0
	Add	MOVE_TEMP0,MOVE_TEMP1
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_SHADOW,MOVE_ALIEN_TABLE_TEMP
	Storew	MOVE_TEMP1,(MOVE_ALIEN_TABLE_TEMP)
.Move_No_New_Sprite:
.Move_Paused:
	Store	MOVE_ALIEN_TABLE,(MOVE_ALIENS_ACTIVE)
	Addq	#4,MOVE_ALIENS_ACTIVE
	Movei	#GpuX_Move_Pointer,MOVE_TEMP0
	Store	MOVE_ALIENS_ACTIVE,(MOVE_TEMP0)
	Movei	#.No_Alien,MOVE_TEMP0
	Jump	(MOVE_TEMP0)
	Nop

.Quit_Move:
	Store	MOVE_NEG,(MOVE_ALIENS_ACTIVE)
	Addq	#4,MOVE_ALIENS_ACTIVE
	Store	MOVE_NEG,(MOVE_ALIENS_ACTIVE)
	Addq	#4,MOVE_ALIENS_ACTIVE
	Movei	#GPUX_MOVE_POINTER,MOVE_TEMP0
	Nop
	Store	MOVE_ALIENS_ACTIVE,(MOVE_TEMP0)
	Nop
	Movei	#Stop_The_GPU,r0
	Jump	(r0)
	Nop

Power_Move:
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_FLIP,MOVE_ALIEN_TABLE_TEMP
	Subq	#1,MOVE_ALIEN_TABLE_TEMP
	Loadb	(MOVE_ALIEN_TABLE_TEMP),MOVE_TEMP1
	Movei	#.No_Flip,MOVE_TEMP0
	Subq	#1,MOVE_TEMP1
	Storeb	MOVE_TEMP1,(MOVE_ALIEN_TABLE_TEMP)
	Jump	CC,(MOVE_TEMP0)
	Nop
	Movei	#140,MOVE_TEMP0
	Storeb	MOVE_TEMP0,(MOVE_ALIEN_TABLE_TEMP)
	Move	MOVE_ALIEN_TABLE,MOVE_ALIEN_TABLE_TEMP
	Add	MOVE_ANMSET,MOVE_ALIEN_TABLE_TEMP
	Loadw	(MOVE_ALIEN_TABLE_TEMP),MOVE_TEMP0
	Movei	#38,MOVE_TEMP1
	Cmp	MOVE_TEMP1,MOVE_TEMP0
	Jr	EQ,.No_Flip
	Nop
	Cmpq	#5,MOVE_TEMP0
	Jr	PL,.Fivsix
	Nop
	Movei	#%100,MOVE_TEMP1
	Xor	MOVE_TEMP1,MOVE_TEMP0
.Fivsix:
	Movei	#%011,MOVE_TEMP1
	Xor	MOVE_TEMP1,MOVE_TEMP0
	Storew	MOVE_TEMP0,(MOVE_ALIEN_TABLE_TEMP)
.No_Flip:
	Movei	#.No_Power_Up,MOVE_TEMP2
	Jump	(MOVE_TEMP2)
	Nop

;--------------------------------------------------------


WEPCOL_WEPLIST		REGEQU		R0
WEPCOL_TEMP		REGEQU		R1
WEPCOL_FF		REGEQU		R2
WEPCOL_STRUCT		REGEQU		R3
WEPCOL_ALIENLIST	REGEQU		R4
WEPCOL_SOURCEX1		REGEQU		R5
WEPCOL_SOURCEX2		REGEQU		R6
WEPCOL_SOURCEY1		REGEQU		R7
WEPCOL_SOURCEY2		REGEQU		R8
WEPCOL_STRUCT_TEMP	REGEQU		R9
WEPCOL_NOMOREALIENS	REGEQU		R10
WEPCOL_ALIENSTRUCT	REGEQU		R11
WEPCOL_ALIENSTRUCT_TEMP	REGEQU		R12
WEPCOL_BOXIDX		REGEQU		R13
WEPCOL_DESTX1		REGEQU		R16
WEPCOL_DESTX2		REGEQU		R17
WEPCOL_DESTY1		REGEQU		R18
WEPCOL_DESTY2		REGEQU		R19
WEPCOL_LOOP		REGEQU		R20
WEPCOL_NEXTWEAPON	REGEQU		R21
WEPCOL_WEPBOXIDX	REGEQU		R22
WEPCOL_HITLIST		REGEQU		R23
WEPCOL_COMPARE		REGEQU		R24


; Weapon active list
; Weapon check table
; Alien box
; Weapon box
; Weapon to alien hit list !

	Align	LONG

Gpu_Weapon_Collision:
	Nop
	Nop
	Movei	#Weapon_Params,r14
	Nop
	Movei	#.Compare_Weapon,WEPCOL_COMPARE
	Nop
	Load	(r14+0),WEPCOL_WEPLIST
	Load	(r14+2),WEPCOL_BOXIDX
	Load	(r14+3),WEPCOL_WEPBOXIDX
	Load	(r14+4),WEPCOL_HITLIST
	Movei	#$ff,WEPCOL_FF	
	Nop
	Movei	#.Alien_Loop,WEPCOL_LOOP
	Nop
	Movei	#.Weapon_Compare_Loop,WEPCOL_NEXTWEAPON
	Nop
	Movei	#.Weapon_Compare_Loop,WEPCOL_NOMOREALIENS

.Weapon_Compare_Loop:
	Loadb	(WEPCOL_WEPLIST),WEPCOL_TEMP
	Cmp	WEPCOL_FF,WEPCOL_TEMP
	Jr	EQ,.No_Weapons
	Nop
	Load	(WEPCOL_WEPLIST),WEPCOL_STRUCT
	Jump	(WEPCOL_COMPARE)
	Addq	#8,WEPCOL_WEPLIST
	Jr	.Weapon_Compare_Loop
	Nop
.No_Weapons:
	Moveq	#1,r0
	Neg	r0
	Store	r0,(WEPCOL_HITLIST)
	Addq	#4,WEPCOL_HITLIST
	Store	r0,(WEPCOL_HITLIST)
	Addq	#4,WEPCOL_HITLIST
	Store	r0,(WEPCOL_HITLIST)
	Movei	#WEPCOL_POINTER,WEPCOL_TEMP
	Store	WEPCOL_HITLIST,(WEPCOL_TEMP)
	Movei	#Stop_The_Gpu,r0
	Jump	(r0)
	Nop

.Compare_Weapon:
	Move	WEPCOL_STRUCT,WEPCOL_STRUCT_TEMP
	Add	WEPCOL_WEPBOXIDX,WEPCOL_STRUCT_TEMP
	Loadw	(WEPCOL_STRUCT_TEMP),WEPCOL_SOURCEX1
	Addq	#2,WEPCOL_STRUCT_TEMP
	Loadw	(WEPCOL_STRUCT_TEMP),WEPCOL_SOURCEY1
	Addq	#2,WEPCOL_STRUCT_TEMP
	Loadw	(WEPCOL_STRUCT_TEMP),WEPCOL_SOURCEX2
	Addq	#2,WEPCOL_STRUCT_TEMP
	Loadw	(WEPCOL_STRUCT_TEMP),WEPCOL_SOURCEY2
	Load	(r14+1),WEPCOL_ALIENLIST
	Shlq	#16,WEPCOL_SOURCEX1
	Shlq	#16,WEPCOL_SOURCEX2
	Shlq	#16,WEPCOL_SOURCEY1
	Shlq	#16,WEPCOL_SOURCEY2
	Sharq	#16,WEPCOL_SOURCEX1
	Sharq	#16,WEPCOL_SOURCEX2
	Sharq	#16,WEPCOL_SOURCEY1
	Sharq	#16,WEPCOL_SOURCEY2
.Alien_Loop:
	Loadb	(WEPCOL_ALIENLIST),WEPCOL_TEMP
	Cmp	WEPCOL_FF,WEPCOL_TEMP
	Jump	EQ,(WEPCOL_NOMOREALIENS)
	Load	(WEPCOL_ALIENLIST),WEPCOL_ALIENSTRUCT
	Loadw	(WEPCOL_ALIENSTRUCT),WEPCOL_TEMP
	Addq	#4,WEPCOL_ALIENLIST
	Shlq	#16,WEPCOL_TEMP
	Sharq	#16,WEPCOL_TEMP
	Cmpq	#0,WEPCOL_TEMP
	Jump	MI,(WEPCOL_LOOP)
	Nop
	Jump	EQ,(WEPCOL_LOOP)
	Move	WEPCOL_ALIENSTRUCT,WEPCOL_ALIENSTRUCT_TEMP
	Add	WEPCOL_BOXIDX,WEPCOL_ALIENSTRUCT_TEMP
	Loadw	(WEPCOL_ALIENSTRUCT_TEMP),WEPCOL_DESTX1
	Addq	#2,WEPCOL_ALIENSTRUCT_TEMP
	Loadw	(WEPCOL_ALIENSTRUCT_TEMP),WEPCOL_DESTY1
	Addq	#2,WEPCOL_ALIENSTRUCT_TEMP
	Loadw	(WEPCOL_ALIENSTRUCT_TEMP),WEPCOL_DESTX2
	Addq	#2,WEPCOL_ALIENSTRUCT_TEMP
	Loadw	(WEPCOL_ALIENSTRUCT_TEMP),WEPCOL_DESTY2
	Shlq	#16,WEPCOL_DESTX1
	Shlq	#16,WEPCOL_DESTX2
	Shlq	#16,WEPCOL_DESTY1
	Shlq	#16,WEPCOL_DESTY2
	Sharq	#16,WEPCOL_DESTX1
	Sharq	#16,WEPCOL_DESTX2
	Sharq	#16,WEPCOL_DESTY1
	Sharq	#16,WEPCOL_DESTY2
	Cmp	WEPCOL_SOURCEX2,WEPCOL_DESTX1
	Jump	PL,(WEPCOL_LOOP)
	Cmp	WEPCOL_SOURCEY2,WEPCOL_DESTY1
	Jump	PL,(WEPCOL_LOOP)
	Cmp	WEPCOL_SOURCEX1,WEPCOL_DESTX2
	Jump	MI,(WEPCOL_LOOP)
	Cmp	WEPCOL_SOURCEY1,WEPCOL_DESTY2
	Jump	MI,(WEPCOL_LOOP)
	Store	WEPCOL_STRUCT,(WEPCOL_HITLIST)
	Addq	#4,WEPCOL_HITLIST
	Store	WEPCOL_ALIENSTRUCT,(WEPCOL_HITLIST)
	Addq	#4,WEPCOL_HITLIST
	Movei	#WEPCOL_POINTER,WEPCOL_TEMP
	Store	WEPCOL_HITLIST,(WEPCOL_TEMP)
;	Addq	#8,WEPCOL_WEPLIST
	Jump	(WEPCOL_NEXTWEAPON)
	Nop
	
;--------------------------------------------------------

Wep_Dmgidx			Equ	0
Wep_Sprite_Numidx		Equ	1
Wep_Animidx			Equ	2
Wep_Delayidx			Equ	3
Wep_Xidx			Equ	4
Wep_Yidx			Equ	6
Wep_Home_Sineidx		Equ	8
Wep_XVidx			Equ	8
Wep_Home_Velidx			Equ	10
Wep_YVidx			Equ	10
Wep_Spriteidx			Equ	12
Wep_Sine_Validx			Equ	13
Wep_Sine_Scaleidx		Equ	14
Wep_Sine_Speedidx		Equ	15
Is_Weapon_A_Rocketidx		Equ	16
Is_Weapon_A_Homing_Missleidx	Equ	17
Wep_FractionXidx		Equ	18
Wep_FractionYidx		Equ	20
Wep_Boxidx			Equ	22
Wep_Explosion_Numidx		Equ		30
Wep_Endidx			Equ	32


WEP_HOMETABLE		REGEQU		R0
WEP_SHOTS_TO_DO		REGEQU		R1
WEP_WEPANIMS		REGEQU		R2
WEP_ALIEN_SINE		REGEQU		R3
WEP_WEAPON_ACTVE_LIST	REGEQU		R4
WEP_WEP_COUNT		REGEQU		R5
WEP_END_REGISTER	REGEQU		R6
WEP_MOVETEMP0		REGEQU		R7
WEP_MOVETEMP1		REGEQU		R8
WEP_MOVETEMP2		REGEQU		R9
WEP_MOVETEMP3		REGEQU		R10
WEP_SHOTS_TO_DO_TEMP	REGEQU		R11
WEP_RETURN		REGEQU		R12
WEP_MOVEANGLE		REGEQU		R13
WEP_NUMHOMES		REGEQU		R16
REMOVE_WEAPON		REGEQU		R17
WEP_ANIMATE		REGEQU		R18
WEP_CLR			REGEQU		R19
WEP_PLAYERBOX		REGEQU		R20
WEP_BOX0		REGEQU		R21
WEP_BOX1		REGEQU		R22
WEP_BOX2		REGEQU		R23
WEP_BOX3		REGEQU		R24
WEP_MOVETEMP4		REGEQU		R25
WEP_MOVETEMP5		REGEQU		R26
WEP_DEC			REGEQU		R27
WEP_NOTHOMING		REGEQU		R28

WEP_X			REGEQU		R29
WEP_Y			REGEQU		R30
SCROLL_X		REGEQU		R31
WEP_XV			REGEQU		R23
WEP_YV			REGEQU		R24

Gpu_Move_Weapons:
	Nop
	Movei	#Move_Weapon_Params,r14
	Nop
	Load	(r14+0),WEP_HOMETABLE
	Load	(r14+1),WEP_SHOTS_TO_DO
	Load	(r14+2),WEP_WEPANIMS
	Load	(r14+3),WEP_ALIEN_SINE
	Load	(r14+4),WEP_WEAPON_ACTVE_LIST
	Load	(r14+5),WEP_WEP_COUNT
	Load	(r14+6),WEP_NUMHOMES
	Load	(r14+7),WEP_PLAYERBOX
	Load	(r14+8),SCROLL_X
	Movei	#Wep_Endidx,WEP_END_REGISTER
	Nop
	Movei	#Gpu_Moveangle,WEP_MOVEANGLE
	Nop
	Movei	#.Remove_Weapon_From_List,REMOVE_WEAPON
	Nop
	Movei	#.Animate_Weapon,WEP_ANIMATE
	Moveq	#0,WEP_CLR	
	Movei	#.Wep_MoveDec,WEP_DEC
	Nop
	Movei	#.Not_Homing,WEP_NOTHOMING
.Wep_loop:
	Loadb	(WEP_SHOTS_TO_DO),WEP_MOVETEMP0
	Cmpq	#0,WEP_MOVETEMP0
	Jr	Eq,.Wep_MoveDec	
	Nop
	Jr	MI,.Wep_MoveDec	
	Nop
	Jr	.Weapon_Active
.Wep_MoveDec:
	Add	WEP_END_REGISTER,WEP_SHOTS_TO_DO
	Subq	#1,WEP_WEP_COUNT
	Jr	NE,.Wep_Loop
	Nop
	Movei	#.End_Weapon_Move,WEP_MOVETEMP0
	Jump	(WEP_MOVETEMP0)
	Nop

.Weapon_Active:
	Sub	WEP_END_REGISTER,WEP_SHOTS_TO_DO
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Is_Weapon_A_Homing_Missleidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP0
	Cmpq	#0,WEP_MOVETEMP0
	Jump	Eq,(WEP_NOTHOMING)
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Is_Weapon_A_Rocketidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP0
	Cmpq	#0,WEP_MOVETEMP0
	Jr	Ne,.Death_Bolt_Move
	Nop
	Movei	#.Back_1,WEP_RETURN
	Jump	(WEP_MOVEANGLE)
	Nop
.Back_1:
	Movei	#.Home_Still_On_Screen,WEP_MOVETEMP5
	Jump	Eq,(WEP_MOVETEMP5)
	Loadb	(WEP_NUMHOMES),WEP_MOVETEMP0
	Subq	#1,WEP_MOVETEMP0
	Storeb	WEP_MOVETEMP0,(WEP_NUMHOMES)
	Jump	(REMOVE_WEAPON)
	Nop

.Death_Bolt_Move:
	Movei	#.Back_2,WEP_RETURN
	Jump	(WEP_ANIMATE)
	Nop
.Back_2:
	Movei	#.Back_3,WEP_RETURN
	Jump	(WEP_MOVEANGLE)
	Nop
.Back_3:
	Jump	NE,(REMOVE_WEAPON)
	Nop
	Movei	#.Not_A_Rocket,WEP_MOVETEMP5
	Jump	(WEP_MOVETEMP5)
	Nop

.Home_Still_On_Screen:
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Spriteidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP0
	Shlq	#2,WEP_MOVETEMP0
	Add	WEP_WEPANIMS,WEP_MOVETEMP0
	Load	(WEP_MOVETEMP0),WEP_MOVETEMP0
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Xvidx,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP1
	Shrq	#4,WEP_MOVETEMP1
	Addq	#1,WEP_MOVETEMP0
	Loadb	(WEP_MOVETEMP0),WEP_MOVETEMP0
	Add	WEP_MOVETEMP0,WEP_MOVETEMP1
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Sprite_Numidx,WEP_SHOTS_TO_DO_TEMP
	Storeb	WEP_MOVETEMP1,(WEP_SHOTS_TO_DO_TEMP)
	Store	WEP_SHOTS_TO_DO,(WEP_HOMETABLE)
	Addq	#4,WEP_HOMETABLE
	Movei	#.Not_A_Rocket,WEP_MOVETEMP5
	Jump	(WEP_MOVETEMP5)
	Nop

.Remove_Weapon_From_List:
	Storeb	WEP_CLR,(WEP_SHOTS_TO_DO)
	Jump	(WEP_DEC)
	Nop
.Not_Homing:
	Movei	#.Back_4,WEP_RETURN
	Jump	(WEP_ANIMATE)
	Nop
.Back_4:
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Xidx,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_X
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_Y
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_XV
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_YV
	Shlq	#16,WEP_X
	Shlq	#16,WEP_Y
	Shlq	#16,WEP_XV
	Shlq	#16,WEP_YV
	Sharq	#16,WEP_X
	Sharq	#16,WEP_Y
	Sharq	#16,WEP_XV
	Sharq	#16,WEP_YV
	Add	WEP_XV,WEP_X
	Add	WEP_YV,WEP_Y
	Movei	#.Back_5,WEP_RETURN
	Nop
	Movei	#.Check_Bound,WEP_MOVETEMP5
	Jump	(WEP_MOVETEMP5)
	Nop
.Back_5:
	Jump	Ne,(REMOVE_WEAPON)
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Is_Weapon_A_Rocketidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP0
	Movei	#.Not_A_Rocket,WEP_MOVETEMP5
	Cmpq	#0,WEP_MOVETEMP0
	Jump	Eq,(WEP_MOVETEMP5)

	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Sine_Validx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP0
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Sine_Scaleidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP1
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Sine_Speedidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP2
	Add	WEP_MOVETEMP2,WEP_MOVETEMP0
	Movei	#$100,WEP_MOVETEMP2
	Cmp	WEP_MOVETEMP2,WEP_MOVETEMP0
	Jr	MI,.No_Max
	Nop
	Subq	#1,WEP_MOVETEMP2
	Move	WEP_MOVETEMP2,WEP_MOVETEMP0
.No_Max:
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Sine_Validx,WEP_SHOTS_TO_DO_TEMP
	Storeb	WEP_MOVETEMP0,(WEP_SHOTS_TO_DO_TEMP)
	Shrq	#2,WEP_MOVETEMP0
	Add	WEP_MOVETEMP0,WEP_MOVETEMP0
	Addq	#1,WEP_MOVETEMP1
	Subq	#30,WEP_ALIEN_SINE
	Add	WEP_MOVETEMP0,WEP_ALIEN_SINE
	Loadw	(WEP_ALIEN_SINE),WEP_MOVETEMP2
	Sub	WEP_MOVETEMP0,WEP_ALIEN_SINE
	Addq	#30,WEP_ALIEN_SINE
	Imult	WEP_MOVETEMP2,WEP_MOVETEMP1
	Sharq	#15,WEP_MOVETEMP1
	Sub	WEP_MOVETEMP1,WEP_Y
.Not_A_Rocket:
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#WEP_Xidx,WEP_SHOTS_TO_DO_TEMP
	Storew	WEP_X,(WEP_SHOTS_TO_DO_TEMP)
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Storew	WEP_Y,(WEP_SHOTS_TO_DO_TEMP)
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Sprite_Numidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP2
	Shlq	#3,WEP_MOVETEMP2
	Add	WEP_PLAYERBOX,WEP_MOVETEMP2
	Loadw	(WEP_MOVETEMP2),WEP_BOX0
	Addq	#2,WEP_MOVETEMP2
	Loadw	(WEP_MOVETEMP2),WEP_BOX1
	Addq	#2,WEP_MOVETEMP2
	Loadw	(WEP_MOVETEMP2),WEP_BOX2
	Addq	#2,WEP_MOVETEMP2
	Loadw	(WEP_MOVETEMP2),WEP_BOX3
	Shlq	#16,WEP_BOX0
	Shlq	#16,WEP_BOX1
	Shlq	#16,WEP_BOX2
	Shlq	#16,WEP_BOX3
	Sharq	#16,WEP_BOX0
	Sharq	#16,WEP_BOX1
	Sharq	#16,WEP_BOX2
	Sharq	#16,WEP_BOX3
	Add	WEP_X,WEP_BOX0
	Add	WEP_Y,WEP_BOX1
	Add	WEP_X,WEP_BOX2
	Add	WEP_Y,WEP_BOX3
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#WEP_BOXidx,WEP_SHOTS_TO_DO_TEMP
	Storew	WEP_BOX0,(WEP_SHOTS_TO_DO_TEMP)
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Storew	WEP_BOX1,(WEP_SHOTS_TO_DO_TEMP)
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Storew	WEP_BOX2,(WEP_SHOTS_TO_DO_TEMP)
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Storew	WEP_BOX3,(WEP_SHOTS_TO_DO_TEMP)
	Store	WEP_SHOTS_TO_DO,(WEP_WEAPON_ACTVE_LIST)
	Addq	#4,WEP_WEAPON_ACTVE_LIST	
	Jump	(WEP_DEC)
	
.Animate_Weapon:
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Spriteidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP0
	Shlq	#2,WEP_MOVETEMP0
	Add	WEP_WEPANIMS,WEP_MOVETEMP0
	Load	(WEP_MOVETEMP0),WEP_MOVETEMP0
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Animidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP1
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Delayidx,WEP_SHOTS_TO_DO_TEMP
	Loadb	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP2
	Addq	#1,WEP_MOVETEMP0
	Subq	#1,WEP_MOVETEMP2
	Movei	#.No_Anim_Change,WEP_MOVETEMP4
	Jump	CC,(WEP_MOVETEMP4)
	Nop
	Subq	#1,WEP_MOVETEMP0
	Loadb	(WEP_MOVETEMP0),WEP_MOVETEMP2
	Addq	#1,WEP_MOVETEMP0
	Addq	#1,WEP_MOVETEMP1
	Add	WEP_MOVETEMP1,WEP_MOVETEMP0
	Loadb	(WEP_MOVETEMP0),WEP_MOVETEMP5
	Sub	WEP_MOVETEMP1,WEP_MOVETEMP0
	Movei	#$ff,WEP_MOVETEMP4
	Cmp	WEP_MOVETEMP5,WEP_MOVETEMP4
	Jr	Ne,.No_Anim_Change
	Nop
	Moveq	#0,WEP_MOVETEMP1
.No_Anim_Change:
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Delayidx,WEP_SHOTS_TO_DO_TEMP
	Storeb	WEP_MOVETEMP2,(WEP_SHOTS_TO_DO_TEMP)
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Animidx,WEP_SHOTS_TO_DO_TEMP
	Storeb	WEP_MOVETEMP1,(WEP_SHOTS_TO_DO_TEMP)
	Add	WEP_MOVETEMP1,WEP_MOVETEMP0
	Loadb	(WEP_MOVETEMP0),WEP_MOVETEMP0
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Sprite_Numidx,WEP_SHOTS_TO_DO_TEMP
	Storeb	WEP_MOVETEMP0,(WEP_SHOTS_TO_DO_TEMP)
	Jump	(WEP_RETURN)
	Nop

.Gpu_Moveangle:
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Xidx,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP0
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP1
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP2
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP3
	Movei	#$ff,WEP_MOVETEMP4
	And	WEP_MOVETEMP4,WEP_MOVETEMP2
	Add	WEP_MOVETEMP2,WEP_MOVETEMP2
	Add	WEP_ALIEN_SINE,WEP_MOVETEMP2
	Loadw	(WEP_MOVETEMP2),WEP_MOVETEMP4
	Movei	#$80,WEP_MOVETEMP5
	Add	WEP_MOVETEMP5,WEP_MOVETEMP2
	Loadw	(WEP_MOVETEMP2),WEP_MOVETEMP5

	Shlq	#16,WEP_MOVETEMP3
	Sharq	#16,WEP_MOVETEMP3
	Imult	WEP_MOVETEMP3,WEP_MOVETEMP4
	Neg	WEP_MOVETEMP3
	Imult	WEP_MOVETEMP3,WEP_MOVETEMP5
	Shlq	#16,WEP_MOVETEMP0
	Shlq	#16,WEP_MOVETEMP1
	Move	WEP_SHOTS_TO_DO,WEP_SHOTS_TO_DO_TEMP
	Addq	#Wep_Fractionxidx,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP2
	Addq	#2,WEP_SHOTS_TO_DO_TEMP
	Loadw	(WEP_SHOTS_TO_DO_TEMP),WEP_MOVETEMP3
	Add	WEP_MOVETEMP2,WEP_MOVETEMP0
	Add	WEP_MOVETEMP3,WEP_MOVETEMP1
	Add	WEP_MOVETEMP4,WEP_MOVETEMP0
	Add	WEP_MOVETEMP5,WEP_MOVETEMP1
	Storew	WEP_MOVETEMP1,(WEP_SHOTS_TO_DO_TEMP)
	Subq	#2,WEP_SHOTS_TO_DO_TEMP
	Storew	WEP_MOVETEMP0,(WEP_SHOTS_TO_DO_TEMP)
	Sharq	#16,WEP_MOVETEMP1
	Sharq	#16,WEP_MOVETEMP0
	Move	WEP_MOVETEMP0,WEP_X
	Move	WEP_MOVETEMP1,WEP_Y

.Check_Bound:
	Movei	#.Out_Range,WEP_MOVETEMP3
	Sub	SCROLL_X,WEP_X
	Moveq	#16,WEP_MOVETEMP5
	Neg	WEP_MOVETEMP5
	Cmp	WEP_MOVETEMP5,WEP_X
	Jump	MI,(WEP_MOVETEMP3)
	Nop
	Movei	#Playfield_Width,WEP_MOVETEMP4
	Cmp	WEP_MOVETEMP4,WEP_X
	Jump	PL,(WEP_MOVETEMP3)
	Nop
	Cmp	WEP_MOVETEMP5,WEP_Y
	Jump	MI,(WEP_MOVETEMP3)
	Nop
	Load	(r14+9),WEP_MOVETEMP4
	Cmp	WEP_MOVETEMP4,WEP_Y
	Jump	PL,(WEP_MOVETEMP3)
	Add	SCROLL_X,WEP_X
	Sub	WEP_MOVETEMP4,WEP_MOVETEMP4
	Jump	(WEP_RETURN)
	Nop
.Out_Range:
	Sub	WEP_MOVETEMP4,WEP_MOVETEMP4
	Addq	#1,WEP_MOVETEMP4
	Jump	(WEP_RETURN)
	Nop

.End_Weapon_Move:
	Moveq	#1,WEP_MOVETEMP4
	Neg	WEP_MOVETEMP4
	Store	WEP_MOVETEMP4,(WEP_HOMETABLE)
	Addq	#4,WEP_HOMETABLE
	Store	WEP_MOVETEMP4,(WEP_WEAPON_ACTVE_LIST)
	Addq	#4,WEP_WEAPON_ACTVE_LIST
	Store	WEP_MOVETEMP4,(WEP_HOMETABLE)
	Store	WEP_MOVETEMP4,(WEP_WEAPON_ACTVE_LIST)
	Movei	#Stop_The_Gpu,r0
	Jump	(r0)
	Nop


;--------------------------------------------------------
	Align	Long

Stop_The_Gpu:
	Nop
	Movei	#$f02114,r0
	Moveq	#$8,r1
	Store	r1,(r0)
	Nop
.Never_Stop:
	Jr	.Never_Stop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop
	Nop



	Align	LONG
Linked_Boat_Offsets:
	Dc.l	-4,2
	Dc.l	1,-3
	Dc.l	-4,2
	Dc.l	2,-3
Tank_Offsets:
	Dc.l	+0,+0
	Dc.l	+1,+0
	Dc.l	+1,+1
	Dc.l	+1,+1
	Dc.l	+1,+2		;90
	Dc.l	+1,+2
	Dc.l	+1,+2
	Dc.l	+1,+2
	Dc.l	+0,+2		;180
	Dc.l	+0,+2
	Dc.l	+1,+2
	Dc.l	+1,+1
	Dc.l	+2,+1		;270
	Dc.l	+2,+1
	Dc.l	+1,+1
	Dc.l	0,0


Gpu_Quick_Ref:
	Dc.l	0,0,0,0,0
GpuX_Move_Pointer:
	Dc.l	0

Link_Params:
Weapon_Params:
Attrib_Params:
Move_Params:
Sort_Params:
Weapon_Params:
Move_Weapon_Params:
Draw_Aliens_Params:
	Dc.l	0,0,0,0,0
	Dc.l	0,0,0,0,0
	Dc.l	0,0,0,0,0
XDraw_Aliens_Shadow_Params:
	Dc.l	0,0,0,0,0
	Dc.l	0,0,0,0,0
	Dc.l	0,0,0,0,0
	Dc.l	0,0,0,0,0

Olist_Return:		Dc.l	0
Num_Objects_In_List:
Num_Objects_Added:	Dc.l	0
WEPCOL_POINTER:		Dc.l	0


