	MOVE.L 4(A7),A5
	MOVE.L #$100,-(SP)
	MOVE.L A5,-(SP)
	MOVE.L #$4A0000,-(SP)
	TRAP #1
	ADD.W #12,SP

	MOVE.W #2,-(SP)
	TRAP #14
	ADD.W #2,SP
	MOVE.L D0,OLD

	LEA CHARSET+2,A0
	LEA ORGPAL,A1
	MOVEQ.W #15,D0
WHACK	MOVE.W (A0)+,(A1)+
	DBRA D0,WHACK

	pea black
	move.w #$26,-(sp)
	trap #14
	add.w #6,sp

	CLR.W -(SP)
	PEA $40000
	PEA $40000
	MOVE.W #5,-(SP)
	TRAP #14
	ADD.W #12,SP

	PEA DRAWPIC
	MOVE.W #$26,-(SP)
	TRAP #14
	ADD.W #6,SP

	MOVE.L SP,SAVESTK
	
	MOVE.L #$2000,SP

	LEA $78000,A0
	MOVE.L #32000/4,D0
CLV	CLR.L (A0)+
	DBRA D0,CLV

	MOVE.L #RAS1,BG

	move.l #TEXT,TPNT
	move.l #CHARSET+34,STCHAR

*	BSR SAMP

	CLR.L -(SP)
	MOVE.W #$20,-(SP)
	TRAP #1
	ADD.W #6,SP
	MOVE.L D0,STACK

	MOVE.B $484,KB
	move.w #1,$4a6

	MOVE.B #$12,$FFFC02

	BSR SETUP

	LEA $40000+32000,A0
	MOVE.W #24000/4,D0
CLEAR	CLR.L (A0)+
	DBRA D0,CLEAR

	MOVE.B #$1A,$FFFC02

	LEA PIC+2,A0
	LEA $FF8240,A1
	MOVE.W #15,D0
COLS	MOVE.W (A0)+,(A1)+
	DBRA D0,COLS

	LEA RAS1,A0
	MOVE.W #199,D0
BACKPAL	MOVE.W $FF8240,(A0)+
	DBRA D0,BACKPAL

	MOVE.L $70,STORE
	MOVE.L #INTA,$70

	LEA PIC+34,A0
	MOVE.L A0,A3
	ADD.L #(199*160),A3
	LEA $40000,A1
	MOVE.L A1,A4
	ADD.L #(199*160),A4
	BSR FLIP_IN

	MOVE.L $118,S118
	MOVE.L #NEWKEY,$118

	MOVE.L #$78000,SCRLADD
	MOVE.L #$48000-480+32+(160*5)+2,INBORDER

	PEA HBL
	MOVE.W #1,-(SP)
	MOVE.W #%1000,-(SP)
	MOVE.W #1,-(SP)
	MOVE.W #31,-(SP)
	TRAP #14
	ADD.W #12,SP


	MOVE.W #5,-(SP)
	MOVE.W #26,-(SP)
	TRAP #14
	ADD.W #4,SP


WAIT	MOVE.W TIMER,D0
HANGIT	CMP.W TIMER,D0
	BEQ HANGIT
	BSR EQUAL
	BSR SCROLL
	MOVE.B KEY,D0
	CMP.B #$B,D0
	BEQ FLIPFREQ
TEST1	CMP.B #02,KEY
	BNE TEST2
SAME_1	MOVE.L #'TERM',D4
	BRA EXITA
TEST2	CMP.B #03,KEY
	BNE WAIT
	BRA SAME_1
EXITA	MOVE.W #8,-(SP)
	MOVE.W #26,-(SP)
	TRAP #14
	ADD.W #4,SP

	

	MOVE.L S118,$118

	MOVE.L STORE,$70

	LEA $78000,A0
	MOVE.W #32000/4,D0
CLRG	CLR.L (A0)+
	DBRA D0,CLRG

	CLR.W -(SP)
	MOVE.L OLD,-(SP)
	MOVE.L OLD,-(SP)
	MOVE.W #5,-(SP)
	TRAP #14
	ADD.W #12,SP


	MOVE.W #$0,$FF8240
	MOVE.W #$777,$FF825E

	MOVE.W #5,-(SP)
	MOVE.W #27,-(SP)
	TRAP #14
	ADD.W #4,SP

	bsr NOFX

	MOVE.B #$80,$FFFC02
	MOVE.W #$FFFF,D0
UYT	NOP
	DBRA D0,UYT
	MOVE.B #$1,$FFFC02

	MOVE.B KB,$484

	MOVE.L STACK,-(SP)
	MOVE.W #$20,-(SP)
	TRAP #1
	ADD.W #6,SP

	PEA SETFREQ
	MOVE.W #$26,-(SP)
	TRAP #14
	ADD.W #6,SP


	CMP.L #'TERM',D4
	BNE LOADIT

	MOVE.L SAVESTK,SP
	MOVE.W #$4C,-(SP)
	TRAP #1

LOADIT	MOVE.L SAVESTK,SP

	MOVE.W #0,-(SP)
	PEA -1
	PEA -1
	MOVE.W #5,-(SP)
	TRAP #14
	ADD.W #12,SP

	CLR.L -(SP)
	MOVE.L #CT,-(SP)
	MOVE.L D4,-(SP)
	CLR.W -(SP)
	MOVE.W #$4B,-(SP)
	TRAP #1

	MOVE.W #$4C,-(SP)
	TRAP #1

CT	DC.L 0
TEMPCOL	DC.L 0
ROT	DC.W 2

	EVEN

DELAY	CLR.W TIMER
	RTS

REG	DS.L 50

INTA	MOVEM.L D0-D7/A0-A6,-(SP)


	LEA PIC+2,A0
	LEA $FF8240,A1
	MOVEQ.W #15,D0
KJH	MOVE.W (A0)+,(A1)+
	DBRA D0,KJH

	ADDQ.W #1,HERTZWAIT
	CMP.W #50,HERTZWAIT
	BNE OK_NOT
	MOVE.W #49,HERTZWAIT
OK_NOT	SUBQ.W #1,ROT
	TST.W ROT
	BNE EXITFLA
	MOVE.W #2,ROT

	TST.W FFLAG
	BMI EXITFLA

	LEA CHARSET+2,A0
	MOVE.W 30(A0),D5
	MOVE.W 28(A0),30(A0)
	MOVE.W 26(A0),28(A0)
	MOVE.W 24(A0),26(A0)
	MOVE.W 22(A0),24(A0)
	MOVE.W 20(A0),22(A0)
	MOVE.W 18(A0),20(A0)
	MOVE.W 16(A0),18(A0)
	MOVE.W 14(A0),16(A0)
	MOVE.W 12(A0),14(A0)
	MOVE.W 10(A0),12(A0)
	MOVE.W 8(A0),10(A0)
	MOVE.W 6(A0),8(A0)
	MOVE.W 4(A0),6(A0)
	MOVE.W 2(A0),4(A0)
	MOVE.W D5,2(A0)


EXITFLA
	
	MOVE.L #RAS1,BG

	LEA $48000-(160*5),A0
	MOVE.W #160/4,D0
ERASE	CLR (A0)+
	DBRA D0,ERASE

	CLR.W LINE

	LEA ROTATE-2,A0
	MOVE.W (A0),SHIFTER
	MOVE.W #39,D0
UPQ	MOVE.W 2(A0),(A0)+
	DBRA D0,UPQ
	MOVE.W SHIFTER,(A0)

	LEA MUSIX,A0
	JSR $74(A0)

	ADD.W #1,TIMER

	MOVEM.L (SP)+,D0-D7/A0-A6
	RTE
SHIFTER		DC.W 0
HERTZWAIT	DC.W 15


*
* ROUTINE TO PLOT EQUALIZERS
*

VOL	DC.W 0

GRAF1	MOVE.W D0,VOL
	TST.W D0
	BEQ NOT_ON
	CMP.W #16,D0
	BLE OK_DRAW
	MOVE.W #16,D0
OK_DRAW
	MOVE.L (A5),(A6)
	MOVE.L 4(A5),4(A6)
	MOVE.L 160(A5),160(A6)
	MOVE.L 164(A5),164(A6)
	MOVE.L 320(A5),320(A6)
	MOVE.L 324(A5),324(A6)
	MOVE.L 480(A5),480(A6)
	MOVE.L 484(A5),484(A6)

	ADDQ.L #8,A6
	ADDQ.L #8,A3
	ADDQ.L #8,A5

	DBRA D0,OK_DRAW

NOT_ON	MOVE.W #16,D5
	SUB.W VOL,D5
	TST.W D5
	BMI OUT_GRAF

NOT_ON2	
	MOVE.L (A3),(A6)
	MOVE.L 4(A3),4(A6)
	MOVE.L 160(A3),160(A6)
	MOVE.L 164(A3),164(A6)
	MOVE.L 320(A3),320(A6)
	MOVE.L 324(A3),324(A6)
	MOVE.L 480(A3),480(A6)
	MOVE.L 484(A3),484(A6)

	ADDQ.L #8,A6
	ADDQ.L #8,A3

	DBRA D5,NOT_ON2

OUT_GRAF
	RTS

EQUAL	MOVEM.L D0-D7/A0-A6,-(SP)
	CLR.W D0
	MOVE.B #$8,$FF8800
	MOVE.B $FF8800,D0
	LEA PIC+34,A3
	LEA $40000,A6
	LEA CHARSET+34+(196*160),A5
	BSR GRAF1
NOT_ON_1
	CLR.W D0
	MOVE.B #$9,$FF8800
	MOVE.B $FF8800,D0
	LEA PIC+34+(160*5),A3
	LEA $40000+(160*5),A6
	LEA CHARSET+34+(196*160),A5
	BSR GRAF1
NOT_ON_2
	CLR.W D0
	MOVE.B #$A,$FF8800
	MOVE.B $FF8800,D0
	LEA PIC+34+(160*10),A3
	LEA $40000+(160*10),A6
	LEA CHARSET+34+(196*160),A5
	BSR GRAF1
NOT_ON_3
	MOVEM.L (SP)+,D0-D7/A0-A6
	RTS



NEWKEY	MOVE.B $FFFC02,KEY
	bclr.b #6,$fffffa11
	RTE
KEY	DC.W 0
S118	DC.L 0

ROCK	DC.B "ROCKROLL.LAW",00
	EVEN
DARIUS	DC.B "DARIUS.LAW",00
	EVEN

NOFX	move.l #$8080000,$ffff8800.w
	move.l #$9090000,$ffff8800.w
	move.l #$a0a0000,$ffff8800.w
	RTS

HBLREG	DS.L 4
BG	DC.L 0
SA0	DC.L 0

HBL	MOVE.L A0,SA0
	MOVE.L BG,A0
	MOVE.W (A0),$FF8240
	ADDQ.L #2,BG
	ADD.W #1,LINE
	CMP.W #199,LINE
	BNE OUTHBL

	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	NOP 
	CLR.B $FFFF820A

	LEA CHARSET+2,A0
	MOVE.W 2(A0),$FF8242
	MOVE.L 4(A0),$FF8244
	MOVE.L 8(A0),$FF8248
	MOVE.L 12(A0),$FF824C
	MOVE.L 16(A0),$FF8250
	MOVE.L 20(A0),$FF8254
	MOVE.L 24(A0),$FF8258
	MOVE.L 28(A0),$FF825C
	

	MOVE.B #2,$FFFF820A

OUTHBL	BCLR.B #0,$FFFA0F
	MOVE.L SA0,A0
	RTE

SCROLL	MOVEM.L D0-D7/A0-A6,-(SP)
	MOVE.W SPEED,D5
FASTER	MOVE.W SCRL,D0
	SUBQ.W #1,D0
	MOVE.W D0,SCRL
	TST.W D0
	BNE GO_SCRL
	MOVEQ.W #15,D0
	MOVE.W D0,SCRL
	MOVEQ.L #0,D1
	MOVE.L TPNT,A0
	ADDQ.L #1,TPNT
BACK	MOVE.B (A0),D1
	CMP.B #$FF,D1		; CHECK FOR LOOP
	BNE NEXT_CH
	MOVE.L #TEXT,TPNT
	MOVE.B #32,D1		; ASC FOR SPC
NEXT_CH	SUB.B #$20,D1
_P	CMP.B #$50,D1
	BLT _BSRK
	ASL D1
	ASL D1
	ASL D1
	ADD.L #5600,D1
	BRA OUT
_BSRK	CMP.B #$3C,D1
	BLT _H
	ASL D1
	ASL D1
	ASL D1
	ADD.L #4160,D1
	BRA OUT
_H	CMP.B #$28,D1
	BLT _4
	ASL D1
	ASL D1
	ASL D1
	ADD.L #2880,D1
	BRA OUT
_4	CMP.B #$14,D1
	BLT _SPC
	ASL D1
	ASL D1
	ASL D1
	ADD.L #1280,D1
	BRA OUT
_SPC	ASL D1
	ASL D1
	ASL D1
OUT	MOVE.L STCHAR,A5
	ADD.L D1,A5
	LEA CHBUFF(PC),A4
	MOVEQ.L #7,D7
FILLBUF	MOVE.L (A5)+,(A4)+
	MOVE.L (A5),(A4)+
	ADD.L #156,A5
	DBRA D7,FILLBUF
GO_SCRL	LEA CHBUFF(PC),A0
	MOVE.L SCRLADD,A2
	MOVEQ.L #7,D2
	ADDQ.L #8,A2
ROWS	ADD.L #152,A2
	MOVEQ.W #3,D1
PLANES	LSL.W (A0)+
	ROXL.W $98(A2)
	ROXL.W $90(A2)
	ROXL.W $88(A2)
	ROXL.W $80(A2)
	ROXL.W $78(A2)
	ROXL.W $70(A2)
	ROXL.W $68(A2)
	ROXL.W $60(A2)
	ROXL.W $58(A2)
	ROXL.W $50(A2)
	ROXL.W $48(A2)
	ROXL.W $40(A2)
	ROXL.W $38(A2)
	ROXL.W $30(A2)
	ROXL.W $28(A2)
	ROXL.W $20(A2)
	ROXL.W $18(A2)
	ROXL.W $10(A2)
	ROXL.W $8(A2)
	ROXL.W (A2)
	ADDQ.L #2,A2
	DBRA D1,PLANES
	DBRA D2,ROWS
	DBRA D5,FASTER
	LEA $78000,A0
	MOVE.L INBORDER,A1
	MOVE.L A1,A2
	ADD.L #160,A2
	MOVEQ.W #12,D0
TWICE	MOVE.W #(156/4),D2
HIGHT	MOVE.L (A0),(A1)+
	MOVE.L (A0)+,(A2)+
	DBRA D2,HIGHT
	ADD.L #160,A1
	ADD.L #160,A2
	DBRA D0,TWICE

	addq.w #1,count
OUTG	movem.l (sp)+,d0-d7/a0-a6
	RTS

FLIP_IN	MOVE.W #99,D3
ALL_SCR	MOVE.W #159,D4
ONE_LIN	MOVE.B (A0)+,(A1)+
	MOVE.B (A3)+,(A4)+
	DBRA D4,ONE_LIN
	ADD.W #160,A0
	ADD.W #160,A1
	SUB.W #480,A3
	SUB.W #480,A4
	MOVE.W TIMER,D4
WAITFOR	CMP.W TIMER,D4
	BEQ WAITFOR
	move.w TIMER,d4
waitfor	cmp.w TIMER,d4
	beq waitfor
	DBRA D3,ALL_SCR	
	RTS



PTR	dc.l $40000+(180*160)
count	dc.w 0
GOTO 	DC.L 0
TIMER	DC.W 0
LINE	DC.W 0
STORE	DC.L 0
STACK	DC.L 0
SPEED	DC.W 2
SCRL	DC.W 1
TPNT	DC.L 0
CHBUFF	DS.W 20*8
SCRLADD	DC.L $78000
STCHAR	DC.L 0




TEXT
	DC.B "   vwxyz{ 184 vwxyz{        ONE MEG FILES!     "
	DC.B "IMPORTANT!! - YOU CANNOT PLAY ROCKET RANGER WITHOUT "
	DC.B "FIRST OBTAINING A PRINT OUT OF THE DOC FILE ON THIS DISK!!       "
	DC.B "WHY?         "
	DC.B "WELL, SUPPOSE YOU WERE IN AMERICA AND YOU WANTED TO FLY TO "
	DC.B "THE ATLANTIC, YOU WOULD LOOK AT U.S.A. DOWN THE SIDE OF THE GRID AND ATLANTIC "
	DC.B "ALONG THE TOP AND COME UP WITH THE NUMBER 26. THIS IS THE AMOUNT OF FUEL "
	DC.B "YOU WOULD LOAD INTO YOUR BACKPACK. AS YOU CAN PROBABLY SEE, THE GAME "
	DC.B "WORKS OUT WHERE YOU WANT TO GO TO FROM YOUR CURRENT "
	DC.B "LOCATION AND THE AMOUNT OF FUEL YOU ENTERED.     "
	DC.B "I CAN STRONGLY RECOMEND HAVING AN ATLAS IN FRONT OF YOU IF YOU DON'T KNOW YOUR GEOGRAPHY!!    "   
	DC.B "THAT IS THE REASON FOR THE DOC FILE....        "
	DC.B "IF YOU THINK I JUST COPPED OUT WITH THE EASY OPTION, THEN THINK AGAIN!  I (abc) DON'T DO THIS!  "
	DC.B "CHECK OUT MY 'LAST CRUSADE' ON CD 124 AND COMPARE IT WITH THE OTHER LAME VERSIONS WITH "
	DC.B "THE PROTECTED LEVEL TWO STILL IN THEM!! "
	DC.B "         "
	DC.B "ALL CODING BY abc     "
	DC.B "DIGI-PIC BY SIMON     "
	DC.B "MUSIC BY CHRISPY NOODLE OF THE ghi (RIPPED FROM A ghi MENU)     "
	DC.B "FONT BY abc AND NEIL.E      "
	DC.B "GREETS GO TO:-   "
	DC.B "THE REST OF vwxyz{    "
	DC.B "def (WHERE ARE YOU?)   "
	DC.B "ghi (SORRY FOR RIPPING YOUR MUSIC, BUT IT IS SO GOOD!!)   "
	DC.B "mno (NICE CODING ON 168)   "
	DC.B "pqr (SPEEDY MENU DELIVERIES)   "
	DC.B "stu (HAVE A WARM CHRISTMAS!)   "
	DC.B "AND DACK (WITHOUT YOUR NEW TRAP THIS GAME WOULD NOT BE POSSIBLE!!), "
	DC.B "COL, BRI, MAL, BOB, NEIL.E, "
	DC.B "CHRIS.E, DEAN, MIKE, MARK, SIMON, "
	DC.B "MIKE.W, MERLIN, K.K.K., THE TEN (STRANGE NAME, GUYS!), "
	DC.B "THE REPLICANTS, TCB, TEX AND THE REST OF THE UNION, "
	DC.B "THE MEDWAY BOYS, AND ANYONE ELSE WHO IS WATCHING.......      "
	DC.B "WHY DON'T DACK OR MYSELF EVER GET GREETED?       "
	DC.B "HERE ARE SOME CODES FOR ROCK 'N' ROLL ON vwxyz{ 166    "
	DC.B "TYPE 'COUNTRY' TO ENTER THE MUSIC PLAYER MENU....      "
	DC.B "ENTER 'RAINBOW ARTS' TO ENTER THE CHEAT COMMAND LINE.    "
	DC.B "ALL THE LEVELS HAVE A NUMERIC CODE....  HERE'S TWO - YOU SHOULD BE ABLE TO "
	DC.B "WORK OUT THE REST FROM THEM.....     "
	DC.B "LEVEL 3:  03XX2010XX30       "
	DC.B "REPEAT..    "
	DC.B "LEVEL 3:  03XX2010XX30       "
	DC.B "AND....       "
	DC.B "LEVEL 24:  24XX2598XX42       "
	DC.B "REPEAT..    "
	DC.B "LEVEL 24:  24XX2598XX42       "
	DC.B "THERE...... HOPE THAT HELPS...   (I (abc) LOVE THIS GAME!!)         "
	DC.B "------------->  MESSAGE REPEATS  ------------->          "      

	DC.B $FF
	
	even

**  1  -  50
** -1  -  60

FFLAG	DC.W 1
FLIPFREQ	
	CMP.W #25,HERTZWAIT
	BLT WAIT
	NEG.W FFLAG
	CLR.W HERTZWAIT
	TST.W FFLAG
	BPL RETURN
	LEA ORGPAL,A0
	LEA CHARSET+2,A1
	MOVEQ.W #15,D0
RES	MOVE.W (A0)+,(A1)+
	DBRA D0,RES
RETURN	BRA WAIT
ORGPAL	DS.W 16

SETFREQ	TST.W FFLAG
	BPL _50HZ
	MOVE.B #%11111100,$FF820A
	RTS
_50HZ	MOVE.B #%11111110,$FF820A
	RTS

black	move.w #15,d0
	lea $ff8240,a0
ffff	clr.w (a0)+
	dbra d0,ffff
	rts
KB	DC.W 0
RAS1	DS.W 201
	DC.W $777
	DC.W $0
ROTATE
	DC.W $10,$20,$30,$40,$50,$60,$70
	DC.W $70,$60,$50,$40,$30,$20,$10

	DC.W $100,$200,$300,$400,$500,$600
	DC.W $600,$500,$400,$300,$200,$100
	
	DC.W $1,$2,$3,$4,$5,$6,$7
	DC.W $7,$6,$5,$4,$3,$2,$1



*SAMP	LEA SAMPLAY,A0
*	ADD.L #28,A0
*
*	MOVE.L #SAMPLE+12,2(A0)
*	MOVE.L #END,A3
*	MOVE.L #SAMPLE+12,A2
*	SUB.L A2,A3
*	MOVE.L A3,6(A0)
*	MOVE.L #4,10(A0)
*
*	JSR (A0)
*	
*	RTS

SETUP
	LEA MUSIX,A0
	JSR (A0)
	RTS


DRAWPIC	LEA PIC+34,A0
	LEA $50000,A1
	MOVE.L #32000/4,D0
TYU	MOVE.L (A0)+,(A1)+
	DBRA D0,TYU

	LEA PIC+2,A0
	LEA $FF8240,A1
	MOVE.W #15,D0
UYT2	MOVE.W (A0)+,(A1)+
	DBRA D0,UYT2

	RTS

*SAMPLAY
*	INCBIN "BASCODE.EXE"
*	EVEN
*SAMPLE	
*	INCBIN "batman.SAM"
*END	EVEN

PIC
	INCBIN "\MENU.184\GRISSOM.PI1"
	EVEN
CHARSET	
	INCBIN "\MENU.184\CHARSETY.PI1"
	EVEN
MUSIX	
	INCBIN "\MENU.184\JETSET.CN"
	EVEN
SAVESTK	DC.L 0
INBORDER	DC.L 0
OLD	DC.L 0


