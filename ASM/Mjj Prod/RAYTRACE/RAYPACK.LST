$m65536
'
win_w&=240
win_h&=150
win_s%=MUL(win_w&,win_h&)
'
off_w&=DIV(win_w&,2)
off_h&=win_h&
off_s%=MUL(off_w&,off_h&)
'
' -- PACK OFFSET-MAPS -------------------------------------
'
PRINT "Packing offset-maps..."
'
in1%=MALLOC(ADD(MUL(off_s%,3),18))
in2%=MALLOC(ADD(MUL(off_s%,3),18))
out%=MALLOC(ADD(off_s%,off_s%))
'
BLOAD "C:\DEMOS\RAYTRACE\REFLECT.TGA", in1%
BLOAD "C:\DEMOS\RAYTRACE\REFRACT.TGA", in2%
'
p_qrt% = SHR(off_s%,1)
p_add& = MUL(off_w&,3)
'
p_in1% = ADD(in1%,18)
p_in2% = ADD(in2%,18)
p_o1x% = out%
p_o1y% = ADD(p_o1x%,p_qrt%)
p_o2x% = ADD(p_o1y%,p_qrt%)
p_o2y% = ADD(p_o2x%,p_qrt%)
'
FOR cy& = 0 TO PRED(SHR(off_h&,1))
  ADD p_in2%, p_add&
  FOR cx& = 0 TO PRED(off_w&)
    x& = BYTE{p_in1%}
    ADD p_in1%, 2
    y& = BYTE{p_in1%}
    INC p_in1%
    BYTE{p_o1x%} = AND(x&,-4)
    INC p_o1x%
    BYTE{p_o1y%} = AND(y&,-4)
    INC p_o1y%
  NEXT cx&
  ADD p_in1%, p_add&
  FOR cx& = 0 TO PRED(off_w&)
    x& = BYTE{p_in2%}
    ADD p_in2%, 2
    y& = BYTE{p_in2%}
    INC p_in2%
    BYTE{p_o2x%} = AND(x&,-4)
    INC p_o2x%
    BYTE{p_o2y%} = AND(y&,-4)
    INC p_o2y%
  NEXT cx&
NEXT cy&
'
BSAVE "C:\DEMOS\RAYTRACE\DISTORT.BIN", out%, ADD(off_s%,off_s%)
'
~MFREE(in1%)
~MFREE(in2%)
~MFREE(out%)
'
' -- PACK LIGHT-MAP ---------------------------------------
'
PRINT "Packing light-map..."
'
inp%=MALLOC(ADD(MUL(win_s%,3),18))
out%=MALLOC(DIV(win_s%,2))
'
BLOAD "C:\DEMOS\RAYTRACE\LIGHT.TGA", inp%
'
p_wid&=DIV(win_w&,2)
p_inp%=ADD(inp%,18)
p_out%=out%
FOR y& = 0 TO PRED(win_h&)
  FOR x& = 0 TO PRED(p_wid&)
    la& = BYTE{p_inp%}
    ADD p_inp%, 3
    lb& = BYTE{p_inp%}
    ADD p_inp%, 3
    BYTE{p_out%} = OR(AND(la&,&HF0),SHR(lb&,4))
    INC p_out%
  NEXT x&
NEXT y&
'
BSAVE "C:\DEMOS\RAYTRACE\LIGHT.BIN", out%, DIV(win_s%,2)
'
~MFREE(inp%)
~MFREE(out%)
'
' -- PACK TEXTURE-MAP -------------------------------------
'
PRINT "Packing texture-map..."
'
inp%=MALLOC(32066)
out%=MALLOC(512)
'
BLOAD "C:\DEMOS\RAYTRACE\TEXTURE.PI1", inp%
'
p_inp%=ADD(inp%,34)
p_out%=out%
'
FOR y& = 0 TO 63
  CARD{p_out%} = CARD{p_inp%}
  ADD p_inp%, 8
  ADD p_out%, 2
  CARD{p_out%} = CARD{p_inp%}
  ADD p_inp%, 8
  ADD p_out%, 2
  CARD{p_out%} = CARD{p_inp%}
  ADD p_inp%, 8
  ADD p_out%, 2
  CARD{p_out%} = CARD{p_inp%}
  ADD p_inp%, 136
  ADD p_out%, 2
NEXT y&
'
BSAVE "C:\DEMOS\RAYTRACE\TEXTURE.BIN", out%, 512
'
' -- PACK PALETTE -----------------------------------------
'
PRINT "Packing palette..."
'
BLOAD "C:\DEMOS\RAYTRACE\PALETTE.PI1", inp%
BSAVE "C:\DEMOS\RAYTRACE\PALETTE.BIN", ADD(inp%,2), 32
'
~MFREE(inp%)
~MFREE(out%)
'
END
'

