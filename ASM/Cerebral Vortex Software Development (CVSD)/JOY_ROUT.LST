'
' Joypad Rout
'
' Hi to Simmonsunnyboy who give the first rout on the atari-forum
' Hi to Zorro2 (Mjjprod) : our atari-forum screen is nearly finished i hope !)
' Hi to Tobe (Never forget Scoobidou is good for you !! but warning some
'             cows are hidden !!!!)
'
' The only things you have to do is to call 'Test_joy_pad' rout
' in supervisor mode (Coz writing in some registers !). The rout
' will set or clear the Boolean var for the differents keys
' and setting the 'pad' var. All the var for the Jagpad in port a
' finished with 0, the var for the jagpad in port b with '1' !!!
'
'
'     Sorry, this code is a 'Tchetchene code' (French expression !)
'     don't blame me if you don't understand anything !!!
'
'                             GT Turbo(Cerebral Vortex)
'
' Supervisor mode needed !!
super%=GEMDOS(32,L:0)               ! supervisor mode
'
ON BREAK GOSUB arret
ON ERROR GOSUB error
DO
  @test_joy_pad
  '
  PRINT AT(1,1);"Joypad 0, port A"
  '
  PRINT AT(1,3);"Pad    : ";pad_0&;" ";
  '
  PRINT AT(1,4);"Pause  : ";pause_0!;" "
  PRINT AT(1,5);"Option : ";option_0!;" "
  '
  PRINT AT(1,7);"Fire a : ";fire_a_0!;" "
  PRINT AT(1,8);"Fire b : ";fire_b_0!;" "
  PRINT AT(1,9);"Fire c : ";fire_c_0!;" "
  '
  PRINT AT(1,16);"* : ";star_0!;" "
  PRINT AT(1,15);"7 : ";sept_0!;" "
  PRINT AT(1,14);"4 : ";quatre_0!;" "
  PRINT AT(1,13);"1 : ";un_0!;" "
  '
  PRINT AT(7,16);"0 : ";zero_0!;" "
  PRINT AT(7,15);"8 : ";huit_0!;" "
  PRINT AT(7,14);"5 : ";cinq_0!;" "
  PRINT AT(7,13);"2 : ";deux_0!;" "
  '
  PRINT AT(14,16);"# : ";diese_0!;" "
  PRINT AT(14,15);"9 : ";neuf_0!;" "
  PRINT AT(14,14);"6 : ";six_0!;" "
  PRINT AT(14,13);"3 : ";trois_0!;" "
  '
  PRINT AT(20,1);"Joypad 1, port B"
  '
  PRINT AT(20,3);"Pad    : ";pad_1&;" ";
  '
  PRINT AT(20,4);"Pause  : ";pause_1!;" "
  PRINT AT(20,5);"Option : ";option_1!;" "
  '
  PRINT AT(20,7);"Fire a : ";fire_a_1!;" "
  PRINT AT(20,8);"Fire b : ";fire_b_1!;" "
  PRINT AT(20,9);"Fire c : ";fire_c_1!;" "
  '
  PRINT AT(18,23);"* : ";star_1!;" "
  PRINT AT(18,22);"7 : ";sept_1!;" "
  PRINT AT(18,21);"4 : ";quatre_1!;" "
  PRINT AT(18,20);"1 : ";un_1!;" "
  '
  PRINT AT(26,23);"0 : ";zero_1!;" "
  PRINT AT(26,22);"8 : ";huit_1!;" "
  PRINT AT(26,21);"5 : ";cinq_1!;" "
  PRINT AT(26,20);"2 : ";deux_1!;" "
  '
  PRINT AT(33,23);"# : ";diese_1!;" "
  PRINT AT(33,22);"9 : ";neuf_1!;" "
  PRINT AT(33,21);"6 : ";six_1!;" "
  PRINT AT(33,20);"3 : ";trois_1!;" "
  '
LOOP
> PROCEDURE arret
  ~GEMDOS(32,L:super%)
  EDIT
RETURN
> PROCEDURE error
  ~GEMDOS(32,L:super%)
  EDIT
RETURN
> PROCEDURE test_joy_pad
  '
  reg_01%=&HFFFF9201
  reg_02%=&HFFFF9202
  reg_03%=&HFFFF9203
  '
  BYTE{reg_03%}=&X11111110
  a&=NOT (BYTE{reg_01%})
  pause_0!=AND(a&,&H1)
  fire_a_0!=AND(a&,&H2)
  pad_0&=NOT (BYTE{reg_02%})
  pad_0&=pad_0& AND &HF
  '
  BYTE{reg_03%}=&X11111101
  a&=NOT (BYTE{reg_01%})
  fire_b_0!=AND(a&,&H2)
  '
  BYTE{reg_03%}=&X11111011
  a&=NOT (BYTE{reg_01%})
  fire_c_0!=AND(a&,&H2)
  '
  BYTE{reg_03%}=&X11111101
  a&=NOT (BYTE{reg_02%})
  star_0!=AND(a&,&H1)
  sept_0!=AND(a&,&H2)
  quatre_0!=AND(a&,&H4)
  un_0!=AND(a&,&H8)
  '
  BYTE{reg_03%}=&X11111011
  a&=NOT (BYTE{reg_02%})
  zero_0!=AND(a&,&H1)
  huit_0!=AND(a&,&H2)
  cinq_0!=AND(a&,&H4)
  deux_0!=AND(a&,&H8)
  '
  BYTE{reg_03%}=&X11110111
  a&=NOT (BYTE{reg_02%})
  diese_0!=AND(a&,&H1)
  neuf_0!=AND(a&,&H2)
  six_0!=AND(a&,&H4)
  trois_0!=AND(a&,&H8)
  '
  BYTE{reg_03%}=&X11110111
  b&=NOT (BYTE{reg_01%})
  option_0!=AND(b&,&H2)
  '
  ' Joypad 1
  '
  '
  BYTE{reg_03%}=&X11101111
  a&=NOT (BYTE{reg_01%})
  pause_1!=AND(a&,&H4)
  fire_a_1!=AND(a&,&H8)
  '
  a&=NOT (BYTE{reg_02%})
  pad_1%=SHR(a&,4)
  pad_1&=AND(pad_1%,&HF)
  '
  BYTE{reg_03%}=&X11011111
  a&=NOT (BYTE{reg_01%})
  fire_b_1!=AND(a&,&H8)
  '
  BYTE{reg_03%}=&X10111111
  a&=NOT (BYTE{reg_01%})
  fire_c_1!=AND(a&,&H8)
  '
  BYTE{reg_03%}=&X11011111
  a&=NOT (BYTE{reg_02%})
  star_1!=AND(a&,16)
  sept_1!=AND(a&,32)
  quatre_1!=AND(a&,64)
  un_1!=AND(a&,128)
  '
  BYTE{reg_03%}=&X10111111
  a&=NOT (BYTE{reg_02%})
  zero_1!=AND(a&,16)
  huit_1!=AND(a&,32)
  cinq_1!=AND(a&,64)
  deux_1!=AND(a&,128)
  '
  BYTE{reg_03%}=&X1111111
  a&=NOT (BYTE{reg_02%})
  diese_1!=AND(a&,16)
  neuf_1!=AND(a&,32)
  six_1!=AND(a&,64)
  trois_1!=AND(a&,128)
  '
  BYTE{reg_03%}=&X1111111
  a&=NOT (BYTE{reg_01%})
  option_1!=AND(a&,8)
  '
RETURN
