********************************************
* 	THE LANDSCAPE SCROLLINE		   *
*		NEW FONT		   *
*	STARFIELD (IMPROVED) ADDED         *
*	MAD MAX MUSIC ALSO ADDED           *
* 	PLUS THE 3D SPIN SIGN!!!	   *
*	WITH ITS VARIABLE WAVES		   *
*	1.2 IMPROVED ROUTINE		   *
*	CODED SEPTEMBER 1989		   *
********************************************
*YOU WILL FIND ALL THE NECESSARY ROUTINES ON THE DISK TO CREATE THIS SCREEN
*EXCEPT FOR THAT REQUIRED TO MAKE UP THE STAR DATA( SEE ROUTINE FOR EXPLANATION
*
* THE SCROLLINE IS ACTUALLY DONE USING RASTERS A PIECE OF VERTICALY STRIPED GRAPHIX IN 9 DIFFERENT COLORS
* IS DRAWN ON THE SCREEN AND THEN THE PALETTE IS SET AT REGULAR INTERVALS TO CREATE THE FONT
* THE GRAPHIX FOR THE ENLARGING SIGNS IS SIMPLY PRE DRAWN AND I THINK THAT IS ABOUT IT

	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP 	#1
	ADD.L	#6,SP

	MOVE.L D0,OLDSSP	
	LEA MYSTACK,A7
	MOVE.L $FFFF8200.W,OLDPHYS
	MOVEQ #$12,D0
	BSR IKBD
	MOVEQ #$1A,D0
	BSR IKBD
	LEA $FFFF8240.W,A0
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+

	LEA GRAPHIX,A0			THIS ROUTINE ERASES SCREEN
	LEA $68000,A1			
	MOVE.W #8192*3-1,D0			
DRAWL1	CLR.L (A1)+
	DBF D0,DRAWL1
	LEA $7be80,A1			THIS DRAWS ON GRAPHICS
	LEA $6BE80,A2
	LEA 160(A1),A1
	LEA 160(A2),A2
	MOVE.W #4000,D0
DRAWL2	MOVE.L (A0),(A1)+
	MOVE.L (A0)+,(A2)+
	DBF D0,DRAWL2

	LEA PALETTE,A0			FILL UP PALETTE WITH 
	MOVE.L #PALEEND,D0		BLANK CHARACTERS
LOOP2	CLR.W (A0)+
	CMP.L D0,A0
	BNE.S LOOP2
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+
	CLR.L (A0)+

	BSR PREPWAVES
	BSR SPRITEPRESHIFT
	BSR MAKEUPSTARS

	MOVEQ #1,D0
	JSR MUSIC			INITIALIZE MUSIC

	MOVE.L #$78000,D0		SET PHYSBASE
	LSR.W #8,D0
	MOVE.L D0,$FF8200

	MOVE.L $118.W,OLDKEY
	BCLR #6,$FFFFFA09.W
	MOVE.L #KEYVECTOR,$118.W
	BSET #6,$FFFFFA09.W
	MOVE.B $FFFFFA09.W,OLDIEB		SET UP THE MFP
	MOVE.B $FFFFFA07.W,OLDIEA
	MOVE.B $FFFFFA13.W,OLDIMA
	MOVE.B $FFFFFA21.W,OLD21
	MOVE.B $FFFFFA1B.W,OLD1B
	AND.B #$DF,$FFFFFA09.W	
	AND.B #$FE,$FFFFFA07.W
	MOVE.L $120.W,OLDHBL
	MOVE.L #HBL,$120.W		NEW TIMER B
	OR.B #1,$FFFFFA07.W
	OR.B #1,$FFFFFA13.W
	MOVE.L $70.W,OLDVBL
	MOVE.L #VBL,$70.W			NEW VBL

HERE	MOVE.W IND,D0
LOOPG	CMP.W IND,D0
	BEQ.S LOOPG
	MOVE.L #$78000,PHYSBASE
	MOVE.L #$68000,LOGBASE
	MOVE.L #OLD1,OLDSPRITE
	MOVE.L PHYSBASE,D0
	LSR.W #8,D0
	MOVE.L D0,$FFFF8200.W
	MOVE.W IND,D0
LOOPH	CMP.W IND,D0
	BEQ.S LOOPH
	MOVE.L #$68000,PHYSBASE
	MOVE.L #$78000,LOGBASE
	MOVE.L #OLD2,OLDSPRITE
	MOVE.L PHYSBASE,D0
	LSR.W #8,D0
	MOVE.L D0,$FFFF8200.W
	BRA.S HERE

***********************************
* THE SCROLL ROUTINE
***********************************

SCROLL	
	ADD.L #16,PALPOINT		MOVE DOWN FONT
	MOVE.L PALPOINT,A2		POINTER
	CMP.L #PALEEND,A2		END OF PALETTE?
	BLT.S CONT
	LEA PALETTE,A2
CONT
	ADD.L #16,DRAWPOINT		MOVE DOWN DRAW POINTER
	CMP.L #PALEEND,DRAWPOINT	(POINTER TO PALETTE FOR TIMERB)
	BLT.S CONTON			RUNS 1 PALETTE AHEAD OF DRAWER
	MOVE.L #PALETTE,DRAWPOINT
CONTON			
	MOVE.L A2,PALPOINT		CHECK FOR END OF LETTER LINE
	SUB.W #1,LETEND			CHECK FOR END OF LETTER
	BNE.S NEXTLETTERLINE
	MOVE.L SCROLLPOINT,A3		GET NEXT SCROLL DATA
	MOVEQ #0,D0
BACK	MOVE.B (A3)+,D0
	CMP.L #$FF,D0			END OF SCROLL
	BNE.S CONT1
	LEA SCROLLINE,A3	
	BRA.S BACK
CONT1	MOVE.L A3,SCROLLPOINT		
	SUB.B #32,D0			GET POSITION IN FONT
	MULU #160,D0
	LEA FONT,A3
	ADD.L D0,A3			
	MOVE.W #10,LETEND		8 LINES PER LETTER
	MOVE.L A3,LETPOINT		POINTER TO LETTER DATA

CONTLETTER
	MOVE.L LETPOINT,A3		DRAW LETTER
	MOVE.L PALPOINT,A2
	MOVEM.L (A3)+,D0-D3
	MOVEM.L D0-D3,(A2)
	RTS

NEXTLETTERLINE
	MOVE.L LETPOINT,A3		GET NEXT LINE AND DRAW
	LEA 16(A3),A3
	MOVE.L A3,LETPOINT
	MOVE.L PALPOINT,A2
	MOVEM.L (A3)+,D0-D3
	MOVEM.L D0-D3,(A2)
	MOVE.W #4,LETIND
	RTS
		
KEYVECTOR
	MOVE.W D2,-(SP)		THE KEYVECTOR ROUTINE
	MOVE.B $FFFFFC00.W,D2
	BTST #7,D2
	BEQ.S	VECTOREND
	MOVE.B $FFFFFC02.W,D2
WACIADN	MOVE.B D2,KBUFF
WACIAS	BCLR #6,$FFFFFA11.W
VECTOREND	MOVE.W (SP)+,D2
	RTE

	
KBUFF	DS.W 1
OLDKEY	DC.L 0

HBL	
	MOVE.L (A0)+,(A1)+		CHANGE PALETTE
	MOVE.L (A0)+,(A1)+		
	MOVE.L (A0)+,(A1)+	
	MOVE.L (A0)+,(A1)+	
	MOVE.L (A0)+,(A1)+
	LEA -20(A1),A1
	CLR.B $FFFFFA1B.W
	MOVE.B (A6)+,$FFFFFA21.W	DATA FOR TIMER B CONTROL
	MOVE.B #8,$FFFFFA1B.W
	BCLR #0,$FFFFFA0F.W
	RTE
HBL1	
	LEA LANDPAL,A0			INITIAL HBL
	LEA HBLTAB1,A6			DATA FOR TIMER B
	MOVE.L #HBL,$120.W		NEXT HBL
	CLR.B $FFFFFA1B.W
	MOVE.B (A6)+,$FFFFFA21.W		CONTROL
	MOVE.B #8,$FFFFFA1B.W
	BCLR #0,$FFFFFA0F.W
	RTE

VBL		
	ADDQ.W #1,IND
	JSR MUSIC+8			PLAY MUSIC
	MOVE.L #HBL1,$120.W		THE VBL
	CLR.B $FFFFFA1B.W		SET UP HBL
	MOVE.B #99,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	LEA $FFFF8240.W,A1		COLORS IN COLOR1
	MOVE.L #$00000677,$FFFF8240.W	DO COLORS
	MOVE.L #$05550667,$FFFF8244.W
	MOVE.L #$03330057,$FFFF8248.W
	MOVE.L #$04230035,$FFFF824C.W
	MOVE.L #$02220046,$FFFF8250.W
	MOVE.L #$04450600,$FFFF8254.W
	MOVE.L #$07000500,$FFFF8258.W
	MOVE.L #$04000300,$FFFF825C.W
	MOVEM.L D0-D7/A2-A5,-(SP)	VBL
	BSR ERASESUB			ERASE STARS SUBROUTINE
	BSR ANIMATE			DO TLB SIGN
	LEA $FFFF8240.W,A1
	BSR SCROLL			DO SCROLL
	BSR DO_GRAPHIX			SORT OUT JOYS POS OF SCROLL
	BSR STARSSUB			DRAW STARS ROUTINE
	BSR DOSPRITE
	MOVEM.L (SP)+,D0-D7/A2-A5
	RTE

IND	DC.W 1
***************************** STAR ROUTINE ************************	
* THE STAR ROUTINE IS SIMPLICITY ITSELF TO MAKE IT VERY FAST THE
* ROUTINE SIMPLY PLOTS DATA TO THE SCREEN FROM A PREVIOUSLY WORKED OUT 
* TABLE. THE	DATA SHOULD CONSIST OF A SCREEN OFFSET ADDRESS AND
* AN NUMBER BETWEEN 0 AND 15 FOR THE BIT TO BE SET.
* AS THE DESIGN OF A STARFIELD IS A PERSONAL THING SO IT IS UP TO YOU 
* TO CONSTRUCT THE DAT AS YOU SO WISH.


****ERASE OLD STARS
ERASESUB
	MOVE.L OLDSPRITE,A4	ERASE THE SPRITES
	MOVEQ.W #9,D7
CC_LOOP
	MOVE.L (A4)+,A3
	REPT 8
	MOVEM.L (A4)+,D0-D6/A5
	MOVEM.L D0-D3,(A3)
	LEA 160(A3),A3
	MOVEM.L D4-D6/A5,(A3)
	LEA 160(A3),A3
	ENDR
	DBF D7,CC_LOOP
	CMP.L #$78000,LOGBASE
	BNE.S ALTEND
* WHICH SCREEN?
	MOVE.L #OLDSTARS,OLDSTARPOINT
	BRA.S COYTU
ALTEND
	MOVE.L #OLDSTARS1,OLDSTARPOINT
COYTU

	MOVE.L OLDSTARPOINT,A2		ERASE STARS ROUTINE
	MOVE.L LOGBASE,A3
	MOVEQ #0,D1
	REPT 100			* 100 STARS!!
	MOVE.W (A2)+,D2			GET POSITION
	MOVE.W D1,(A3,D2.W)		WIPE EM OUT
	ENDR
	RTS

STARSSUB
	LEA STARS(PC),A2		PLOT THE STARS
	MOVE.L LOGBASE,A3
	MOVE.L OLDSTARPOINT(PC),A5
	MOVE.W #99,D0
	MOVE.W #$FFFF,D1
DRAWLOOP
	MOVE.L (A2)+,A4
	MOVE.W (A4)+,D2
	CMP.W D1,D2			END OF TRACK IF -1 THEN GET NEW DATA TRACK
	BNE.S CONTB
	MOVE.L (A2),-(A2)
	BRA.S DRAWLOOP
CONTB
	MOVE.W (A4)+,D3
	MOVE.L A4,-(A2)
	LEA 8(A2),A2
	TST.W (A3,D3.W)
	BNE.S BYPASSDRAW
	OR.W D2,(A3,D3.W)		PLOT ACCORDING TO TABLE
BYPASSDRAW
	MOVE.W D3,(A5)+
	DBF D0,DRAWLOOP
	RTS

MAKEUPSTARS
	LEA STARS(PC),A0
	MOVEQ #0,D0
	MOVE.W #84,D1
CVLOOP
	MOVE.L (A0),A1
	ADD.L D0,A1
	MOVE.L A1,(A0)
	LEA 8(A0),A0
	ADD.L #4,D0
	DBF D1,CVLOOP

	MOVEQ #0,D0
	MOVE.W #14,D1
CVLOOP1
	MOVE.L (A0),A1
	ADD.L D0,A1
	MOVE.L A1,(A0)
	LEA 8(A0),A0
	ADD.L #24,D0
	DBF D1,CVLOOP1
	RTS

OLDSTARPOINT	DC.L 0

OLDSTARS	
	REPT 100
	DC.W 0
	ENDR

OLDSTARS1
	REPT 100
	DC.W 0
	ENDR

STARS	
ADDIT	SET 0
	REPT 100
	DC.L DEFSTARS+ADDIT,DEFSTARS+ADDIT
ADDIT	SET ADDIT+360
	ENDR
	
**********************************
*	GRAPHIX MANIPULATION ROUTINES
* SCROLL IS MADE TO MOVE LEFT AND RIGHT
* BY REARRANGING SCROLL DATA INTO
* DIFFERENT COLUMNS. THE FOLLOWING 9 
* ROUTINES DO THAT.
**********************************

DO_GRAPHIX		
	MOVEQ #0,D0	
	MOVE.W POSIND,D0	GET POS IN TABLE
	MOVE.L #PALEEND,D3
	LSL.W #2,D0
	LEA ROUTTAB,A2		LIST OF 9 ROUTINES
	ADD.L D0,A2		GET POS
	MOVE.L (A2),A2		MOVE INTO A2
	JMP (A2)		JUMP TO IT

LAND1	MOVE.L DRAWPOINT,A2
	LEA BACKPAL,A4
	LEA LANDPAL,A3
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVEQ #43,D1
LLOOP1	MOVE.W (A2)+,D2
	MOVE.W (A5),(A3)
	TST.W D2
	BNE.S LL1
	MOVE.W (A4),(A3)
LL1	MOVE.W (A2)+,D2
	MOVE.W (A5),2(A3)
	TST.W D2
	BNE.S LL2
	MOVE.W (A4),2(A3)
LL2
	MOVE.W (A2)+,D2
	MOVE.W (A5),4(A3)
	TST.W D2
	BNE.S LL3
	MOVE.W (A4),4(A3)
LL3
	MOVE.W (A2)+,D2
	MOVE.W (A5),6(A3)
	TST.W D2
	BNE.S LL4
	MOVE.W (A4),6(A3)
LL4
	MOVE.W (A2)+,D2
	MOVE.W (A5),8(A3)
	TST.W D2
	BNE.S LL5
	MOVE.W (A4),8(A3)
LL5
	MOVE.W (A2)+,D2
	MOVE.W (A5),10(A3)
	TST.W D2
	BNE.S LL6
	MOVE.W (A4),10(A3)
LL6
	MOVE.W (A2)+,D2
	MOVE.W (A5),12(A3)
	TST.W D2
	BNE.S LL7
	MOVE.W (A4),12(A3)
LL7	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,14(A3)
	TST.W D2
	BNE.S LL8
	MOVE.W (A4),14(A3)
LL8
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,16(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT1
	LEA PALETTE,A2
CONNT1
	DBF D1,LLOOP1
	BRA JOY

LAND2	MOVE.L DRAWPOINT,A2
	LEA BACKPAL,A4
	LEA LANDPAL,A3
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVE.W #43,D1
LLOOP2
	MOVE.W (A2)+,D2
	MOVE.W (A5),2(A3)
	TST.W D2
	BNE.S LL21
	MOVE.W (A4),2(A3)
LL21	MOVE.W (A2)+,D2
	MOVE.W (A5),4(A3)
	TST.W D2
	BNE.S LL22
	MOVE.W (A4),4(A3)
LL22
	MOVE.W (A2)+,D2
	MOVE.W (A5),6(A3)
	TST.W D2
	BNE.S LL23
	MOVE.W (A4),6(A3)
LL23
	MOVE.W (A2)+,D2
	MOVE.W (A5),8(A3)
	TST.W D2
	BNE.S LL24
	MOVE.W (A4),8(A3)
LL24
	MOVE.W (A2)+,D2
	MOVE.W (A5),10(A3)
	TST.W D2
	BNE.S LL25
	MOVE.W (A4),10(A3)
LL25
	MOVE.W (A2)+,D2
	MOVE.W (A5),12(A3)
	TST.W D2
	BNE.S LL26
	MOVE.W (A4),12(A3)
LL26
	MOVE.W (A2)+,D2
	MOVE.W (A5),14(A3)
	TST.W D2
	BNE.S LL27
	MOVE.W (A4),14(A3)
LL27	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,16(A3)
	TST.W D2
	BNE.S LL28
	MOVE.W (A4),16(A3)
LL28
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT2
	LEA PALETTE,A2
CONNT2
	DBF D1,LLOOP2
	BRA JOY

LAND3	MOVE.L DRAWPOINT,A2
	LEA BACKPAL,A4
	LEA LANDPAL,A3
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVE.W #43,D1
LLOOP3
	MOVE.W (A2)+,D2
	MOVE.W (A5),4(A3)
	TST.W D2
	BNE.S LL31
	MOVE.W (A4),4(A3)
LL31	MOVE.W (A2)+,D2
	MOVE.W (A5),6(A3)
	TST.W D2
	BNE.S LL32
	MOVE.W (A4),6(A3)
LL32
	MOVE.W (A2)+,D2
	MOVE.W (A5),8(A3)
	TST.W D2
	BNE.S LL33
	MOVE.W (A4),8(A3)
LL33
	MOVE.W (A2)+,D2
	MOVE.W (A5),10(A3)
	TST.W D2
	BNE.S LL34
	MOVE.W (A4),10(A3)
LL34
	MOVE.W (A2)+,D2
	MOVE.W (A5),12(A3)
	TST.W D2
	BNE.S LL35
	MOVE.W (A4),12(A3)
LL35
	MOVE.W (A2)+,D2
	MOVE.W (A5),14(A3)
	TST.W D2
	BNE.S LL36
	MOVE.W (A4),14(A3)
LL36
	MOVE.W (A2)+,D2
	MOVE.W (A5),16(A3)
	TST.W D2
	BNE.S LL37
	MOVE.W (A4),16(A3)
LL37	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,(A3)
	TST.W D2
	BNE.S LL38
	MOVE.W (A4),(A3)
LL38
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,2(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT3
	LEA PALETTE,A2
CONNT3
	DBF D1,LLOOP3
	BRA JOY

LAND4	MOVE.L DRAWPOINT,A2
	LEA BACKPAL,A4
	LEA LANDPAL,A3
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVE.W #43,D1
LLOOP4
	MOVE.W (A2)+,D2
	MOVE.W (A5),6(A3)
	TST.W D2
	BNE.S LL41
	MOVE.W (A4),6(A3)
LL41	MOVE.W (A2)+,D2
	MOVE.W (A5),8(A3)
	TST.W D2
	BNE.S LL42
	MOVE.W (A4),8(A3)
LL42
	MOVE.W (A2)+,D2
	MOVE.W (A5),10(A3)
	TST.W D2
	BNE.S LL43
	MOVE.W (A4),10(A3)
LL43
	MOVE.W (A2)+,D2
	MOVE.W (A5),12(A3)
	TST.W D2
	BNE.S LL44
	MOVE.W (A4),12(A3)
LL44
	MOVE.W (A2)+,D2
	MOVE.W (A5),14(A3)
	TST.W D2
	BNE.S LL45
	MOVE.W (A4),14(A3)
LL45
	MOVE.W (A2)+,D2
	MOVE.W (A5),16(A3)
	TST.W D2
	BNE.S LL46
	MOVE.W (A4),16(A3)
LL46
	MOVE.W (A2)+,D2
	MOVE.W (A5),(A3)
	TST.W D2
	BNE.S LL47
	MOVE.W (A4),(A3)
LL47	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,2(A3)
	TST.W D2
	BNE.S LL48
	MOVE.W (A4),2(A3)
LL48
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,4(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT4
	LEA PALETTE,A2
CONNT4
	DBF D1,LLOOP4
	BRA JOY

LAND5	MOVE.L DRAWPOINT,A2
	LEA BACKPAL,A4
	LEA LANDPAL,A3
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVE.W #43,D1
LLOOP5
	MOVE.W (A2)+,D2
	MOVE.W (A5),8(A3)
	TST.W D2
	BNE.S LL51
	MOVE.W (A4),8(A3)
LL51	MOVE.W (A2)+,D2
	MOVE.W (A5),10(A3)
	TST.W D2
	BNE.S LL52
	MOVE.W (A4),10(A3)
LL52
	MOVE.W (A2)+,D2
	MOVE.W (A5),12(A3)
	TST.W D2
	BNE.S LL53
	MOVE.W (A4),12(A3)
LL53
	MOVE.W (A2)+,D2
	MOVE.W (A5),14(A3)
	TST.W D2
	BNE.S LL54
	MOVE.W (A4),14(A3)
LL54
	MOVE.W (A2)+,D2
	MOVE.W (A5),16(A3)
	TST.W D2
	BNE.S LL55
	MOVE.W (A4),16(A3)
LL55
	MOVE.W (A2)+,D2
	MOVE.W (A5),(A3)
	TST.W D2
	BNE.S LL56
	MOVE.W (A4),(A3)
LL56
	MOVE.W (A2)+,D2
	MOVE.W (A5),2(A3)
	TST.W D2
	BNE.S LL57
	MOVE.W (A4),2(A3)
LL57	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,4(A3)
	TST.W D2
	BNE.S LL58
	MOVE.W (A4),4(A3)
LL58
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,6(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT5
	LEA PALETTE,A2
CONNT5
	DBF D1,LLOOP5
	BRA JOY

LAND6	MOVE.L DRAWPOINT,A2
	LEA BACKPAL,A4
	LEA LANDPAL,A3
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVE.W #43,D1
LLOOP6
	MOVE.W (A2)+,D2
	MOVE.W (A5),10(A3)
	TST.W D2
	BNE.S LL61
	MOVE.W (A4),10(A3)
LL61	MOVE.W (A2)+,D2
	MOVE.W (A5),12(A3)
	TST.W D2
	BNE.S LL62
	MOVE.W (A4),12(A3)
LL62
	MOVE.W (A2)+,D2
	MOVE.W (A5),14(A3)
	TST.W D2
	BNE.S LL63
	MOVE.W (A4),14(A3)
LL63
	MOVE.W (A2)+,D2
	MOVE.W (A5),16(A3)
	TST.W D2
	BNE.S LL64
	MOVE.W (A4),16(A3)
LL64
	MOVE.W (A2)+,D2
	MOVE.W (A5),(A3)
	TST.W D2
	BNE.S LL65
	MOVE.W (A4),(A3)
LL65
	MOVE.W (A2)+,D2
	MOVE.W (A5),2(A3)
	TST.W D2
	BNE.S LL66
	MOVE.W (A4),2(A3)
LL66
	MOVE.W (A2)+,D2
	MOVE.W (A5),4(A3)
	TST.W D2
	BNE.S LL67
	MOVE.W (A4),4(A3)
LL67	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,6(A3)
	TST.W D2
	BNE.S LL68
	MOVE.W (A4),6(A3)
LL68
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,8(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT6
	LEA PALETTE,A2
CONNT6
	DBF D1,LLOOP6
	BRA JOY

LAND7	MOVE.L DRAWPOINT,A2
	LEA BACKPAL,A4
	LEA LANDPAL,A3
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVE.W #43,D1
LLOOP7
	MOVE.W (A2)+,D2
	MOVE.W (A5),12(A3)
	TST.W D2
	BNE.S LL71
	MOVE.W (A4),12(A3)
LL71	MOVE.W (A2)+,D2
	MOVE.W (A5),14(A3)
	TST.W D2
	BNE.S LL72
	MOVE.W (A4),14(A3)
LL72
	MOVE.W (A2)+,D2
	MOVE.W (A5),16(A3)
	TST.W D2
	BNE.S LL73
	MOVE.W (A4),16(A3)
LL73
	MOVE.W (A2)+,D2
	MOVE.W (A5),(A3)
	TST.W D2
	BNE.S LL74
	MOVE.W (A4),(A3)
LL74
	MOVE.W (A2)+,D2
	MOVE.W (A5),2(A3)
	TST.W D2
	BNE.S LL75
	MOVE.W (A4),2(A3)
LL75
	MOVE.W (A2)+,D2
	MOVE.W (A5),4(A3)
	TST.W D2
	BNE.S LL76
	MOVE.W (A4),4(A3)
LL76
	MOVE.W (A2)+,D2
	MOVE.W (A5),6(A3)
	TST.W D2
	BNE.S LL77
	MOVE.W (A4),6(A3)
LL77	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,8(A3)
	TST.W D2
	BNE.S LL78
	MOVE.W (A4),8(A3)
LL78
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,10(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT7
	LEA PALETTE,A2
CONNT7
	DBF D1,LLOOP7
	BRA JOY

LAND8	MOVE.L DRAWPOINT,A2
	LEA LANDPAL,A3
	LEA BACKPAL,A4
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVE.W #43,D1
LLOOP8
	MOVE.W (A2)+,D2
	MOVE.W (A5),14(A3)
	TST.W D2
	BNE.S LL81
	MOVE.W (A4),14(A3)
LL81	MOVE.W (A2)+,D2
	MOVE.W (A5),16(A3)
	TST.W D2
	BNE.S LL82
	MOVE.W (A4),16(A3)
LL82
	MOVE.W (A2)+,D2
	MOVE.W (A5),(A3)
	TST.W D2
	BNE.S LL83
	MOVE.W (A4),(A3)
LL83
	MOVE.W (A2)+,D2
	MOVE.W (A5),2(A3)
	TST.W D2
	BNE.S LL84
	MOVE.W (A4),2(A3)
LL84
	MOVE.W (A2)+,D2
	MOVE.W (A5),4(A3)
	TST.W D2
	BNE.S LL85
	MOVE.W (A4),4(A3)
LL85
	MOVE.W (A2)+,D2
	MOVE.W (A5),6(A3)
	TST.W D2
	BNE.S LL86
	MOVE.W (A4),6(A3)
LL86
	MOVE.W (A2)+,D2
	MOVE.W (A5),8(A3)
	TST.W D2
	BNE.S LL87
	MOVE.W (A4),8(A3)
LL87	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,10(A3)
	TST.W D2
	BNE.S LL88
	MOVE.W (A4),10(A3)
LL88
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,12(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT8
	LEA  PALETTE,A2
CONNT8
	DBF D1,LLOOP8
	BRA JOY

LAND9	MOVE.L DRAWPOINT,A2
	LEA LANDPAL,A3
	LEA BACKPAL,A4
	LEA 2(A3),A3
	LEA LETPAL,A5
	MOVE.W #43,D1
LLOOP9
	MOVE.W (A2)+,D2
	MOVE.W (A5),16(A3)
	TST.W D2
	BNE.S LL91
	MOVE.W (A4),16(A3)
LL91	MOVE.W (A2)+,D2
	MOVE.W (A5),(A3)
	TST.W D2
	BNE.S LL92
	MOVE.W (A4),(A3)
LL92
	MOVE.W (A2)+,D2
	MOVE.W (A5),2(A3)
	TST.W D2
	BNE.S LL93
	MOVE.W (A4),2(A3)
LL93
	MOVE.W (A2)+,D2
	MOVE.W (A5),4(A3)
	TST.W D2
	BNE.S LL94
	MOVE.W (A4),4(A3)
LL94
	MOVE.W (A2)+,D2
	MOVE.W (A5),6(A3)
	TST.W D2
	BNE.S LL95
	MOVE.W (A4),6(A3)
LL95
	MOVE.W (A2)+,D2
	MOVE.W (A5),8(A3)
	TST.W D2
	BNE.S LL96
	MOVE.W (A4),8(A3)
LL96
	MOVE.W (A2)+,D2
	MOVE.W (A5),10(A3)
	TST.W D2
	BNE.S LL97
	MOVE.W (A4),10(A3)
LL97	
	MOVE.W (A2)+,D2
	MOVE.W (A5)+,12(A3)
	TST.W D2
	BNE.S LL98
	MOVE.W (A4),12(A3)
LL98
	MOVE.W (A4),-2(A3)
	MOVE.W (A4)+,14(A3)
	LEA 20(A3),A3
	CMP.L D3,A2
	BNE.S CONNT9
	LEA PALETTE,A2
CONNT9
	DBF D1,LLOOP9
	BRA	JOY

**************************************
* QUIT ROUTINE
**************************************

QUIT	MOVE.W #$2700,SR
	MOVE.L OLDKEY,$118.W
	MOVE.B OLDIEB,$FFFFFA09.W	SET UP THE MFP
	MOVE.B OLDIEA,$FFFFFA07.W
	MOVE.B OLDIMA,$FFFFFA13.W
	MOVE.B OLD1B,$FFFFFA1B.W
	MOVE.B OLD21,$FFFFFA21.W
	MOVE.L OLDHBL,$120.W
	MOVE.L OLDVBL,$70.W
	MOVE.L OLDPHYS,$FFFF8200.W
	MOVE.W #$2300,SR
	MOVEQ #$8,D0
	BSR IKBD
	MOVEQ #$16,D0
	BSR IKBD
	LEA $FFFF8800.W,A0
	MOVE.L #$8080000,(A0)
	MOVE.L #$9090000,(A0)
	MOVE.L #$A0A0000,(A0)
	MOVE.L OLDSSP,A7
	CLR.W	-(SP)
	TRAP	#1			GOOD BYE OH CRUEL WORLD!!

IKBD	LEA $FFFFFC00.W,A1
IKLOOP	MOVE.B (A1),D1
	BTST #1,D1
	BEQ.S IKLOOP
	MOVE.B D0,2(A1)
	RTS

OLDSSP	DC.L 0
OLD1B	DC.B 0
OLD21	DC.B 0

**************************************
* THESE ROUTINES READ THE JOYSTICK AND THE KEYBOARD
* VARIABLES PREVIOUSLY OBTAINED FROM THE CUSTOM KEY VECTOR
* THEY THEN TAKE APPROPRIATE ACTION
***************************************

JOY	MOVEQ #0,D0
	CMP.B #$4D,KBUFF
	BEQ.S RIGHT
	CMP.B #$4B,KBUFF
	BEQ.S LEFT
	CMP.B #$39,KBUFF
	BEQ QUIT
	RTS

LEFT
	ADD.W #1,POSIND
	CMP.W #9,POSIND
	BNE.S DOCONT
	CLR.W POSIND
DOCONT	
	RTS
RIGHT	
	SUB.W #1,POSIND
	CMP.W #-1,POSIND
	BNE.S DOCONT1
	MOVE.W #8,POSIND
DOCONT1	
	RTS
OLDPHYS	DC.L 0

*********************************
* LIST OF THE 9 LANDSCAPE ROUTINES
*********************************

ROUTTAB
	DC.L LAND1
	DC.L LAND2
	DC.L LAND3
	DC.L LAND4
	DC.L LAND5
	DC.L LAND6
	DC.L LAND7
	DC.L LAND8
	DC.L LAND9

**********************************
*	HBL TABLE DATA
* THE TABLES ARE OF THE DAT WHICH IS WRITTEN TO
* THE HBL. THERE ARE TO SETS OF DATA. THE SECOND IS
* TW0 LINES HIGHER THAN THE FIRST SO ANIMATION OF
* SCROLL IS SMOOTH EVEN THOUGH. THE SCROLL IS ACTUALLY
* DRAWN EVERY OTHER FRAME (25HZ)
**********************************


HBLTAB1	
	DC.B 1,1,1,1,1,1,1,1,1,1
	DC.B 2,2,2,2,2,2,2,2,2,2
	DC.B 3,3,3,3,3,3,3,3,3,3
	DC.B 4,4,4,4,4,4,4,4,4,4
	DC.B 4,4,4,100,100,100

**********************************
* PALETTE FOR THE BACKGROUND COLOR
**********************************

BACKPAL
	DC.W $1,$1,$1,$2
	DC.W $1,$2,$2,$2,$2,$3
	DC.W $2,$3,$3,$3,$3,$3,$3,$4
	DC.W $3,$4,$4,$4,$4,$4,$4,$5
	DC.W $4,$5,$5,$5,$5,$5,$5,$6
	DC.W $5,$6,$6,$6,$6,$6,$6,$7
	DC.W $6,$7,$7,$7,$7,$7,$7,$7

LETPAL
	DC.W $100,$100,$100,$210
	DC.W $100,$210,$210,$210,$210,$320
	DC.W $210,$320,$320,$320,$320,$320,$320,$430
	DC.W $320,$430,$430,$430,$430,$430,$430,$540
	DC.W $430,$540,$540,$540,$540,$540,$540,$650
	DC.W $540,$650,$650,$650,$650,$650,$650,$760
	DC.W $650,$760,$760,$760,$760,$760,$760,$760

**********************************
*	FONT DATA
* FONT STORED AS 0'S AND 1'S  AND
* THEN CONVERTED TO A LIST OF PALETTE DATA
* AT THE BEGINNING OF THE PROGRAM
**********************************

FONT
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0


	dc.w 1,1,1,1,0,0,0,0
	dc.w 1,1,1,1,0,0,0,0
	dc.w 1,1,1,1,0,0,0,0
	dc.w 1,1,1,1,0,0,0,0
	dc.w 1,1,1,1,0,0,0,0
	dc.w 0,0,0,0,1,1,1,1
	dc.w 0,0,0,0,1,1,1,1
	dc.w 0,0,0,0,1,1,1,1
	dc.w 0,0,0,0,1,1,1,1
	dc.w 0,0,0,0,1,1,1,1

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,1,1,1,1
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,0,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,0,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 1,1,1,1,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 1,1,1,1,1,1,1,0
	dc.w 1,1,1,1,1,1,1,0
	dc.w 1,1,1,1,1,1,1,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,1,1,0,0,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,0,1,1,1
	dc.w 1,1,1,0,1,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,1,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,0,0,0,0
	dc.w 1,1,0,1,1,1,0,0
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,1,1
	dc.w 0,0,0,0,1,1,1,0
	dc.w 1,1,0,1,1,1,0,0
	dc.w 1,1,1,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,1,1,1,1,1
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 1,1,1,0,1,1,1,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,1,1,1,0
	dc.w 1,1,1,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,1,1,1,1,1,0
	dc.w 0,1,1,1,0,1,1,1
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,1,1,1,0,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,1,1,1,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,1,1,0,0,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,1,1,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,1,1,1,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,0,0
	dc.w 1,1,1,0,1,1,1,0
	dc.w 1,1,1,0,1,1,1,0
	dc.w 1,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,1,1,1,1,1
	dc.w 0,0,1,1,1,0,1,1
	dc.w 0,1,1,1,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 0,0,1,1,1,0,1,1
	dc.w 0,0,0,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,0,0
	dc.w 1,1,1,0,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,1,1,1,0
	dc.w 1,1,1,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,1,1,1,1,1,0
	dc.w 0,1,1,1,0,1,1,1
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,1,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,1,0,1,1,1
	dc.w 0,0,1,1,1,1,1,0
	dc.w 0,0,0,0,0,0,0,0
	
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 1,1,1,1,1,1,1,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,1,1,1,1,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,0
	dc.w 1,1,1,0,1,1,0,0
	dc.w 1,1,1,1,1,0,0,0
	dc.w 1,1,1,1,1,0,0,0
	dc.w 1,1,1,0,1,1,0,0
	dc.w 1,1,1,0,0,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,0,0,0,0,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,0,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,1,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,0,0,0
	
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	
	dc.w 0,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,0,1,1,1
	dc.w 0,1,1,1,1,1,0,0
	dc.w 0,0,0,0,0,1,0,0
	dc.w 0,0,0,0,0,0,1,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,0
	dc.w 1,1,1,0,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 0,1,1,1,1,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,0,0,0
	dc.w 0,1,1,1,1,0,0,0
	dc.w 0,0,0,1,1,1,1,0
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,1,1,1,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,1,1,1,1,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,0,1,0,0,1,0,0
	dc.w 0,0,1,0,0,1,0,0
	dc.w 0,0,1,0,0,1,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0
	
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,1,1,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,0,0,0,0,1,1
	dc.w 1,0,0,0,0,0,0,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,1,1,0,0,1,1,0
	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,0,0,1,1,1
	dc.w 1,1,1,0,0,1,1,1
	dc.w 0,1,1,0,0,1,1,0
	dc.w 0,0,1,1,1,1,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,1,1,0,0,0
	dc.w 0,0,0,0,0,0,0,0

	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,0,1,1,1
	dc.w 0,0,0,0,1,1,1,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,1,1,1,0,0,0
	dc.w 0,1,1,1,0,0,0,0
	dc.w 1,1,1,0,0,0,0,0
	dc.w 1,1,1,1,1,1,1,1
	dc.w 0,0,0,0,0,0,0,0

*********************************
*	THE SCROLLINE
********************************

SCROLLINE
	DC.B "#####################################     "
	DC.B "AW SHIT IT'S A SCROLLING LINE!!  THOSE FUCKING "
	DC.B "LOST BOYS HAVE DONE IT AGAIN AND BLOWN MY MIND."
	DC.B " GOOD DEMOS, FIRST SEEN ON THE AMIGA, NOW SHOWING, "
	DC.B "COURTESY OF THE LOST BOYS. BEST ON THE ST, MODESTY HAS "
	DC.B " NEVER BEEN OUR STRONGEST POINT. WE ARE THE BEST SO FORGET"
	DC.B " THE REST!!! "

	DC.B $FF
	EVEN

****************************************
*          THE STRETCH SIGN            *
****************************************

RES	MOVE.L STRETCHTABPOINT,A1
	MOVE.L (A1)+,STRETCHPOINT
	MOVE.L (A1)+,STREND
	CMP.L #STABEND,A1
	BNE.S RES1
	LEA STRETCHTAB,A1
RES1	MOVE.L A1,STRETCHTABPOINT

ANIMATE
	MOVEQ #0,D0
	MOVEQ #0,D1
	MOVE.L STRETCHPOINT,A0
	CMP.L STREND,A0
	BEQ.S RES
	MOVE.W (A0)+,D1
	MOVE.W (A0)+,D0
	MOVE.L A0,STRETCHPOINT
	LEA STRETCH,A0
	ADD.L D0,A0
	MOVE.L LOGBASE,A1
	SUB.L #$21F8,A1
	ADD.L D1,A1

	REPT 54
	MOVEM.L (A0)+,D0-D7/A2-A6
	MOVEM.L D0-D7/A2-A6,(A1)
	MOVE.L (A0)+,52(A1)
	LEA 160(A1),A1
	ENDR
	MOVEM.L BLANK,D0-D7/A2-A6
	LEA 0,A0
	MOVEM.L D0-D7/A0/A2-A6,(A1)
	MOVEM.L D0-D7/A0/A2-A6,160(A1)
	MOVEM.L D0-D7/A0/A2-A6,320(A1)
	MOVEM.L D0-D7/A0/A2-A6,480(A1)
	MOVEM.L D0-D7/A0/A2-A6,-8800(A1)
	MOVEM.L D0-D7/A0/A2-A6,-8960(A1)
	MOVEM.L D0-D7/A0/A2-A6,-9120(A1)
	MOVEM.L D0-D7/A0/A2-A6,-9280(A1)
	BRA.S ANIMATE1

RES2	MOVE.L STRETCHTABPOINT1,A1
	MOVE.L (A1)+,STRETCHPOINT1
	MOVE.L (A1)+,STREND1
	CMP.L #STABEND1,A1
	BNE.S RES3
	LEA STRETCHTAB1,A1
RES3	MOVE.L A1,STRETCHTABPOINT1

ANIMATE1
	MOVEQ #0,D0
	MOVEQ #0,D1
	MOVE.L STRETCHPOINT1,A0
	CMP.L STREND1,A0
	BEQ.S RES2
	MOVE.W (A0)+,D1
	MOVE.W (A0)+,D0
	MOVE.L A0,STRETCHPOINT1
	LEA STRETCH,A0
	ADD.L D0,A0
	MOVE.L LOGBASE,A1
	LEA -$2260(A1),A1
	ADD.L D1,A1
	REPT 54
	MOVEM.L (A0)+,D0-D7/A2-A6
	MOVEM.L D0-D7/A2-A6,(A1)
	MOVE.L (A0)+,52(A1)
	LEA 160(A1),A1
	ENDR
	MOVEM.L BLANK,D0-D7/A2-A6
	LEA 0,A0
	MOVEM.L D0-D7/A0/A2-A6,0(A1)
	MOVEM.L D0-D7/A0/A2-A6,160(A1)
	MOVEM.L D0-D7/A0/A2-A6,320(A1)
	MOVEM.L D0-D7/A0/A2-A6,480(A1)
	MOVEM.L D0-D7/A0/A2-A6,-8800(A1)
	MOVEM.L D0-D7/A0/A2-A6,-8960(A1)
	MOVEM.L D0-D7/A0/A2-A6,-9120(A1)
	MOVEM.L D0-D7/A0/A2-A6,-9280(A1)


ANIMATE2
	MOVEQ #0,D0
	MOVEQ #0,D1
	MOVE.L SIGNPOINT,A0
	MOVE.W (A0)+,D1
	LEA 2(A0),A0
	CMP.L #SPINEND,A0
	BNE.S RES4
	LEA SPIN,A0
RES4	MOVE.L A0,SIGNPOINT
	LEA SIGN,A0
	MOVE.L LOGBASE,A1
	LEA -$2228(A1),A1
	ADD.L D1,A1
	REPT 54
	MOVEM.L (A0)+,D0-D7/A2-A5
	MOVEM.L D0-D7/A2-A5,(A1)
	LEA 160(A1),A1
	ENDR

	LEA BLANK,A0
	MOVEM.L (A0)+,D0-D7/A2-A5
	
	MOVEM.L D0-D7/A2-A5,0(A1)
	MOVEM.L D0-D7/A2-A5,160(A1)
	MOVEM.L D0-D7/A2-A5,320(A1)
	MOVEM.L D0-D7/A2-A5,480(A1)
	MOVEM.L D0-D7/A2-A5,-8800(A1)
	MOVEM.L D0-D7/A2-A5,-8960(A1)
	MOVEM.L D0-D7/A2-A5,-9120(A1)
	MOVEM.L D0-D7/A2-A5,-9280(A1)

	RTS

**************************************
* SPRITE ROUTINE
**************************************


DOSPRITE
	MOVE.L OLDSPRITE,A4
	LEA 2340(A4),A4
	LEA WAVEPOINT,A5
	MOVEQ.W #9,D7
SS_LOOP
	MOVE.W D7,-(SP)
	MOVE.L (A5),A3
	MOVE.L (A3)+,A2
	MOVE.W (A3)+,D1
	CMP.L WAVESTOREND,A3
	BNE.S SJUMP
	LEA WAVESTORE,A3
SJUMP	MOVE.L A3,(A5)+
	MOVE.L LOGBASE,A3
	ADD.W D1,A3			GET Y LINE
	MOVE.L A3,(A4)+
ADDIT	SET 0
	REPT 16
	MOVEM.L (A3),D2-D5
	MOVEM.L D2-D5,ADDIT(A4)
	AND.L (A2),D2
	AND.L (A2)+,D3
	OR.L (A2)+,D2
	OR.L (A2)+,D3
	AND.L (A2),D4
	AND.L (A2)+,D5
	OR.L (A2)+,D4
	OR.L (A2)+,D5
	MOVEM.L D2-D5,(A3)
	LEA 160(A3),A3
ADDIT	SET ADDIT+16
	ENDR
	MOVE.W (SP)+,D7
	LEA -264(A4),A4
	DBF D7,SS_LOOP
	RTS

SPRITEPRESHIFT
	LEA GRAPH,A2
	LEA MASK,A3
	MOVE.W #15,D5
LOOP4S
	MOVEM.W (A2)+,D1-D4
	OR.W D1,D4
	OR.W D2,D4
	OR.W D3,D4
	NOT.W D4
	MOVE.W D4,(A3)+
	MOVE.W D4,(A3)+
	MOVEM.W (A2)+,D1-D4
	OR.W D1,D4
	OR.W D2,D4
	OR.W D3,D4
	NOT.W D4
	MOVE.W D4,(A3)+
	MOVE.W D4,(A3)+
	DBF D5,LOOP4S


	MOVE.W #15,D0
	LEA SPRITE,A1
LOOPS
	LEA GRAPH,A0
	LEA MASK,A2
	MOVE.W #15,D1
LOOP1S	
	MOVE.L (A2)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A2)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	DBF D1,LOOP1S
	LEA GRAPH,A0
	
	MOVE.W #15,D1
LOOP2S
	ROXR.W (A0)
	ROXR.W 8(A0)
	ROXR.W 2(A0)
	ROXR.W 10(A0)
	ROXR.W 4(A0)
	ROXR.W 12(A0)
	ROXR.W 6(A0)
	ROXR.W 14(A0)
	LEA 16(A0),A0

	DBF D1,LOOP2S

	LEA GRAPH,A2
	LEA MASK,A3
	MOVE.W #15,D5
LOOP4
	MOVEM.W (A2)+,D1-D4
	OR.W D1,D4
	OR.W D2,D4
	OR.W D3,D4
	NOT.W D4
	MOVE.W D4,(A3)+
	MOVE.W D4,(A3)+
	MOVEM.W (A2)+,D1-D4
	OR.W D1,D4
	OR.W D2,D4
	OR.W D3,D4
	NOT.W D4
	MOVE.W D4,(A3)+
	MOVE.W D4,(A3)+
	DBF D5,LOOP4
	DBF D0,LOOPS
	RTS
PREPWAVES
	LEA WAVE,A5
	LEA WAVESTORE,A4
	MOVE.W #1619,D7
SS_LOOP1
	MOVE.W (A5)+,D0
	MOVE.W (A5)+,D6

	MOVE.W D0,D1
	AND.L #15,D1			FIND FRAME
	MULU #384,D1
	LEA SPRITE,A2
	ADD.L D1,A2

	MOVE.L A2,(A4)+

	AND.W #$FFF0,D0			FIND WORD ON LINE

	LSR.W #1,D0

	ADD.W D0,D6
	MOVE.W D6,(A4)+
	DBF D7,SS_LOOP1
	MOVE.L A4,WAVESTOREND
	RTS

****************************
* WAVES
****************************

WAVEPOINT
	DC.L WAVESTORE
	DC.L WAVESTORE+30
	DC.L WAVESTORE+60
	DC.L WAVESTORE+90
	DC.L WAVESTORE+120
	DC.L WAVESTORE+150
	DC.L WAVESTORE+180
	DC.L WAVESTORE+210
	DC.L WAVESTORE+240
	DC.L WAVESTORE+270

WAVE		INCBIN	a:\landscap.e\landSCAP.E\DATA\XY.DAT
WAVEND
WAVESTOREND	DC.L 0
OLDSPRITE	DC.L OLD1
OLD1		REPT 10
		DC.L $78000
		DS.B 256
		ENDR
OLD2		REPT 10
		DC.L $78000
		DS.B 256
		ENDR
****************************
* GRAPHIX
****************************
MASK	DS.L 100
GRAPH	INCBIN	a:\landscap.e\landSCAP.E\GRAPHICS\SPRITE.DAT

**************************
* VARIABLES TABLES AND SHIT FOR STRETCH
**************************

STRETCHPOINT	
	DC.L PAUSE
STREND	DC.L PAUSEEND
STRETCHTABPOINT
	DC.L STRETCHTABB
STRETCHPOINT1	
	DC.L PAUSE
STREND1	DC.L PAUSEEND
STRETCHTABPOINT1
	DC.L STRETCHTAB1B
STRETCHTABB
	DC.L PAUSE,PAUSEEND
	DC.L PAUSE,PAUSEEND
	DC.L PAUSE,PAUSEEND
STRETCHTAB
	DC.L PAUSE,PAUSEEND
	DC.L COMEIN,COMEINEND
	DC.L SPIN,SPINEND,SPIN,SPINEND
	DC.L SPIN1,SPIN1END,SPIN1,SPIN1END,SPIN1,SPIN1END
	DC.L SPINBACK,SPINBACKEND,SPINBACK,SPINBACKEND
	DC.L SPIN1,SPIN1END,SPIN1,SPIN1END
	DC.L SPINBACK,SPINBACKEND
	DC.L SPINBACK,SPINBACKEND
	DC.L SPINBACK,SPINBACKEND
	DC.L SPINBACK,SPINBACKEND
	DC.L SPINBACK,SPINBACKEND
	DC.L SPINBACK,SPINBACKEND
	DC.L SPINBACK,SPINBACKEND
	DC.L GOOUT,GOOUTEND
STABEND
STRETCHTAB1B
	DC.L PAUSE,PAUSEEND
	DC.L PAUSE,PAUSEEND
	DC.L PAUSE,PAUSEEND
STRETCHTAB1
	DC.L PAUSE,PAUSEEND
	DC.L COMEIN,COMEINEND
	DC.L SPIN,SPINEND,SPIN,SPINEND
	DC.L SPIN1,SPIN1END,SPIN1,SPIN1END,SPIN1,SPIN1END
	DC.L SPINBACK,SPINBACKEND,SPINBACK,SPINBACKEND
	DC.L SPIN1,SPIN1END,SPIN1,SPIN1END
	DC.L SPINBACK,SPINBACKEND
	DC.L SPIN,SPINEND,SPIN,SPINEND
	DC.L SPIN,SPINEND,SPIN,SPINEND
	DC.L SPIN,SPINEND,SPIN,SPINEND
	DC.L SPIN,SPINEND,SPIN,SPINEND
	DC.L SPIN,SPINEND,SPIN,SPINEND
	DC.L SPINBACK,SPINBACKEND
	DC.L GOOUT,GOOUTEND
STABEND1
SIGN	INCBIN	a:\landscap.e\landSCAP.E\GRAPHICS\SIGN.DAT
STRETCH	INCBIN	a:\landscap.e\landSCAP.E\GRAPHICS\STRETCH.DAT
SPIN	INCBIN	a:\landscap.e\landSCAP.E\DATA\SPIN.DAT
SPINEND
SPINBACK INCBIN	a:\landscap.e\landSCAP.E\DATA\SPINBACK.DAT
SPINBACKEND
COMEIN	INCBIN	a:\landscap.e\landSCAP.E\DATA\COMEIN.DAT
COMEINEND
GOOUT	INCBIN	a:\landscap.e\landSCAP.E\DATA\GOOUT.DAT
GOOUTEND
SIGNPOINT DC.L SPIN
SPIN1 
	DC.W $3020,15*3080
	DC.W $3020,14*3080
	DC.W $3020,13*3080
	DC.W $3020,12*3080
	DC.W $3020,11*3080
	DC.W $3020,10*3080
	DC.W $3020,9*3080
	DC.W $3020,8*3080
	DC.W $3020,7*3080
	DC.W $3020,6*3080
	DC.W $3020,5*3080
	DC.W $3020,4*3080
	DC.W $3020,3*3080
	DC.W $3020,2*3080
	DC.W $3020,1*3080
	DC.W $3020,0
	
	DC.W $3020,1*3080
	DC.W $3020,2*3080
	DC.W $3020,3*3080
	DC.W $3020,4*3080
	DC.W $3020,5*3080
	DC.W $3020,6*3080
	DC.W $3020,7*3080
	DC.W $3020,8*3080
	DC.W $3020,9*3080
	DC.W $3020,10*3080
	DC.W $3020,11*3080
	DC.W $3020,12*3080
	DC.W $3020,13*3080
	DC.W $3020,14*3080
	DC.W $3020,15*3080
SPIN1END
PAUSE	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
	DC.W 0,15*3080,0,15*3080,0,15*3080,0,15*3080
PAUSEEND
DEFSTARS	INCBIN	a:\landscap.e\LANDSCAP.E\DATA\HALFSTAR.DAT

**********************************
* VARIABLES AND SHIT
* THESE ARE THE VARIOUS VARIABLES USED
* IN THE PROGRAM
**********************************

BLANK		DS.L 14
POSIND		DC.W 4 
LANDPOINT	DC.L LANDPAL
PHYSBASE	DC.L $78000
LOGBASE		DC.L $68000
STRING		DC.B $12,$14
LETEND		DC.W 1
LETIND		DC.W 1
SCROLLPOINT	DC.L SCROLLINE
LETPOINT	DC.L FONT
OLDVBL		DC.L 0
OLDHBL		DC.L 0
OLDIEB 		DC.B 0
OLDIEA		DC.B 0
OLDIMA		DC.W 0
DRAWPOINT	DC.L PALETTE-16
PALPOINT	DC.L PALEEND-32
LANDPAL	DS.L 230
PALETTE	DS.L 500
PALEEND	DS.L 10
	DS.L 100
MYSTACK	DC.L 0
PALETTESCR	DC.W $707,$607,$507,$407,$307,$207,$107,$7
GRAPHIX	INCBIN	a:\landscap.e\landscap.e\graphics\Land.dat
MUSIC	INCBIN	a:\landscap.e\landscap.e\music\mad_max.bin
	EVEN
SPRITE
WAVESTORE	EQU SPRITE+20000
