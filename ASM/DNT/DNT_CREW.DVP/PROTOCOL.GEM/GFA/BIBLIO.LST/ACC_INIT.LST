> PROCEDURE init_tube_acc
  '
  ' Proc‚dure d'initialisation pour accessoire
  ' Fichier ACC_INIT.LST
  '
  cookie4&=0                    ! ne pas ‚craser
init_tub0:
  cookie1%=CVL("_TUB")          ! cookie recherch‚
  cookie2%=VARPTR(new_jar%(0))  !
  cookie3%=VARPTR(tab_tub&(0))
  @cookie_jar
  '
  IF cookie2%<>0
    IF cookie3%=VARPTR(tab_tub&(0))     ! si c'est ma liste
      tab_tub&(0)=CVI("PR")     ! je met son en-tˆte
      tab_tub&(1)=CVI("OT")
      tab_tub&(2)=10      ! le nbd d'app_id quelle peut contenir
      tab_tub&(3)=app_id& ! mon APP_ID
      tab_tub&(4)=-1      ! et la marque de fin
    ELSE
      ' V‚rifions le tableau Protocole en place...
      IF LPEEK(cookie3%)<>CVL("PROT")
        cookie4&=1      ! tableau invalide, nous
        GOTO init_tub0  ! allons l'‚craser...
      ELSE
        ' Parcourons le tableau en place pour placer
        ' notre APP_ID et clore par &HFFFF
        maxi&=DPEEK(cookie3%+4) ! nbr maxi d'app_id autoris‚s
        cookie3%=cookie3%+6     ! saute l'en-tˆte
        REPEAT
          ' Nous pr‚voyons le cas d'ACC lanc‚s par Multidesk
          ' qui am‚ne … avoir plusieurs fois le mˆme APP_ID:
          ' Si nous y trouvons d‚ja le notre -> bye bye!!!
          EXIT IF DPEEK(cookie3%)=app_id&
          IF DPEEK(cookie3%)=&HFFFF     ! fin de la liste ?
            DPOKE cookie3%,app_id&      ! donc met mon APPID
            DPOKE cookie3%+2,&HFFFF     ! et l'indication de fin
          ELSE
            cookie3%=cookie3%+2         ! sinon passe … l'app_id
            DEC maxi&                   ! suivant, et le compte
          ENDIF
        UNTIL DPEEK(cookie3%)=app_id& OR maxi&=0
      ENDIF
    ENDIF
  ENDIF
RETURN
