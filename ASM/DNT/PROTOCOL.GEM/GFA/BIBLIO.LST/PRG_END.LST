> PROCEDURE off_tube_prg
  '
  ' Proc‚dure devant ˆtre ex‚cut‚e … la fin du PRG afin que
  ' celui-ci retire son APP_ID de la liste du Protocole
  ' Fichier PRG_END.LST
  '
  IF jy_suis!=TRUE      ! sinon c'est que je ne suis pas dans la liste !
    cookie1%=CVL("_TUB") ! Cookie recherch‚
    cookie2%=0            ! pas de cookie-jar … mettre
    cookie3%=0            ! nous ne pouvons pas mettre de tableau (car PRG!!)
    cookie4&=0            ! prenons juste l'adresse...
    @cookie_jar
    ' S'il y a un tableau avec une adresse de liste protocole...
    ' Il est en effet possible que quelqu'un soit venu d‚truire le cookie-jar,
    ' cela fait partie des ennuis du "multitƒche"!!!
    IF cookie2%<>0 AND cookie3%<>0
      ' Nous v‚rifions l'en-tˆte de cette liste protocole (PROT)
      IF LPEEK(cookie3%)=CVL("PROT")   ! si c'est = PROT
        maxi&=DPEEK(cookie3%+4) ! nbr maxi d'APP_ID
        cookie3%=cookie3%+6     ! saute les 6 octets d'en-tˆte
        ' Je recherche maintenant mon APP_ID
        REPEAT
          IF DPEEK(cookie3%)=app_id&   ! si nous avons trouv‚ notre app_id
            ' Puisque nous avons trouv‚ notre APP_ID, nous remontons
            ' la liste d'un cran, pour retirer notre APP_ID de celle-ci.
            temp%=cookie3%+2    ! temp% pointe plus loin
            REPEAT
              DPOKE cookie3%,DPEEK(temp%) ! word suivant dans le pr‚c‚dent
              cookie3%=cookie3%+2         ! passe au word suivant dans la liste
              temp%=temp%+2               ! idem
              DEC maxi&                   ! compte cet APP_ID
            UNTIL DPEEK(cookie3%)=&HFFFF OR maxi&=1
            ' Si maxi&=1, c'est qu'il y a une erreur. En effet nous venons de
            ' retirer notre APP_ID il devrait donc y avoir de la place.
            IF maxi&=1
              DPOKE (cookie3%),&HFFFF
            ENDIF
            maxi&=0     ! pour forcer la sortie
          ELSE
            DEC maxi&             ! compte un APP_ID
            cookie3%=cookie3%+2   ! passe 2 octets (chaque app_id est sur 2 oct)
          ENDIF
        UNTIL maxi&=0
        '
      ENDIF
    ENDIF
  ENDIF
RETURN
