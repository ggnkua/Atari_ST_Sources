> PROCEDURE cookie_jar
  '
  ' Proc‚dure permettant de lire et/ou d'‚crire dans le Cookie-Jar.
  ' Par simplification, il s'agit de la mˆme routine pour ACC et PRG
  ' alors qu'il aurait ‚t‚ possible d'en faire des diff‚rentes.
  ' Fichier COOKIJAR.LST
  '
  LOCAL temp%,x%,cmp%
cookie_jar0:
  temp%=LPEEK(&H5A0)         !cherche adresse cookie-jar
  ' S'il n'y a pas de boite, nous pla‡ons la notre
  IF temp%=0
    IF cookie2%<>0              ! si nous avons une boite … mettre...
      SLPOKE &H5A0,cookie2%      ! adresse de celle-ci
      IF cookie1%<>0            ! si nous avons un cookie...
        LPOKE cookie2%,cookie1%
        LPOKE cookie2%+4,cookie3%
        LPOKE cookie2%+8,0
        LPOKE cookie2%+12,16
      ENDIF
    ENDIF
  ELSE     ! Il y a un Cookie-Jar
    IF cookie1%<>0      ! si nous devons chercher un gateaux
      cmp%=0                      ! init. compteur de Cookie
      REPEAT
        x%=LPEEK(temp%)           ! pr‚l‚ve l'identif. d'un cookie
        temp%=temp%+8             ! avance sur le suivant
        INC cmp%                  ! et compte ce cookie
      UNTIL x%=0 OR x%=cookie1%
      temp%=temp%-4               !reculons sur l'info. de ce cookie
      ' Si nous avons trouv‚ notre cookie1
      IF x%=cookie1%
        IF cookie4&=0   ! si nous devons juste noter l'information,
          cookie3%=LPEEK(temp%)     ! nous la notons et bye bye...
        ELSE
          LPOKE temp%,cookie3%      ! sinon nous la for‡ons
        ENDIF
      ELSE
        ' Nous avons trouv‚ la fin de la boite, nous mettons
        ' notre cookie, s'il reste de la place...
        IF LPEEK(temps%)=0      ! pr‚caution si nbr de slot nul,
          SLPOKE &H5A0,0         ! la boite est mauvaise...
          GOTO cookie_jar0
        ENDIF
        IF cookie3%<>0  ! si nous avons quelque chose … mettre
          IF cmp%<LPEEK(temp%)             ! s'il reste de la place...
            LPOKE (temp%+4),0               ! flag de fin
            LPOKE (temp%+8),LPEEK(temp%)    ! transfert le nbr d'emplacement
            LPOKE (temp%-4),cookie1%        ! place l'identif. de notre cookie
            LPOKE (temp%),cookie3%          ! et sa valeur d'info
          ELSE
            ' Il n'y a pas assez de place: pla‡ons une plus grosse boite
            IF cmp%<16 AND cookie2%<>0      ! si nous pouvons...
              temp%=LPEEK(&H5A0)        ! adr ancien cookie-jar
              SLPOKE &H5A0,cookie2%      ! note adr du nouveau
              WHILE LPEEK(temp%)<>0
                LPOKE cookie2%,LPEEK(temp%)
                LPOKE cookie2%+4,LPEEK(temp%+4)
                temp%=temp%+8
                cookie2%=cookie2%+8
              WEND
              LPOKE (cookie2%),cookie1%
              LPOKE (cookie2%+4),cookie3%
              LPOKE (cookie2%+8),0
              LPOKE (cookie2%+12),16
            ELSE
              ' Boite pas assez grosse ou pas de boite … mettre ...
              cookie3%=0
            ENDIF
          ENDIF
        ENDIF
      ENDIF
    ENDIF
  ENDIF
  cookie2%=LPEEK(&H5A0)
RETURN
