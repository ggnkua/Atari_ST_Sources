'
'
' OVERSCAN 
'  PROGRAMED BY  CORSAIR OF THE  GIANTS, 01.08.1992
'   ENGLISH COMMENT BONUS SOFTWARE
'
bm%=28874
q|=170
init
main
exit                                    !PUT EVERYTHING BACK
EDIT                                    !QUIT!
'
PROCEDURE init
  OUT 4,18                              !SHUT UP MOUSE
  HIDEM                                 !...AND HIDE HIM
  super%=GEMDOS(&H20,L:0)               !SUPERVISOR MODE ON
  '
  org%=XBIOS(3)                         !ORG. SCREEN POINTER
  RESERVE FRE()-(32512+50*160)          !RESERVE MEMORY
  screen_%=MALLOC(32256+50*160)         !ALLOCATE MEMORY
  screen%=(screen_%+256)-screen_% MOD 256  !MUST BE ON 256 EVEN POINTER
  res%=XBIOS(4)                         !REMEMBER OLD RESOLUTION
  ~XBIOS(5,L:screen%,L:screen%,W:0)     !INSTALL SCREEN, SWITCH TO LOW RES
  SETCOLOR 15,0                         !SETCOLOR 15 TO BLACK
  mfp(0)                                !KILL SOME MFP INTERUPS (STEADY)
  '
  ' LET'S DRAW SOMETHING ON SCREEN
  ' ===========================
  CLS
  PRINT
  PRINT " THE LOW BORDER IS REMOVED BY CORSAIR" ! JUST SOME TEXT FOR BORDER
  PRINT " USE ARROW KEY'S UP, DOWN, + AND -"
  PRINT " TO GET SYNC STEADY "
  '
  BMOVE XBIOS(2),XBIOS(2)+32000,6400    ! COPY TEXT IN BORDER (!)
  COLOR 4
  DEFLINE 4,0,0,1
  LINE 50,199,319,199                   !DRAW LINE ON 200 SCREEN LINE
RETURN
'
PROCEDURE main
  DO
    VSYNC                               !SYNC
    k|=BYTE{&HFFFC02}                   !READ A KEY (IF ANY)
    BMOVE 10000,10000,bm%               !WAIT FOR LAST SCAN LINE
    overscan                            !OPEN LOWER BORDER
    keys                                !GET KEY'S FOR ALTERATIONS
  LOOP UNTIL k|=57                      !WAIT FOR SPAC_BAR
RETURN
'
PROCEDURE overscan
  DO
  LOOP UNTIL BYTE{&HFF8209}>170         !EXTRA SYNCHRONISATION (steady)
  VOID SHL(1,q|-BYTE{&HFF8209})         !"MAGIC SYNCHRONISATION"
  CARD{&HFF8240}=&H700                  !BORDER COLOR RED
  BYTE{&HFF820A}=0                      !60 HZ
  {&HFF8240}={&HFF8240}                 !JUST WAIT
  BYTE{&HFF820A}=2                      !50 HZ
  CARD{&HFF8240}=&H777                  !BORDER COLOR WHITE
RETURN
'
PROCEDURE exit
  mfp(1)                                !MFP ON AGAIN
  OUT 4,8                               !MOUSE ON
  ~GEMDOS(&H20,L:super%)                !USER MODE
  ~XBIOS(5,L:org%,L:org%,W:res%)        !INSTALL ORG. RES AND POINTER
  ~MFREE(screen_%)                      !FREE MALLOC MEMORY
  RESERVE                               !FREE RESERVE MEMORY
RETURN
'
PROCEDURE keys
  SELECT k|
  CASE 72                               !
    SUB bm%,2
  CASE 80                               !
    ADD bm%,2
  CASE 74
    DEC q|                              !-
  CASE 78
    INC q|                              !+
  ENDSELECT
RETURN
'
PROCEDURE mfp(m|)
  ~XBIOS(26+m|,0)                       !TURN ON/OFF MFP INTERUPS
  ~XBIOS(26+m|,1)
  ~XBIOS(26+m|,2)
  ~XBIOS(26+m|,5)
  ~XBIOS(26+m|,6)
  ~XBIOS(26+m|,8)
  ~XBIOS(26+m|,9)
  ~XBIOS(26+m|,10)
  ~XBIOS(26+m|,11)
  ~XBIOS(26+m|,12)
  ~XBIOS(26+m|,13)
  ~XBIOS(26+m|,14)
  ~XBIOS(26+m|,15)
RETURN
