;;  LOADER POUR LA DEMO.
;;  DEMO RETOURNE NUMERO CHOIX (0-X) A L'AIDE FONCTION $4C GEMDOS
;; PRG DEMO DOIT S'APPELER 'A:\DEMO.PRG'
	OPT	O+,W-
	OUTPUT	A:\LOADER.PRG

DEB
	MOVE.L	A7,A5
	lea PILE,a7
	MOVE.L	4(A5),A5
	MOVE.L	12(A5),D0		; TAILLE TEXT
	ADD.L	$14(A5),D0		; +TAILLE DATA
	ADD.L	$1C(A5),D0		; +TAILLE BSS
	ADD.L	#256,D0
	MOVE.L	D0,-(SP)
	MOVE.L	A5,-(SP)
	CLR.W	-(SP)
	MOVE	#$4A,-(SP)
	TRAP	#1		; SHRINK
	LEA	12(SP),SP
LOOP
	LEA	DEMO(PC),A0		; LANCE DEMO => SORTIE: 
	BSR	EXEC		; D0: NUMERO CHOIX (0-9)
	
	LEA	CHOIX_ADR(PC),A0
	ADD	D0,D0
	ADD	D0,D0
	MOVE.L	(A0,D0),A0		; PREND ADR NOM CHOIX
	TST.B	(A0)		; SI NON FICHIER=0
	BEQ.S	.TO_GEM		; ALORS VA AU GEM
	BSR	EXEC
	BRA	LOOP
.TO_GEM
	CLR	-(SP)
	TRAP	#1

EXEC	PEA	ENV(PC)
	PEA	COM(PC)
	PEA	(A0)
	CLR	-(SP)
	MOVE	#$4B,-(SP)
	TRAP	#1
	LEA	16(SP),SP
	TST.L D0
	BMI.S ERR_LEC
	RTS
ERR_LEC
	PEA ROUT_SUPER(PC)
	MOVE.W #38,-(sp)
	TRAP #14
	ADDQ.L #6,SP
	RTS
ROUT_SUPER
	NOT.W $FFFF8240.W
	BRA ROUT_SUPER
CHOIX_ADR	DC.L	NOM1,NOM2,NOM3,NOM4,NOM5,NOM6,NOM7,NOM8,NOM9,NOM10
	even
ENV
COM	DC.B	0,0
DEMO	DC.B	"A:\DEMO.PRG",0
NOM1	DC.B	"A:\CHOIX1.PRG",0
NOM2	DC.B	"A:\CHOIX2.PRG",0
NOM3	DC.B	"A:\CHOIX3.PRG",0
NOM4	DC.B	"A:\CHOIX4.PRG",0
NOM5	DC.B	"A:\CHOIX5.PRG",0
NOM6
NOM7
NOM8
NOM9
NOM10	DC.B	0
	SECTION BSS
	DS.L	512
PILE
	DS.L	1