; Reflection effect (horizontal axis)..
; Meant for inclusion in the scapegoat screen.

******** OBJECT EQUS ********

			RSRESET
scalebuf:		RS.W	1<<16
worddoubler_table:	RS.L	65536


		RSRESET
sprposentryid:	RS.W	1
sprposentryx:	RS.W	1
sprposentryy:	RS.W	1
sprpossize:	RS.B	0

		RSRESET
sprlistadr:	RS.L	1
sprlistx:	RS.W	1
sprlisty:	RS.W	1
sprlistdist:	RS.W	1
sprentrysize:	RS.B	0

******** OBJECT TABLE ********

* Must be first in object!!
scape_tbl:
	DC.L	SCAPEGOATMAINLOOP
	DC.L	INIT_SCAPEGOAT
	DC.L	SET_SCAPEGOATRES
* Add more addresses here..
	DC.L	0

******** RESOLUTION SETTING ROUTINE ********

SET_SCAPEGOATRES:
	move.l	#dummy,pal_irq

	lea	pal,a0
; new: all in 3 planes
; background part..
	move.w	#$0000,(a0)+
	move.w	#$04b3,(a0)+
	move.w	#$0291,(a0)+
	move.w	#$07e6,(a0)+
	move.w	#$03a2,(a0)+
	move.w	#$0180,(a0)+
	move.w	#$05c4,(a0)+
	move.w	#$06d5,(a0)+
; blended part..
	REPT	4
	clr.l	(a0)+
	ENDR

	bsr	blue_it_up
	bsr	calc_waves

	move.l	#TRIGGER_REFPAL,pal_irq

	IFEQ	testmode 
	move.w	$0468.w,d0
.ck466:	cmp.w	$0468.w,d0
	beq.s	.ck466

	move.l	$466.w,scapestart466

	cmpi.w	#falcon,computer
	beq.s	.falc
	clr.b	$ffff8260.w
	rts

.falc:	move.w	monitormode,d0
	cmpi.w	#vga60,d0
	beq.s	.vga
	cmpi.w	#vga100,d0
	beq.s	.vga
	cmpi.w	#rgb50,d0
	beq.s	.rgb50
* Unknown monitormode..
	rts
.vga:	lea	stlowvga_fv,a0
	bsr	CHANGE_FVFAST
	rts
.rgb50:	MOVE.L   #$3E0032,$FFFF8282.W
        MOVE.L   #$9023F,$FFFF8286.W
        MOVE.L   #$1C0035,$FFFF828A.W
        MOVE.L   #$2710265,$FFFF82A2.W
        MOVE.L   #$2F006F,$FFFF82A6.W
        MOVE.L   #$1FF026B,$FFFF82AA.W
        MOVE.W   #$200,$FFFF820A.W
        MOVE.W   #$81,$FFFF82C0.W
        CLR.W    $FFFF8266.W
        MOVE.B   #$0,$FFFF8260.W
        MOVE.W   #$0,$FFFF82C2.W
        MOVE.W   #$50,$FFFF8210.W
	ENDC
	rts

******** INIT SUBROUTINE ********

INIT_SCAPEGOAT:
	rts

******** MAINLOOP SUBROUTINE ********

SCAPEGOATMAINLOOP:
	move.l	frmcnt,d0
	sub.l	lastfrmcnt,d0
	bne.s	.res_done
	move.l	d0,-(sp)
	bsr	SET_SCAPEGOATRES
	move.l	(sp)+,d0
.res_done:
	cmpi.l	#3,d0
	bhs.s	.clear_done
	movea.l	scr,a0
	lea	piccy+34,a1
	bsr	COPY_STSCREEN
.clear_done:

	move.w	$468.w,.old468

	bsr	reflect

	moveq	#1,d7				; Limit to 1vbl speed..

; d7.w=#vbls to wait
	IFEQ	testmode
	lea	scr,a0
	move.l	(a0)+,d0
	move.l	(a0)+,d1
	move.l	(a0),-4(a0)
	move.l	d0,(a0)
	move.l	d1,-8(a0)
	move.l	d0,d1
	lsr.w	#8,d0
	move.l	d0,$ffff8200.w

	move.w	.old468(pc),d0
.cmp468:move.w	$468.w,d1
	sub.w	d0,d1
	cmp.w	d7,d1
	blt.s	.cmp468
	ENDC
	rts

.old468:DS.W	1

******** OBJECT SUBROUTINES ********

TRIGGER_REFPAL:
	movem.l	pal,d0-d3
	movem.l	d0-d3,$FFFF8240.w

	move.w	horizon,d0
	cmpi.b	#200,d0
	bhs.s	.end			; No raster if horizon off screen!
	addq.b	#1,d0

	move	#$2700,sr
	clr.b	$fffffa1b.w
	move.b	d0,$fffffa21.w
	move.l	#REFHBL,$120.w
	move.b	#8,$fffffa1b.w
	bset	#0,$fffffa07.w
	bset	#0,$fffffa13.w
	move	#$2300,sr
.end:	rts

REFHBL:	movem.l	a0-a1,-(sp)
	lea	refpal,a1
	lea	$FFFF8240.w,a0
	move.l	(a1)+,(a0)+
	move.l	(a1)+,(a0)+
	move.l	(a1)+,(a0)+
	move.l	(a1)+,(a0)+
	movem.l	(sp)+,a0-a1
	move	#$2700,sr
	bclr	#0,$fffffa07.w			; tib off
	bclr	#0,$fffffa13.w			; tib off
	rte

blue_it_up:
	lea	pal,a1
	lea	refpal,a0
	moveq	#16-1,d7	
.loop:	move.w	(a1)+,d0
	move.w	d0,d1
	andi.w	#$7,d0
	addq.w	#3,d0
	cmpi.w	#$7,d0
	blt.s	.ok
	moveq	#$7,d0
.ok:	andi.w	#$FF8,d1
	or.w	d0,d1
	move.w	d1,(a0)+
	dbf	d7,.loop
	rts

COPY_STSCREEN:
	move.w	#16000-1,d7
.loop:	move.l	(a1)+,(a0)+
	dbf	d7,.loop
	rts

; Precalc those nice waveforms for the waterreflection..
; Generates a period of 32.
calc_waves:
	lea	wavetable,a0
	move.w	#80+32-1,d7

.loop:
; get src offset..
	move.w	d7,d0
	subi.w	#32,d0
	lsl.w	#6,d0
	Do_SinModulo	d0
	lea	sine_tbl+2,a1
	Get_Sin	a1,d0,d0
	asr.w	#8,d0
	asr.w	#5,d0
	addq.w	#4,d0
	mulu.w	#160,d0
	neg.l	d0
	move.l	d0,(a0)+
	dbf	d7,.loop
	rts

; Water reflection rout.
; Source is squeezed by factor of 2.
reflect:
; setup destination..
	movea.l	scr,a0
	move.w	horizon,d0
	move.w	d0,d1
	mulu.w	#160,d0
	adda.l	d0,a0
; setup source..
	lea	-160(a0),a2
; setup wave position..
	move.w	$0468.w,d0
	andi.w	#$1F,d0
	lsl.w	#2,d0
	lea	wavetable,a3
	adda.w	d0,a3
; setup height of reflected area..
	move.w	#200-1,d7
	sub.w	horizon,d7
	cmpi.w	#200,d7
	bhs.s	.end

.loop:
; get src offset..
	move.l	(a3)+,d0
	lea	(a2,d0.l),a1

; copy line..
	movem.l	(a1)+,d0-d6/a4-a6
	movem.l	d0-d6/a4-a6,(a0)
	movem.l	(a1)+,d0-d6/a4-a6
	movem.l	d0-d6/a4-a6,40(a0)
	movem.l	(a1)+,d0-d6/a4-a6
	movem.l	d0-d6/a4-a6,80(a0)
	movem.l	(a1)+,d0-d6/a4-a6
	movem.l	d0-d6/a4-a6,120(a0)

	lea	160(a0),a0			; next dst line..
	lea	-320(a2),a2			; next src line..
	dbf	d7,.loop
.end:	rts

******** OBJECT DATA ********

	DATA

piccy:	INCBIN	TOWER.PI1

horizon:DC.W	200-13

******** OBJECT RESERVES ********

	BSS

scapestart466:
	DS.L	1
pal:	DS.W	16
refpal:	DS.W	16
wavetable:
	DS.L	200+32

******** END OF DEMO-EFFECT OBJECT ********
