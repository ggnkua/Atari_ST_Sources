* SAMPLE REPLAY ROUTINE BY POWERMAN OF THE WILD BOYS
* THIS ROUTINE IS THE SAME ONE I HAVE USED IN MY 'BETTER THAN
* THE TCB ONE' FULLSCREEN.
* YOU MUST CREDIT ME IF YOU USE THIS ROUT.......
* DO JSR INITSC TO INITIALISE EVERYTHING, AND THEN
* DO A JSR SETMUS TO START THE SAMPLE PLAYING.
* CALL ENDMUS TO STOP THE SAMPLE.
* SEE MY ARTICLE FOR FULL INSTRUCTIONS..........

SETMUS	MOVE.L #PLAY_IT,$134.W
	MOVE.B	#$11,$FFFFFA19.W
 	MOVE.B	#$28,$FFFFFA1F.W
	BSET #5,$FFFFFA07.W
	BSET #5,$FFFFFA13.W		ENABLE TIMER A
	BCLR #3,$FFFFFA17.W
	RTS

ENDMUS	BCLR #5,$FFFFFA07.W
	BCLR #5,$FFFFFA13.W		
	CLR.B $FFFFFA19.W
	BSET #3,$FFFFFA17.W
	RTS

PLAY_IT	MOVEM.L D1/A2/A3,-(A7)
	LEA SAMAD,A2			
	ADDQ.L #1,(A2)			
	SUBQ.L #1,4(A2)				End of sample?
	BEQ.S EXIT				If yes, get next sample
	MOVEA.L (A2),A2			
	MOVEQ.W #0,D1			
	MOVE.B (A2),D1			
	LSL.W #3,D1			
	LEA $FFFF8800.W,A2		
	LEA STABLE,A3				A3=sound table address		
	LEA (A3,D1.W),A3	
	MOVE.L (A3)+,D1			
	MOVEP.L D1,(A2)			
	MOVE.L (A3)+,(A2)		
	MOVEM.L (A7)+,D1/A2/A3
	RTE			
	
EXIT	MOVEA.L POS,A3						
	CMPI.L #-1,(A3)				End of table?		 
	BEQ.S .L1		
	MOVE.L (A3)+,SAMAD			Get next sample			
	MOVE.L (A3)+,SAML			
	MOVE.L A3,POS			
	MOVEM.L (A7)+,D1/A2/A3
	RTE			
.L1	LEA PLAY,A3				Start again from beginning
	MOVE.L (A3)+,SAMAD		
	MOVE.L (A3)+,SAML		
	MOVE.L A3,POS			
	MOVEM.L (A7)+,D1/A2/A3
	RTE				

INITSC	MOVEQ.W #0,D0
	LEA $FFFF8800.W,A0			SOUND CHIP ADDRESS
	MOVE.B #0,(A0)	
	MOVE.B D0,2(A0)
	MOVE.B #1,(A0)
	MOVE.B D0,2(A0)
	MOVE.B #2,(A0)
	MOVE.B D0,2(A0)
	MOVE.B #3,(A0)
	MOVE.B D0,2(A0)
	MOVE.B #4,(A0)
	MOVE.B D0,2(A0)
	MOVE.B #5,(A0)
	MOVE.B D0,2(A0)
	MOVE.B #7,(A0)
	MOVE.B #$FF,2(A0)
	MOVE.B #8,(A0)
	MOVE.B D0,2(A0)
	MOVE.B #9,(A0)
	MOVE.B D0,2(A0)
	MOVE.B #10,(A0)
	MOVE.B D0,2(A0)
	MOVE.L #PLAY+8,POS
	MOVE.L PLAY,SAMAD
	MOVE.L PLAY+4,SAML 
	RTS	

SEND	DS.B 2			

SAMAD	DS.L 1		

SAML	DS.L 1		

POS	DS.L 1

PLAY	DC.L SAM+12,34242-12
	DC.L -1

STABLE	INCBIN STABLE4.DAT	

SAM	INCBIN *.SAM		; NAME OF YOUR SAMPLE GOES HERE <<<<--
