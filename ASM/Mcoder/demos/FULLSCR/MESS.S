;
; A6=FIN FONTE OPTIMISEE
; A5=FIN MESSAGE OPTIMISE
;
; OPTIMISATION DE LA FONTE
;
; (ON NE PREND QUE LES LETTRES UTILISEES...)
;
FONTE=$40000	;DEXION.FNT CHARGE ICI !!!
NEWMESS=$50000	;ADRESSE MESSAGE OPTIMISE
FONTBIS=$60000	;ADRESSE FONTE OPTIMISEE
;
; transforme les codes ascii du message en code DEXION
;
NBCAR=42

	lea	MESSAGE(PC),A0
	LEA	FINMESS(PC),A2
	LEA	CONVERT-$20(PC),A1
	MOVEQ	#0,D0
	LEA	PRES,A3
TRANSG:
	MOVE.B	(A0),D0
	MOVE.B	(A1,D0.W),(A0)
	MOVE.B	(A0)+,D0
	ST	(A3,D0.W)
	CMP.L	A2,A0
	BLO.S	TRANSG

	LEA	PRES+1,A0	;PTR LETTRES (ON SAUTE L'ESPACE)
	LEA	FONTE,A1	;PTR FONTE
	MOVEQ	#1,D7		;ON SAUTE L'ESPACE
	LEA	NEWCONV,A2
	LEA	FONTBIS,A6
	CLR.B	(A2)+		;CODE ESPACE NE CHANGE PAS
	MOVEQ	#NBCAR-2,D6	;-1 POUR ESPACE
OPTIM:
	TST.B	(A0)+
	BNE.S	CARUSED
	CLR.B	(A2)+
	BRA.S	NXTCAR
CARUSED:
	MOVE.B	D7,(A2)+	;DEUXIEME CODAGE
	ADDQ	#1,D7
	MOVEQ	#64/4-1,D0
	MOVE.L	A1,A4
COPYFNT:MOVE.L	(A4)+,(A6)+
	DBRA	D0,COPYFNT
NXTCAR:
	LEA	64(A1),A1
	DBRA	D6,OPTIM

	LEA	MESSAGE(PC),A0
	LEA	FINMESS(PC),A2
	LEA	NEWCONV,A1
	MOVEQ	#0,D0
	LEA	NEWMESS,A5
TRANS2:
	MOVE.B	(A0)+,D0
	MOVE.B	(A1,D0.W),(A5)+
	CMP.L	A2,A0
	BLO.S	TRANS2

	ILLEGAL
CONVERT:
	DC.B	0		;' '
	DC.B	38		;!
	DC.B	0,0,0,0,0,0
	DC.B	41		;(
	DC.B	42		;)
	DC.B	0,0
	DC.B	37		;,
	DC.B	0
	DC.B	37		;.
	DC.B	40		;/
	DC.B	27,28,29,30,31	;01234
	DC.B	32,33,34,35,36	;56789
	DC.B	43		;:
	DC.B	0,0,0,0
	DC.B	39		;?
	DC.B	0
	DC.B	1,2,3,4,5	;ABCDE
	DC.B	6,7,8,9,10	;FGHIJ
	DC.B	11,12,13,14,15	;KLMNO
	DC.B	16,17,18,19,20	;PQRST
	DC.B	21,22,23,24,25	;UVWXY
	DC.B	26		;Z
	EVEN
MESSAGE:
	DC.B	'       THE STARFIRES      '
	DC.B	' ARE VERY PROUD TO PRESENT'
	DC.B	'THEIR OVERSCAN DEMO CALLED'
	DC.B	'  GENIUS HAS NO BORDER... '
	DC.B	'IT WORKS ON ALL SHIFTERS !'
	DC.B	'  IDEA AND CODE BY MCODER '
;	DC.B	' THERE IS STILL TIME LEFT '
;	DC.B	' NOT ENOUGH FOR A SCROLLER'
;	DC.B	'TRY TO READ THE SPRITES !!'
	DC.B	' NO TIME FOR A SCROLLER...'
	DC.B	'WAIT FOR OUR NEXT DEMO !!!'
;	DC.B	'        WHAT ELSE ?       '
	DC.B	'      SEE YOU SOON..      '
	DC.B	'  TRY TO BEAT DIS TCB !!! '
FINMESS:
	EVEN
PRES:	BLK.B	NBCAR,0
NEWCONV:BLK.B	NBCAR,0
