;
; 1er essai de fast-draw (pas terrible)
;  avantage : marche dans tous les cas !
;  d‚faut   : n'affiche que 42 points au maximum
;
; am‚liorations :
;	- afficher de droite … gauche (ADD plus rapide que ROR)
;	- DRAWV avec offsets
;	- transformer SUB/ADD par DIV
;	- DRAWH avec offsets
;
	OPT	O+
RETOUR	MACRO
	JMP	(A6)		;SPEEDUP
	ENDM
DESCEND	MACRO
	CMP	D4,D5
	BHI.S	\@
	\1
	BRA	DRAW1
\@	\2
	BRA	DRAW3
	ENDM
MONTE	MACRO
	CMP	D4,D5
	BHI.S	\@
	\1
	BRA	DRAW2
\@	\2
	BRA	DRAW4
	ENDM
X0Y0	MACRO
	MOVEQ	#$F,D2
	EOR	D2,D0		;INVERSE LES 4 BITS
	AND	D0,D2
	EOR	D2,D0
	ASR	#1,D0
	ADD	D1,D1
	ADD	(A1,D1.W),D0
	MOVE.L	A2,A0
	ADD	D0,A0
	MOVEQ	#0,D7
	BSET	D2,D7
	ENDM
X0Y1	MACRO
	MOVEQ	#$F,D2
	EOR	D2,D0		;INVERSE LES 4 BITS
	AND	D0,D2
	EOR	D2,D0
	ASR	#1,D0
	ADD	D3,D3
	ADD	(A1,D3.W),D0
	MOVE.L	A2,A0
	ADD	D0,A0
	MOVEQ	#0,D7
	BSET	D2,D7
	ENDM
X1Y1	MACRO
	MOVEQ	#$F,D0
	EOR	D0,D2		;INVERSE LES 4 BITS
	AND	D2,D0
	EOR	D0,D2
	ASR	#1,D2
	ADD	D3,D3
	ADD	(A1,D3.W),D2
	MOVE.L	A2,A0
	ADD	D2,A0
	MOVEQ	#0,D7
	BSET	D0,D7
	ENDM
X1Y0	MACRO
	MOVEQ	#$F,D0
	EOR	D0,D2		;INVERSE LES 4 BITS
	AND	D2,D0
	EOR	D0,D2
	ASR	#1,D2
	ADD	D1,D1
	ADD	(A1,D1.W),D2
	MOVE.L	A2,A0
	ADD	D2,A0
	MOVEQ	#0,D7
	BSET	D0,D7
	ENDM

DRAWH	MACRO
	SUB	D5,D3
	BHS.S	.\@1
	ADD	D4,D3
	OR	D0,(A0)
	LEA	\1(A0),A0
	CLR	D0
.\@1	ADD	D7,D7
	BCC.S	.\@2
	ADDX	D7,D7
	OR	D0,(A0)
	SUBQ	#8,A0
	CLR	D0
.\@2	OR	D7,D0
	ENDM

DRAWV	MACRO
	OR	D7,\1(A0)
	SUB	D4,D3
	BHS.S	.\@
	ADD	D5,D3
	ADD	D7,D7
	BCC.S	.\@
	ADDX	D7,D7
	SUBQ	#8,A0
.\@
	ENDM

CODE1	MACRO
	BRA	.1
	BRA	.2
	BRA	.3
	BRA	.4
	BRA	.5
	BRA	.6
	BRA	.7
	BRA	.8
	BRA	.9
	BRA	.10
	BRA	.11
	BRA	.12
	BRA	.13
	BRA	.14
	BRA	.15
	BRA	.16
	BRA	.17
	BRA	.18
	BRA	.19
	BRA	.20
	BRA	.21
	BRA	.22
	BRA	.23
	BRA	.24
	BRA	.25
	BRA	.26
	BRA	.27
	BRA	.28
	BRA	.29
	BRA	.30
	BRA	.31
	BRA	.32
	BRA	.33
	BRA	.34
	BRA	.35
	BRA	.36
	BRA	.37
	BRA	.38
	BRA	.39
	BRA	.40
	BRA	.41
	BRA	.42
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL

.42	DRAWH	\1
.41	DRAWH	\1
.40	DRAWH	\1
.39	DRAWH	\1
.38	DRAWH	\1
.37	DRAWH	\1
.36	DRAWH	\1
.35	DRAWH	\1
.34	DRAWH	\1
.33	DRAWH	\1
.32	DRAWH	\1
.31	DRAWH	\1
.30	DRAWH	\1
.29	DRAWH	\1
.28	DRAWH	\1
.27	DRAWH	\1
.26	DRAWH	\1
.25	DRAWH	\1
.24	DRAWH	\1
.23	DRAWH	\1
.22	DRAWH	\1
.21	DRAWH	\1
.20	DRAWH	\1
.19	DRAWH	\1
.18	DRAWH	\1
.17	DRAWH	\1
.16	DRAWH	\1
.15	DRAWH	\1
.14	DRAWH	\1
.13	DRAWH	\1
.12	DRAWH	\1
.11	DRAWH	\1
.10	DRAWH	\1
.9	DRAWH	\1
.8	DRAWH	\1
.7	DRAWH	\1
.6	DRAWH	\1
.5	DRAWH	\1
.4	DRAWH	\1
.3	DRAWH	\1
.2	DRAWH	\1
.1
	ENDM

CODE2	MACRO
	BRA	.1
	BRA	.2
	BRA	.3
	BRA	.4
	BRA	.5
	BRA	.6
	BRA	.7
	BRA	.8
	BRA	.9
	BRA	.10
	BRA	.11
	BRA	.12
	BRA	.13
	BRA	.14
	BRA	.15
	BRA	.16
	BRA	.17
	BRA	.18
	BRA	.19
	BRA	.20
	BRA	.21
	BRA	.22
	BRA	.23
	BRA	.24
	BRA	.25
	BRA	.26
	BRA	.27
	BRA	.28
	BRA	.29
	BRA	.30
	BRA	.31
	BRA	.32
	BRA	.33
	BRA	.34
	BRA	.35
	BRA	.36
	BRA	.37
	BRA	.38
	BRA	.39
	BRA	.40
	BRA	.41
	BRA	.42
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL
	ILLEGAL

.42	DRAWV	\1*41
.41	DRAWV	\1*40
.40	DRAWV	\1*39
.39	DRAWV	\1*38
.38	DRAWV	\1*37
.37	DRAWV	\1*36
.36	DRAWV	\1*35
.35	DRAWV	\1*34
.34	DRAWV	\1*33
.33	DRAWV	\1*32
.32	DRAWV	\1*31
.31	DRAWV	\1*30
.30	DRAWV	\1*29
.29	DRAWV	\1*28
.28	DRAWV	\1*27
.27	DRAWV	\1*26
.26	DRAWV	\1*25
.25	DRAWV	\1*24
.24	DRAWV	\1*23
.23	DRAWV	\1*22
.22	DRAWV	\1*21
.21	DRAWV	\1*20
.20	DRAWV	\1*19
.19	DRAWV	\1*18
.18	DRAWV	\1*17
.17	DRAWV	\1*16
.16	DRAWV	\1*15
.15	DRAWV	\1*14
.14	DRAWV	\1*13
.13	DRAWV	\1*12
.12	DRAWV	\1*11
.11	DRAWV	\1*10
.10	DRAWV	\1*9
.9	DRAWV	\1*8
.8	DRAWV	\1*7
.7	DRAWV	\1*6
.6	DRAWV	\1*5
.5	DRAWV	\1*4
.4	DRAWV	\1*3
.3	DRAWV	\1*2
.2	DRAWV	\1*1
.1
	ENDM

;
; A1=MUL160
; A2=ADRESSE ECRAN
; A0=PTR DYNAMIQUE
;
DRAW_IT
	MOVE	D2,D4
	SUB	D0,D4		;dX
	BMI	OCTANT3

	MOVE	D3,D5
	SUB	D1,D5		;dY
	BMI.S	OCTANT2
;
; dX>=0,dY>=0
;
	MONTE	X1Y1,X1Y0

OCTANT2
;
; dX>=0,dY<0
;
	NEG	D5

	DESCEND	X1Y1,X1Y0

OCTANT3
	NEG	D4

	MOVE	D3,D5
	SUB	D1,D5		;dY
	BMI.S	OCTANT4
;
; dX<0,dY>=0
;
	DESCEND	X0Y0,X0Y1

OCTANT4
;
; dX<0,dY<0
;
	NEG	D5

	MONTE	X0Y0,X0Y1

DRAW1
	MOVE	D4,D3
	LSR	#1,D3
	MOVE	D4,D2
	ADD	D2,D2
	ADD	D2,D2
	MOVE	D7,D0
	JMP	DRW1(PC,D2.W)
DRW1
	CODE1	160
	OR	D0,(A0)
	RETOUR
DRAW3
	MOVE	D5,D3
	LSR	#1,D3
	MOVE	D5,D2
	ADD	D2,D2
	ADD	D2,D2
	JMP	DRW3(PC,D2.W)
DRW3
	CODE2	-160
	OR	D7,(A0)
	RETOUR

DRAW2
	MOVE	D4,D3
	LSR	#1,D3
	MOVE	D4,D2
	ADD	D2,D2
	ADD	D2,D2
	MOVE	D7,D0
	JMP	DRW2(PC,D2.W)
DRW2
	CODE1	-160
	OR	D0,(A0)
	RETOUR
DRAW4
	MOVE	D5,D3
	LSR	#1,D3
	MOVE	D5,D2
	ADD	D2,D2
	ADD	D2,D2
	JMP	DRW4(PC,D2.W)
DRW4
	CODE2	160
	OR	D7,(A0)
	RETOUR
