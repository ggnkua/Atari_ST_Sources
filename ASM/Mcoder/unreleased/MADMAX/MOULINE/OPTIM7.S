LOAD=$40000
FREQ=$66
; FREQ=$3E POUR CHAMBER
; FREQ=$65 POUR LES AUTRES
; FREQ=$7A POUR TURRICAN
;
; 1ERE PASSE : SCANNING DES SAMPLES UTILISES
;               +ON MET LA FREQUENCE APRES $EC
;
	LEA	LOAD,A6

	MOVE.B	#$FF,D5
	CMP.L	#'TFMX',32(A6)
	BEQ.S	OK
	MOVE.B	#$80,D5
OK:

	MOVE.L	4(A6),D0
	MOVE.L	A6,A0
	ADD	(A6,D0.W),A0

	MOVE.L	A6,A1
	ADD.L	8(A6),A1

	LEA	TBSPL,A4
LOOP1:
	CMP.L	A1,A0
	BHS.S	FIN1

	CMP.B	#$EC,(A0)+
	BNE.S	LOOP1

	CLR	D0
	MOVE.B	(A0)+,D0
	ADDQ.B	#1,(A4,D0.W)
	BRA.S	LOOP1
FIN1:
;
; 2EME PASSE : RENUMEROTE LES SAMPLES
;
	MOVE.L	4(A6),D0
	MOVE.L	A6,A0
	ADD	(A6,D0.W),A0

	MOVE.L	A6,A1
	ADD.L	8(A6),A1

	LEA	TBSPL,A4
LOOPX1:
	CMP.L	A1,A0
	BHS.S	FIN2

	CMP.B	#$EC,(A0)+
	BNE.S	LOOPX1

	CLR	D1
	CLR	D0
	MOVE.B	(A0),D0
	BEQ.S	NOCNT

	MOVE.L	A4,A2
CNT:
	TST.B	(A2)+
	BEQ.S	CNT2
	ADDQ	#1,D1
CNT2:
	SUBQ	#1,D0
	BNE.S	CNT
NOCNT:
	MOVE.B	D1,(A0)+


	MOVE.B	#FREQ,D0
	CMP.B	#$3E,D0
	BEQ.S	NOFREQ
	MOVE.B	D0,(A0)+
NOFREQ:
	BRA.S	LOOPX1
FIN2:
;
; 3) STOCKE LES DEBUTS DE CHAQUE SAMPLE
;
	MOVE.L	A6,A0
	ADD.L	28(A6),A0
	MOVE	$3E(A6),D0
	LEA	STARTSPL,A2
START:
	MOVE.L	A6,A1
	ADD	(A0)+,A1
	MOVE.L	A1,(A2)+

	SUBQ	#1,D0
	BNE.S	START

	MOVE.L	A6,A0
	ADD.L	28(A6),A0
	MOVE.L	28(A6),D0
	MOVE.L	A6,A1
	ADD	(A6,D0.W),A1

	MOVE.L	A1,D7
	SUB.L	A0,D7
	LSR	#1,D7	;D7=NB SAMPLES
	CMP	$3E(A6),D7
	BNE	BUG
;
; A2=POINTEUR SUR FIN SAMPLES
;
; A0=POINTEUR SUR DIGIT 1 SOURCE
; A1=POINTEUR SUR SAMPLES SOURCE
;
;
; COMPTAGE DU NOMBRE DE SAMPLES UTILISES
;
	MOVE	D7,D0
	CLR	D6
	MOVE.L	A4,A3
COUNT:
	TST.B	(A3)+
	BEQ.S	NOTU
	ADDQ	#1,D6
NOTU:	SUBQ	#1,D0
	BNE.S	COUNT

	TST	D6
	BEQ	BUG

	MOVE	D6,LOAD+$3E

	MOVE.L	A0,A2
	ADD	D6,A2
	ADD	D6,A2
;
; A0=PTR ECRITURE OFFSETS
; A2=PTR ECRITURE OCTETS
; A1=PTR LECTURE OCTETS
;
	LEA	STARTSPL,A3
LOOP2:
	MOVE.L	(A3)+,A1
	CMP.L	A1,A2
	BHI.S	BUG

	TST.B	(A4)+
	BEQ.S	NOTUSED

	MOVE.L	A2,D0
	SUB.L	A6,D0
	MOVE	D0,(A0)+
COPY2:
	MOVE.B	(A1)+,D0
	MOVE.B	D0,(A2)+
	CMP.B	D5,D0
	BLO.S	COPY2
NOTUSED:
	SUBQ	#1,D7
	BNE.S	LOOP2

	LEA	LOAD,A0
	MOVE.L	A2,A1

	ILLEGAL
BUG:	MOVEQ	#-1,D0
	ILLEGAL

STARTSPL:BLK.L	100,0
TBSPL:	BLK.B	100,0
