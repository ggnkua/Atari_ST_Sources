;
; DETECTEUR DE PATTERNS (PAS LES SEQUENCES !!!) INUTILISES
;
LOAD=$40000
SAVE=$50000
LONG=32000

PRINT:	MACRO
	PEA	?1
	MOVE	#9,-(SP)
	TRAP	#1
	ADDQ	#6,SP
	ENDM

	MOVE	#$2F,-(SP)
	TRAP	#1
	ADDQ	#2,SP
	MOVE.L	D0,DTA

	LEA	DIRECTORY,A6

	CLR	-(SP)
	PEA	FILE(PC)
	MOVE	#$4E,-(SP)
	TRAP	#1
	ADDQ	#8,SP
	TST	D0
	BNE	ERREUR
PUSHDIR:
	MOVE.L	DTA,A0
	LEA	30(A0),A0
COPYDIR:MOVE.B	(A0)+,(A6)+
	BNE.S	COPYDIR

	MOVE	#$4F,-(SP)
	TRAP	#1
	ADDQ	#2,SP
	TST	D0
	BEQ.S	PUSHDIR
	ST	(A6)

	LEA	DIR,A6
LOADER:
	MOVEQ	#0,D0
	TST.B	(A6)
	BMI	ERREUR

	PRINT	CR0(PC)
	PRINT	(A6)
	PRINT	CR1(PC)

	LEA	LOAD,A0
	MOVE	#LONG/4-1,D0
	MOVEQ	#0,D1
CLEARE:	MOVE.L	D1,(A0)+
	DBRA	D0,CLEARE

	CLR	-(SP)
	PEA	(A6)
	MOVE	#$3D,-(SP)
	TRAP	#1
	ADDQ	#8,SP
	MOVE.L	D0,D7
	BMI	ERREUR

	PEA	LOAD
	PEA	LONG
	MOVE	D7,-(SP)
	MOVE	#$3F,-(SP)
	TRAP	#1
	LEA	12(SP),SP
	TST.L	D0
	BMI.L	ERREUR
	MOVE.L	D0,NBBYTES

	MOVE	D7,-(SP)
	MOVE	#$3E,-(SP)
	TRAP	#1
	ADDQ	#4,SP

	LEA	LOAD,A5

	PEA	(A6)
	BSR	FILTRE
	MOVE.L	(SP)+,A6

LOOP:	TST.B	(A6)+
	BNE.S	LOOP
	BRA	LOADER

ERREUR:
	MOVEQ	#-1,D1
	illegal

FILTRE:
	LEA	FREQ,A0
	LEA	RENUMBUF,A1
	MOVE	#256-1,D0
CLEAR:	CLR.B	(A0)+
	CLR.B	(A1)+
	DBRA	D0,CLEAR

	MOVE.L	A5,A0
	ADD.L	$10(A5),A0
	MOVE.L	A0,A1

	MOVE	LOAD+$2A,D0
	ADDQ	#1,D0
	MULU	#12,D0
	ADD	D0,A1


;
; CALCUL DES PATTERNS UTILISES
;
	LEA	FREQ,A2
	MOVE.L	A0,A3
CFR:
	CMP.L	A1,A0
	BHS.S	FINFR
	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	ADDQ	#4,A0
	ADDQ.B	#1,(A2,D0.W)
	BNE.S	CFR
	ST	(A2,D0.W)
	BRA.S	CFR
FINFR:
;
; RENUMEROTE LES PATTERNS
;
	LEA	FREQ,A2
	LEA	RENUMBUF,A3
	MOVE	#256-1,D0
	MOVEQ	#0,D7
RENUM1:
	TST.B	(A2)+
	BEQ.S	DEL1
	MOVE.B	D7,(A3)+
	ADDQ	#1,D7
	BRA.S	RENUM2
DEL1:	ST	(A3)+
RENUM2:
	DBRA	D0,RENUM1

	CMP	LOAD+$28,D7
	BNE	RENUMALL
	RTS
RENUMALL:

	MOVE	D7,NBPAT

	PRINT	OPTIM(PC)

	LEA	SAVE,A6
	MOVE.L	A5,A0
	MOVE.L	A0,A1
	ADD.L	$C(A5),A1

COPY1:	MOVE	(A0)+,(A6)+
	CMP.L	A1,A0
	BLO.S	COPY1

	MOVE.L	A6,A4
	ADD	NBPAT,A6
	ADD	NBPAT,A6

	LEA	FREQ,A1
	MOVE	#256-1,D7
LOP2:
	TST.B	(A1)+
	BEQ.S	SKIP1

	MOVE.L	A6,D0
	SUB.L	#SAVE,D0
	MOVE	D0,(A4)+

	MOVE.L	A5,A2
	ADD	(A0),A2
COPY2:
	MOVE.B	(A2)+,D0
	MOVE.B	D0,(A6)+
	CMP.B	#$FD,D0
	BLO.S	COPY2
	CMP.B	#$FF,D0
	BEQ.S	OK1
	MOVE.B	(A2)+,(A6)+
	BRA.S	COPY2
OK1:
SKIP1:
	ADDQ	#2,A0
	DBRA	D7,LOP2

	MOVE	A6,D0
	LSR	#1,D0
	BCC.S	PAIR1
	CLR.B	(A6)+
PAIR1:

	MOVE.L	A6,D0
	SUB.L	#SAVE,D0
	MOVE.L	D0,SAVE+$10
	MOVE	NBPAT,D0
	MOVE	D0,SAVE+$28

	MOVE.L	A5,A0
	ADD.L	$10(A5),A0
	MOVE.L	A0,A1

	MOVE	LOAD+$2A,D0
	ADDQ	#1,D0
	MULU	#12,D0
	ADD	D0,A1
	LEA	RENUMBUF,A2
COPY3:
	CLR	D0
	MOVE.B	(A0)+,D0
	MOVE.B	(A2,D0.W),(A6)+
	MOVE.B	(A0)+,(A6)+
	MOVE.B	(A0)+,(A6)+
	MOVE.B	(A0)+,(A6)+
	CMP.L	A1,A0
	BLO.S	COPY3

	MOVE.L	A6,D0
	SUB.L	#SAVE,D0
	MOVE.L	D0,SAVE+$14
	CLR.L	SAVE+$18

	LEA	LOAD,A1
	ADD.L	NBBYTES,A1

COPY4:
	MOVE	(A0)+,(A6)+
	CMP.L	A1,A0
	BLO.S	COPY4

;	LEA	LOAD,A0
;	MOVE.L	A0,A1
;	ADD.L	NBBYTES,A1
;	ILLEGAL

	LEA	SAVE,A0
	MOVE.L	A6,A1
	MOVEQ	#0,D0
	ILLEGAL
	RTS


NBPAT:	DC.W	0
FREQ:	BLK.B	256,0
RENUMBUF:BLK.B	256,0

NBBYTES:DC.L	0
DTA:	DC.L	0
CHANGE:	DC.B	0
CR:	DC.B	13,10,0
CR0:	DC.B	13,0
CR1:	DC.B	27,'K',0
FILE:	DC.B	'*.MUS',0
OPTIM:	DC.B	9,'OPTIMISABLE',13,10,0

	EVEN
DIRECTORY:
DIR:
	BLK.B	1000
