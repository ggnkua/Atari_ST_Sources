;
; AMELIORE LES FICHIERS DIGITS ET LES CONVERTIT EN STE
;  (DIGITS EN ADRESSES PAIRES)
;
LOAD=$80000
SAVE=$90000
LONG=32000
STE=1		;0=FORMAT STE

PRINT:	MACRO
	PEA	?1
	MOVE	#9,-(SP)
	TRAP	#1
	ADDQ	#6,SP
	ENDM

	MOVE	#$2F,-(SP)
	TRAP	#1
	ADDQ	#2,SP
	MOVE.L	D0,DTA

	LEA	DIRECTORY,A6

	CLR	-(SP)
	PEA	FILE(PC)
	MOVE	#$4E,-(SP)
	TRAP	#1
	ADDQ	#8,SP
	TST	D0
	BNE	ERREUR
PUSHDIR:
	MOVE.L	DTA,A0
	LEA	30(A0),A0
COPYDIR:MOVE.B	(A0)+,(A6)+
	BNE.S	COPYDIR

	MOVE	#$4F,-(SP)
	TRAP	#1
	ADDQ	#2,SP
	TST	D0
	BEQ.S	PUSHDIR
	ST	(A6)

	LEA	DIR,A6
LOADER:
	MOVEQ	#0,D0
	TST.B	(A6)
	BMI	ERREUR

	PRINT	CR0(PC)
	PRINT	(A6)
	PRINT	CR1(PC)

	LEA	LOAD,A0
	MOVE	#LONG/4-1,D0
	MOVEQ	#0,D1
CLEARE:	MOVE.L	D1,(A0)+
	DBRA	D0,CLEARE

	CLR	-(SP)
	PEA	(A6)
	MOVE	#$3D,-(SP)
	TRAP	#1
	ADDQ	#8,SP
	MOVE.L	D0,D7
	BMI	ERREUR

	PEA	LOAD
	PEA	LONG
	MOVE	D7,-(SP)
	MOVE	#$3F,-(SP)
	TRAP	#1
	LEA	12(SP),SP
	TST.L	D0
	BMI.L	ERREUR
	MOVE.L	D0,NBBYTES

	MOVE	D7,-(SP)
	MOVE	#$3E,-(SP)
	TRAP	#1
	ADDQ	#4,SP

	LEA	LOAD,A5

	CMP.L	#'DIGI',(A5)
	BNE	LOOP

	LEA	SAVE,A4
	MOVE	#LONG/4-1,D0
CLRL:	CLR.L	(A4)+
	DBRA	D0,CLRL

	LEA	LOAD,A3
	LEA	SAVE,A4
;
; 1) COPIE DU GROS
;
	MOVE.L	A3,A0
	ADD.L	$14(A0),A0
COPY:	MOVE	(A3)+,(A4)+
	CMP.L	A0,A3
	BLO.S	COPY
;
; 2) COPIE DES DIGITS
;
	MOVEQ	#-1,D7		;$FF
	CMP.L	#'TFMX',32(A5)
	BEQ	DIG1
	CMP.L	#'JOCH',32(A5)
	BNE	ERREURDIG
	MOVEQ	#-$80,D7
DIG1:
	LEA	LOAD,A3
	ADD.L	$1C(A3),A3

	MOVE.L	A4,D0
	SUB.L	#SAVE,D0
	MOVE.L	D0,SAVE+$1C

	LEA	LOAD,A0
	ADD	(A3),A0
	MOVE.L	A0,A1

	MOVE.L	A4,A2
	ADD.L	A0,A4
	SUB.L	A3,A4

COPYDIG:
	MOVE.L	A1,D0
	SUB.L	#LOAD,D0
	CMP	(A3)+,D0
	BNE	ERREURDIG

	MOVE.L	A4,D0
	SUB.L	#SAVE,D0
	MOVE	D0,(A2)+

	IF	STE=0
	BRA	COPD
	ENDIF

	CMP.L	#'JOCH',32(A5)	;DEJA 4 BITS
	BEQ	COPD
L1:
	CLR	D0
	MOVE.B	(A1)+,D0
	CMP.B	D7,D0
	BHS.S	L2
	LSR.B	#4,D0
	MOVE.B	L4E(PC,D0.W),(A4)+
	BRA.S	L1
L2:	MOVE.B	#$88,(A4)+
	BRA	PAIR

L4E:	DC.B	$0D,$0D,$0E,$0E,$0E,$0F,$0F,$0F
	DC.B	$00,$07,$09,$0A,$0B,$0C,$0C,$0D

COPD:
	MOVE.B	(A1)+,D0
	CMP.B	D7,D0
	BHS.S	FINDIG
	MOVE.B	D0,(A4)+
	BRA.S	COPD
FINDIG:
	MOVE.B	D0,(A4)+

	IF	STE=1
	BRA	PAIR		;PAS DE SORITE STE
	ENDIF

	CMP.L	#'JOCH',32(A5)	;NE SERT A RIEN DE GRATTER...
	BEQ.S	PAIR		;PAS DE SORTIE STE
	MOVE	A4,D1
	LSR	#1,D1
	BCC.S	PAIR
	MOVE.B	D0,(A4)+	;PARITE POUR STE
PAIR:
	CMP.L	A0,A3
	BLO.S	COPYDIG

	IF	STE=1
	MOVE.L	#'JOCH',SAVE+32
	ENDIF

	MOVE	A4,D1
	LSR	#1,D1
	BCC.S	PAIR2
	CLR.B	(A4)+
PAIR2:
	MOVE.L	A4,D0
	SUB.L	#SAVE,D0
	MOVE.L	D0,SAVE+$14

	LEA	LOAD,A0
	ADD.L	$14(A0),A0
COPYMUS:
	MOVE	(A0)+,(A4)+
	MOVE	(A0)+,(A4)+
	MOVE	(A0)+,(A4)+
	MOVE	-6(A0),D0
	OR	-4(A0),D0
	OR	-2(A0),D0
	BNE.S	COPYMUS
	SUBQ	#6,A4

	BSR	SAUVE

	BRA	LOOP

ERREURDIG:
	MOVEQ	#-1,D0
	ILLEGAL

LOOP:	TST.B	(A6)+
	BNE.S	LOOP
	BRA	LOADER
ERREUR:
	MOVEQ	#-1,D1
	illegal

SAUVE:
	PRINT	CR(PC)
;	RTS
;	ILLEGAL

	MOVE.L	A6,A0
SRC:	CMP.B	#'.',(A0)+
	BNE.S	SRC
	MOVE.B	#'M',(A0)+
	MOVE.B	#'U',(A0)+
	MOVE.B	#'S',(A0)+

	CLR	-(SP)
	PEA	(A6)
	MOVE	#$3C,-(SP)
	TRAP	#1
	ADDQ	#8,SP
	MOVE.L	D0,D7
	BMI	ERREUR

	PEA	SAVE
	MOVE.L	A4,D6
	SUB.L	#SAVE,D6
	MOVE.L	D6,-(SP)
	MOVE	D7,-(SP)
	MOVE	#$40,-(SP)
	TRAP	#1
	LEA	12(SP),SP
	CMP.L	D6,D0
	BNE	ERREUR

	MOVE	D7,-(SP)
	MOVE	#$3E,-(SP)
	TRAP	#1
	ADDQ	#4,SP
	RTS

NBBYTES:DC.L	0
DTA:	DC.L	0
CR:	DC.B	13,10,0
CR0:	DC.B	13,0
CR1:	DC.B	27,'K',0
FILE:	DC.B	'*.PAK',0

	EVEN
DIRECTORY:
DIR:
	BLK.B	1000

