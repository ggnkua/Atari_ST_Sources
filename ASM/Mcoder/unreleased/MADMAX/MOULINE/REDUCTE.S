LOAD=$40000
SAVE=$50000
MUS=2		;NUMERO DE LA MUSIQUE SAUVEE
;
; ANALYSEUR DE MUSIQUE MAD-MAX
;
; ENLEVE LES MUSIQUES NON VOULUES
;
	LEA	LOAD,A5
	LEA	SAVE,A6
	CMP.L	#'COSO',(A5)
	BNE	ERREUR
	CMP.L	#'TFMX',32(A5)
	BNE	ERREUR


	MOVE.L	A5,A0
	ADD.L	$14(A5),A0
	MOVEQ	#0,D7
MAXD7:
	MOVE	(A0),D0
	OR	2(A0),D0
	OR	4(A0),D0
	BEQ.S	FIN1

	CMP	2(A0),D7
	BHS.S	LOWD7
	MOVE	2(A0),D7
LOWD7:
	ADDQ	#6,A0
	BRA.S	MAXD7
FIN1:
;
; D7=DERNIER PATTERN=$2A(A5)
;
	MOVE.L	A5,A0
	ADD.L	$14(A5),A0
	MOVE	#MUS,D0
	SUBQ	#1,D0
	MULU	#6,D0
	ADD	D0,A0

	MOVE	(A0)+,PUSH1	;DEBUT SEQUENCE
	MOVE	(A0)+,PUSH2	;FIN SEQUENCE
	MOVE	(A0)+,PUSH3	;VITESSE

	MOVE.L	A5,A0
	ADD.L	$10(A5),A0
	MOVE.L	A0,A1

	MOVE	PUSH1,D0
	MULU	#12,D0
	ADD	D0,A0

	MOVE	PUSH2,D0
	ADDQ	#1,D0
	MULU	#12,D0
	ADD	D0,A1
;
; CALCUL DES PATTERNS UTILISES
;
	LEA	FREQ,A2
	MOVE.L	A0,A3
CFR:
	CMP.L	A1,A0
	BHS.S	FINFR
	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	ADDQ	#4,A0
	ADDQ.B	#1,(A2,D0.W)
	BNE.S	CFR
	ST	(A2,D0.W)
	BRA.S	CFR
FINFR:
;
; RENUMEROTE LES PATTERNS
;
	LEA	FREQ,A2
	LEA	RENUMBUF,A3
	MOVE	#256-1,D0
	MOVEQ	#0,D7
RENUM1:
	TST.B	(A2)+
	BEQ.S	DEL1
	MOVE.B	D7,(A3)+
	ADDQ	#1,D7
	BRA.S	RENUM2
DEL1:	ST	(A3)+
RENUM2:
	DBRA	D0,RENUM1

	MOVE	D7,NBPAT

	MOVE.L	A5,A0
	MOVE.L	A0,A1
	ADD.L	$C(A5),A1

COPY1:	MOVE	(A0)+,(A6)+
	CMP.L	A1,A0
	BLO.S	COPY1

	MOVE.L	A6,A4
	ADD	NBPAT,A6
	ADD	NBPAT,A6

	LEA	FREQ,A1
	MOVE	#256-1,D7
LOP2:
	TST.B	(A1)+
	BEQ.S	SKIP1

	MOVE.L	A6,D0
	SUB.L	#SAVE,D0
	MOVE	D0,(A4)+

	MOVE.L	A5,A2
	ADD	(A0),A2

COPY2:
	MOVE.B	(A2)+,D0
	MOVE.B	D0,(A6)+
	CMP.B	#$FD,D0
	BLO.S	COPY2
	CMP.B	#$FF,D0
	BEQ.S	OK1
	MOVE.B	(A2)+,(A6)+
	BRA.S	COPY2
OK1:
SKIP1:
	ADDQ	#2,A0
	DBRA	D7,LOP2

	MOVE	A6,D0
	LSR	#1,D0
	BCC.S	PAIR1
	CLR.B	(A6)+
PAIR1:

	MOVE.L	A6,D0
	SUB.L	#SAVE,D0
	MOVE.L	D0,SAVE+$10

	MOVE.L	A5,A0
	ADD.L	$10(A5),A0
	MOVE.L	A0,A1

	MOVE	PUSH1,D0
	MULU	#12,D0
	ADD	D0,A0

	MOVE	PUSH2,D0
	ADDQ	#1,D0
	MULU	#12,D0
	ADD	D0,A1
	LEA	RENUMBUF,A2
COPY3:
	CLR	D0
	MOVE.B	(A0)+,D0
	MOVE.B	(A2,D0.W),(A6)+
	MOVE.B	(A0)+,(A6)+
	MOVE.B	(A0)+,(A6)+
	MOVE.B	(A0)+,(A6)+
	CMP.L	A1,A0
	BLO.S	COPY3

	MOVE.L	A6,D0
	SUB.L	#SAVE,D0
	MOVE.L	D0,SAVE+$14
	CLR.L	SAVE+$18

	CLR	(A6)+
	MOVE	PUSH2,D0
	SUB	PUSH1,D0
	MOVE	D0,(A6)+
	MOVE	PUSH3,(A6)+

	LEA	SAVE,A0
	MOVE.L	A6,A1
	MOVEQ	#0,D0
	ILLEGAL


ERREUR:
	MOVEQ	#-1,D0
	ILLEGAL
NBPAT:	DC.W	0
PUSH1:	DC.W	0
PUSH2:	DC.W	0
PUSH3:	DC.W	0
FREQ:	BLK.B	256,0
RENUMBUF:BLK.B	256,0
