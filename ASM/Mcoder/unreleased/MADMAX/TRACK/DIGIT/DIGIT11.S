;
; TRANSFORMEUR DE FICHIER DIGIT MADMAX EN FICHIER NORMAL
;
SOURCE=$40E8E
DIGITS=$418E6
DEST=$50000
O=DEST
FINDIGIT=$FF

	MOVE.L	#DIGITS-SOURCE,SOURCE+28

	LEA	SOURCE,A5
	LEA	DEST,A6
	CMP.L	#'COSO',(A5)
	BNE	ERREUR
	MOVE.L	#'DIGI',(A6)+
;
; 7 POINTEURS EN LONG A AJUSTER AU FUR ET A MESURE
;
	MOVEQ	#-1,D0
	MOVE.L	D0,(A6)+
	MOVE.L	D0,(A6)+
	MOVE.L	D0,(A6)+
	MOVE.L	D0,(A6)+
	MOVE.L	D0,(A6)+
	MOVE.L	D0,(A6)+
	MOVE.L	D0,(A6)+

	IF	FINDIGIT=$FF
	MOVE.L	#'TFMX',(A6)+
	ELSE
	MOVE.L	#'JOCH',(A6)+
	ENDIF

	LEA	$24(A5),A5
	MOVEQ	#28-1,D0
COP1:	MOVE.B	(A5)+,(A6)+
	DBRA	D0,COP1

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE.L	D0,DEST+4

	MOVE	DEST+36,D7	;NB INS-1
	MOVE.L	A5,A3
	MOVE.L	A6,A4
	MOVE	D7,D0
	ADDQ	#1,D0
	ADD	D0,D0
	ADD	D0,A6
	ADD	D0,A5
	ADD	D0,A5
COP2:
	MOVE.L	(A3)+,D0
	ADD.L	#SOURCE,D0
	CMP.L	A5,D0
	BNE	ERREUR

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE	D0,(A4)+

	MOVEQ	#64-1,D1

COP3:
	MOVE.B	(A5)+,D0
	CMP.B	#$EB,D0
	BHS	ERREUR2

	CMP.B	#$E7,D0
	BNE.S	NOTE7
	MOVE.B	#$EB,D0
NOTE7:
	CMP.B	#$EA,D0
	BNE.S	NOTEA
	MOVE.B	#$EC,D0
NOTEA:
	MOVE.B	D0,(A6)+

	CMP.B	#$E1,D0
	BEQ.S	E1
	CMP.B	#$E0,D0
	BEQ.S	E0

	DBRA	D1,COP3
	BRA.S	E1
E0:
	MOVE.B	(A5)+,(A6)+
E1:
	DBRA	D7,COP2

	BSR	PARITE

	MOVE.L	A5,D0
	SUB.L	#SOURCE,D0
	CMP.L	SOURCE+8,D0
	BNE	ERREUR

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE.L	D0,DEST+8

	MOVE	DEST+38,D7	;NB INS-1
	MOVE.L	A5,A3
	MOVE.L	A6,A4
	MOVE	D7,D0
	ADDQ	#1,D0
	ADD	D0,D0
	ADD	D0,A6
	ADD	D0,A5
	ADD	D0,A5
LCOP2:
	MOVE.L	(A3)+,D0
	ADD.L	#SOURCE,D0
	CMP.L	A5,D0
	BNE	ERREUR

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE	D0,(A4)+

	MOVEQ	#64-1,D1
LCOP3:
	MOVE.B	(A5)+,D0
	MOVE.B	D0,(A6)+

	CMP.B	#$E0,D0
	BLO.S	NOERR

	CMP.B	#$E1,D0
	BEQ.S	LE1
	CMP.B	#$E0,D0
	BEQ.S	LE0

	CMP.B	#$E8,D0
	BEQ.S	NOERR

	ILLEGAL
NOERR:
	DBRA	D1,LCOP3

;	MOVEQ	#64/4-1,D0
;	LEA	-64(A6),A0
;	MOVEQ	#0,D1
;LO:	OR.L	(A0)+,D1
;	DBRA	D0,LO
;
;	TST.L	D1
;	BNE	ERREUR

;	LEA	-64(A6),A6
;	MOVE.B	#0,(A6)+
;	MOVE.B	#$E1,(A6)+

	BRA.S	LE1
LE0:
	MOVE.B	(A5)+,(A6)+
LE1:
	DBRA	D7,LCOP2

	BSR	PARITE

	MOVE.L	A5,D0
	SUB.L	#SOURCE,D0

	CMP.L	SOURCE+12,D0
	BNE	ERREUR

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE.L	D0,DEST+12

	MOVE	DEST+40,D7	;NB INS-1
	MOVE.L	A5,A3
	MOVE.L	A6,A4
	MOVE	D7,D0
	ADDQ	#1,D0
	ADD	D0,D0
	ADD	D0,A6
	ADD	D0,A5
	ADD	D0,A5

MCOP2:
	MOVE.L	(A3)+,D0
	ADD.L	#SOURCE,D0
	CMP.L	A5,D0
	BNE	ERREUR

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE	D0,(A4)+

MCOP3:
	MOVE.B	(A5)+,D0
	MOVE.B	D0,(A6)+

	CMP.B	#$FF,D0
	BEQ.S	FF

	MOVE.B	(A5)+,D1
	MOVE.B	D1,(A6)+

	CMP.B	#$FD,D0
	BEQ.S	MCOP3
	CMP.B	#$FE,D0
	BEQ.S	MCOP3

	AND	#$E0,D1
	BEQ.S	MCOP3
	MOVE.B	(A5)+,(A6)+
	BRA.S	MCOP3
FF:
	DBRA	D7,MCOP2

	BSR	PARITE

	MOVE.L	A5,D0
	SUB.L	#SOURCE,D0
	CMP.L	SOURCE+16,D0
	BNE	ERREUR

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE.L	D0,DEST+16

	MOVE	DEST+42,D7	;NB PAT-1
	ADDQ	#1,D7
	MULU	#12/4,D7
	SUBQ	#1,D7
COP4:
	MOVE.L	(A5)+,(A6)+
	DBRA	D7,COP4

	MOVE.L	A5,D0
	SUB.L	#SOURCE,D0
	CMP.L	SOURCE+20,D0
	BNE	ERREUR

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE.L	D0,DEST+20

COP5:
	MOVE	(A5),D0
	OR	2(A5),D0
	OR	4(A5),D0

	MOVE	(A5)+,(A6)+
	MOVE	(A5)+,(A6)+
	MOVE	(A5)+,(A6)+
	TST	D0
	BNE.S	COP5

	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE.L	D0,DEST+28

	LEA	SOURCE,A5
	ADD.L	SOURCE+28,A5

	CLR	D7
	MOVE.L	A5,A0
COUNT:
	MOVE	(A0),D0		;DERNIER SAMPLE=FIN SAMPLE
	BLE.S	FINCNT
	ADDQ	#1,D7
	ADDQ	#8,A0
	BRA.S	COUNT
FINCNT:
	MOVE.L	A5,A3
	MOVE.L	A6,A4

	SUBQ	#1,D7
	BMI.S	ERREUR

	MOVE	D7,DEST+62	;NB SAMPLES

	ADD	D7,A6
	ADD	D7,A6
	SUBQ	#1,D7
COP6:
	MOVE.L	A6,D0
	SUB.L	#DEST,D0
	MOVE	D0,(A4)+

	MOVE	(A5),D0
	MOVE.L	A3,A0
	ADD	D0,A0

COP7:
	MOVE.B	(A0)+,D0
	MOVE.B	D0,(A6)+
	CMP.B	#FINDIGIT,D0
	BLO.S	COP7

	ADDQ	#8,A5

	DBRA	D7,COP6

	BSR	PARITE

	LEA	DEST,A0
	MOVE.L	A6,A1

	MOVEQ	#0,D0
	ILLEGAL

ERREUR:
	ILLEGAL
ERREUR2:ILLEGAL

PARITE:
	MOVE	A6,D0
	LSR	#1,D0
	BCC.S	PAIR1
	CLR.B	(A6)+
PAIR1:

	MOVE	A5,D0
	LSR	#1,D0
	BCC.S	PAIR2
	ADDQ	#1,A5
PAIR2:
	RTS
