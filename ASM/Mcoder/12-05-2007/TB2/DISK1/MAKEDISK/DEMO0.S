;
; EXEMPLE TYPE DE DEMO
;
; 0=INTRO
; 1=MAIN MENU
; 2=DEMO STANDARD
; 3=DEMO STANDARD
;
DEMO=0
	IFEQ	DEMO-0
	OUTPUT	DEMO0.PRG
	ENDC
	IFEQ	DEMO-1
	OUTPUT	DEMO1.PRG
	ENDC
	IFEQ	DEMO-2
	OUTPUT	DEMO2.PRG
	ENDC
	IFEQ	DEMO-3
	OUTPUT	DEMO3.PRG
	ENDC

	ORG	$8000

	MOVE	#$2700,SR
	MOVEQ	#0,D0
	MOVE.L	D0,A0
	MOVEP.L	D0,$FFFFFA01(A0)
	MOVEP.L	D0,$FFFFFA09(A0)
	MOVEP.L	D0,$FFFFFA11(A0)
	MOVEP.L	D0,$FFFFFA19(A0)

	MOVE.L	#VBL,$70.W
	MOVE	#$2300,SR
	CLR	SYNC
WAITVBL:
	TST	SYNC
	BEQ.S	WAITVBL
	CLR.B	$FFFF8260.W
	MOVE.B	#2,$FFFF820A.W
	MOVEM.L	PALETTE(PC),D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	MOVE.L	#$00070080,$FFFF8200.W

	LEA	$78000,A0
	MOVE	#32000/4-1,D0
	MOVEQ	#0,D1
.CLS	MOVE.L	D1,(A0)+
	DBRA	D0,.CLS

	LEA	MESSAGE,A5		;MESSAGE A AFFICHER
	LEA	$78000,A6		;ECRAN

.AFF1
	MOVEQ	#0,D0
	MOVE.B	(A5)+,D0
	BEQ.S	.FINI

	BPL.S	.NOCR

	MOVE.L	A6,D0
	SUB.L	#$78000,D0
	DIVU	#160,D0
	MULU	#160,D0
	ADD.L	#$78000+8*160,D0
	MOVE.L	D0,A6
	BRA.S	.AFF1

.NOCR
	BSR	CONV
	ASL	#3,D0
	LEA	FONTE,A0
	ADD	D0,A0

X	SET	0
	REPT	8
	MOVE.B	(A0)+,X(A6)
X	SET	X+160
	ENDR

	ADDQ	#1,A6
	MOVE	A6,D0
	LSR	#1,D0
	BCS.S	.AFF1
	ADDQ	#6,A6
	BRA.S	.AFF1

.FINI:
	IFEQ	DEMO-1
.WW
	CLR	D0
	MOVE.B	$FFFFFC02.W,D0
	SUB.B	#$3B,D0
	CMP.B	#2,D0
	BHS.S	.WW
	ADDQ	#2,D0
	MOVE	D0,$4FE.W
	ELSEIF
.WW	CMP.B	#$39,$FFFFFC02.W
	BNE.S	.WW
	ENDC

	JMP	$500.W
VBL:	ADDQ	#1,SYNC
	RTE
;
; ENTREE :
;  D0=CODE A CONVERTIR
; SORTIE :
;  D0=CODE
;
CONV:
	LEA	CONVERT(PC),A0
CV1:
	MOVE.B	(A0)+,D1
	BMI.S	FINCV
	CMP.B	D1,D0
	BNE.S	CV1
FINCV:
	MOVE.L	A0,D0
	SUB.L	#CONVERT+1,D0
	RTS

SYNC:	DS.W	1

PALETTE:DC.W	0
	DCB.W	15,$777

CONVERT:
	DC.B	'0123456789'
	DC.B	'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
	DC.B	"?!:;,'()-+ "
	DC.B	-1
;
; 0=END
; -1=CR
;
MESSAGE:
	IFEQ	DEMO-0
	DC.B	"INTRO",0
	ENDC
	IFEQ	DEMO-1
	DC.B	"F1->DEMO 1",-1
	DC.B	"F2->DEMO 2",0
	ENDC
	IFEQ	DEMO-2
	DC.B	"DEMO 1",0
	ENDC
	IFEQ	DEMO-3
	DC.B	"DEMO 2",0
	ENDC
	EVEN
FONTE:
	INCBIN	FONTE1.BIN
