;
; … utiliser aprŠs avoir compact‚ un fichier avec H25.S
;
; DECOMPACTEUR HUFFMANN 8 BITS OPTIMAL ???
;
; 03/09/89
;
; le 06/09/89, j'ai trouv‚ une optimisation dans la routine
;  de d‚codage d'un octet Huffmann !!! (la trouveras-tu ?)
; on peut donc encore gagner quelques cycles !!!!
;
; actuellement, le d‚compacteur d"compresse … une vitesse (th‚orique)
;  de 20 Kilooctets/sec !!!
; avec mon optimisation, on gagne peut-etre 512 octets/sec...
;
; LONGUEUR : 100 OCTETS !!! (C'EST UNE PETITE MERVEILLE...)
;
LOADED=$40000		;ADRESSE DE CHARGEMENT DU CODE COMPRESSE
DEBUT=$50000		;ADRESSE DE DECOMPRESSION
;
; LA FIN DU FICHIER EST AUTOMATIQUEMENT DETECTEE GRACE
;   AU CODE DE FIN DU FICHIER QUI EST LE DERNIER DANS L'ARBRE
;
; DEFAUTS :
;	- UN OCTET EST SAUVE EN TROP
;	- BUG DANS H25.S -> CET OCTET EST $FF
;
; REGISTRES UTILISES :
;	D0,D1,D2
;	A0,A4,A5,A6
;
; PROFONDEUR DE PILE UTILISEE :
;	NB BITS HUFFMANN MAXIMUM*8
;	+4 POUR LE PREMIER BSR
;	+(NB DE NOEUDS DE L'ARBRE+NB FEUILLES)*2
;	+QUELQUES OCTETS POUR LES INTERRUPTIONS SI MODE SUPERVISEUR
;
; SOIT A PEU PRES 31*8+4+??*2+60
;
; 1500 OCTETS SEMBLENT SUFFIRE AMPLEMENT
;
	LEA	-1500(A7),A4
	LEA	LOADED,A2
	LEA	DEBUT,A5

	TAS	D0
	NEG.B	D0		;MET C … 1

	MOVE.L	A4,A6
	BSR.S	DECODE0		;COLLECTE LES NOEUDS A PARTIR DE A4
;
; ICI :
; A4=DEBUT ARBRE
; A6=FIN ARBRE
;
	MOVE	(A4)+,D1	;STOCKE LA RACINE DANS UN REGISTRE
	BMI.S	FINI		;SI ARBRE=UN SEUL OCTET (IMPOSSIBLE)
				;C'EST QU'IL N'Y A RIEN A FAIRE !!!
BCL:
;
; ON DECODE UN OCTET EN PARCOURANT L'ARBRE
;
	MOVE.L	A4,A0
	MOVE	D1,D0		;RELIT LA RACINE
X1:	ADD.L	D2,D2
	BNE.S	X2
	MOVE.L	(A2)+,D2
	ADDX.L	D2,D2
X2:	BCC.S	X3		;ON SAUTE 2 OCTETS OU PLUS...
	ADD	D0,A0		;TOUT BETEMENT
X3:	MOVE	(A0)+,D0
	BPL.S	X1
;
; D0.B=OCTET DECODE !!!
;
	MOVE.B	D0,(A5)+
	CMP.L	A6,A0		;SI PTR COURANT=PTR FINAL
	BLO.S	BCL		; => FIN DU FICHIER
FINI:
	ILLEGAL
;
; DECODAGE DE L'ARBRE DE HUFFMANN
;
; ROUTINE RECURSIVE
;
DECODE3:
	ADDQ	#2,A6		;ON RESERVE DE LA PLACE POUR UN POINTEUR
	PEA	(A6)		;MARQUAGE D'UNE BRANCHE
	BSR.S	DECODE		;A6 POINTE SUR LA FEUILLE DE DROITE
	MOVE.L	(A7)+,A0
	MOVE	A6,D0		;NOUVEAU POINTEUR-ANCIEN POINTEUR
	SUB	A0,D0		;TOUJOURS >0 ET <32768 !!!
	MOVE	D0,-(A0)	;POINTE SUR LA FEUILLE DE GAUCHE
DECODE:
	ADD.L	D2,D2
	BNE.S	TSTCARY2
DECODE0:
	MOVE.L	(A2)+,D2
	ADDX.L	D2,D2
TSTCARY2:
	BCC.S	DECODE3

	MOVE	#$8001,D0	;MARQUE UN NOEUD TERMINAL (BIT 15)
L1F2EE:	ADD.L	D2,D2
	BNE.S	TSTCARY3
	MOVE.L	(A2)+,D2
	ADDX.L	D2,D2
TSTCARY3:
	ADDX.B	D0,D0
	BCC.S	L1F2EE
	MOVE	D0,(A6)+	;ET STOCKE SA VALEUR
	RTS
;
; C'EST TRES COURT, N'EST-CE PAS ?
;
; ET SIMPLE, NON ?  HE HE !!!
;
