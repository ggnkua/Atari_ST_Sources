     	
	**************
	* DECRUNCHER *
	**************
	
DEPACK	LEA	DEPACK(PC),A1
	LEA	PRGM(PC),A2
	MOVE.L	A1,(A2)
	ADDA.L	LEN(PC),A1
	LEA	END(PC),A2
	MOVE.L	A1,(A2)
	LEA	PARAM(PC),A1
	LEA	BEGIN(PC),A2
	MOVE.L	A1,(A2)

	*CLR.L	-(A7)		INUTILE SI DEJA 
	*MOVE.W	#$20,-(A7)	EN MODE SUPER
	*TRAP	#1
	*ADDQ	#6,A7
	*LEA	STACK2(PC),A6
	*MOVE.L	D0,(A6)
	BSR.S	UNPACK
	MOVE.W	ANC_COL(PC),$FFFF8240.W
	LEA	BUFFER+2(PC),A1
	MOVE.L	$4C6.W,A2
	*MOVE.L	STACK2(PC),-(A7)
	*MOVE.W	#$20,-(A7)	INUTILE SI DEJA
	*TRAP	#1		EN MODE SUPER
	*ADDQ	#6,A7
BUFFER	LEA	COPY1(PC),A1
COPY0	MOVEM.L	(A1),D0-D2
	MOVEM.L	D0-D2,(A2)
	MOVEA.L	BEGIN(PC),A1
	MOVE.L	LENGHT(PC),D2
	LSR.L	#3,D2
	MOVEA.L	PRGM(PC),A4
	MOVEA.L	A4,A3
	JMP	(A2)
COPY1	MOVE.L	(A1)+,(A3)+
	MOVE.L	(A1)+,(A3)+
	DBF	D2,COPY1
	RTS		POUR QUE LE DATA RENDE LA MAIN APRES DECRUNCH
	*JMP	(A4)	POUR QUE LE DATA S'EXECUTE APRES DECRUNCH




UNPACK	LEA	ANC_COL(PC),A2
	MOVE.W	$FFFF8240.W,(A2)
	MOVEA.L	END(PC),A0
	MOVEA.L	BEGIN(PC),A1
	MOVEA.L	-(A0),A2
	LEA	LENGHT(PC),A3
	MOVE.L	A2,(A3)
	ADDA.L	A1,A2
	LEA	BOTTOM(PC),A3
	MOVE.L	A2,(A3)
	MOVE.L	-(A0),D5
	MOVE.L	-(A0),D0
	EOR.L	D0,D5

NOP0	LSR.L	#1,D0
	BNE.S	SKIP3
	BSR	ALGO0
SKIP3	BCS.S	EXIT3
	MOVEQ	#8,D1
	MOVEQ	#1,D3
	LSR.L	#1,D0
	BNE.S	SKIP4
	BSR	ALGO0
SKIP4	BCS.S	EXIT5
	MOVEQ	#3,D1
	MOVEQ	#0,D4
LOOP6	BSR	ALGO1
	MOVE.W	D2,D3
	ADD.W	D4,D3
LOOP5	MOVEQ	#7,D1
LOOP4	LSR.L	#1,D0
	BNE.S	SKIP2
	BSR	ALGO0
SKIP2	ROXL.L	#1,D2
	DBF	D1,LOOP4
	MOVE.B	D2,-(A2)
	DBF	D3,LOOP5
	BRA	NEXT3
LOOP04	MOVEQ	#8,D1
	MOVEQ	#8,D4
	BRA.S	LOOP6
EXIT3	MOVEQ	#2,D1
	BSR	ALGO1
	CMP.B	#2,D2
	BLT.S	EXIT4
	CMP.B	#3,D2
	BEQ.S	LOOP04
	MOVEQ	#8,D1
	BSR	ALGO1
	MOVE.W	D2,D3
	MOVEQ	#$C,D1
	BRA	EXIT5
EXIT4	MOVEQ	#9,D1
	ADD.W	D2,D1
	ADDQ.W	#2,D2
	MOVE.W	D2,D3
EXIT5	BSR	ALGO1
LOOP3	MOVE.B	-1(A2,D2.W),-(A2)
	DBF	D3,LOOP3
NEXT3	*ADDQ	#1,$FFFF8240.W		CLIGNOTEMENT PENDANT DECRUNCH
	CMPA.L	A2,A1
	BLT.S	NOP0
	RTS

ALGO0	MOVE.L	-(A0),D0
	EOR.L	D0,D5
	MOVE.B	#$10,CCR
	ROXR.L	#1,D0
	RTS

ALGO1	SUBQ.W	#1,D1
	MOVEQ	#0,D2
LOOP1	LSR.L	#1,D0
	BNE.S	NEXT2
	MOVE.L	-(A0),D0
	EOR.L	D0,D5
	MOVE.B	#$10,CCR
	ROXR.L	#1,D0
NEXT2	ROXL.L	#1,D2
	DBF	D1,LOOP1
	RTS
STACK2	DC.L	0
END	DC.L	0
BEGIN	DC.L	0
LENGHT	DC.L	0
PRGM	DC.L	0
ANC_COL	DC.W	0
BOTTOM	DC.L	0
LEN	DC.L	PRG0-DEPACK-4
PARAM	INCBIN	\CITY!.SEQ
PRG0