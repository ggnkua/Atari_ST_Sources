> FUNCTION demand_second_screen
'
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
' !! (C)1993  -Alle Rechte vorbehalten-                                 !!
' !! Autoren: Marcus Oehler / Jan Kundmller  -HAMBURG-                 !!
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
' !! Version 1.0 vom 10.2.93        !! Sprache: GFA-Basic 3.6           !!
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'
' Fordert einen zweiten Bildschirm von CTRL2SCR an bzw. legt im Fehlerfall
' selbst einen an
' ------------------------------------------------------------------------
' Rckgabe:       0   Fehler (es konnte kein zweiter Bildschirm angelegt
'                     werden)
'                >0   Pointer auf die Screen-Structure, die bei Umschalten
'                     und Abmelden der Bildschirme angegebene werden muž
' ------------------------------------------------------------------------
' Kommentar:    Unbedingt am Programmende (auch bei Abbruch)
'               RELEASE_SECOND_SCREEN aufrufen!!
'
LOCAL second_screen_struc%,ss_x_aufl&,ss_y_aufl&,ss_bitplanes&
LOCAL ss_cookie%,ss_event&
'
second_screen_struc%=MALLOC(14)
'   0 L switch_screen_adr%
'   4 L switch_screen_nrm_adr%
'   8 L switch_screen_memory%
'  12 W CTRL2SCR-Id
'
IF second_screen_struc%>0                             ! konnte die Struktur angelegt werden?
  '
  LONG{second_screen_struc%}=0                        ! switch_screen_adr%
  LONG{second_screen_struc%+4}=XBIOS(3)               ! switch_screen_nrm_adr%
  LONG{second_screen_struc%+8}=0                      ! switch_screen_memory%
  WORD{second_screen_struc%+12}=APPL_FIND("CTRL2SCR") ! CTRL2SCR-Id
  '
  IF WORD{second_screen_struc%+12}>0                  ! Exis. CTRL2SCR?
    '
    DIM ss_my_gem_buffer&(7)
    '
    ss_my_gem_buffer&(0)=1050                         ! Bildschirm anfordern
    ss_my_gem_buffer&(1)=APPL_INIT()
    ss_my_gem_buffer&(2)=0
    '
    ~APPL_WRITE(WORD{second_screen_struc%+12},16,V:ss_my_gem_buffer&(0))
    '
    ' Warten auf eine Antwort von CTRL2SCR innerhalb von 5 Sekunden sonst weiter
    ' mit eigenem zweiten Bildschirm
    '
    REPEAT
      ss_event&=EVNT_MULTI(&X110000,0,0,0,0,0,0,0,0,0,0,0,0,0,V:ss_my_gem_buffer&(0),5000)
    UNTIL (ss_event&=&X10000 AND ss_my_gem_buffer&(0)=1051) OR (ss_event&=&X100000)
    '
    IF ss_event&=&X10000 AND ss_my_gem_buffer&(0)=1051
      LONG{second_screen_struc%}=LONG{V:ss_my_gem_buffer&(3)}
    ELSE
      WORD{second_screen_struc%+12}=0                  ! Muž selbst Bildschirm aufbauen
    ENDIF
    '
    ERASE ss_my_gem_buffer&()
    '
  ENDIF
  '
  IF LONG{second_screen_struc%}<=0
    '
    CONTRL(0)=102
    CONTRL(1)=0
    CONTRL(2)=6
    CONTRL(3)=1
    CONTRL(4)=45
    CONTRL(6)=V~H
    INTIN(0)=0
    VDISYS
    ss_x_aufl&=INTOUT(0)+1    ! x-Aufl”sung
    ss_y_aufl&=INTOUT(1)+1    ! y-Aufl”sung
    INTIN(0)=1
    VDISYS
    ss_bitplanes&=INTOUT(4)   ! Bitplanes
    '
    ss_cookie%=LPEEK(1440)    ! Adresse des Cookie-Jar
    WHILE LONG{ss_cookie%}<>0 AND MKL$(LONG{ss_cookie%})<>"_MCH"
      ADD ss_cookie%,8
    WEND
    '
    IF (LONG{ss_cookie%}<>0) AND (MKL$({ss_cookie%})="_MCH") AND ({ADD(ss_cookie%,4)} DIV 65535=2) ! Fastram (TT)?
      LONG{second_screen_struc%+8}=GEMDOS(68,L:ADD(MUL(ss_bitplanes&,MUL(ADD(DIV(ADD(ss_x_aufl&,15),16),DIV(ADD(ss_x_aufl&,15),16)),ss_y_aufl&)),512),0)
    ELSE
      LONG{second_screen_struc%+8}=MALLOC(ADD(MUL(ss_bitplanes&,MUL(ADD(DIV(ADD(ss_x_aufl&,15),16),DIV(ADD(ss_x_aufl&,15),16)),ss_y_aufl&)),512))
    ENDIF
    '
    IF LONG{second_screen_struc%+8}>0                 ! Speicher bekommen?
      '                                               ! Bildschirmgrenze korr.!
      LONG{second_screen_struc%}=(((ADD(LONG{second_screen_struc%+8},256)) DIV 256)*256)
      '
    ELSE
      '
      ~MFREE(LONG{second_screen_struc%+8})            ! Kein Speicher bekommen
      LONG{second_screen_struc%+8}=0
      LONG{second_screen_struc%}=0
      '
    ENDIF
  ENDIF
  '
  IF LONG{second_screen_struc%}<=0                  ! Noch immer nix...
    ~MFREE(second_screen_struc%)
    second_screen_struc%=0
  ENDIF
  '
ENDIF
'
RETURN second_screen_struc%
'
ENDFUNC
> PROCEDURE release_second_screen(VAR second_screen_struc%)
'
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
' !! (C)1993  -Alle Rechte vorbehalten-                                 !!
' !! Autoren: Marcus Oehler / Jan Kundmller  -HAMBURG-                 !!
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
' !! Version 1.0 vom 10.2.93        !! Sprache: GFA-Basic 3.6           !!
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'
' Gibt einen zweiten Bildschirm wieder frei
' ------------------------------------------------------------------------
' Eingabe:    Screen-Structure, die von Demand_Second_Screen zurckkam
' Rckgabe:   keine
'
IF second_screen_struc%>0                     ! Muž ich berhaupt was entfernen?
'
IF WORD{second_screen_struc%+12}>0          ! Wurde Schirm virtuell angefordert?
  '
  DIM ss_my_gem_buffer&(7)
  '
  ss_my_gem_buffer&(0)=1059                 ! Bei CNTRL2SCR abmelden
  ss_my_gem_buffer&(1)=APPL_INIT()
  ss_my_gem_buffer&(2)=0
  '
  ~APPL_WRITE(WORD{second_screen_struc%+12},16,V:ss_my_gem_buffer&(0))
  '
  ERASE ss_my_gem_buffer&()
  '
ELSE
  IF LONG{second_screen_struc%+8}>0
    ~MFREE(LONG{second_screen_struc%+8})    ! Speicher freigeben
  ENDIF
ENDIF
'
~MFREE(second_screen_struc%)                ! Struktur l”schen
second_screen_struc%=0
'
ENDIF
'
RETURN
> PROCEDURE switch_first_second_screen(second_screen_struc%)
'
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
' !! (C)1993  -Alle Rechte vorbehalten-                                 !!
' !! Autoren: Marcus Oehler / Jan Kundmller  -HAMBURG-                 !!
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
' !! Version 1.0 vom 10.2.93        !! Sprache: GFA-Basic 3.6           !!
' !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
'
' Schaltet alle VDI-Graphikroutinen zwischen logischen und physischen
' Schirm hin- und her
' ------------------------------------------------------------------------
' Eingabe:    Screen-Structure, die von Demand_Second_Screen zurckkam
' Rckgabe:   keine
'
IF second_screen_struc%>0                     ! Alles OK?
'
IF LONG{second_screen_struc%+4}=LPEEK(&H44E)
~GRAF_MOUSE(256,0)                        ! Hide-Mouse
~WIND_UPDATE(1)                           ! Bildschirmblock.
SLPOKE &H44E,LONG{second_screen_struc%}   ! Umschalten
ELSE
SLPOKE &H44E,LONG{second_screen_struc%+4} ! Umschalten
~WIND_UPDATE(0)                           ! Bidschirmfreig.
~GRAF_MOUSE(257,0)                        ! Show-Mouse
ENDIF
'
ENDIF
'
RETURN
