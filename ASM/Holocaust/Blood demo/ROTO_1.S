;Rotozoom. Code by Elric/Holocaust.
;Un excellent code, je trouve! (Zappy)

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	BSR	PREPARE_ROTO_ZOOM
	BSR	PREPARE_CODE
	BSR	PREPARE_ROT
	BSR	INIT_SYSTEME

	LEA	BUF_OFFSET,A2
	MOVE.L	A2,PT_OFF

	MOVE.L	SCREEN1,A0
	BSR	EFFAC_SCREEN
*
	MOVE.L	SCREEN1,A1
	LEA	160*3+48(A1),A1
	MOVE.L	SCREEN2,A2
	LEA	160*3+48(A2),A2
	MOVEQ	#-1,D1
	MOVE	#12-1,D7
.A1
	MOVE.L	D1,(A1)
	MOVE.L	D1,(A2)
	MOVE.L	D1,4(A1)
	MOVE.L	D1,4(A2)
	MOVE.L	D1,160*193(A1)
	MOVE.L	D1,160*193+4(A1)
	MOVE.L	D1,160*193(A2)
	MOVE.L	D1,160*193+4(A2)
	ADDQ	#8,A1
	ADDQ	#8,A2
	DBF	D7,.A1
	MOVE.L	SCREEN1,A1
	LEA	160*3+48-8(A1),A1
	MOVE.L	SCREEN2,A2
	LEA	160*3+48-8(A2),A2
	MOVE.L	#$00010001,D1
	MOVE.L	#$80008000,D2
	MOVE	#194-1,D7
.A2
	MOVE.L	D1,(A1)
	MOVE.L	D1,(A2)
	MOVE.L	D1,4(A1)
	MOVE.L	D1,4(A2)

	MOVE.L	D2,13*8(A1)
	MOVE.L	D2,13*8(A2)
	MOVE.L	D2,13*8+4(A1)
	MOVE.L	D2,13*8+4(A2)
	
	LEA	160(A1),A1
	LEA	160(A2),A2
	DBF	D7,.A2

*
	CLR.W	NB_VBL
	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#VBLR,$70.W
	MOVE.W	#$2300,SR

IT_VBL	BSR	VSYNC

	LEA	TABLE_OFFSET,A0
	LEA	BUF_CODE,A1
	MOVE.L	PT_OFF,A2
	MOVEQ	#0,D1
	MOVEQ	#96-1,D7
.POZ	MOVE.W	(A0)+,D0
	MOVE.W	(A2)+,D1
	MOVE.W	D1,0(A1,D0.W)
	DBRA	D7,.POZ
	MOVE.L	A2,PT_OFF

	LEA	BUF_GFX+4*90*90*1,A0
	LEA	BUF_GFX+4*90*90*4,A1
	LEA	BUF_GFX+4*90*90*7,A2
	LEA	BUF_GFX+4*90*90*10,A3

	MOVE.L	SCREEN2,A5
	LEA	160*4+48(A5),A5
	MOVE.L	PT_OFF,A6
	MOVEQ	#96-1,D7
	JSR	BUF_CODE
	CMPI.W	#$1234,(A6)
	BNE.S	.OK
	LEA	BUF_OFFSET,A6
.OK	MOVE.L	A6,PT_OFF

;	DCB.W	50,$4E71
;	CLR.W	$FFFF8240.W

	BSR	SWAPEC

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	BRA	IT_VBL

VSYNC	CMPI.W	#2,NB_VBL
	BLT.S	VSYNC
	CLR.W	NB_VBL
	RTS

VBLR	ADDQ.W	#1,NB_VBL
	MOVE.W	#$801,$FFFF8240.W
	RTE

SWAPEC	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W
	RTS

	;A0=ADRESSE DE L'ECRAN A EFFACER
EFFAC_SCREEN	MOVE.W	#8000-1,D7
.EFF	CLR.L	(A0)+
	DBRA	D7,.EFF
	RTS

INIT_SYSTEME	CLR.B	$FFFF8260.W

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.B	SCREEN1+1,$FFFF8201.W
	MOVE.B	SCREEN1+2,$FFFF8203.W

	MOVEM.L	GFX,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W
	RTS

PREPARE_ROTO_ZOOM
	LEA	GFX+32,A0
	LEA	BUF_COLOR,A3
	MOVEQ	#0,D0	;X
	MOVEQ	#0,D1	;Y
.NEXT_Y	MOVE.L	A0,A1
.NEXT_WORDS	MOVEM.W	(A1)+,D2-D5
.NEXT_X	MOVE.W	D0,D7
	ANDI.W	#15,D7
	SUBI.W	#15,D7
	NEG.W	D7
	MOVEQ	#0,D6
	BTST	D7,D2
	BEQ.S	.NO_1
	ADDQ.W	#1,D6
.NO_1	BTST	D7,D3
	BEQ.S	.NO_2
	ADDQ.W	#2,D6
.NO_2	BTST	D7,D4
	BEQ.S	.NO_3
	ADDQ.W	#4,D6
.NO_3	BTST	D7,D5
	BEQ.S	.NO_4
	ADDQ.W	#8,D6
.NO_4	;D6=No DE LA COULEUR
	MOVE.B	D6,(A3)+
	ADDQ.W	#1,D0	;X=X+1
	MOVE.W	D0,D7
	ANDI.W	#15,D7
	TST.W	D7
	BEQ.S	.NEXT_WORDS
	CMPI.W	#90,D0
	BLT.S	.NEXT_X
	ADDQ.W	#1,D1	;Y=Y+1
	MOVEQ	#0,D0	;X=0
	LEA	80(A0),A0
	CMPI.W	#90,D1
	BLT.S	.NEXT_Y

	LEA	BUF_GFX,A2
	MOVEQ	#0,D6
	MOVEQ	#4-1,D7
TEST_DECA	MOVE.L	A2,A6
	LEA	BUF_COLOR,A0
	MOVE.W	#90-1,D3
TEST_Y	MOVEQ	#90-1,D4
TEST_X	MOVEQ	#0,D5
	MOVE.B	(A0)+,D5
	;D5=No DE LA COULEUR
	LEA	RT_PUT(PC),A1
	ADD.W	D5,D5
	ADD.W	D5,D5
	MOVE.L	(A1,D5.W),A1
	JSR	(A1)
	LSR.L	D6,D5
	MOVE.L	D5,(A2)+
	DBRA	D4,TEST_X
	DBRA	D3,TEST_Y
;	MOVE.L	A6,A4
	MOVE.W	#90*90*2-1,D3
.COPY	MOVE.L	(A6)+,(A2)+
	DBRA	D3,.COPY
	ADDQ.W	#2,D6
	DBRA	D7,TEST_DECA
	RTS

RT_PUT	DC.L	COUL_0,COUL_1,COUL_2,COUL_3
	DC.L	COUL_4,COUL_5,COUL_6,COUL_7
	DC.L	COUL_8,COUL_9,COUL_10,COUL_11
	DC.L	COUL_12,COUL_13,COUL_14,COUL_15
COUL_0	MOVEQ	#0,D5
	RTS
COUL_1	MOVE.L	#$C0000000,D5
	RTS
COUL_2	MOVE.L	#$00C00000,D5
	RTS
COUL_3	MOVE.L	#$C0C00000,D5
	RTS
COUL_4	MOVE.L	#$0000C000,D5
	RTS
COUL_5	MOVE.L	#$C000C000,D5
	RTS
COUL_6	MOVE.L	#$00C0C000,D5
	RTS
COUL_7	MOVE.L	#$C0C0C000,D5
	RTS
COUL_8	MOVE.L	#$000000C0,D5
	RTS
COUL_9	MOVE.L	#$C00000C0,D5
	RTS
COUL_10	MOVE.L	#$00C000C0,D5
	RTS
COUL_11	MOVE.L	#$C0C000C0,D5
	RTS
COUL_12	MOVE.L	#$0000C0C0,D5
	RTS
COUL_13	MOVE.L	#$C000C0C0,D5
	RTS
COUL_14	MOVE.L	#$00C0C0C0,D5
	RTS
COUL_15	MOVE.L	#$C0C0C0C0,D5
	RTS

PREPARE_ROT	LEA	ROTATIONS(PC),A0
	LEA	BUF_OFFSET,A2
	LEA	MODULO_90(PC),A4

CONT	CMPI.W	#$1234,(A0)
	BEQ	FINI
	LEA	TEMP,A1
	MOVE.W	(A0)+,D0	;PAS_X*256
	MOVE.W	(A0)+,D1	;PAS_Y*256
	EXT.L	D0
	EXT.L	D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVE.W	#96-1,D7
CALC_XY	MOVE.L	D2,D4
	ASR.L	#8,D4
	MOVE.W	D4,(A1)+
	MOVE.L	D3,D4
	ASR.L	#8,D4
	MOVE.W	D4,(A1)+
	ADD.L	D0,D2
	ADD.L	D1,D3
	DBRA	D7,CALC_XY
	LEA	TEMP,A1
	MOVEQ	#96-1,D7
CALC_OFF_1	MOVE.W	(A1)+,D0	;X
	MOVE.W	(A1)+,D1	;Y
	ADD.W	D0,D0
	ADD.W	D1,D1
	MOVE.W	(A4,D1.W),D1
	MULS.W	#360,D1
	MOVE.W	(A4,D0.W),D0
	ADD.W	D0,D0
	ADD.W	D0,D0	;X*4
	ADD.W	D0,D1
	MOVE.W	D1,(A2)+	;OFFSET POUR LES MOVE-OR
	DBRA	D7,CALC_OFF_1
	LEA	TEMP+4,A1
	MOVEQ	#0,D2
	MOVEQ	#96-1,D7
CALC_OFF_2	MOVEQ	#0,D0
	MOVE.W	(A1)+,D1	;Y
	MOVE.W	(A1)+,D0
	NEG.W	D0	;X
	ADD.W	D0,D0
	MOVE.W	(A4,D0.W),D0
	ADD.W	D0,D0
	ADD.W	D0,D0	;X*4
	ADD.W	D1,D1
	MOVE.W	(A4,D1.W),D1
	MULS.W	#360,D1
	ADD.W	D0,D1
	MOVE.W	D1,D3
	SUB.W	D2,D1
	MOVE.W	D1,(A2)+	;OFFSET POUR LES LEA(s)
	MOVE.W	D3,D2
	DBRA	D7,CALC_OFF_2

	BRA	CONT

FINI	MOVE.W	#$1234,(A2)
	RTS

N	SET	0
	REPT	90
	DC.W	N
N	SET	N+1
	ENDR
N	SET	0
	REPT	90
	DC.W	N
N	SET	N+1
	ENDR
MODULO_90
N	SET	0
	REPT	90
	DC.W	N
N	SET	N+1
	ENDR
N	SET	0
	REPT	90
	DC.W	N
N	SET	N+1
	ENDR

PREPARE_CODE	LEA	BUF_CODE,A1

	MOVEQ	#12-1,D6
.COPY_ALL	LEA	DEBUT_CODE(PC),A0
	MOVE.W	#LONG_CODE,D7
.COPY	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.COPY
	DBRA	D6,.COPY_ALL

	LEA	CODE_INCREMENT(PC),A0
	MOVE.W	#LONG_CODE2,D7
.COPY_2	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.COPY_2
	SUBQ.W	#2,A1
	MOVE.L	#BUF_CODE,D0
	SUB.L	A1,D0
	MOVE.W	D0,(A1)+

	MOVE.W	#$4E75,(A1)+

	LEA	TABLE_OFFSET,A0
	MOVE.W	#LONG_CODE,D0
	ADDQ.W	#1,D0
	ADD.W	D0,D0	;LONGUEUR D'UNE PLAGE
	MOVEQ	#0,D1
	MOVEQ	#12-1,D7
.REPEAT	MOVE.W	#OFF_1-DEBUT_CODE,(A0)
	ADD.W	D1,(A0)+
	MOVE.W	#OFF_2-DEBUT_CODE,(A0)
	ADD.W	D1,(A0)+
	MOVE.W	#OFF_3-DEBUT_CODE,(A0)
	ADD.W	D1,(A0)+
	MOVE.W	#OFF_4-DEBUT_CODE,(A0)
	ADD.W	D1,(A0)+
	MOVE.W	#OFF_5-DEBUT_CODE,(A0)
	ADD.W	D1,(A0)+
	MOVE.W	#OFF_6-DEBUT_CODE,(A0)
	ADD.W	D1,(A0)+
	MOVE.W	#OFF_7-DEBUT_CODE,(A0)
	ADD.W	D1,(A0)+
	MOVE.W	#OFF_8-DEBUT_CODE,(A0)
	ADD.W	D1,(A0)+
	ADD.W	D0,D1
	DBRA	D7,.REPEAT
	RTS

DEBUT_CODE
OFF_1 = *+2
	MOVE.L	$1234(A0),D0	;4
OFF_2 = *+2
	OR.L	$1234(A1),D0	;5
OFF_3 = *+2
	OR.L	$1234(A2),D0	;5
OFF_4 = *+2
	OR.L	$1234(A3),D0	;5
	MOVEP.L	D0,(A5)	;6
OFF_5 = *+2
	MOVE.L	$1234(A0),D0	;4
OFF_6 = *+2
	OR.L	$1234(A1),D0	;5
OFF_7 = *+2
	OR.L	$1234(A2),D0	;5
OFF_8 = *+2
	OR.L	$1234(A3),D0	;5
	MOVEP.L	D0,1(A5)	;6
	MOVE.L	(A5)+,156(A5)	;6
	MOVE.L	(A5)+,156(A5)	;6
LONG_CODE = ((*-DEBUT_CODE)/2)-1

CODE_INCREMENT
;	MOVEQ	#0,D0
	MOVE.W	(A6)+,D0
	ADDA.W	D0,A0
	ADDA.W	D0,A1
	ADDA.W	D0,A2
	ADDA.W	D0,A3

	LEA	160+64(A5),A5
	DBRA	D7,CODE_INCREMENT
LONG_CODE2 = ((*-CODE_INCREMENT)/2)-1

ROTATIONS	INCBIN	ROTO_5.ROT
	DC.W	$1234

GFX	INCBIN	ROTOGFX.GFX

	SECTION	BSS
NB_VBL	DS.W	1
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000
X_DEP	DS.W	1
Y_DEP	DS.W	1
TABLE_OFFSET	DS.W	320
BUF_CODE	DS.B	5000
BUF_GFX	DS.B	4*90*90*4*3
PT_OFF	DS.L	1
TEMP	DS.W	400
BUF_COLOR	DS.B	90*90
BUF_OFFSET	DS.W	(96+96)*350