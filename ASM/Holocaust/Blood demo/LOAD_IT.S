;Loader sous interruption.
;Code by Elric/Holocaust.

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP
	LEA	NEW_PILE,SP

	LEA	DATA_LOAD,A0
	BSR	LOAD_ALL

WAIT	TST.W	FLAG_LOAD
	BNE.S	WAIT

	BSR	UNSELECT_ALL

	MOVE.W	#$2700,SR
	MOVE.W	#$101,$FFFF8240.W
J	BRA.S	J

LOAD_ALL	MOVE.W	(A0)+,FACE
	MOVE.W	(A0)+,NO_TRACK	;PISTE DE DEPART
	MOVE.W	(A0)+,NB_SECT_READ  ;SECTEUR DE DEPART
	MOVE.W	(A0)+,NB_SECT_TO_READ   ;NB DE SECTEURS
	MOVE.L	#BUFFER,ADR_DMA

	LEA	BUFFER,A0
	LEA	FIN_BUF,A1
.CONT_EFF	CLR.L	(A0)+
	CMPA.L	A0,A1
	BGT.S	.CONT_EFF

	MOVEQ	#0,D0	;DRIVE A
	MOVE.W	FACE,D1	;FACE
	BSR	SET_DRIVE

	BSR	FORCE_IT
	BSR	RESTORE

	BSR	LANCE
	RTS

VBL	MOVEM.L	D0-A6,-(SP)

	MOVE.L	$44E.W,A0
	MOVE.L	#$55555555,D0
	MOVE.W	#3999,D7
.F	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A0)+
	DBRA	D7,.F

	MOVEM.L	(SP)+,D0-A6

	RTE

*** COMMANDES FDC...
ADR_DMA	DC.L	0

STEP_IN_FLAG	DC.W	0	;FLAG DE COMMANDE STEP_IN

NB_SECT_TO_READ	DC.W	0 ;NB DE SECTEURS RESTANTS A LIRE (EN TOUT)

;NB DE SECTEURS LUS PAR PISTE (+1), CETTE VARIABLE SERT AUSSI A DONNER
;LE No DU PROCHAIN SECTEUR A LIRE.
NB_SECT_READ	DC.W	0

FACE	DC.W	0	;1=0, 2=1
NO_TRACK	DC.W	0	;NO DE PISTE

FLAG_LOAD	DC.W	0

LANCE	ST	FLAG_LOAD

	LEA	SOV_REGS,A0
	MOVE.L	$70.W,(A0)+
	MOVE.L	$11C.W,(A0)+
	MOVE.B	$FFFFFA09.W,(A0)+
	MOVE.B	$FFFFFA15.W,(A0)+
	MOVE.B	$FFFFFA17.W,(A0)+

	MOVE.W	#$2700,SR
	BCLR	#3,$FFFFFA17.W

	BCLR	#7,$FFFFFA09.W
	MOVE.L	#IT_FDC,$11C.W
	BSET	#7,$FFFFFA09.W
	BSET	#7,$FFFFFA15.W

	MOVE.L	#VBL,$70.W
	MOVE.W	#$2300,SR

	MOVE.W	#$90,$FFFF8606.W
	MOVE.W	#$190,$FFFF8606.W
	MOVE.W	#$90,$FFFF8606.W

	MOVE.W	#$A,$FFFF8604.W	;SECTOR COUNT REGISTER
	MOVE.L	ADR_DMA,D0
	BSR	SET_DMA

	ST	STEP_IN_FLAG
;PISTE SOUHAITE DANS DATA REGISTER
	MOVE.W	#$86,$FFFF8606.W
	MOVE.W	NO_TRACK,$FFFF8604.W
	MOVE.W	#$80,$FFFF8606.W        ;COMMAND REGISTER
	NOP
	NOP
	MOVE.W	#$11,$FFFF8604.W	;SEEK

	RTS

IT_FDC	MOVE.L	D0,-(SP)
	MOVE.W	#$700,$FFFF8240.W

	MOVE.W	#$80,$FFFF8606.W
	MOVE.W	$FFFF8604.W,D0	;STATUS REGISTER
	ANDI.W	#$18,D0	;RNF OU CRC ERROR?
	BEQ.S	NO_ERROR	;=> SECTEUR PAS LU
	MOVE.L	ADR_DMA,D0
	BSR	SET_DMA
	MOVE.W	#$90,$FFFF8606.W
	MOVE.W	#$A,$FFFF8604.W ;SECTOR COUNT REGISTER = 10
	BRA.S	STEPIN_CMD
NO_ERROR
;COMMANDE PRECEDENTE : STEPIN ?
	TST.B	STEP_IN_FLAG
	BNE.S	STEPIN_CMD
;DONC C'ETAIT UN READ:  UN SECTEUR A ETE LU.
	SUBQ.W	 #1,NB_SECT_TO_READ
	BEQ.S	STOP_ALL
	ADDQ.W	#1,NB_SECT_READ
	ADD.L	#$200,ADR_DMA

STEPIN_CMD	SF	STEP_IN_FLAG
	CMP.W	#$B,NB_SECT_READ ;A T'ON LU LES 10 SECTEURS?
	BEQ.S	NEXT_TRACK
	MOVE.W	#$84,$FFFF8606.W      ;SECTOR REGISTER
;Nb DE SECTEURS LUS+1 = No DU PROCHAIN SECTEUR A LIRE.
	MOVE.W	NB_SECT_READ,$FFFF8604.W
	MOVE.W	#$80,$FFFF8606.W      ;COMMAND REGISTER
	NOP
	NOP
;-> READ SECTOR ( NO MULTIPLE )
	MOVE.W	#$80,$FFFF8604.W
	BRA.S	OUT
NEXT_TRACK
;NB DE SECTEURS LUS+1 REMIS A 1
	MOVE.W	#1,NB_SECT_READ
	ADDQ.W	#1,NO_TRACK
	MOVE.W	#$90,$FFFF8606.W    ;SECTOR COUNT REGISTER
	MOVE.W	#$A,$FFFF8604.W     ;->REMISE A 10 SECTEURS
	MOVE.W	#$80,$FFFF8606.W    ;COMMAND REGISTER
	NOP
	NOP
	MOVE.W	#$53,$FFFF8604.W     ;-> STEP IN
	ST	STEP_IN_FLAG
OUT	MOVE.L	(SP)+,D0

	CLR.W	$FFFF8240.W
	RTE

STOP_ALL	BCLR	#7,$FFFFFA09.W
	BCLR	#7,$FFFFFA15.W

	SF	FLAG_LOAD
	MOVE.L	(SP)+,D0

	CLR.W	$FFFF8240.W
	RTE

	;D0=DRIVE (0/1), D1=FACE (0/1)
SET_DRIVE	ADDQ.B	#1,D0
	ADD.B	D0,D0
	OR.B	D1,D0
	EOR.B	#7,D0
	MOVE.B	#14,$FFFF8800.W
	MOVE.B	$FFFF8800.W,D1
	AND.B	#$F8,D1
	OR.B	D0,D1
	MOVE.B	D1,$FFFF8802.W
	RTS

UNSELECT_ALL	MOVE.W	#$80,$FFFF8606.W
.MOTOR	MOVE.W	$FFFF8604.W,D0
	AND.W	#$80,D0
	BNE.S	.MOTOR	;DESELECTION DRIVE
	LEA	$FFFF8800.W,A0
	MOVE.B	#14,(A0)
	MOVE.B	(A0),D7
	ORI.B	#7,D7
	MOVE.B	D7,2(A0)

	MOVE.W	#$2700,SR
	LEA	SOV_REGS,A0
	MOVE.L	(A0)+,$70.W
	MOVE.L	(A0)+,$11C.W
	MOVE.B	(A0)+,$FFFFFA09.W
	MOVE.B	(A0)+,$FFFFFA15.W
	MOVE.B	(A0)+,$FFFFFA17.W
	MOVE.W	#$2300,SR

	RTS

	;D0=ADRESSE A VALIDER
SET_DMA	MOVE.B	D0,$FFFF860D.W
	LSR.L	#8,D0
	MOVE.B	D0,$FFFF860B.W
	LSR.L	#8,D0
	MOVE.B	D0,$FFFF8609.W
	RTS

FORCE_IT	MOVE.W	#$80,$FFFF8606.W     ;COMMAND REGISTER
	NOP
	NOP
	MOVE.W	#$D0,$FFFF8604.W
	MOVEQ	#40,D7
.WT	DBRA	D7,.WT
	RTS

RESTORE	MOVE.W	#$80,$FFFF8606.W
	NOP
	NOP
	MOVE.W	#1,$FFFF8604.W
	BSR	WAIT_FDC
	RTS

WAIT_FDC	MOVE.W	#$180,D5
.WT	DBRA	D5,.WT
	MOVE.L	#$40000,D5
.MFP	BTST	#5,$FFFFFA01.W
	BEQ.S	.OK
	SUBQ.L	#1,D5
	BNE.S	.MFP
	BSR	ERROR
.OK	RTS

ERROR	BSR	FORCE_IT
	RTS

CMD	DS.W	1

	;FACE, PISTE DE DEPART, SECTEUR DE DEPART, NB DE SECTEURS
DATA_LOAD	DC.W	0,0,1,10*8

	SECTION	BSS
	DS.B	500
NEW_PILE	DS.B	500
SOV_REGS	DS.B	12
BUFFER	DS.B	512*150
FIN_BUF