;Objet 3D: la 'toupie' Ö la sauce Braindamage, mais en plus gros, et qui
;prend (bcp) moins de temps machine. (TAB!) D'accord, c'est pas vraiment
;comparable.... pour ma part, seul le rÇsultat compte, peu importe la
;mÇthode employÇe.... hÇhÇ.
;
;Code by Zappy/Holocaust.

DONNEE_INIT	= 400
TIME_F	= 306+2
NB_PTS_	= 24
NB_FACES	= 18
INC_D	= 0
COEFF_MULTI	= 2
COEFF_INIT	= 380
X_DEP	= 160
Y_DEP	= -200

X1	EQU	0
X2	EQU	4
X3	EQU	8
X4	EQU	12
X5	EQU	16
X6	EQU	20
X7	EQU	24
X8	EQU	28
X9	EQU	32
X10	EQU	36
X11	EQU	40
X12	EQU	44
X13	EQU	48
X14	EQU	52
X15	EQU	56
X16	EQU	60
X17	EQU	64
X18	EQU	68
X19	EQU	72
X20	EQU	76
X21	EQU	80
X22	EQU	84
X23	EQU	88
X24	EQU	92
X25	EQU	96
X26	EQU	100
X27	EQU	104
X28	EQU	108
X29	EQU	112
X30	EQU	116
X31	EQU	120
X32	EQU	124
X33	EQU	128
X34	EQU	132
X35	EQU	136
X36	EQU	140
X37	EQU	144
X38	EQU	148
X39	EQU	152
X40	EQU	156
X41	EQU	160
X42	EQU	164
X43	EQU	168
X44	EQU	172
X45	EQU	176
X46	EQU	180
X47	EQU	184
X48	EQU	188
X49	EQU	192
X50	EQU	196
X51	EQU	200
X52	EQU	204
X53	EQU	208
X54	EQU	212
X55	EQU	216
X56	EQU	220
X57	EQU	224
X58	EQU	228
X59	EQU	232
X60	EQU	236

MAIN	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.W	#6,SP

	CLR	-(SP)
	PEA	-1.W
	PEA	-1.W
	MOVE	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
*************************************************************************

	JMP	DEBUT
	
unpack:	moveq	#0,d0
	movem.l	d0-a6,-(sp)
	lea	sp3_53(pc),a6
	movea.l	a0,a1
	cmpi.l	#'SPv3',(a1)+
	bne.s	sp3_02
	tst.w	(a1)
	bne.s	sp3_02
	move.l	(a1)+,d5
	move.l	(a1)+,d0
	move.l	(a1)+,(sp)
	movea.l	a0,a2
	adda.l	d0,a0
	move.l	-(a0),-(a1)
	move.l	-(a0),-(a1)
	move.l	-(a0),-(a1)
	move.l	-(a0),-(a1)
	adda.l	(sp),a1
	lea	sp3_58-sp3_53(a6),a3
	moveq	#128-1,d0
sp3_01:	move.l	(a2)+,(a3)+
	dbf	d0,sp3_01
	suba.l	a2,a3
	move.l	a3,-(sp)
	bsr.s	sp3_03
	bsr	sp3_21
	move.b	-(a0),d0
	adda.l	(sp)+,a0
	move.b	d0,(a0)+
	lea	sp3_58-sp3_53(a6),a2
	bsr	sp3_22
	bsr	sp3_15
sp3_02:	movem.l	(sp)+,d0-a6
	rts
sp3_03:	move.w	SR,d1
	andi.w	#$2000,d1
	beq.s	sp3_04
	move.w	$FFFF8240.W,2(a6)
	btst	#1,$FFFF8260.W
	bne.s	sp3_04
	swap	d5
sp3_04:	clr.w	d5
	move.w	-(a0),d6
	lea	sp3_54-sp3_53(a6),a3
	move.b	d6,(a3)+
	moveq	#1,d3
	moveq	#6,d4
sp3_05:	cmp.b	d6,d3
	bne.s	sp3_06
	addq.w	#2,d3
sp3_06:	move.b	d3,(a3)+
	addq.w	#2,d3
	dbf	d4,sp3_05
	moveq	#$10,d4
	move.b	-(a0),(a3)+
	move.b	d4,(a3)+
	move.b	-(a0),(a3)+
	move.b	d4,(a3)+
	move.b	-(a0),d4
	move.w	d4,(a6)
	lea	sp3_57-sp3_53(a6),a5
	move.b	-(a0),d4
	lea	1(a5,d4.w),a3
sp3_07:	move.b	-(a0),-(a3)
	dbf	d4,sp3_07
	move.b	-(a0),-(a3)
	beq.s	sp3_08
	suba.w	d4,a0
sp3_08:	moveq	#0,d2
	move.b	-(a0),d2
	move.w	d2,d3
	move.b	-(a0),d7
sp3_09:	bsr.s	sp3_10
	bsr.s	sp3_10
	dbf	d2,sp3_09
	rts
sp3_10:	not.w	d4
	add.b	d7,d7
	bne.s	sp3_11
	move.b	-(a0),d7
	addx.b	d7,d7
sp3_11:	bcs.s	sp3_12
	move.w	d2,d0
	subq.w	#1,d3
	sub.w	d3,d0
	add.w	d0,d0
	add.w	d4,d0
	add.w	d0,d0
	neg.w	d0
	move.w	d0,-(a3)
	rts
sp3_12:	moveq	#2,d1
	bsr	sp3_44
	add.w	d0,d0
	beq.s	sp3_13
	move.b	d0,-(a3)
	moveq	#2,d1
	bsr	sp3_44
	add.w	d0,d0
	move.b	d0,-(a3)
	rts
sp3_13:	moveq	#2,d1
	bsr	sp3_44
	move.w	sp3_55-sp3_53(a6),d1
	add.w	d0,d0
	beq.s	sp3_14
	move.w	sp3_55+2-sp3_53(a6),d1
sp3_14:	or.w	d1,d0
	move.w	d0,-(a3)
	rts
sp3_15:	move.w	SR,d1
	andi.w	#$2000,d1
	beq.s	sp3_16
	move.w	2(a6),$FFFF8240.W
sp3_16:	tst.w	d6
	bpl.s	sp3_20
	movea.l	a1,a2
	movea.l	a1,a3
	adda.l	4(sp),a3
sp3_17:	moveq	#3,d6
sp3_18:	move.w	(a2)+,d0
	moveq	#3,d5
sp3_19:	add.w	d0,d0
	addx.w	d1,d1
	add.w	d0,d0
	addx.w	d2,d2
	add.w	d0,d0
	addx.w	d3,d3
	add.w	d0,d0
	addx.w	d4,d4
	dbf	d5,sp3_19
	dbf	d6,sp3_18
	cmpa.l	a2,a3
	blt.s	sp3_20
	movem.w	d1-d4,-8(a2)
	cmpa.l	a2,a3
	bne.s	sp3_17
sp3_20:	rts
sp3_21:	move.b	-(a0),-(a1)
sp3_22:	swap	d5
	beq.s	sp3_23
	move.w	d5,$FFFF8240.W
sp3_23:	lea	sp3_56+2-sp3_53(a6),a3
	cmpa.l	a0,a2
	blt.s	sp3_25
	rts
sp3_24:	adda.w	d3,a3
sp3_25:	add.b	d7,d7
	bcc.s	sp3_28
	beq.s	sp3_27
sp3_26:	move.w	(a3),d3
	bmi.s	sp3_24
	bra.s	sp3_29
sp3_27:	move.b	-(a0),d7
	addx.b	d7,d7
	bcs.s	sp3_26
sp3_28:	move.w	-(a3),d3
	bmi.s	sp3_24
sp3_29:	ext.w	d3
	jmp	sp3_30(pc,d3.w)
sp3_30:	bra.s	sp3_30
	bra.s	sp3_41
	bra.s	sp3_41
	bra.s	sp3_41
	bra.s	sp3_41
	bra.s	sp3_41
	bra.s	sp3_37
	bra.s	sp3_36
	bra.s	sp3_32
	bra.s	sp3_33
	bra.s	sp3_31
	bra.s	sp3_34
	bra.s	sp3_21
sp3_31:	move.b	(a5),-(a1)
	bra.s	sp3_22
sp3_32:	bsr.s	sp3_43
	move.b	1(a5,d0.w),-(a1)
	bra.s	sp3_22
sp3_33:	bsr.s	sp3_43
	add.w	(a6),d0
	move.b	1(a5,d0.w),-(a1)
	bra.s	sp3_22
sp3_34:	moveq	#3,d1
	bsr.s	sp3_44
	lsr.w	#1,d0
	bcc.s	sp3_35
	not.w	d0
sp3_35:	move.b	(a1),d1
	add.w	d0,d1
	move.b	d1,-(a1)
	bra.s	sp3_22
sp3_36:	lea	sp3_52-2-sp3_53(a6),a4
	bsr.s	sp3_48
	addi.w	#16,d0
	lea	1(a1,d0.w),a3
	move.b	-(a3),-(a1)
	move.b	-(a3),-(a1)
	bra	sp3_22
sp3_37:	moveq	#3,d1
	bsr.s	sp3_44
	tst.w	d0
	beq.s	sp3_38
	addq.w	#5,d0
	bra.s	sp3_40
sp3_38:	move.b	-(a0),d0
	beq.s	sp3_39
	addi.w	#20,d0
	bra.s	sp3_40
sp3_39:	moveq	#13,d1
	bsr.s	sp3_44
	addi.w	#276,d0
sp3_40:	move.w	d0,d3
	add.w	d3,d3
sp3_41:	lea	sp3_52-sp3_53(a6),a4
	bsr.s	sp3_48
	lsr.w	#1,d3
	lea	1(a1,d0.w),a3
	move.b	-(a3),-(a1)
sp3_42:	move.b	-(a3),-(a1)
	dbf	d3,sp3_42
	bra	sp3_22
sp3_43:	moveq	#0,d1
	move.b	(a3),d1
sp3_44:	moveq	#0,d0
	cmpi.w	#7,d1
	bpl.s	sp3_47
sp3_45:	add.b	d7,d7
	beq.s	sp3_46
	addx.w	d0,d0
	dbf	d1,sp3_45
	rts
sp3_46:	move.b	-(a0),d7
	addx.b	d7,d7
	addx.w	d0,d0
	dbf	d1,sp3_45
	rts
sp3_47:	move.b	-(a0),d0
	subq.w	#8,d1
	bpl.s	sp3_45
	rts
sp3_48:	moveq	#0,d1
	move.b	(a3),d1
	adda.w	d1,a4
	move.w	(a4),d1
	bsr.s	sp3_44
	tst.b	d6
	beq.s	sp3_51
	move.w	d0,d4
	andi.w	#$FFF0,d4
	andi.w	#$000F,d0
	beq.s	sp3_50
	lsr.w	#1,d0
	beq.s	sp3_49
	roxr.b	#1,d7
	bcc.s	sp3_50
	move.b	d7,(a0)+
	moveq	#-128,d7
	bra.s	sp3_50
sp3_49:	moveq	#2,d1
	bsr.s	sp3_44
	add.w	d0,d0
	or.w	d4,d0
	bra.s	sp3_51
sp3_50:	lea	sp3_54-sp3_53(a6),a3
	or.b	(a3,d0.w),d4
	move.w	d4,d0
sp3_51:	add.w	18(a4),d0
	rts

	DC.W	3
sp3_52:	DC.W	4,5,7,8,9,10,11,12
	DC.W	-16
	DC.W	0,32,96,352,864,1888,3936,8032

sp3_53:	DS.L	1
sp3_54:	DS.B	8
sp3_55:	DS.W	2*64
sp3_56:	DS.W	2
	DS.B	1
sp3_57:	DS.B	1
	DS.B	2*64
sp3_58:	DS.B	512

GERE_TIME	ADDQ	#1,TIME
	ADDQ	#2,MOD_Y+2
	RTS
DEBUT	MOVE.L	#FIN,$008.W
	MOVE.L	#FIN,$00C.W
	MOVE.L	#FIN,$010.W
	MOVE.L	#FIN,$014.W
	MOVE.L	#FIN,$018.W
	MOVE.L	#FIN,$01C.W
	MOVE.L	#FIN,$020.W

	LEA	DEB_BSS,A0
	LEA	END_BSS,A1
.KILL_IT	CLR.L	(A0)+
	CMP.L	A1,A0
	BLE.S	.KILL_IT

	MOVE.L	#BUFFER,D0
	CLR.B	D0
	MOVE.L	D0,SCREEN1
	ADDI.L	#32000,D0
	MOVE.L	D0,SCREEN2

	MOVE.L	SCREEN1,A0
	MOVE.L	SCREEN2,A1
	MOVEQ	#0,D0
	MOVE	#7999,D7
.EFF_ECRS	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A1)+
	DBF	D7,.EFF_ECRS

	LEA	DEB_TY,A0
	MOVE	#2*640-1,D7
.R1	MOVE	#-1,(A0)+
	DBF	D7,.R1
	MOVE	#200-1,D7
.R2	CLR	(A0)+
	DBF	D7,.R2
	MOVE	#2*640-1,D7
.R3	MOVE	#2,(A0)+
	DBF	D7,.R3

	LEA	POINTS+2,A1
	MOVE	#COEFF_MULTI,D0
	MOVE	#(NB_PTS_*3)-1,D7
.OQP	MOVEQ	#0,D1
	MOVE	(A1),D1
	MULS	D0,D1
	MOVE	D1,(A1)+
	DBF	D7,.OQP

	LEA	POINTS,A0
	MOVE	(A0)+,D7
	SUBQ	#1,D7
.X2	REPT	3
	MOVE	(A0),D0
	ADD	D0,D0
	MOVE	D0,(A0)+
	ENDR
	DBF	D7,.X2

	LEA	POINTS,A0
	LEA	OBJ,A1
	MOVE	(A0)+,D7
	SUBQ	#1,D7
.COPIT	MOVE.L	(A0)+,(A1)+
	MOVE	(A0)+,(A1)+
	DBF	D7,.COPIT

	LEA	TABLE_ADR_COD2,A0
	MOVE	#5279,D7
.C2	MOVE.L	#RT_COOL2,(A0)+
	DBF	D7,.C2

	JSR	PREPA_LOG_EXP
	CLR	CHOICE
	JSR	INIT_ROUT_POLY2
	MOVE.L	#RT_COOL2,TABLE_ADR_COD2+40
	MOVE	#DONNEE_INIT,DONNEE
	MOVE.L	#BUF_TABLOS,ADR_TABLOS
	MOVE.L	#BUF_VISION,ADR_VISION

	LEA	SAVE_VALUES,A0
	LEA	ANGLES,A1
	MOVE	DONNEE,(A0)+
	MOVE.L	(A1)+,(A0)+
	MOVE	(A1),(A0)

	LEA	SINUS,A0
	LEA	SINUS+LONG_SINUS,A1
	MOVE.W	#(LONG_SINUS/8)-1,D7
.RECOPY	MOVE.W	(A0)+,(A1)+
	DBRA	D7,.RECOPY

	MOVE.W	#$2700,SR
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#INTER_RTE,$70.W
	MOVE.W	#$2300,SR

	LEA	SAVE_VALUES,A0
	LEA	ANGLES,A1
	MOVE	(A0)+,DONNEE
	MOVE.L	(A0)+,(A1)+
	MOVE	(A0),(A1)

	CLR.W	Z_BASE
	JSR	GERE_Z
	JSR	INIT_EFF

	LEA	MODIF,A1
	MOVE	#$4EF9,(A1)+
	MOVE.L	#JUMP,(A1)
	MOVE	#1,VISION

	CLR	TIME
	MOVE.L	#BUF_TABLOS,ADR_TABLOS
	LEA	SAVE_VALUES,A0
	LEA	ANGLES,A1
	MOVE	(A0)+,DONNEE
	MOVE.L	(A0)+,(A1)+
	MOVE	(A0),(A1)
	JSR	GERE_Z
	MOVE	#X_DEP,MOD_X+2
	MOVE	#Y_DEP,MOD_Y+2

LOOP_PR_T
	LEA	POINTS,A0
	LEA	ANGLES,A1
	LEA	TRANS,A6
	LEA	BUF_PTS,A2
	JSR	ROTATE

	BSR	AFFICH
	JSR	GERE_Z

	MOVE.B	$FFFFFC02.W,D7
	LEA	ANGLES,A0
	MOVE.W	(A0),D0
	ADD	ADX,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	MOVE.W	(A0),D0
	ADD	ADY,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	MOVE.W	(A0),D0
	ADD	ADZ,D0
	ANDI.W	#1023,D0
	MOVE.W	D0,(A0)+

	JSR	GERE_TIME
	ADD	#INC_D,DONNEE
	CMPI	#TIME_F,TIME
	BEQ.S	KOD_GEN_OUT
	BRA	LOOP_PR_T
KOD_GEN_OUT
LAST_HOPE
;	LEA	DEB_BSS3,A0
;	LEA	END_BSS3,A1
;.KILL_IT3	CLR.L	(A0)+
;	CMP.L	A1,A0
;	BLE.S	.KILL_IT3


	CLR	TIME		;Indispensable.
	MOVE.L	#TABLE_ADR,SAVE_ULTIME_A1+2	;Effaáage, ready.
	MOVE.L	#BUF_TABLOS,ADR_TABLOS	;Affichage, ready, pfoooo.
	MOVE.L	#BUF_VISION,ADR_VISION	;Visible ou non
	CLR	VISION
	MOVE	#X_DEP,MOD_X+2
	MOVE	#Y_DEP,MOD_Y+2

	MOVE	#$2700,SR

	MOVE.L	#INTER_RTE,$68.W
	MOVE.L	#INTER_RTE,$134.W
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	CLR.B	$FFFFFA13.W
	CLR.B	$FFFFFA15.W

	MOVE.B	#$12,$FFFFFC02.W

	CLR.B	$FFFFFA1B.W
	BSET	#0,$FFFFFA07.W
	BSET	#0,$FFFFFA13.W
	
	MOVE.L	#LAST_RUN,$70.W
	MOVE.L	#TIMER,$120.W
	MOVE	#$2300,SR
GUILIGUILI	BRA.S	GUILIGUILI

LAST_RUN
	CLR.B	$FFFFFA1B.W
	;MOVE.L	#TIMER,$120.W
	;MOVE.B	#28,$FFFFFA21.W
	;MOVE.B	#8,$FFFFFA1B.W
	;BSET	#0,$FFFFFA07.W
	;BSET	#0,$FFFFFA13.W
	;MOVE	#$2300,SR

	MOVE	#$001,$FFFF8240.W
	MOVE	#$112,$FFFF8242.W
	MOVE	#$223,$FFFF8244.W
	MOVE	#$223,$FFFF8246.W
	MOVE	#$334,$FFFF8248.W
	MOVE	#$334,$FFFF824A.W
	MOVE	#$334,$FFFF824C.W
	MOVE	#$334,$FFFF824E.W

	MOVE	#$445,$FFFF8250.W
	MOVE	#$445,$FFFF8252.W
	MOVE	#$445,$FFFF8254.W
	MOVE	#$445,$FFFF8256.W
	MOVE	#$445,$FFFF8258.W
	MOVE	#$445,$FFFF825A.W
	MOVE	#$445,$FFFF825C.W
	MOVE	#$445,$FFFF825E.W

SAVE_ULTIME_A1	LEA	TABLE_ADR,A1
	TST	(A1)
	BNE.S	.CA_TOURNE
	JMP	DEF_FUCK
.CA_TOURNE	MOVE.L	(A1)+,A2
	MOVE.L	A1,SAVE_ULTIME_A1+2
	MOVE.L	SCREEN2,A0
	MOVEQ	#0,D0
	JSR	(A2)

	JSR	AFFICH2

	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2

	MOVE.L	SCREEN1,D0
	lsr.l	#8,d0
	LEA	$FFFF8201.W,A0
	movep	d0,(a0)

	CMPI.B	#$F,$FFFFFC02.W
	BNE.S	.NO_TIME
	ST	$FFFF8240.W
.NO_TIME
	CMPI	#TIME_F-1,TIME
	BEQ.S	DEF_FUCK
	ADDQ	#1,TIME
.RTE	RTE
INTER_RTE	RTE

DEF_FUCK
	MOVE	#2,TIME		;Indispensable.
	MOVE.L	#TABLE_ADR,SAVE_ULTIME_A1+2	;Effaáage, ready.
	MOVE.L	#BUF_TABLOS,ADR_TABLOS	;Affichage, ready, pfoooo.
	MOVE.L	#BUF_VISION,ADR_VISION	;Visible ou non
	CLR	VISION

	MOVE.L	SAVE_ULTIME_A1+2,A1
	ADDQ	#8,A1
	MOVE.L	A1,SAVE_ULTIME_A1+2

	MOVE.L	SAVE_POUR_APRES,ADR_TABLOS

	MOVE.L	ADR_VISION,A0
	LEA	NB_FACES*2(A0),A0
	MOVE.L	A0,ADR_VISION

	RTE

TIMER
	CLR.B	$FFFFFA1B.W
	;MOVE.B	V1,$FFFFFA21.W
	;MOVE.B	#8,$FFFFFA1B.W
	;MOVE.L	#TIMER2,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE

GERE_Z	LEA	OBJ,A0
	LEA	POINTS+2,A1
	MOVE	DONNEE,D0

	MOVE	#NB_PTS_*3-1,D7
.D	MOVEQ	#0,D1
	MOVE	(A0)+,D1
	MULS	D0,D1
	LSR.L	#8,D1
	ADD	D1,D1
	MOVE	D1,(A1)+
	DBF	D7,.D

	;REPT	NB_PTS_
	;MOVE.L	(A0)+,(A1)+
	;MOVE	(A0)+,(A1)
	;ADD	D0,(A1)+
	;ENDR

	RTS

DONNEE	DC	194
TIME	DC	0

FIN	MOVE.L	4.W,A0
	JMP	(A0)

AFFICH	LEA	BUF_PTS,A0
	JSR	PROJETTE

	LEA	DONNêES_AFFICH,A6
	MOVE	(A6)+,D7
.LOOP_AFF	MOVE.L	D7,-(SP)
	MOVE	(A6)+,MOD_PLAN2
	LEA	COORD,A0
	LEA	face_en_cours,A1
	MOVE	(A6),(A1)+
	MOVE	(A6)+,D6
	SUBQ	#1,D6
.FACES	MOVE	(A6)+,D5
	MOVE.L	(A0,D5.W),(A1)+
	DBF	D6,.FACES
	LEA	face_en_cours,A0
	MOVE.L	A6,-(SP)
	JSR	RT_POLY
	MOVE.L	(SP)+,A6
	MOVE.L	(SP)+,D7
	DBF	D7,.LOOP_AFF
	RTS

SPE	DC	0
AFFICH2
	MOVE.L	ADR_TABLOS,A3

	LEA	DONNêES_AFFICH,A6
	MOVE	(A6)+,D7
.LOOP_AFF	MOVE.L	D7,-(SP)
	;MOVE	(A6)+,MOD_PLAN
	MOVE	(A6)+,MOD_PLAN2
	LEA	COORD,A0
	LEA	face_en_cours,A1
	MOVE	(A6),(A1)+
	MOVE	(A6)+,D6
	SUBQ	#1,D6
.FACES	MOVE	(A6)+,D5
	MOVE.L	(A0,D5.W),(A1)+
	DBF	D6,.FACES
	LEA	face_en_cours,A0
	MOVE.L	A6,-(SP)
	JSR	RT_POLY2
	MOVE.L	(SP)+,A6
	MOVE.L	(SP)+,D7
	DBF	D7,.LOOP_AFF

	MOVE.L	A3,ADR_TABLOS
	ADDQ	#1,SPE
	CMPI	#2,SPE
	BNE.S	.FOI
	MOVE.L	A3,SAVE_POUR_APRES
.FOI	RTS
SAVE_POUR_APRES	DC.L	0

RETOUR_POLY	RTS

RT_POLY2
	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
;	MOVE.W	(A1),D3
;	SUB.W	4(A1),D3
;	MOVE.W	2(A1),D4
;	SUB.W	6(A1),D4
;	MOVE.W	8(A1),D6
;	SUB.W	4(A1),D6
;	MOVE.W	$A(A1),D5
;	SUB.W	6(A1),D5
;	MULS.W	D3,D5
;	MULS.W	D6,D4
;	SUB.L	D4,D5
;	BGT.S	.VISIBLE
;	RTS
;.VISIBLE

	MOVE.L	ADR_VISION,A6
	TST.B	(A6)+
	BNE.S	.CONTN
	MOVE.L	A6,ADR_VISION
	RTS
.CONTN	MOVE.L	A6,ADR_VISION

	MOVEQ	#0,D1
	MOVE	(A3)+,D1
APPEL	;MOVE.L	SAVE_ADR_TAB,A3

	LEA	TABLE_ADR_COD2+40,A6
	MOVEQ	#-1,D7
	MOVE.L	SCREEN2,D6
;	MOVE	MOD_PLAN,KODE_DE_KRAD
MOD_PLAN2	NOP
	ADD.L	D1,D6	;...+Y_MIN*160
	SUBI.L	#160,D6
	MOVE.L	#160,D5
	MOVEQ	#0,D2
	MOVEQ	#0,D3

	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)

	;MOVEQ	#0,D3	1
	MOVE.B	(A3)+,D3	2
	ADD	D3,A5	2

	;MOVEQ	#0,D3	1
	MOVE.B	(A3)+,D3	2
	ADD	D3,A4	2

	MOVE	(A3)+,D4	2
	MOVE.L	(A6,D4.W),A2	5
	JMP	(A2)	2

RT_COOL2	ADD.L	D5,D6	1  PROCHAINE LIGNE:+160
	MOVE.L	D6,A4	1  A3=ADR ECRAN EN .W, WARNING!
	MOVE.L	A4,A5	1  A6=ECRAN TOO (LE A5 DE FULBERT)

	;MOVEQ	#0,D3	1
	MOVE.B	(A3)+,D3	2
	ADD	D3,A5	2

	;MOVEQ	#0,D3	1
	MOVE.B	(A3)+,D3	2
	ADD	D3,A4	2

	MOVE	(A3)+,D4	2
	MOVE.L	(A6,D4.W),A2	5
	JMP	(A2)	2

LONG_RT_COOL2 = ((*-RT_COOL2)/2)-1

VISION	DC	0

DONNêES_AFFICH	
	DC	NB_FACES-1

	DC	$4E71,8,X1,X2,X3,X4,X5,X6,X7,X8
	DC	$5446,8,X24,X23,X22,X21,X20,X19,X18,X17

	DC	$5446,4,X8,X16,X9,X1
	DC	$5446,4,X2,X10,X11,X3
	DC	$5446,4,X4,X12,X13,X5
	DC	$5446,4,X6,X14,X15,X7

	DC	$5846,4,X1,X9,X10,X2
	DC	$5846,4,X3,X11,X12,X4
	DC	$5846,4,X5,X13,X14,X6
	DC	$5846,4,X7,X15,X16,X8

	DC	$4E71,4,X18,X10,X9,X17
	DC	$4E71,4,X24,X16,X15,X23
	DC	$4E71,4,X22,X14,X13,X21
	DC	$4E71,4,X20,X12,X11,X19

	DC	$5C46,4,X17,X9,X16,X24
	DC	$5C46,4,X23,X15,X14,X22
	DC	$5C46,4,X12,X20,X21,X13
	DC	$5C46,4,X11,X10,X18,X19

INIT_EFF
	LEA	MOVEW,A0
	LEA	MOVEW_DEP,A1
.RECOP1	MOVE	(A0)+,(A1)+
	CMPI	#$1234,(A0)
	BNE.S	.RECOP1
	LEA	MOVEW_DEP,A0
	JSR	unpack

	LEA	MOVEL,A0
	LEA	MOVEL_DEP,A1
.RECOP2	MOVE	(A0)+,(A1)+
	CMPI	#$1234,(A0)
	BNE.S	.RECOP2
	LEA	MOVEL_DEP,A0
	JSR	unpack
	JSR	RE_BUILD

;Now, reconstruit la table...
	LEA	TABLE_ADR,A0
	LEA	BUF_COD,A1
.NOT_END	MOVE.L	A1,(A0)+
.LOOK_FOR_IT	CMPI	#$4E75,(A1)+
	BNE.S	.LOOK_FOR_IT
	CMPI	#$9999,(A1)
	BNE.S	.NOT_END

FINI	RTS

RE_BUILD	LEA	MOVEW_DEP,A1
	LEA	MOVEL_DEP,A2
	LEA	BUF_COD,A0
	MOVE	#306+2,D7	Nb d'Çtapes Ö dÇcompacter
.LOOP
	MOVEQ	#0,D1	Somme 1
	MOVEQ	#0,D2	Somme 2
;D'abord les MOVE.W:
.MOVE_W	MOVE	(A1)+,D0
	CMPI	#$4E75,D0
	BEQ.S	.SUITE
	MOVE	#$3140,(A0)+
	MOVE	D0,(A0)
	ADD	D1,(A0)
	MOVE	(A0)+,D1
	BRA.S	.MOVE_W
.SUITE
;Maintenant, les MOVE.L
.MOVE_L	MOVE	(A2)+,D0
	CMPI	#$4E75,D0
	BEQ.S	.ONCE_MORE
	MOVE	#$2140,(A0)+
	MOVE	D0,(A0)
	ADD	D2,(A0)
	MOVE	(A0)+,D2
	BRA.S	.MOVE_L
.ONCE_MORE	MOVE	#$4E75,(A0)+
	SUBQ	#1,D7
	BNE.S	.LOOP
	MOVE	#$9999,(A0)
	RTS
	
MOVEW	INCBIN	OBJ1_W1.PAK
	DC	$1234
MOVEL	INCBIN	OBJ1_L1.PAK
	DC	$1234
PROJETTE
	LEA	COEFF+COEFF_INIT*2,A1	380*2
	LEA	COORD,A5
	MOVE.W	#NB_PTS_-1,D7
AFF_ALL	MOVE.W	(A0)+,D0
	MOVE.W	(A0)+,D1
	MOVE.W	(A0)+,D2
	ADD.W	Z_BASE,D2
	ADD.W	D2,D2
	MOVE.W	(A1,D2.W),D2
	MULS.W	D2,D0
	ASR.L	#8,D0
	MULS.W	D2,D1
	ASR.L	#8,D1
MOD_X	ADDI.W	#X_DEP,D0
MOD_Y	ADDI.W	#Y_DEP,D1
	MOVE	D0,(A5)+
	MOVE	D1,(A5)+
	DBRA	D7,AFF_ALL
	RTS

;ENTRêES : A0=TABLE DE POINTS ( .W:NB DE POINTS, ET LES POINTS)
;          A1=TABLE D'ANGLE ( ANGLE X, ANGLE Y, ANGLE Z)
;          A6=TABLE DE TRANSLATION ( U_X, U_Y, U_Z)
;          A2=TABLE DE DESTINATION POUR LES POINTS

;Formules de rotation 3 axes:
;X'=X[SIN(T)SIN(U)SIN(V)+COS(T)COS(V)]+Y[COS(T)SIN(U)SIN(V)-SIN(T)COS(V)]+Z[COS(U)SIN(V)]
;Y'=X[SIN(T)COS(U)]+Y[COS(T)COS(U)]-ZSIN(U)
;Z'=X[SIN(T)SIN(U)COS(V)-COS(T)SIN(V)]+Y[COS(T)SIN(U)COS(V)+SIN(T)SIN(V)]+Z[COS(U)COS(V)]
ROTATE	LEA	SINUS(PC),A4	;SINUS
	LEA	LONG_SINUS/4(A4),A3	;COSINUS

	MOVE.W	(A1)+,D0	;ANGLE_X
	ADD.W	D0,D0
	MOVE.W	(A3,D0.W),D1	;COSINUS TETA_X
	MOVE.W	(A4,D0.W),D0	;SINUS TETA_X

	MOVE.W	(A1)+,D2	;ANGLE_Y
	ADD.W	D2,D2
	MOVE.W	(A3,D2.W),D3	;COSINUS TETA_Y
	MOVE.W	(A4,D2.W),D2	;SINUS TETA_Y

	MOVE.W	(A1)+,D4	;ANGLE_Z
	ADD.W	D4,D4
	MOVE.W	(A3,D4.W),D5	;COSINUS TETA_Z
	MOVE.W	(A4,D4.W),D4	;SINUS TETA_Z

	LEA	MATRICE,A4
	LEA	LOGARITHMES+512*2(PC),A3
	;ON FABRIQUE SIN(T)*SIN(U)*SIN(V)+COS(T)*COS(V)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D2,D6	;D6=SIN(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D4,D6	;D6=SIN(T)*SIN(U)*SIN(V)
	MOVE.W	D1,D7	;D7=COS(T)
	MULS.W	D5,D7	;D7=COS(T)*COS(V)
	ADD.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK1
	MOVEQ	#1*2,D6
.OK1	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*SIN(U)*SIN(V)-SIN(T)*COS(V)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D2,D6	;D6=COS(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D4,D6	;D6=COS(T)*SIN(U)*SIN(V)
	MOVE.W	D0,D7	;D7=SIN(T)
	MULS.W	D5,D7	;D7=SIN(T)*COS(V)
	SUB.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK2
	MOVEQ	#1*2,D6
.OK2	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(U)*SIN(V)
	MOVE.W	D3,D6	;D6=COS(U)
	MULS.W	D4,D6	;D6=COS(U)*SIN(V)
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK3
	MOVEQ	#1*2,D6
.OK3	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE SIN(T)*COS(U)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D3,D6	;D6=SIN(T)*COS(U)
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK4
	MOVEQ	#1*2,D6
.OK4	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*COS(U)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D3,D6	;D6=COS(T)*COS(U)
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK5
	MOVEQ	#1*2,D6
.OK5	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE -SIN(U)
	MOVE.W	D2,D6
	NEG.W	D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK6
	MOVEQ	#1*2,D6
.OK6	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE SIN(T)*SIN(U)*COS(V)-COS(T)*SIN(V)
	MOVE.W	D0,D6	;D6=SIN(T)
	MULS.W	D2,D6	;D6=SIN(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D5,D6	;D6=SIN(T)*SIN(U)*COS(V)
	MOVE.W	D1,D7	;D7=COS(T)
	MULS.W	D4,D7	;D7=COS(T)*SIN(V)
	SUB.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK7
	MOVEQ	#1*2,D6
.OK7	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(T)*SIN(U)*COS(V)+SIN(T)*SIN(V)
	MOVE.W	D1,D6	;D6=COS(T)
	MULS.W	D2,D6	;D6=COS(T)*SIN(U)
	ASR.L	#8,D6
	MULS.W	D5,D6	;D6=COS(T)*SIN(U)*COS(V)
	MOVE.W	D0,D7	;D7=SIN(T)
	MULS.W	D4,D7	;D7=SIN(T)*SIN(V)
	ADD.L	D7,D6
	ASR.L	#8,D6
	ADD.W	D6,D6
	TST.W	D6
	BNE.S	.OK8
	MOVEQ	#1*2,D6
.OK8	MOVE.W	(A3,D6.W),(A4)+

	;ON FABRIQUE COS(U)*COS(V)
	MULS.W	D3,D5	;D5=COS(U)*COS(V)
	ASR.L	#8,D5
	ADD.W	D5,D5
	TST.W	D5
	BNE.S	.OK9
	MOVEQ	#1*2,D5
.OK9	MOVE.W	(A3,D5.W),(A4)+

	MOVE.W	(A6)+,D0
	ADD.W	D0,D0	;U_X*2
	MOVE.W	D0,TRANSLATE_X
	MOVE.W	(A6)+,D0
	ADD.W	D0,D0	;U_Y*2
	MOVE.W	D0,TRANSLATE_Y
	MOVE.W	(A6),D0
	ADD.W	D0,D0	;U_Z*2
	MOVE.W	D0,TRANSLATE_Z

	MOVE.W	(A0)+,D7	;D7=NB DE POINTS
	SUBQ.W	#1,D7
	MOVE.L	#MATRICE,D6
	LEA	BUF_EXP,A5

ROTATE_ALL	MOVE.L	D6,A4
	MOVE.W	(A0)+,D0	;X*2
TRANSLATE_X = *+2
	ADDI.W	#$1234,D0
	MOVE.W	(A0)+,D1	;Y*2
TRANSLATE_Y = *+2
	ADDI.W	#$1234,D1
	MOVE.W	(A0)+,D2	;Z*2
TRANSLATE_Z = *+2
	ADDI.W	#$1234,D2

	REPT	3
	MOVE.W	(A3,D0.W),D3	;LOG(X)
	ADD.W	(A4)+,D3	;LOG(X)+LOG(COEFF)
	MOVE.W	(A5,D3.W),D3	;D3=X*COEFF
	MOVE.W	(A3,D1.W),D4	;LOG(Y)
	ADD.W	(A4)+,D4	;LOG(Y)+LOG(COEFF)
	MOVE.W	(A5,D4.W),D4	;D4=Y*COEFF
	MOVE.W	(A3,D2.W),D5	;LOG(Z)
	ADD.W	(A4)+,D5	;LOG(Z)+LOG(COEFF)
	MOVE.W	(A5,D5.W),D5	;D5=Z*COEFF
	ADD.W	D4,D3
	ADD.W	D5,D3
	MOVE.W	D3,(A2)+
	ENDR

	DBRA	D7,ROTATE_ALL
	RTS

PREPA_LOG_EXP	;ON ARRANGE LA PARTIE NEGATIVE DE LA TABLE DES LOG.
	LEA	LOGARITHMES(PC),A0
	MOVE.W	#512-1,D7
LOG_NEGATIFS	MOVE.W	(A0),D0
	ADD.W	D0,D0
	ADDI.W	#LONG_EXP/2,D0
	MOVE.W	D0,(A0)+
	DBRA	D7,LOG_NEGATIFS
	MOVE.W	#LONG_EXP/2,D7
	MULU.W	#3,D7
	MOVE.W	D7,(A0)+	;D7=LONG_EXP*3 (ZERO)
	MOVE.W	#512-1,D7
LOG_POSITIFS	MOVE.W	(A0),D0
	ADD.W	D0,D0
	MOVE.W	D0,(A0)+	;LOG*2
	DBRA	D7,LOG_POSITIFS

;ON CREE LA PARTIE NEGATIVE ET LA 2EME PARTIE POSITIVE DES EXPONENTIELLES.
	LEA	EXPONENTIELLES(PC),A0
	LEA	BUF_EXP,A1
	LEA	LONG_EXP/2(A1),A2
	LEA	LONG_EXP/2(A2),A3
	MOVE.W	#LONG_EXP/4-1,D7
MAKE_EXP	MOVEQ	#0,D0
	MOVE.L	(A0)+,D0
	LSR.L	#8,D0	;VALEUR REELLE DU RESULTAT
	MOVE.W	D0,(A1)+
	MOVE.W	D0,(A3)+
	NEG.W	D0
	MOVE.W	D0,(A2)+
	DBRA	D7,MAKE_EXP

;ON CREE LES 2 TABLES DE ZERO LONGUES CHACUNES DE LONG_EXP.
	LEA	BUF_EXP+(LONG_EXP/2)*3,A0
	MOVE.W	#(LONG_EXP/4)*2-1,D7
MAKE_ZERO_TABLE	CLR.W	(A0)+
	DBRA	D7,MAKE_ZERO_TABLE
	RTS

SINUS	INCBIN	SINUSROT.DAT
LONG_SINUS = *-SINUS
	DCB.W	LONG_SINUS/4,0

LOGARITHMES	INCBIN	LOG512.LOG

EXPONENTIELLES	INCBIN	EXP512.EXP
LONG_EXP = *-EXPONENTIELLES

COEFF	INCBIN	COEFF2.3D

*** Partie affichage
CHOICE	DC	0	;0=MOVE
			;1=OR
RT_POLY
	TST	VISION
	BEQ.S	.NO
	MOVE.L	ADR_VISION,A1
	CLR.B	(A1)+
	MOVE.L	A1,ADR_VISION
.NO
	MOVE.L	A0,ADR_DEPART
	MOVE	#$4E75,MODIFIê
	LEA	2(A0),A1	;A1 POINTE SUR LES POINTS
	MOVE.W	(A1),D3
	SUB.W	4(A1),D3
	MOVE.W	2(A1),D4
	SUB.W	6(A1),D4
	MOVE.W	8(A1),D6
	SUB.W	4(A1),D6
	MOVE.W	$A(A1),D5
	SUB.W	6(A1),D5
	MULS.W	D3,D5
	MULS.W	D6,D4
	SUB.L	D4,D5
	BGT.S	.VISIBLE
	RTS
.VISIBLE
	MOVE	#25,REMP_GAUCHE+256*2
	MOVE	#15,REMP_DROIT+256*2

;	LEA	REMP_GAUCHE,A1
;	MOVE	#(500/2)-1,D6
;.EFF_TBX	CLR.L	(A1)+
;	DBF	D6,.EFF_TBX

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	MOVEQ	#0,D7

	MOVE.W	(A0)+,D0	;NB DE POINTS COMPOSANT LA SURFACE
	LEA	BUF_EXAM,A1
	MOVE.L	A0,A2
	SUBQ.W	#1,D0
	MOVE.W	D0,NB_PTS
.COPYING	MOVE.L	(A0)+,(A1)+
	DBRA	D0,.COPYING
	MOVE.L	(A2),(A1)+

	LEA	BUF_EXAM,A0
	LEA	BUF_GAUCHE,A2
	LEA	BUF_DROIT,A3
	ADDQ.W	#4,A0
	MOVE.W	-2(A0),D3	;Y DE REFERENCE
NB_PTS = *+2
	MOVE.W	#$1234,D0
	MOVEQ	#-1,D6
	MOVEQ	#-1,D7
ALL_PTS	MOVE.W	(A0),D1	;X
	MOVE.W	2(A0),D2	;Y
	CMP.W	D2,D3
	BGE.S	ON_MONTE
ON_DESCEND	MOVE.L	-4(A0),(A2)+
	MOVE.L	(A0)+,(A2)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D6
	DBRA	D0,ALL_PTS
	BRA.S	FINITO
ON_MONTE	MOVE.L	-4(A0),(A3)+
	MOVE.L	(A0)+,(A3)+
	MOVE.W	D2,D3
	ADDQ.W	#1,D7
	DBRA	D0,ALL_PTS
FINITO	;ICI LES TABLEAU BUF_GAUCHE ET BUF_DROIT SONT CONSTITUêS
	;D6=NB DE PTS-1 DANS BUF_GAUCHE
	;D7=NB DE PTS-1 DANS BUF_DROIT

	LEA	BUF_GAUCHE,A0
MAKE_REMP_GAUCHE
	LEA	REMP_GAUCHE,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	MOVE	#$4E71,MODIFIê
	SUB.W	D1,D3	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D1,D1
	ADDA.W	D1,A1

	ADDQ.W	#1,D3
	SUB.W	D0,D2
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D3
.PUT	MOVE.W	D0,(A1)+
	DBRA	D3,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D2
	MOVEQ	#8,D4
	ASL.L	D4,D2
	DIVS.W	D3,D2
	EXT.L	D2
	ASL.L	#8,D2
	SUBQ.W	#2,D3
	EXT.L	D0
	MOVEQ	#16,D4
	MOVE.W	D0,(A1)+
	ASL.L	D4,D0
.MK_X	ADD.L	D2,D0
	MOVE.L	D0,D1
	SWAP	D1
	MOVE.W	D1,(A1)+
	DBRA	D3,.MK_X
.NEXT	DBRA	D6,MAKE_REMP_GAUCHE

MODIFIê	RTS

	TST	VISION
	BEQ.S	.NO2
	MOVE.L	ADR_VISION,A0
	MOVE.B	#1,-1(A0)
.NO2

	LEA	BUF_DROIT,A0
MAKE_REMP_DROIT	LEA	REMP_DROIT,A1
	MOVE.W	(A0)+,D0	;X1
	MOVE.W	(A0)+,D1	;Y1
	MOVE.W	(A0)+,D2	;X2
	MOVE.W	(A0)+,D3	;Y2
	BSR	CLIPPING_Y
	TST.W	D5
	BLT.S	.NEXT
	SUB.W	D3,D1	;HAUTEUR-1 DU SEGMENT
	BEQ.S	.NEXT

	ADD.W	D3,D3
	ADDA.W	D3,A1

	ADDQ.W	#1,D1
	SUB.W	D2,D0
	BNE.S	.PAS_VERTICALE
.VERTICALE	SUBQ.W	#1,D1
.PUT	MOVE.W	D2,(A1)+
	DBRA	D1,.PUT
	BRA.S	.NEXT
.PAS_VERTICALE	EXT.L	D0
	MOVEQ	#8,D4
	ASL.L	D4,D0
	DIVS.W	D1,D0
	EXT.L	D0
	ASL.L	#8,D0
	SUBQ.W	#2,D1
	MOVE.W	D2,(A1)+
	EXT.L	D2
	MOVEQ	#16,D4
	ASL.L	D4,D2
.MK_X	ADD.L	D0,D2
	MOVE.L	D2,D3
	SWAP	D3
	MOVE.W	D3,(A1)+
	DBRA	D1,.MK_X
.NEXT	DBRA	D7,MAKE_REMP_DROIT

*****
ADR_DEPART= *+2
	LEA	$12345678,A0
	MOVE.W	(A0)+,D7
	SUBQ.W	#1,D7
	MOVE.W	#-1000,D0	;MAXIMUM
	MOVE.W	#20000,D1	;MINIMUM
.TEST_MAX	CMP.W	2(A0),D0
	BGE.S	.FUCK
	MOVE.W	2(A0),D0
.FUCK	CMP.W	2(A0),D1
	BLE.S	.FUCK_2
	MOVE.W	2(A0),D1
.FUCK_2	ADDQ.W	#4,A0
	DBRA	D7,.TEST_MAX
	;D0 = Y MAXIMUM
	;D1 = Y MINIMUM

	CMPI.W	#200,D0
	BLT.S	.OK_Y_MAX
	MOVE.W	#199,D0
.OK_Y_MAX	TST.W	D1
	BGE.S	.OK_Y_MIN
	MOVEQ	#0,D1
.OK_Y_MIN

INIT	LEA	REMP_GAUCHE,A0
	LEA	REMP_DROIT,A1
	ADDQ.W	#1,D0
	ADD.W	D0,D0
	MOVE.W	#15,(A1,D0.W)
	MOVE.W	#$9999,2(A1,D0.W)
	MOVE.W	#25,(A0,D0.W)

.F	ADD.W	D1,D1
	ADDA.W	D1,A0
	ADDA.W	D1,A1
	MULU.W	#80,D1       ;D1=Y_MIN*160 (POUR ADD ECRAN)

	SWAP	D1
	CLR	D1
	SWAP	D1
MODIF	NOP
	NOP
	NOP

CLIPPING_Y	MOVEM.L	D4/D6/A6,-(SP)
	MOVEQ	#0,D5
	LEA	TABLE_Y,A6
	ADD.W	D1,D1
	ADD.W	D3,D3
	MOVEQ	#0,D4
	ADD.W	(A6,D1.W),D4
	ADD.W	(A6,D3.W),D4
	ASR.W	#1,D1
	ASR.W	#1,D3
	TST.W	D4
	BEQ	FIN_NORMALE
	CMPI.W	#-2,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#4,D4
	BEQ	RIEN_DU_TOUT
	CMPI.W	#-1,D4
	BEQ	CLIP_HAUT
	CMPI.W	#2,D4
	BEQ	CLIP_BAS

CLIP_HAUT_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	BRA.S	SUIT
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1

SUIT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_BAS	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	MOVE.W	D2,D5
	SUB.W	D0,D5	;D5=X2-X1
	MULS.W	#200,D5	;D5=200*(X2-X1)
	DIVS.W	D4,D5
	DIVS.W	D4,D6
	ADD.W	D5,D6	;D6=X2 CLIPPê
	CMPI.W	#200,D1
	BGE.S	.C_EST_Y1
.C_EST_Y2	MOVE.W	D6,D2
	MOVE.W	#199,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y1	MOVE.W	D6,D0
	MOVE.W	#199,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

CLIP_HAUT	MOVE.W	D3,D4
	SUB.W	D1,D4	;D4=Y2-Y1
	MOVE.W	D2,D5
	MULS.W	D1,D5	;D5=Y1*X2
	MOVE.W	D3,D6
	MULS.W	D0,D6	;D6=X1*Y2
	SUB.L	D5,D6	;D6=X1*Y2-Y1*X2
	DIVS.W	D4,D6
	;D6=X CLIPPê
	TST.W	D1
	BGE.S	.C_EST_Y2
.C_EST_Y1	MOVE.W	D6,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE
.C_EST_Y2	MOVE.W	D6,D2
	MOVEQ	#0,D3
	MOVEQ	#0,D5
	BRA.S	FIN_NORMALE

RIEN_DU_TOUT	MOVE.W	#-1,D5
FIN_NORMALE	MOVEM.L	(SP)+,D4/D6/A6
	RTS
********************************************************************
INIT_ROUT_POLY2	LEA	BUF_COD_GEN2,A0
	LEA	TABLE_ADR_COD2,A1

	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D3
.GO	MOVE.L	#RT_COOL2,(A1)+
	DBRA	D3,.GO

	MOVE.W	#$FFFF,D0	;1ER MOTIF
	MOVE.W	#$8000,D1	;DERNIER MOTIF
	MOVEQ	#0,D3	;DECALAGE

	MOVEQ	#16-1,D6
.DECAL
	MOVEQ	#16+1,D5
	ADD.W	#18*16,D5	;D5=LONGUEUR DE LA LIGNE
	ADD.W	D3,D5

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
.COPY_MOVED7	BSR	POZ_ADRESSE
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	SUBI.W	#16,D5
	DBRA	D7,.COPY_MOVED7

	MOVE.W	#16+1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_1
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_1
.PAS_D7_1
	TST	CHOICE
	BNE.S	.OR1
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV1
.OR1	MOVE	#$0054,(A0)+	OR.W #$Imm,(A4)
.MOV1	MOVE.W	D0,(A0)+
.RETOUR_1	MOVE.W	#1,D5
	ADD.W	D3,D5
	BSR	POZ_ADRESSE
	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_2
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_2
.PAS_D7_2
	TST	CHOICE
	BNE.S	.OR2
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV2
.OR2	MOVE	#$0055,(A0)+	OR "             "
.MOV2	MOVE.W	D1,(A0)+
.RETOUR_2
	BSR	COPY_RT_COOL2

	ASR.W	#1,D1
	ADDQ.W	#1,D3	;LONGUEUR SUIVANT
	DBRA	D6,.DECAL

	LEA	TABLE_ADR_COD2+1320,A1

	MOVE.W	#$7FFF,D3	;1ER MOTIF
	MOVEQ	#14,D5

	MOVEQ	#14,D2
ALL_RT2
	MOVE.L	#RETOUR_POLY,(A1)+
	MOVEQ	#9-1,D0
.GO	MOVE.L	#RT_COOL2,(A1)+
	DBRA	D0,.GO

	MOVE.W	#1,A3

	MOVEQ	#0,D0
	MOVE.W	D5,D6
LITTLE_TWO	BSR	POZ_ADRESSE2
	BSET	D6,D0
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_LITTLE
	MOVE.W	#$3887,(A0)+
	BRA.S	.RETOUR_LITTLE
.PAS_D7_LITTLE
	TST	CHOICE
	BNE.S	.OR3
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV3
.OR3	MOVE	#$0054,(A0)+
.MOV3	MOVE.W	D0,(A0)+

.RETOUR_LITTLE
	BSR	COPY_RT_COOL2

	ADDQ.W	#1,A3
	DBRA	D6,LITTLE_TWO

	MOVE.W	D3,D0
	MOVE.W	#$8000,D1	;DERNIER MOTIF

	MOVEQ	#16-1,D6
DECAL_TWO
	MOVE.W	#18*16,A3
	ADD.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3

	MOVE.W	#-144,D4
	MOVEQ	#18-1,D7
COPY_MOVED7_	BSR	POZ_ADRESSE2
	MOVE.W	#$3B47,(A0)+	;MOVE.W D7,d16(A5)
	MOVE.W	D4,(A0)+
	ADDQ.W	#8,D4
	LEA	-16(A3),A3
	DBRA	D7,COPY_MOVED7_

	MOVE.W	D5,A3
	ADDQ.W	#1,A3
	MOVEQ	#16,D4
	SUB.W	D6,D4
	ADDA.W	D4,A3
	BSR	POZ_ADRESSE2
	CMPI.W	#-1,D0
	BNE.S	.PAS_D7_3
	MOVE.W	#$3887,(A0)+	;MOVE.W D7,(A4)
	BRA.S	.RETOUR_3
.PAS_D7_3
	TST	CHOICE
	BNE.S	.OR4
	MOVE.W	#$38BC,(A0)+	;MOVE.W #$Imm,(A4)
	BRA.S	.MOV4
.OR4	MOVE	#$0054,(A0)+
.MOV4	MOVE.W	D0,(A0)+
.RETOUR_3	CMPI.W	#-1,D1
	BNE.S	.PAS_D7_4
	MOVE.W	#$3A87,(A0)+	;MOVE.W D7,(A5)
	BRA.S	.RETOUR_4
.PAS_D7_4
	TST	CHOICE
	BNE.S	.OR5
	MOVE.W	#$3ABC,(A0)+	;MOVE.W #$Imm,(A5)
	BRA.S	.MOV5
.OR5	MOVE	#$0055,(A0)+
.MOV5	MOVE.W	D1,(A0)+
.RETOUR_4
	BSR	COPY_RT_COOL2

	ASR.W	#1,D1
	DBRA	D6,DECAL_TWO

	SUBQ.W	#1,D5
	LSR.W	#1,D3
	LEA	1280(A1),A1
	DBRA	D2,ALL_RT2

TBL4:	LEA	TABLE_A4,A0
	MOVE	#639,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	D6,(A0)+
	MOVE	(A1),(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#639,D7
.FINT	MOVE	#319*4,(A0)+
	MOVE	#152,(A0)+
	;CLR	(A0)+
	DBF	D7,.FINT
TBL2:
	LEA	TABLE_A2,A0
	MOVE	#639,D7
.DEBT	CLR.L	(A0)+
	DBF	D7,.DEBT

	LEA	MINI_TABLE,A1
	MOVEQ	#0,D6	D6=X
	MOVEQ	#20-1,D7
.F	MOVEQ	#16-1,D0
.SXTEEN	MOVE	(A1),(A0)+
	MOVE	D6,(A0)
	MOVE	D6,D5
	LSR	#2,D5
	AND	#15,D5
	MULU	#1320,D5
	SUB	D5,(A0)+
	ADDQ	#4,D6
	DBF	D0,.SXTEEN
	ADDQ	#2,A1
	DBF	D7,.F

	MOVE	#639,D7
.FINT	;CLR	(A0)+
	MOVE	#152,(A0)+
	MOVE	#319*4,(A0)+
	DBF	D7,.FINT
	RTS
MINI_TABLE
N	SET	0
	REPT	20
	DC	N
N	SET	N+8
	ENDR

POZ_ADRESSE2	MOVEM.L	D5/A0,-(SP)
	MOVE.W	A3,D5
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS

POZ_ADRESSE	MOVEM.L	D5/A0,-(SP)
	SUBQ.W	#1,D5
	ADD.W	D5,D5
	ADD.W	D5,D5
;	SUBA.L	#BUF_COD_GEN,A0
	MOVE.L	A0,(A1,D5.W)
	MOVEM.L	(SP)+,D5/A0
	RTS

COPY_RT_COOL2	LEA	RT_COOL2,A6
	MOVE.W	#LONG_RT_COOL2,D7
.COPY	MOVE.W	(A6)+,(A0)+
	DBRA	D7,.COPY
	RTS

FLAG_DêB	DC	0
JUMP
PRêCALC	CLR	FLAG_DêB
	MOVE.L	#TABLE_A2+640*4,D4
	LEA	TABLE_A4+640*4,A3

	MOVE.L	ADR_TABLOS,A4
	MOVE	D1,(A4)+
.LOOP_PRê
	MOVE.L	D4,A2	1  D4=A2=TABLE_A2
	MOVE.W	(A1)+,D1	2  X2
	CMPI	#$9999,D1
	BNE.S	.NOT_END
	MOVE.L	A4,ADR_TABLOS
	RTS
.NOT_END	TST	FLAG_DêB
	BNE.S	.NO_TEST
	TST	D1
	BGE.S	.OK1
	TST	(A0)
	BGE.S	.NO_TEST
;Ici, les 2 x sont nÇgatifs.
	ADDQ	#2,A0
	ADD	#160,-2(A4)
	BRA.S	.LOOP_PRê
.OK1	CMPI	#320,(A0)
	BLT.S	.NO_TEST
	ADDQ	#2,A0
	ADD	#160,-2(A4)
	BRA.S	.LOOP_PRê

.NO_TEST	MOVE	#1,FLAG_DêB
	ADD.W	D1,D1	1  
	ADD.W	D1,D1	1
	MOVE.L	(A3,D1.W),D3	5
	MOVE.B	D3,(A4)+
	SWAP	D3	1
	MOVE.W	(A0)+,D1	2  X1
	ADD	D1,D1	1
	ADD	D1,D1	1
	ADD	D1,A2	2  A2=TABLE_A2+X1*4
	MOVE	(A2)+,D1
	MOVE.B	D1,(A4)+
	SUB	(A2),D3	2  (A2)=X1*2-LE DêCALAGE*1320... RUSê, HEIN?   ;D'OU D3=X2*2-(X1*2-DêCALAGE)=(X2-X1)*2+DêCALAGE*1320, ZOU.
	MOVE	D3,(A4)+
	BRA.S	.LOOP_PRê

ADX	DC	2	Z
ADY	DC	0	X
ADZ	DC	2	Y

POINTS
	DC	NB_PTS_
N	SET	-25
 DC 0,20,N
 DC 14,14,N
 DC 20,0,N
 DC 14,-15,N
 DC 0,-20,N
 DC -15,-15,N
 DC -20,-1,N
 DC -15,14,N
N	SET	0
 DC 0,60,N
 DC 42,42,N
 DC 60,0,N
 DC 42,-43,N
 DC 0,-60,N
 DC -43,-43,N
 DC -60,-1,N
 DC -43,42,N
N	SET	25
 DC 0,20,N
 DC 14,14,N
 DC 20,0,N
 DC 14,-15,N
 DC 0,-20,N
 DC -15,-15,N
 DC -20,-1,N
 DC -15,14,N

ANGLES	DC.W	0,0,-20
TRANS	DC.W	0,0,0
	SECTION	BSS
DEB_BSS
DEB_TY	DS.W	2*640
TABLE_Y	DS.W	200
	DS.W	2*640

Z_BASE	DS.W	1
MATRICE	DS.W	9
BUF_EXP	DS.W	(LONG_EXP/4)*5
BUF_PTS	DS.W	NB_PTS_*3
TABLE_A4	DS.L	320+640*2
TABLE_A2	DS.L	320+640*2
BUF_EXAM	DS.W	2*50
BUF_GAUCHE	DS.W	4*50
BUF_DROIT	DS.W	4*50
REMP_GAUCHE	DS.W	260
REMP_DROIT	DS.W	260
;MOVEW_DEP	DS.B	120000
;MOVEL_DEP	DS.B	10000

COORD	DS	2*NB_PTS_
face_en_cours	dS.w	1
	ds.l	10
OBJ	DS	3*NB_PTS_

TABLE_ADR_COD2	DS.B	(1280+40)*16
BUF_COD_GEN2	DS.B	30000
SAVE_VALUES	DS.W	4
ADR_VISION	DS.L	1
BUF_VISION	DS.B	20000
TABLE_ADR	DS.L	400
ADR_TABLOS	DS.L	1
BUF_TABLOS	DS.B	406000
BUF_COD	DS.B	166000
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	256
BUFFER	DS.B	64000
MOVEW_DEP	EQU	BUF_TABLOS
MOVEL_DEP	EQU	BUF_TABLOS+300000
END_BSS