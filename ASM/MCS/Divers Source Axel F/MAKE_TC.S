****************************************************************************
*	GENERATEUR DE CODE POUR SPRITE TRUE COLOR
*	  BY AXEL F.  /   M.C.S.
*	   25/10/93
*
*	    AVEC DEVPAC, CODE 68030, TABULATION A 8
*
* VOIR A LA FIN POUR LES PARAMETRES CONCERNANT LE SPRITE A TRAITER
* ET OU L'AFFICHER

	BSR	PASS1			* AFFICHAGE DU SPRITE
	BSR	PASS2			* SAUVEGARDE DU FOND
	BSR	PASS3			* RESTAURATION DU FOND
	BSR	WRITE_TO_DISK	
	CLR.L	-(A7)
	TRAP	#1


****************************************************************************
*      GENERE LES 'MOVE' POUR L'AFFICHAGE DU SPRITE EN TRUE COLOR
*      SACHANT QUE         4 MOVE.L (A0)+,10(A1)
*
*      SONT + RAPIDES QUE    MOVEM.L (A0)+,D0-D3
*                            MOVEM.L D0-D3,10(A1)
*      ON PEUT, AVEC CETTE METHODE, DEPLACER ENVIRON 10000 POINTS
*      EN UNE VBL (ECRAN 320*200*TC 50 HZ)

PASS1	
	MOVE.L	#DEPART,A0
	LEA	CODE+10,A1
	LEA	DATA,A2
	LEA	TEMPO,A6
	MOVEQ	#0,D1
	MOVE.W	#0,COMPTEUR_OFFSET
	
LINE	BSR	CHECK
	MOVE.L	#X,D0
	MOVEQ	#0,D3			* COMPTE LES POINTS ALIGNES
	MOVEQ	#0,D4			* OFFSET PAR RAPORT AU PT D'ORIGINE
	MOVEQ	#0,D6
	MOVEQ	#%11111,D7		* POUR LA DEST DE MOVEM.L
NEXT_PT	SUBQ.L	#1,D0
	BMI.S	NEW_LINE		
	MOVE.W	(A0,D4.W),D5
	BEQ.S	BLANK			* C'EST LE FOND DONC ON TRAITE CE QUE L'ON A TROUVE
	ADDQ.L	#1,D3
	ADDQ.L	#2,D4
	CMP.W	#26,D3
	BEQ	MAX			* 26 POINTS ATTEINDS
	BRA	NEXT_PT
	
NEW_LINE
	BSR	TRAITE
	ADDQ.L	#1,D1
	CMP.W	#Y,D1
	BEQ	FIN_PASS1
	ADD.W	#LONG-(X*2),COMPTEUR_OFFSET
	ADD.W	#LEN-(X*2),A0
	BRA	LINE
	
BLANK	BSR	TRAITE
	ADDQ.W	#2,COMPTEUR_OFFSET
	ADDQ.W	#2,A0
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	BRA	NEXT_PT
	
MAX	BSR	TRAITE
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	BRA	NEXT_PT
	
FIN_PASS1
	CMP.W	#$D0FC,-4(A1)
	BNE.S	BON
	SUBQ.W	#4,A1
	MOVEQ	#0,D5
	MOVE.W	-(A6),D5
	SUB.L	D5,RESTE

BON	MOVE.W	#$4E75,(A1)+
	MOVE.L	A1,A3
	SUB.L	#CODE,A1
	MOVE.L	A1,LEN_CODE
	MOVE.L	A1,CODE+6
	ADD.L	#48,CODE+6
	SUB.L	#DATA,A2
	MOVE.L	A2,LEN_DATA
	ADDQ.L	#2,LEN_DATA
	MOVE.L	A2,D0
	LEA	DATA,A2
COP3	MOVE.B	(A2)+,(A3)+
	SUBQ.L	#1,D0
	BNE.S	COP3
	MOVE.W	#$4E75,(A3)+
	RTS	
***************************************************************************
CHECK	CMP.W	#32*1024-LONG,COMPTEUR_OFFSET
	BLE	POINT0					* RTS
	MOVE.W	#$D0FC,(A1)+				* ADD.W #XXXX,A0
	MOVEQ	#0,D5
	MOVE.W	COMPTEUR_OFFSET,D5
	MOVE.W	D5,(A1)+
	MOVE.W	D5,(A6)+
	ADD.L	D5,RESTE
	MOVE.W	#0,COMPTEUR_OFFSET
	RTS
RESTE	DC.L	0
TEMPO	DS.L	10

***************************************************************************
* ICI ON GENERE LE CODE EN FONCTION DU NOMBRE DE POINTS CONSECUTIFS TROUVES

TRAITE	LEA	TABLE,A3
	ADD.L	D3,NB_PT
	MOVE.L	(A3,D3.W*4),A3
	JMP	(A3)
	
POINT0	RTS

POINT1	MOVE.W	#$3159,(A1)+			* MOVE.W  (A1)+,XXXX(A0)
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET		
	RTS

POINT2	MOVE.W	#$2159,(A1)+			* MOVE.L  (A1)+,XXXX(A0)
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET		
	RTS

POINT3	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	RTS
	
POINT4	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	RTS

POINT5	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	RTS
	
POINT6	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	RTS

POINT7	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	RTS

POINT8	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	RTS

POINT9	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$2159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.L	(A0)+,(A2)+
	ADDQ.W	#4,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	RTS
	
POINT26 ADDQ.W	#1,D6
	BSET	#14,D7
POINT25 ADDQ.W	#1,D6
	BSET	#13,D7
POINT24	ADDQ.W	#1,D6
	BSET	#13,D7
POINT23	ADDQ.W	#1,D6
	BSET	#12,D7
POINT22	ADDQ.W	#1,D6
	BSET	#12,D7
POINT21	ADDQ.W	#1,D6
	BSET	#11,D7
POINT20	ADDQ.W	#1,D6
	BSET	#11,D7
POINT19	ADDQ.W	#1,D6
	BSET	#10,D7
POINT18	ADDQ.W	#1,D6
	BSET	#10,D7
POINT17	ADDQ.W	#1,D6
	BSET	#7,D7
POINT16	ADDQ.W	#1,D6
	BSET	#7,D7
POINT15	ADDQ.W	#1,D6
	BSET	#6,D7
POINT14	ADDQ.W	#1,D6
	BSET	#6,D7
POINT13	ADDQ.W	#1,D6
	BSET	#5,D7
POINT12	ADDQ.W	#1,D6
	BSET	#5,D7	
POINT11	ADDQ.W	#1,D6
	
POINT10	

	BTST	#0,D6
	BNE	PLUS1

	MOVE.W	#$4CD9,(A1)+		* MOVEM.L (A1)+,
	MOVE.W	D7,(A1)+
	MOVE.W	#$48E8,(A1)+		* MOVEM.L   ,XXXX(A0)
	MOVE.W	D7,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	D6,D5
	ADD.W	#9,D5
	MOVE.W	D5,D7
COP1	MOVE.W	(A0,D5.W*2),(A2,D5.W*2)
	DBF	D5,COP1
	ADDQ.W	#1,D7
	ADD.W	D7,D7
	ADD.W	D7,A0	
	ADD.W	D7,A2
	ADD.W	D7,COMPTEUR_OFFSET
	BRA	FINI
	
PLUS1	MOVE.W	#$4CD9,(A1)+		* MOVEM.L (A1)+,
	MOVE.W	D7,(A1)+
	MOVE.W	#$48E8,(A1)+		* MOVEM.L   ,XXXX(A0)
	MOVE.W	D7,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	D6,D5
	ADDQ.W	#8,D5
	MOVE.W	D5,D7
COP2	MOVE.W	(A0,D5.W*2),(A2,D5.W*2)
	DBF	D5,COP2
	ADDQ.W	#1,D7
	ADD.W	D7,D7
	ADD.W	D7,A0	
	ADD.W	D7,A2
	ADD.W	D7,COMPTEUR_OFFSET
	MOVE.W	#$3159,(A1)+
	MOVE.W	COMPTEUR_OFFSET,(A1)+
	MOVE.W	(A0)+,(A2)+
	ADDQ.W	#2,COMPTEUR_OFFSET
	
FINI	MOVEQ	#0,D6
	MOVEQ	#%11111,D7
	RTS
****************************************************************************
*	SAUVEGARDE DU FOND
*	EN FAIT ICI ON SE BASE SUR CE QU'A DONNE LA PREMIERE PASSE
PASS2
	MOVE.L	LEN_DATA,D0
	ADD.L	LEN_CODE,D0
	ADD.L	#$28,D0
	MOVE.L	D0,BUFFER+8

	MOVEQ	#0,D6
	MOVE.L	A3,D7
	LEA	CODE+10,A0
CONVERT	MOVE.W	(A0)+,D1
	CMP.W	#$D0FC,D1			* ADD.W	#XXXX,A0
	BNE.S	NOT_ADD_W
	MOVE.W	D1,(A3)+
	MOVE.W	(A0)+,(A3)+
	BRA	CONVERT

NOT_ADD_W
	CMP.W	#$2159,D1			* MOVE.L (A1)+,XXXX(A0)
	BNE	NOT_MOVE_L
	MOVE.W	#$2328,(A3)+			* MOVE.L XXXX(A0),-(A1)
	MOVE.W	(A0)+,(A3)+
	ADDQ.L	#4,D6
	BRA	CONVERT
NOT_MOVE_L
	CMP.W	#$3159,D1			* MOVE.W (A0)+,XXXX(A1)
	BNE	NOT_MOVE_W
	MOVE.W	#$3328,(A3)+			* MOVE.W XXXX(A0),-(A1)
	MOVE.W	(A0)+,(A3)+
	ADDQ.L	#2,D6
	BRA	CONVERT
NOT_MOVE_W
	CMP.W	#$4CD9,D1
	BNE.S	NOT_MOVEM
	MOVE.W	#$4CE8,(A3)+
	MOVE.W	(A0)+,(A3)+
	MOVE.W	4(A0),(A3)+
	MOVE.W	#$48E1,(A3)+
	MOVE.W	2(A0),D0
	BSR	CONVER
	MOVE.W	D2,(A3)+
	ADDQ.W	#6,A0
	BRA	CONVERT
NOT_MOVEM
	MOVE.W	#$4E75,(A3)+
	MOVE.L	A3,A5
	SUB.L	D7,A5
	MOVE.L	A5,LEN_SAV
	RTS

CONVER	MOVEQ	#0,D4
	MOVEQ	#0,D2
	MOVEQ	#15,D5
SUIV	BTST	D4,D0
	BEQ	VIDE
	BSET	D5,D2
	ADDQ.L	#4,D6
	BRA	GOTO
VIDE	BCLR	D5,D2
GOTO	ADDQ.W	#1,D4
	SUBQ.W	#1,D5
	CMP.W	#-1,D5
	BNE.S	SUIV
	RTS
***************************************************************************
*	RESTITUTION DU FOND
*	ICI AUSSI ON SE BASE SUR CE QUI A DEJA ETE FAIT
PASS3
	MOVE.L	LEN_DATA,D0
	ADD.L	LEN_CODE,D0
	ADD.L	LEN_SAV,D0
	ADD.L	#$22,D0
	MOVE.L	D0,BUFFER+14
	
	MOVE.L	A3,D7
	MOVE.L	A3,A0
	MOVE.W	#$93FC,(A3)+			* SUB.L	#XXXXXXXX,A1
	MOVE.L	D6,(A3)+
	MOVE.W	#$D1FC,(A3)+			* ADD.L	#XXXXXXXX,A0
	MOVE.L	RESTE,(A3)+
	
	MOVE.W	-(A0),D1
CONV2	MOVE.W	-(A0),D1
	CMP.W	#$D0FC,D1			* ADD.W	#XXXX,A0
	BNE.S	N_ADD_W
	MOVE.W	#$90FC,(A3)+			* SUB.W #XXXX,A0
	MOVE.W	-(A6),(A3)+
	*MOVE.W	2(A0),(A3)+
	BRA	CONV2
N_ADD_W	
	CMP.W	#$2328,D1			* MOVE.L XXXX(A0),-(A1)
	BNE.S	N_MOVE_L
	MOVE.W	#$2159,(A3)+			* MOVE.L (A1)+,XXXX(A0)
	MOVE.W	2(A0),(A3)+
	BRA.S	CONV2
N_MOVE_L
	CMP.W	#$3328,D1			* MOVE.W XXXX(A0),-(A1)
	BNE.S	N_MOVE_W
	MOVE.W	#$3159,(A3)+			* MOVE.W (A1)+,XXXX(A0)
	MOVE.W	2(A0),(A3)+
	BRA.S	CONV2
N_MOVE_W	
	CMP.W	#$4CE8,D1
	BNE.S	N_MOVEM
	MOVE.W	#$4CD9,(A3)+
	MOVE.W	2(A0),(A3)+
	MOVE.W	#$48E8,(A3)+
	MOVE.L	2(A0),(A3)+
	BRA.S	CONV2
N_MOVEM	
	CMP.W	#$4E75,D1
	BNE.S	CONV2
	MOVE.W	#$4E75,(A3)+
	SUB.L	D7,A3
	MOVE.L	A3,LEN_REST
	RTS
***************************************************************************
WRITE_TO_DISK
	MOVE.W	#0,-(A7)
	PEA	FILENAME
	MOVE.W	#$3C,-(A7)
	TRAP	#1
	ADD.W	#8,A7
	MOVE.L	D0,D7
	
	MOVE.L	#52,D2
	ADD.L	LEN_CODE,D2
	ADD.L	LEN_DATA,D2
	ADD.L	LEN_SAV,D2
	ADD.L	LEN_REST,D2
	PEA	BUFFER
	MOVE.L	D2,-(A7)
	MOVE.W	D7,-(A7)
	MOVE.W	#$40,-(A7)
	TRAP	#1
	ADD.W	#12,A7
	
	MOVE.W	D7,-(A7)
	MOVE.W	#$3E,-(A7)
	TRAP	#1
	ADDQ.W	#4,A7
	RTS
	
***************************************************************************
* ICI SE TROUVE LE NOM DU FICHIER GENERE

FILENAME	DC.B	'SPRTC.BIN',0
		EVEN
		
***************************************************************************
TABLE	DC.L	POINT0,POINT1,POINT2,POINT3,POINT4,POINT5,POINT6
	DC.L	POINT7,POINT8,POINT9,POINT10,POINT11,POINT12,POINT13
	DC.L	POINT14,POINT15,POINT16,POINT17,POINT18,POINT19,POINT20
	DC.L	POINT21,POINT22,POINT23,POINT24,POINT25,POINT26
LEN_TEMP	DC.L	0	
LEN_SAV		DC.L	0	
LEN_REST	DC.L	0	
LEN_CODE	DC.L	0
LEN_DATA	DC.L	0
COMPTEUR_OFFSET	DC.W	0


***************************************************************************
* ICI SE TROUVE LE SPRITE. IL EST NECESSAIRE DE CONNAITRE LA LONGUEUR DE
* L'ENTETE POUR BIEN SE REPERER DANS L'IMAGE
* EX: TRUE PAINT (FORMAT TRUE COLOR 320*200)
*     ENTETE DE 128 OCTETS
*     UNE LIGNE FAIT 320*2 = 640 OCTETS
*     ET IL Y A 200 LIGNES
*     DONC LE FICHIER FAIT 128+640*200 = 128128 OCTETS
*     ET CE QUI NOUS INTERRESSE SE TROUVE A ECRAN+128

ECRAN		INCBIN	DRAGON.TPI

*			ICI LE DRAGON FAIT 32*32 PIXELS
*			SOIT 128+32*32*2 = 2176 OCTETS

**********************************************************************************
* ICI SE SONT LES PARAMETRES EN FONCTION DE VOS SPRITES

X	EQU	32		* TAILLE DU SPRITE EN POINT
Y	EQU	32		* TAILLE DU SPRITE EN POINT
LEN	EQU	64		* LONGUEUR D'UNE LIGNE (SPRITE) EN OCTET (SOURCE)
LONG	EQU	640		* LONGUEUR D'UNE SCANLINE EN OCTET       (DEST)

DEPART  EQU	ECRAN+128	* OU EST LE SPRITE (SOURCE)

***************************************************************************
*	BUFFER	  : AFFICHE SPRITE   A0:ADD ECRAN+OFFSET XY DU SPR
*
*	BUFFER+6  : SAUVE FOND       A0:ADD ECRAN+OFFSET XY DU SPR
*				     A1:ADD FOND + LONG FOND (EX:32*32*2)
*
*	BUFFER+12 : RESTITU FOND     A0:ADD ECRAN+OFFSET XY DU SPR
*				     A1:ADD FOND + LONG FOND (EX:32*32*2)

BUFFER		BRA.L	CODE
		BRA.L	SAV_FOND		* VALEURS FICTIVES CAR ELLES
		BRA.L	RESTITU			* SERONT MODIFIEES PLUS TARD
		
NB_PT		DC.L	0			* ICI NB DE POINTS POUR 1 SPRITE
		DC.W	X
		DC.W	Y
		DC.W	LONG
		DC.B	"MCS'S SPRITE TC V1.2"
CODE		LEA	BUFFER(PC),A1
		ADD.L	#$FFFFFF,A1
		DS.W	2*X*Y			* LES OCTETS INUTILES SERONT
DATA		DS.W	2*X*Y			* ENLEVES AUTOMATIQUEMENT
SAV_FOND					* APRES ANALYSE.
RESTITU						* MAIS IL VAUT MIEUX PREVOIR PLUS
						* LARGE QUE PAS ASSEZ
						

