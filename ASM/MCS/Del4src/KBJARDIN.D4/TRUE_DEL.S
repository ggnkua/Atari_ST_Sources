* MANY SCROLLS DEMO
* DON'T TOUCH TO A5 (RASTERS)

	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	
	MOVE.L	#SCREEN,D0
	AND.W	#$FF00,D0
	ADD.L	#256,D0
	MOVE.L	D0,PHYS
	MOVE.L	D0,ACTUAL
	ADD.L	#32000,D0
	MOVE.L	D0,PHYS2
	MOVE.L	D0,LOGIC
	ADD.L	#32000,D0
	MOVE.L	D0,TXTSCREEN
	
	MOVE.L	PHYS,A0
	MOVE.L	#16000+8000-1,D1
YOOHOO:	CLR.L	(A0)+
	DBRA	D1,YOOHOO
	
	MOVE.B	#0,$FFFFFFF8260.W
	MOVE.L	ACTUAL,D0
	LSR.L	#8,D0
	MOVE.B	D0,$FFFF8203.W
	LSR.W	#8,D0
	MOVE.B	D0,$FFFF8201.W
	
	;MOVE.W	#0,-(SP)
	;MOVE.L	ACTUAL,-(SP)
	;MOVE.L	ACTUAL,-(SP)
	;MOVE.W	#5,-(SP)
	;TRAP	#14
	;LEA	12(SP),SP
	
	MOVEM.L	CAR+2,D0-D7
	MOVEM.L	D0-D7,$FFFFFF8240.W
	MOVE.W	#$777,$FFFFFFFF8242.W
	
	MOVE.W	#$2700,SR
	MOVE.B	$FFFFFFA07.W,SA07
	MOVE.B	$FFFFFFA09.W,SA09
	MOVE.B	$FFFFFFA1B.W,SA1B
	MOVE.B	$FFFFFFA13.W,SA13
	MOVE.B	$FFFFFFA21.W,SA21
	MOVE.B	#0,$FFFFFA07.W
	MOVE.B	#0,$FFFFFA09.W
	MOVE.L	$70.W,S70
	MOVE.L	$120.W,S120
	MOVE.L	#VBL,$70.W
	BCLR	#3,$FFFFFA17.W
	MOVE.B	#0,$FFFFFA09.W
	MOVE.B	#1,$FFFFFA07.W
	ORI.B	#1,$FFFFFA13.W
	MOVE.B	#0,$FFFFFA21.W
	MOVE.B	#2,$FFFF820A.W	* 50 Hz
	MOVE.B	#0,$FFFFFA1B.W
	MOVE.L	#RASTER,$120
	
	MOVE.W	#$2300,SR
	
	JSR	UPCAR
	
	MOVEQ	#1,D0
	JSR	ZIK
	
MAIN:	BRA	MAIN
	
VBL:	CLR.B	$FFFFFFF8260.W
	MOVEM.L	CAR+2,D0-D7
	MOVEM.L	D0-D7,$FFFFFFF8240.W
	CLR.W	LINEPOS
	
	JSR	ZIK+8
	
	MOVE.L	ACTUAL,D0		* SWAP SCREEN ADRESSES
	MOVE.L	LOGIC,ACTUAL
	MOVE.L	D0,LOGIC
	
	LSR.L	#8,D0
	MOVE.B	D0,$FFFF8203.W
	LSR.W	#8,D0
	MOVE.B	D0,$FFFF8201.W
	
	MOVE.B	#0,$FFFFFA1B.W
	MOVE.B	#1,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	
	MOVE.L	#COLORS,A5

	MOVE.L	NOSPR,D0
	MOVE.L	NOSPR2,NOSPR
	MOVE.L	D0,NOSPR2
	
	JSR	EFF_IT

*  ONE PIXEL SCROLLER  *

	MOVE.L	LOGIC,A0
	ADD.L	#132*160,A0
	MOVE.L	#PARALX1+64*16,A1
	SUB.W	SCRPOS1,A1
	
N	SET	0
M	SET	0
	REPT	6
	MOVEM.L	M(A1),D1-D2
	MOVEM.L	M(A1),D3-D4
	MOVEM.L	M(A1),D5-D6
	MOVEM.L	M(A1),A2-A3
	
	MOVEM.L	D1-D4,N+000(A0)
	MOVEM.L	D5-D6/A2-A3,N+016(A0)
	
	MOVEM.L	D1-D4,N+032(A0)
	MOVEM.L	D1-D4,N+0112(A0)
	
	MOVEM.L	D1-D4,N+128(A0)
	MOVEM.L	D5-D6/A2-A3,N+144(A0)
N	SET	N+160
M	SET	M+8
	ENDR
	
*  TWO PIXEL SCROLLER  *

	ADD.W	#6*160,A0		*138*160
	MOVE.L	#PARALX2+64*8,A1
	SUB.W	SCRPOS2,A1
	
N	SET	0
M	SET	0
	REPT	8
	MOVEM.L	M(A1),D1-D2
	MOVEM.L	M(A1),D3-D4
	MOVEM.L	M(A1),D5-D6
	MOVEM.L	M(A1),A2-A3
	
	MOVEM.L	D1-D4,N+000(A0)
	MOVEM.L	D5-D6/A2-A3,N+016(A0)
	
	MOVEM.L	D1-D4,N+032(A0)
	MOVEM.L	D5-D6/A2-A3,N+048(A0)
	
	MOVEM.L	D1-D4,N+064(A0)
	MOVEM.L	D5-D6/A2-A3,N+080(A0)
	
	MOVEM.L	D1-D4,N+096(A0)
	MOVEM.L	D5-D6/A2-A3,N+112(A0)
	
	MOVEM.L	D1-D4,N+128(A0)
	MOVEM.L	D5-D6/A2-A3,N+144(A0)
N	SET	N+160
M	SET	M+8
	ENDR
	
* THREE PIXEL SCROLLER *

	ADD.W	#8*160,A0		*146*160 
	MOVE.L	#PARALX3+16*240,A1
	SUB.W	SCRPOS3,A1
	
N	SET	0
M	SET	0
	REPT	7
	MOVEM.L	M(A1),D1-D6
	MOVEM.L	D1-D6,N+000(A0)
	MOVEM.L	D1-D6,N+024(A0)
	MOVEM.L	D1-D6,N+048(A0)
	MOVEM.L	D1-D6,N+072(A0)
	MOVEM.L	D1-D6,N+096(A0)
	MOVEM.L	D1-D6,N+120(A0)
	MOVEM.L	D1-D4,N+144(A0)
N	SET	N+160
M	SET	M+24
	ENDR
	
* FOUR PIXEL  SCROLLER *	

	ADD.W	#7*160,A0		* 153*160
	MOVE.L	#PARALX4+80*4,A1
	SUB.W	SCRPOS4,A1
	
N:	SET	0
M:	SET	0
	
	REPT	8
	MOVEM.L	M(A1),D1-D2
	MOVEM.L	M(A1),D3-D4
	MOVEM.L	M(A1),D5-D6
	MOVEM.L	M(A1),A2-A3
	
	MOVEM.L	D1-D4,N+000(A0)
	MOVEM.L	D5-D6/A2-A3,N+016(A0)
	
	MOVEM.L	D1-D4,N+032(A0)
	MOVEM.L	D5-D6/A2-A3,N+048(A0)
	
	MOVEM.L	D1-D4,N+064(A0)
	MOVEM.L	D5-D6/A2-A3,N+080(A0)
	
	MOVEM.L	D1-D4,N+096(A0)
	MOVEM.L	D5-D6/A2-A3,N+112(A0)
	
	MOVEM.L	D1-D4,N+128(A0)
	MOVEM.L	D5-D6/A2-A3,N+144(A0)
	
N	SET	N+160
M	SET	M+8
	ENDR
	
* FIVE PIXEL  SCROLLER *

	ADD.W	#8*160,A0		* 161*160
	MOVE.L	#PARALX5+40*8*16,A1
	SUB.W	SCRPOS5,A1
	
N:	SET	0
M:	SET	0
	REPT	8
	MOVEM.L	M(A1),D0-D6/A2-A4
	
	MOVEM.L	D0-D5,N+000(A0)
	MOVEM.L	D6/A2-A4,N+024(A0)
	
	MOVEM.L	D0-D5,N+040(A0)
	MOVEM.L	D6/A2-A4,N+064(A0)
	
	MOVEM.L	D0-D5,N+080(A0)
	MOVEM.L	D6/A2-A4,N+104(A0)
	
	MOVEM.L	D0-D5,N+120(A0)
	MOVEM.L	D6/A2-A4,N+144(A0)
	
N:	SET	N+160
M:	SET	M+40
	ENDR
	
*  SIX PIXEL SCROLLER  *

	MOVE.L	LOGIC,A0
	ADD.L	#169*160,A0
	MOVE.L	#PARALX6+8*120,A1
	SUB.W	SCRPOS6,A1
	
N:	SET	0
M:	SET	0
	REPT	5
	MOVEM.L	M(A1),D1-D6
	MOVEM.L	D1-D6,N+000(A0)
	MOVEM.L	D1-D6,N+024(A0)
	MOVEM.L	D1-D6,N+048(A0)
	MOVEM.L	D1-D6,N+072(A0)
	MOVEM.L	D1-D6,N+096(A0)
	MOVEM.L	D1-D6,N+120(A0)
	MOVEM.L	D1-D4,N+144(A0)
N	SET	N+160
M	SET	M+24
	ENDR	
	
* SEVEN PIXEL SCROLLER *

	MOVE.L	LOGIC,A0
	ADD.L	#174*160,A0
	MOVE.L	#PARALX7+56*3*16,A1
	SUB.W	SCRPOS7,A1
	
N:	SET	0
M:	SET	0
	REPT	3
	MOVEM.L	M(A1),D0-D6
	MOVEM.L	D0-D6,N(A0)
	MOVEM.L	D0-D6,N+56(A0)
	MOVEM.L	D0-D6,N+112(A0)
	
	MOVEM.L	M+28(A1),D0-D6
	MOVEM.L	D0-D6,N+28(A0)
	MOVEM.L	D0-D6,N+56+28(A0)
	MOVEM.L	D0-D4,N+112+28(A0)
N:	SET	N+160
M:	SET	M+56
	ENDR
	
	MOVE.L	LOGIC,A0
	MOVE.L	#TAB,A6
	
	JSR	EFFPOINT		* STARFIELD EFFACEMENT
	JSR	COPY
	
	MOVE.L	LOGIC,A0
	MOVE.L	#TAB,A6
	ADDQ	#1,RUN
	ADDQ	#1,RUN2
	
	JSR	POINT		* STARFIELD !!! YOOHOO !!!
	
	CMPI.W	#320,RUN
	BLT.S	GLAGLA
	
	CLR.W	RUN
	
GLAGLA:	CMPI.W	#320,RUN2
	BLT.S	FROUFROU
	
	CLR.W	RUN2
	
FROUFROU:	JSR	VROOM		* PRINT CAR'S LOWER PART
	JSR	WHEELS		* MAKE WHEELS TURN
	JSR	TEXT		* ROUT TO PRINT TEXT ON 3rd SCREEN
	JSR	APPEAR		* ROUT TO MAKE TEXT APPEAR
	JSR	DISAP		* ROUT TO MAKE TEXT DISAPPEAR
	JSR	THROWS		* THROWN ITEMS ROUT
	JSR	MIRROR
	
* SIX PIXEL SCROLLING CLOUDS *

	MOVE.L	LOGIC,A0
	MOVE.L	#CLOUD1+576*16,A1
	SUB.W	CLOUDPOS1,A1
	
N:	SET	0
M:	SET	0
	REPT	12
	MOVEM.L	M(A1),D0-D7/A2-A3
	
	MOVEM.L	D0-D3,N(A0)
	MOVEM.L	D4-D7/A2-A3,N+16(A0)
	
	MOVEM.L	D0-D3,N+48(A0)
	MOVEM.L	D4-D7/A2-A3,N+48+16(A0)
	
	MOVEM.L	D0-D3,N+96(A0)
	MOVEM.L	D4-D7/A2-A3,N+96+16(A0)
	
	MOVEM.L	D0-D3,N+144(A0)
	
	MOVEM.L	M+40(A1),D0-D1
	MOVEM.L	D0-D1,N+40(A0)
	MOVEM.L	D0-D1,N+88(A0)
	MOVEM.L	D0-D1,N+136(A0)
	
N:	SET	N+160
M:	SET	M+48
	ENDR
	
* FIVE PIXEL SCROLLING CLOUDS *

	MOVE.L	LOGIC,A0
	ADD.L	#12*160,A0
	MOVE.L	#CLOUD2+800*32,A1
	SUB.W	CLOUDPOS2,A1
	
N:	SET	0
M:	SET	0
	REPT	10
	MOVEM.L	M(A1),D0-D7/A2-A3
	
	MOVEM.L	D0-D5,N(A0)
	MOVEM.L	D6-D7/A2-A3,N+24(A0)
	
	MOVEM.L	D0-D5,N+80(A0)
	MOVEM.L	D6-D7/A2-A3,N+104(A0)
	
	MOVEM.L	M+40(A1),D0-D7/A2-A3
	
	MOVEM.L	D0-D5,N+40(A0)
	MOVEM.L	D6-D7/A2-A3,N+64(A0)
	
	MOVEM.L	D0-D5,N+120(A0)
	MOVEM.L	D6-D7/A2-A3,N+144(A0)
	
N:	SET	N+160
M:	SET	M+80
	ENDR

* FOUR PIXEL SCROLLING CLOUDS *

	MOVE.L	LOGIC,A0
	ADD.L	#22*160,A0
	MOVE.L	#CLOUD3+128*8,A1
	SUB.W	CLOUDPOS3,A1
	
N:	SET	0
M:	SET	0
	REPT	8
	MOVEM.L	M(A1),D0-D3
	MOVEM.L	M(A1),D4-D7
	MOVEM.L	D0-D7,N(A0)
	MOVEM.L	D0-D7,N+32(A0)
	MOVEM.L	D0-D7,N+64(A0)
	MOVEM.L	D0-D7,N+96(A0)
	MOVEM.L	D0-D7,N+128(A0)
N:	SET	N+160
M:	SET	M+16
	ENDR

* THREE PIXEL SCROLLING CLOUDS *

	MOVE.L	LOGIC,A0
	ADD.L	#30*160,A0
	MOVE.L	#CLOUD4+288*32,A1
	SUB.W	CLOUDPOS4,A1
	
N:	SET	0
M:	SET	0
	REPT	6
	MOVEM.L	M(A1),D0-D7/A2-A3
	MOVEM.L	D0-D7/A2-A3,N(A0)
	MOVEM.L	D0-D7/A2-A3,N+48(A0)
	MOVEM.L	D0-D7/A2-A3,N+96(A0)
	MOVEM.L	D0-D3,N+144(A0)
	
	MOVEM.L	M+40(A1),D0-D1
	MOVEM.L	D0-D1,N+40(A0)
	MOVEM.L	D0-D1,N+88(A0)
	MOVEM.L	D0-D1,N+136(A0)
N:	SET	N+160
M:	SET	M+48
	ENDR

* TWO PIXEL SCROLLING CLOUDS *

	MOVE.L	LOGIC,A0
	ADD.L	#36*160,A0
	MOVE.L	#CLOUD5+64*16,A1
	SUB.W	CLOUDPOS5,A1
	
N:	SET	0
M:	SET	0
	REPT	4
	MOVEM.L	M(A1),D0-D3
	MOVEM.L	M(A1),D4-D7
	MOVEM.L	D0-D7,N(A0)
	MOVEM.L	D0-D7,N+32(A0)
	MOVEM.L	D0-D7,N+64(A0)
	MOVEM.L	D0-D7,N+96(A0)
	MOVEM.L	D0-D7,N+128(A0)
N:	SET	N+160
M:	SET	M+16
	ENDR

* ONE PIXEL SCROLLING CLOUDS *

	MOVE.L	LOGIC,A0
	ADD.L	#40*160,A0
	MOVE.L	#CLOUD6+32*32,A1
	SUB.W	CLOUDPOS6,A1
	
N:	SET	0
M:	SET	0
	REPT	2
	MOVEM.L	M(A1),D0-D3
	MOVEM.L	M(A1),D4-D7
	MOVEM.L	D0-D7,N(A0)
	MOVEM.L	D0-D7,N+32(A0)
	MOVEM.L	D0-D7,N+64(A0)
	MOVEM.L	D0-D7,N+96(A0)
	MOVEM.L	D0-D7,N+128(A0)
N:	SET	N+160
M:	SET	M+16
	ENDR
	
	JSR	PANNEAU
	
* MAKE ALL PARALLX SCROLL *

	ADD.W	#064,SCRPOS1
	ADD.W	#064,SCRPOS2
	ADD.W	#240,SCRPOS3
	ADD.W	#080,SCRPOS4
	ADD.W	#320,SCRPOS5
	ADD.W	#120,SCRPOS6
	ADD.W	#168,SCRPOS7
	
	ADD.W	#576,CLOUDPOS1
	ADD.W	#800,CLOUDPOS2
	ADD.W	#128,CLOUDPOS3
	ADD.W	#288,CLOUDPOS4
	ADD.W	#064,CLOUDPOS5
	ADD.W	#32,CLOUDPOS6
	

* TEST END OF SCROLLS *
	
L1:	CMPI.W	#64*16,SCRPOS1
	BLO.S	L2
	CLR.W	SCRPOS1
	
L2:	CMPI.W	#64*8,SCRPOS2
	BLO.S	L3
	CLR.W	SCRPOS2
	
L3:	CMPI.W	#240*16,SCRPOS3
	BLO.S	L4
	CLR.W	SCRPOS3
	
L4:	CMPI.W	#80*4,SCRPOS4
	BLO.S	L5
	CLR.W	SCRPOS4
	
L5:	CMPI.W	#16*320,SCRPOS5
	BLO.S	L6
	CLR.W	SCRPOS5

L6:	CMPI.W	#120*8,SCRPOS6
	BLO.S	L7
	CLR.W	SCRPOS6
	
L7:	CMPI.W	#16*168,SCRPOS7
	BLO.S	C1
	CLR.W	SCRPOS7
	
C1:	CMPI.W	#16*576,CLOUDPOS1
	BLO.S	C2
	CLR.W	CLOUDPOS1
	
C2:	CMPI.W	#800*32,CLOUDPOS2
	BLO.S	C3
	CLR.W	CLOUDPOS2
	
C3:	CMPI.W	#128*8,CLOUDPOS3
	BLO.S	C4
	CLR.W	CLOUDPOS3
	
C4:	CMPI.W	#288*32,CLOUDPOS4
	BLO.S	C5
	CLR.W	CLOUDPOS4
	
C5:	CMPI.W	#64*16,CLOUDPOS5
	BLO.S	C6
	CLR.W	CLOUDPOS5
	
C6:	CMPI.W	#32*32,CLOUDPOS6
	BLO	OUT
	CLR.W	CLOUDPOS6

* TEST SPACE AND RTE *
	
OUT:	CMPI.B	#$39,$FFFFFC02.W
	BEQ	END
	RTE


**** PRINT CAR ****

UPCAR:	MOVE.L	PHYS,A0		* ADR SCREEN
	ADD.L	#84*160+40,A0
	MOVE.L	#CAR+34,A1	* ADR CAR
	MOVEQ	#1,D0

MAKE_IT:	
N:	SET	0
	REPT	49
	MOVEM.L	N(A1),D1-D7/A2-A4
	MOVEM.L	D1-D7/A2-A4,N(A0)
	MOVEM.L	N+40(A1),D1-D7/A2-A4
	MOVEM.L	D1-D7/A2-A4,N+40(A0)
N:	SET	N+160
	ENDR
	
	MOVE.L	PHYS2,A0
	ADD.L	#84*160+40,A0
	
	DBRA	D0,MAKE_IT
	
	RTS
	
VROOM:	MOVE.L	LOGIC,A0
	ADD.L	#(48+84)*160+40,A0		* ADR SCREEN
	MOVE.L	#CAR+34+48*160,A1		* ADR CAR
	MOVE.L	#CAR+34+48*160+80,A2	* ADR MASK
	
N:	SET	0
	REPT	12
M:	SET	0
	MOVEM.L	M+N(A2),D0-D7
	AND.L	D0,M+N(A0)
	AND.L	D1,M+N+4(A0)
	AND.L	D2,M+N+8(A0)
	AND.L	D3,M+N+12(A0)
	AND.L	D4,M+N+16(A0)
	AND.L	D5,M+N+20(A0)
	AND.L	D6,M+N+24(A0)
	AND.L	D7,M+N+28(A0)
M:	SET	M+32
	MOVEM.L	M+N(A2),D0-D7
	AND.L	D0,M+N(A0)
	AND.L	D1,M+N+4(A0)
	AND.L	D2,M+N+8(A0)
	AND.L	D3,M+N+12(A0)
	AND.L	D4,M+N+16(A0)
	AND.L	D5,M+N+20(A0)
	AND.L	D6,M+N+24(A0)
	AND.L	D7,M+N+28(A0)
M:	SET	M+32
	MOVEM.L	M+N(A2),D0-D3
	AND.L	D0,M+N(A0)
	AND.L	D1,M+N+4(A0)
	AND.L	D2,M+N+8(A0)
	AND.L	D3,M+N+12(A0)

M:	SET	0
	MOVEM.L	M+N(A1),D0-D7
	OR.L	D0,M+N(A0)
	OR.L	D1,M+N+4(A0)
	OR.L	D2,M+N+8(A0)
	OR.L	D3,M+N+12(A0)
	OR.L	D4,M+N+16(A0)
	OR.L	D5,M+N+20(A0)
	OR.L	D6,M+N+24(A0)
	OR.L	D7,M+N+28(A0)
M:	SET	M+32
	MOVEM.L	M+N(A1),D0-D7
	OR.L	D0,M+N(A0)
	OR.L	D1,M+N+4(A0)
	OR.L	D2,M+N+8(A0)
	OR.L	D3,M+N+12(A0)
	OR.L	D4,M+N+16(A0)
	OR.L	D5,M+N+20(A0)
	OR.L	D6,M+N+24(A0)
	OR.L	D7,M+N+28(A0)
M:	SET	M+32
	MOVEM.L	M+N(A1),D0-D3
	OR.L	D0,M+N(A0)
	OR.L	D1,M+N+4(A0)
	OR.L	D2,M+N+8(A0)
	OR.L	D3,M+N+12(A0)
N:	SET	N+160
	ENDR
	RTS

WHEELS:	MOVE.L	LOGIC,A0
	ADD.L	#130*160+48,A0
	MOVE.L	#CAR+34+83*160,A1
	MOVE.L	#CAR+34+83*160+80,A2
	ADD.W	WHEELPOS1,A1
	
N:	SET	0
	REPT	10
	MOVEM.L	N(A1),D0-D3
	MOVEM.L	D0-D3,N(A0)
N:	SET	N+160
	ENDR
	
N:	SET	1600
	REPT	15
	MOVEM.L	N(A2),D0-D3
	AND.L	D0,N(A0)
	AND.L	D1,N+4(A0)
	AND.L	D2,N+8(A0)
	AND.L	D3,N+12(A0)
	MOVEM.L	N(A1),D0-D3
	OR.L	D0,N(A0)
	OR.L	D1,N+4(A0)
	OR.L	D2,N+8(A0)
	OR.L	D3,N+12(A0)
N:	SET	N+160
	ENDR
	
	MOVE.L	LOGIC,A0
	ADD.L	#56+40+(47+84)*160,A0
	MOVE.L	#CAR+34+112*160,A1
	MOVE.L	#CAR+34+112*160+80,A2
	ADD.W	WHEELPOS1,A1
	
N:	SET	0
	REPT	10
	MOVEM.L	N(A1),D0-D3
	MOVEM.L	D0-D3,N(A0)
N:	SET	N+160
	ENDR
	
N:	SET	1600
	REPT	14
	MOVEM.L	N(A2),D0-D3
	AND.L	D0,N+0(A0)
	AND.L	D1,N+4(A0)
	AND.L	D2,N+8(A0)
	AND.L	D3,N+12(A0)
	MOVEM.L	N(A1),D0-D3
	OR.L	D0,N+0(A0)
	OR.L	D1,N+4(A0)
	OR.L	D2,N+8(A0)
	OR.L	D3,N+12(A0)
N:	SET	N+160
	ENDR
	
	ADD.W	#1,WHEELWAIT
	
	CMPI.W	#3,WHEELWAIT
	BNE.S	NO_TURN
	
	CLR.W	WHEELWAIT
	ADD.W	#16,WHEELPOS1
	CMPI.W	#32,WHEELPOS1
	BLE.S	NO_TURN
	CLR.W	WHEELPOS1
	
NO_TURN:	RTS

*****************************
****                     ****
**** WRITE TEXT IN OTHER ****
****       SCREEN        ****
****                     ****
*****************************

TEXT:	CMPI.W	#30*8,SCREENPOS
	BEQ	PLIZ_WAIT

	CMPI.W	#20*8,SCREENPOS
	BEQ	NO_TALK
	
	MOVE.W	#1,NO_DIS
	MOVE.W	#1,NO_APP
	
APPL1:	MOVE.L	#TALK,A2
	ADD.W	TEXTPOS,A2
	MOVEQ	#0,D5
	MOVE.B	(A2),D5
	
	CMPI.B	#-1,D5
	BEQ	END_TALK
	
	CMPI.B	#$20,D5
	BEQ	SPACE1
	
	CMPI.B	#'A',D5
	BGE.S	NO_PONCT
	MOVE.L	#CAR+34+182*160,A1	* ADR PONCTUATION
	SUBI.W	#$27,D5		* ! = PRIMS
	LSL	#3,D5
	ADD.W	D5,A1
	BRA	AFF
	
NO_PONCT:	SUBI.W	#'A',D5
	CMPI.W	#19,D5
	BGT.S	UVWXYZ
	LSL	#3,D5
	MOVE.L	#CAR+34+148*160,A1
	ADD.W	D5,A1
	BRA	AFF
	
UVWXYZ:	SUBI	#20,D5
	LSL	#3,D5
	MOVE.L	#CAR+34+160*165,A1
	ADD.W	D5,A1
	
AFF:	MOVE.L	TXTSCREEN,A0
	ADD.W	SCREENPOS,A0
	ADD.W	#50*160,A0
N:	SET	0
	REPT	16
	MOVEM.L	N(A1),D0-D1
	MOVEM.L	D0-D1,N(A0)
N:	SET	N+160
	ENDR
	
RTS:	ADD.W	#1,TEXTPOS
	ADD.W	#8,SCREENPOS
PLIZ_WAIT:RTS

END_TALK:	CLR.W	TEXTPOS
	RTS
	
SPACE1:	MOVE.L	TXTSCREEN,A0
	ADD.L	#50*160,A0
	ADD.W	SCREENPOS,A0
	MOVEQ	#0,D0
	MOVEQ	#0,D1
N:	SET	0
	REPT	16
	MOVEM.L	D0-D1,N(A0)
N:	SET	N+160
	ENDR
	ADD.W	#1,TEXTPOS
	ADD.W	#8,SCREENPOS
	RTS

NO_TALK:	CLR.W	NO_APP
	MOVE.W	#30*8,SCREENPOS
	RTS

*****************************
****                     ****
****  MAKE TEXT APPEAR.  ****
****                     ****
*****************************

APPEAR:	CMPI.W	#1,NO_APP
	BEQ	GO_OUT
	
	CMPI.W	#160*17,COUNT
	BEQ	END_AFF
	
	CMPI.W	#160*17,COUNT
	BGT	END_IT
	
	MOVE.L	TXTSCREEN,A0
	ADD.L	#(50-16)*160,A0
	ADD.W    	COUNT,A0
	
	MOVE.L	LOGIC,A1
	ADD.L	#178*160,A1

SCROL_UP:	
N:	SET	0
	REPT	16
	
	MOVEM.L	0+N(A0),D0-D5
	MOVEM.L	0+N+24(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,0+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,0+N+24(A1)
	
	MOVEM.L	48+N(A0),D0-D5
	MOVEM.L	24+48+N(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,48+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,24+48+N(A1)
	
	MOVEM.L	96+N(A0),D0-D5
	MOVEM.L	24+96+N(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,96+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,24+96+N(A1)
	
	MOVEM.L	144+N(A0),D0-D3
	MOVEM.L	D0-D3,144+N(A1)
N:	SET	N+160
	ENDR
	ADD.W	#160,COUNT
GO_OUT	RTS

END_AFF:	MOVE.L	TXTSCREEN,A0
	ADD.L	#50*160,A0
	
	MOVE.L	LOGIC,A1
	ADD.L	#178*160,A1
	
N:	SET	0
	REPT	16
	MOVEM.L	0+N(A0),D0-D5
	MOVEM.L	0+N+24(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,0+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,0+N+24(A1)
	
	MOVEM.L	48+N(A0),D0-D5
	MOVEM.L	24+48+N(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,48+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,24+48+N(A1)
	
	MOVEM.L	96+N(A0),D0-D5
	MOVEM.L	24+96+N(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,96+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,24+96+N(A1)
	
	MOVEM.L	144+N(A0),D0-D3
	MOVEM.L	D0-D3,144+N(A1)
N:	SET	N+160
	ENDR
	
	ADD.W	#160,COUNT
	RTS
	
END_IT:	ADD.W	#1,VBLCOUNT
	MOVE.W	#1,PANO_ON
	CMPI.W	#100,VBLCOUNT
	BNE.S	YO
	CLR.W	VBLCOUNT
	SUB.W	#160,COUNT
	MOVE.W	#1,NO_APP
	CLR.W	NO_DIS
	MOVE.W	#8,COORX
	CLR.W	PANO_ON
YO:	RTS

*****************************
****                     ****
**** MAKE TEXT DISAPPEAR ****
****                     ****
*****************************
	
DISAP:	CMPI.W	#1,NO_DIS
	BEQ	GO_AWAY
	
	MOVE.W	#1,NO_APP
	
	CMPI.W	#0,COUNT
	BLT	SUITE
	
	MOVE.L	TXTSCREEN,A0
	ADD.L	#(50-17)*160,A0
	ADD.W	COUNT,A0
	
	MOVE.L	LOGIC,A1
	ADD.W	#178*160,A1
	
N:	SET	0
	REPT	16
	MOVEM.L	0+N(A0),D0-D5
	MOVEM.L	0+N+24(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,0+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,0+N+24(A1)
	
	MOVEM.L	48+N(A0),D0-D5
	MOVEM.L	24+48+N(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,48+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,24+48+N(A1)
	
	MOVEM.L	96+N(A0),D0-D5
	MOVEM.L	24+96+N(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,96+N(A1)
	MOVEM.L	D6-D7/A2-A4/A6,24+96+N(A1)
	
	MOVEM.L	144+N(A0),D0-D3
	MOVEM.L	D0-D3,144+N(A1)
N:	SET	N+160 
	ENDR
	
	SUB.W	#160,COUNT
GO_AWAY:	RTS

SUITE:	MOVE.W	#1,NO_DIS
	CLR.W	SCREENPOS
	RTS
	
******************************
****                      ****
**** ROUT TO CLEAR THROWN ****
****         ITEMS        ****
****                      ****
******************************
	
EFF_IT:	MOVE.L	LOGIC,A0
	ADD.L	NOSPR,A0
	
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVEQ	#0,D2
	MOVEQ	#0,D3
	
	CMPI.W	#0,BOUNCE
	BEQ	HEHE
	
	CMPI.W	#2,BOUNCE
	BEQ	EFF_5
	
	CMPI.W	#4,BOUNCE
	BEQ	EFF_5
	
	CMPI.W	#6,BOUNCE
	BEQ	EFF_9
	
	CMPI.W	#8,BOUNCE
	BEQ	EFF_13
	
N:	SET	0
	REPT	18
	MOVEM.L	D0-D3,N(A0)
N:	SET	N+160
	ENDR
HEHE:	RTS

EFF_5:
N:	SET	0
	REPT	5
	MOVEM.L	D0-D3,N(A0)
N:	SET	N+160
	ENDR
	RTS
EFF_9:
N:	SET	0
	REPT	9
	MOVEM.L	D0-D3,N(A0)
N:	SET	N+160
	ENDR
	RTS
EFF_13:
N:	SET	0
	REPT	13
	MOVEM.L	D0-D3,N(A0)
N:	SET	N+160
	ENDR
	RTS
	
*****************************
****                     ****
**** THROWN OBJECTS ROUT ****
****                     ****
*****************************
	
THROWS:	CMPI.W	#0,NO_THROW
	BNE	BOOH
	
OUT_CAR:	MOVE.L	#THROWPOS,A4
	ADD.W	BOUNCE,A4
	MOVEQ	#0,D0
	MOVEQ	#0,D1

	MOVE.W	XPOS,D0

NOT_BACK:	MOVE.W	(A4),D1
	CMPI.W	#-1,D1
	BNE.S	GO_FOR_IT
	
	MOVE.W	#1,NO_THROW
	CLR.W	BOUNCE
	MOVE.W	#160,XPOS
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	
GO_FOR_IT:MULU.W	#160,D1
	
	MOVE.L	LOGIC,A0
	MOVE.L	#SPRITE,A1
	
	MOVE.W	D0,D2		* SAUVE X
	AND.W	#$FF0,D0		* ARRONDI A MULT DE 16
	LSR.W	#1,D0		* DIVISE PAR 2 (1 OCTET = 2 PIXEL)

	ADD.W	D0,D1		* D1 = OFFSET DE L'ENDROIT
	LEA.L	(A0,D1.W),A0	* A0 = OU AFFICHER
	MOVE.L	D1,NOSPR		* SAVE FOR CLEARANCE

	MOVE.L	#SPRMSK,A2	* A1 = SPRITE
	AND.W	#$F,D2		* NE GUARDE QUE OFFSET DE 16
	MULU.W	#288,D2	 	* TAILLE EN OCT D'UN SPR
	LEA.L	(A1,D2.W),A1	* OU PRENDRE
	LEA.L	(A2,D2.W),A2	* ADR MASK !
	
	CMPI.W	#0,BOUNCE
	BEQ	THROW_5
	CMPI.W	#2,BOUNCE
	BEQ	THROW_9
	CMPI.W	#4,BOUNCE
	BEQ	THROW_13
	
THROW_IT:	
N:	SET	0
M:	SET	0
	REPT	18
	MOVEM.L	M(A2),D0-D3
	AND.L	D0,00+N(A0)
	AND.L	D1,04+N(A0)
	AND.L	D2,08+N(A0)
	AND.L	D3,12+N(A0)
	
	MOVEM.L	M(A1),D0-D3
	OR.L	D0,00+N(A0)
	OR.L	D1,04+N(A0)
	OR.L	D2,08+N(A0)
	OR.L	D3,12+N(A0)
N:	SET	N+160
M:	SET	M+16
	ENDR
	
	ADD.W	#2,BOUNCE
	ADD.W	#3,XPOS
	
BOOH:	RTS

THROW_5:	
N:	SET	0
M:	SET	0
	REPT	5
	MOVEM.L	M(A2),D0-D3
	AND.L	D0,00+N(A0)
	AND.L	D1,04+N(A0)
	AND.L	D2,08+N(A0)
	AND.L	D3,12+N(A0)
	
	MOVEM.L	M(A1),D0-D3
	OR.L	D0,00+N(A0)
	OR.L	D1,04+N(A0)
	OR.L	D2,08+N(A0)
	OR.L	D3,12+N(A0)
N:	SET	N+160
M:	SET	M+16
	ENDR
	
	ADD.W	#2,BOUNCE
	ADD.W	#3,XPOS
	
	RTS
	
THROW_9:	
N:	SET	0
M:	SET	0
	REPT	9
	MOVEM.L	M(A2),D0-D3
	AND.L	D0,00+N(A0)
	AND.L	D1,04+N(A0)
	AND.L	D2,08+N(A0)
	AND.L	D3,12+N(A0)
	MOVEM.L	M(A1),D0-D3
	OR.L	D0,00+N(A0)
	OR.L	D1,04+N(A0)
	OR.L	D2,08+N(A0)
	OR.L	D3,12+N(A0)
N:	SET	N+160
M:	SET	M+16
	ENDR
	
	ADD.W	#2,BOUNCE
	ADD.W	#3,XPOS
	
	RTS
	
THROW_13:	
N:	SET	0
M:	SET	0
	REPT	13
	MOVEM.L	M(A2),D0-D3
	AND.L	D0,00+N(A0)
	AND.L	D1,04+N(A0)
	AND.L	D2,08+N(A0)
	AND.L	D3,12+N(A0)
	MOVEM.L	M(A1),D0-D3
	OR.L	D0,00+N(A0)
	OR.L	D1,04+N(A0)
	OR.L	D2,08+N(A0)
	OR.L	D3,12+N(A0)
N:	SET	N+160
M:	SET	M+16
	ENDR
	
	ADD.W	#2,BOUNCE
	ADD.W	#3,XPOS
	
	RTS

*****************************
****                     ****
**** MAKE PANNEAU SCROLL ****
****                     ****
*****************************

PANNEAU:	CMPI.W	#1,PANO_ON
	BNE	WHERE
	
	CMPI.W	#320+40,COORX
	BGE	NOT_INDIC
	
	MOVE.L	LOGIC,A0
	MOVE.L	#PAN_SPR,A1
	MOVE.L	#PAN_MSK,A2
	
	CMPI.W	#8,COORX
	BEQ	APP8
	
	CMPI.W	#16,COORX
	BEQ	APP16
	
	CMPI.W	#24,COORX
	BEQ	APP24
	
	CMPI.W	#32,COORX
	BEQ	APP32
	
	
	CMPI.W	#320+8,COORX
	BEQ	DISAP8
	
	CMPI.W	#320+16,COORX
	BEQ	DISAP16
	
	CMPI.W	#320+24,COORX
	BEQ	DISAP24
	
	CMPI.W	#320+32,COORX
	BEQ	DISAP32
	
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	COORX,D0		* COORD -X-
	SUBI.W	#40,D0
	MOVE.W	#160*(177-30),D1	* COORD -Y-
	
	MOVE.W	D0,D2		* SAUVE X
	AND.W	#$FF0,D0		* ARRONDI A MULT DE 16
	LSR.W	#1,D0		* DIVISE PAR 2 (1 OCTET = 2 PIXEL)

	ADD.W	D0,D1		* D1 = OFFSET DE L'ENDROIT
	LEA.L	(A0,D1.W),A0	* A0 = OU AFFICHER

	AND.W	#$F,D2		* NE GUARDE QUE OFFSET DE 16
	MULU.W	#960/8,D2	 	* TAILLE EN OCT D'UN SPR
	LEA.L	(A1,D2.W),A1	* OU PRENDRE
	LEA.L	(A2,D2.W),A2	* ADR MASK !

SHR:	SET	0
	REPT	30
	
	MOVEM.L	SHR*32(A2),D0-D5
	AND.L	D0,SHR*160+00(A0)
	AND.L	D1,SHR*160+04(A0)
	AND.L	D2,SHR*160+08(A0)
	AND.L	D3,SHR*160+12(A0)
	AND.L	D4,SHR*160+16(A0)
	AND.L	D5,SHR*160+20(A0)
	
	MOVEM.L	SHR*32(A1),D0-D5
	OR.L	D0,SHR*160+00(A0)
	OR.L	D1,SHR*160+04(A0)
	OR.L	D2,SHR*160+08(A0)
	OR.L	D3,SHR*160+12(A0)
	OR.L	D4,SHR*160+16(A0)
	OR.L	D5,SHR*160+20(A0)
	
SHR:	SET	SHR+1
	ENDR
	ADD.W	#8,COORX
WHERE:	RTS


APP8:	ADD.W	#160*(177-30),A0

SHR	SET	0
	REPT	30
	
	MOVEM.L	SHR*32+16(A2),D0-D1
	AND.L	D0,SHR*160+00(A0)
	AND.L	D1,SHR*160+04(A0)
	
	MOVEM.L	SHR*32+16(A1),D0-D1
	OR.L	D0,SHR*160+00(A0)
	OR.L	D1,SHR*160+04(A0)
	
SHR	SET	SHR+1
	ENDR
	
	ADD.W	#8,COORX
	RTS

APP16:	ADD.W	#960,A1
	ADD.W	#960,A2
	BRA	APP8
	
APP24:	ADD.W	#160*(177-30),A0

SHR	SET	0
	REPT	30
	
	MOVEM.L	SHR*32+8(A2),D0-D3
	AND.L	D0,SHR*160+00(A0)
	AND.L	D1,SHR*160+04(A0)
	AND.L	D2,SHR*160+08(A0)
	AND.L	D3,SHR*160+12(A0)
	
	MOVEM.L	SHR*32+8(A1),D0-D3
	OR.L	D0,SHR*160+00(A0)
	OR.L	D1,SHR*160+04(A0)
	OR.L	D2,SHR*160+08(A0)
	OR.L	D3,SHR*160+12(A0)	
SHR	SET	SHR+1
	ENDR
	
	ADD.W	#8,COORX
	RTS

APP32:	ADD.W	#960,A1
	ADD.W	#960,A2
	BRA	APP24
	
DISAP8:	ADD.W	#160*(177-30)+(160-16),A0

SHR	SET	0
	REPT	30
	
	MOVEM.L	SHR*32(A2),D0-D3
	AND.L	D0,SHR*160+00(A0)
	AND.L	D1,SHR*160+04(A0)
	AND.L	D2,SHR*160+08(A0)
	AND.L	D3,SHR*160+12(A0)
	
	MOVEM.L	SHR*32(A1),D0-D3
	OR.L	D0,SHR*160+00(A0)
	OR.L	D1,SHR*160+04(A0)
	OR.L	D2,SHR*160+08(A0)
	OR.L	D3,SHR*160+12(A0)
	
SHR	SET	SHR+1
	ENDR
	
	ADD.W	#8,COORX
	RTS
	
DISAP16:	ADD.W	#960,A1
	ADD.W	#960,A2
	BRA	DISAP8
		
DISAP24:	ADD.W	#160*(177-30)+(160-8),A0

SHR	SET	0
	REPT	30
	
	MOVEM.L	SHR*32(A2),D0-D1
	AND.L	D0,SHR*160+00(A0)
	AND.L	D1,SHR*160+04(A0)
	
	MOVEM.L	SHR*32(A1),D0-D1
	OR.L	D0,SHR*160+00(A0)
	OR.L	D1,SHR*160+04(A0)
	
SHR	SET	SHR+1
	ENDR	
	
	ADD.W	#8,COORX
	RTS

DISAP32:	ADD.W	#960,A1
	ADD.W	#960,A2
	CLR.W	NO_THROW
	BRA	DISAP24
	
NOT_INDIC:MOVE.W	#1,PANO_ON
	RTS
	
****************************
****                    ****
**** ROUT TO EFF POINTS ****
****                    ****
****************************
	
EFFPOINT:	MOVE.L	#PT_TAB,A4

	MOVE.W	(A6),D0
	
	CMPI.W	#-1,D0
	BEQ	DIR 
	
	ADD.W	RUN2,D0
	
	CMPI.W	#320,D0
	BLT.S	NOTHING
	
	SUBI.W	#320,D0
	
NOTHING:	MOVE.W	D0,D2
	
	ANDI.W	#$FF0,D0
	LSR	#1,D0
	
	MOVE.W	2(A6),D1
	ADD.W	D1,D0
	
	LEA	(A0,D0.W),A1
	
	CLR.W	6(A1)
	
	ADDQ	#4,A6
	BRA.S	EFFPOINT
	
.BOOH:	CLR.W	(A1)
	ADD.W	#4,A6
	BRA.S	EFFPOINT

DIR:	RTS

***************************
****                   ****
****     POINT ROUT    ****
****                   ****
***************************

POINT:	MOVE.L	#PT_TAB,A4

	MOVE.W	(A6),D0
	
	CMPI.W	#-1,D0
	BEQ.S	RTS2 
	
	ADD.W	RUN,D0
	
	CMPI.W	#320,D0
	BLT.S	GAGA
	
	SUBI.W	#320,D0
	
GAGA:	MOVE.W	D0,D2
	
	ANDI.W	#$FF0,D0
	LSR	#1,D0
	
	MOVE.W	2(A6),D1
	ADD.W	D1,D0
	
	LEA	(A0,D0.W),A1
	
	ANDI.W	#$F,D2
	ADD.W	D2,A4
	MOVE.B	(A4),D3
	
	CMPI.W	#8,D2
	BGE.S	KABOOH
	
	BSET	D3,6(A1)
	ADDQ.W	#4,A6
	BRA.S	POINT
	
KABOOH:	BSET	D3,7(A1)
	ADDQ	#4,A6
	BRA.S	POINT

RTS2:	RTS

MIRROR:	MOVE.L	LOGIC,A0		
	
N:	SET	198*160
E:	SET	178*160
	REPT	4
	MOVEM.L	0+E(A0),D0-D5
	MOVEM.L	0+E+24(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,0+N(A0)
	MOVEM.L	D6-D7/A2-A4/A6,0+N+24(A0)
	
	MOVEM.L	48+E(A0),D0-D5
	MOVEM.L	24+48+E(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,48+N(A0)
	MOVEM.L	D6-D7/A2-A4/A6,24+48+N(A0)
	
	MOVEM.L	96+E(A0),D0-D5
	MOVEM.L	24+96+E(A0),D6-D7/A2-A4/A6
	MOVEM.L	D0-D5,96+N(A0)
	MOVEM.L	D6-D7/A2-A4/A6,24+96+N(A0)
	
	MOVEM.L	144+E(A0),D0-D3
	MOVEM.L	D0-D3,144+N(A0)
N:	SET	N-160
E:	SET	E+(4*160)
	ENDR
	RTS
	
***************************
****                   ****
****   TIMER-B  ROUT   ****
****                   ****
***************************

RASTER:	MOVE.W	(A5)+,$FFFFFF8240.W
	RTE

COPY:	MOVE.L	#CAR+34+6,A0
	MOVE.L	LOGIC,A1
	ADD.L	#84*160+6+40,A1
	
KB:	SET	0
	REPT	40
	MOVE.W	KB*160(A0),D0
	MOVE.W	KB*160+8(A0),D2
	MOVE.W	KB*160+16(A0),D4
	MOVE.W	KB*160+24(A0),D6
	
	MOVE.W	D0,KB*160(A1)
	MOVE.W	D2,KB*160+8(A1)
	MOVE.W	D4,KB*160+16(A1)
	MOVE.W	D6,KB*160+24(A1)
	
	MOVE.W	KB*160+32(A0),D0
	MOVE.W	KB*160+40(A0),D2
	MOVE.W	KB*160+48(A0),D4
	MOVE.W	KB*160+56(A0),D6
	MOVE.W	KB*160+64(A0),D7
	MOVE.B	KB*160+72(A0),D1
	
	MOVE.W	D0,KB*160+32(A1)
	MOVE.W	D2,KB*160+40(A1)
	MOVE.W	D4,KB*160+48(A1)
	MOVE.W	D6,KB*160+56(A1)
	MOVE.W	D7,KB*160+64(A1)
	MOVE.B	D1,KB*160+72(A1)
	
KB:	SET	KB+1
	ENDR
	RTS
		
	
END:	MOVE.B	#8,$FFFFFFFF8800.W
	CLR.B	$FFFFFFF8802.W
	
	MOVE.B	#9,$FFFFFFFF8800.W
	CLR.B	$FFFFFFF8802.W
	
	MOVE.B	#10,$FFFFFFFF8800.W
	CLR.B	$FFFFFFF8802.W
	
	MOVE.B	SA07,$FFFFFFFA07.W
	MOVE.B	SA09,$FFFFFFFA09.W
	MOVE.B	SA1B,$FFFFFFFA1B.W
	MOVE.B	SA13,$FFFFFFFA13.W
	MOVE.B	SA21,$FFFFFFFA21.W
	MOVE.L	S70,$70
	MOVE.L	S120,$120
	
	MOVE.W	#$444,$FFFFFF8246.W
	MOVE.W	#$000,$FFFFFF8240.W
	CLR.W	-(SP)
	TRAP	#1

SA07:	DC.B	0
SA09:	DC.B	0
SA1B:	DC.B	0
SA13:	DC.B	0
SA21:	DC.B	0
	EVEN

COORX:	DC.W	8

S70: 	DC.L	0
S120:	DC.L	0
NOSPR:	DC.L	0
NOSPR2:	DC.L	0

PANO_ON:	DC.W	0

PHYS:	DC.L	0
PHYS2:	DC.L	0
TXTSCREEN:DC.L	0
ACTUAL:	DC.L	0
LOGIC:	DC.L	0

XPOS:	DC.W	160
LINEPOS:	DC.W	0
SCRPOS1:	DC.W	0
SCRPOS2:	DC.W	0
SCRPOS3:	DC.W	0
SCRPOS4:	DC.W	0
SCRPOS5:	DC.W	0
SCRPOS6:	DC.W	0
SCRPOS7:	DC.W	0

COUNT:	DC.W	160

NO_APP:	DC.W	1
NO_DIS:	DC.W	1

RUN:	DC.W	1
RUN2:	DC.W	0
PT_TAB:	DC.B	7,6,5,4,3,2,1,0,7,6,5,4,3,2,1,0

NO_THROW:	DC.W	1

CLOUDPOS1:DC.W	0
CLOUDPOS2:DC.W	0
CLOUDPOS3:DC.W	0
CLOUDPOS4:DC.W	0
CLOUDPOS5:DC.W	0
CLOUDPOS6:DC.W	0

WHEELWAIT:DC.W	0
WHEELPOS1:DC.W	0



******************************
**** RASTERS COLORS TABLE ****
******************************

COLORS:	DCB.W	67,$000
	DC.W	$100,$000,$000,$100,$100,$000,$100
	DCB.W	5,$100
	DC.W	$200,$100,$100,$200,$200,$100,$200
	DCB.W	5,$200
	DC.W	$300,$200,$200,$300,$300,$200,$300
	DCB.W	5,$300
	DC.W	$400,$300,$300,$400,$400,$300,$400
	DCB.W	3,$400
	DC.W	$500,$400,$400
	DCB.W	4,$500
	DC.W	$600,$500,$500,$600
	DCB.W	2,$600
	DC.W	$700,$600,$600,$700,$700
	DCB.W	62,$000
	DC.W	$640
	DCB.W	4,$770
	DC.W	$640
	DC.W	$000

WAIT:	DC.W	0

*****************************
****                     ****
****     TEXT PART       ****
****                     ****
*****************************


TALK:	DC.B	"      WOTSIT.       "
	DC.B	"  AS YOU CAN SEE,   "
	DC.B	"   IN THIS DEMO,    "
	DC.B	" THERE AREN'T ANY : "
	DC.B	"-    FULLSCREEN    -"
	DC.B	"-    HARDSCROLL    -"
	DC.B	"-  SPRITES RECORD  -"
	DC.B	"-8200 DOT STARFIELD-"
	DC.B	"-    SOUNDTRACK    -"
	DC.B	"                    "
	DC.B	"  BUT WHAT A LAME   "
	DC.B	"     SCREEN ...     "
	DC.B	"                    "
	DC.B	"   HELLO GUYS ...   "
	DC.B	"    I'M SHREIDER    "
	DC.B	" FROM THE BRAND NEW "
	DC.B	" HITCH-HICKERS TEAM "
	DC.B	"                    "
	DC.B	"I'M NOT SO PROUD TO "
	DC.B	"  PRESENT YOU THIS  "
	DC.B	"   SCREEN CALLED:   "
	DC.B	"'     KB JARDIN    '"
	DC.B	"                    "
	DC.B	"    YOU CERTAINLY   "
	DC.B	"  WONDER WHY IT IS  "
	DC.B	" NAMED SO WIERDLY..."
	DC.B	"   JUST ASK IT TO   "
	DC.B	"   THE HOOLIGAN...  "
	DC.B	"BUT I SUPPOSE IT IS "
	DC.B	" TOLD SOMEWHERE IN  "
	DC.B	"    A LONELY AND    "
	DC.B	"  UNREADABLE  TEXT  "
	DC.B	"COZ ZE HOOLIGAN TOLD"
	DC.B	"   ME THAT IT MADE  " 
	DC.B	"   ZORGLUB  LAUGH   "
	DC.B	"  EVEN  IF THERE'S  "
	DC.B	"   NOTHING  FUNNY   "
	DC.B	"        ...         "
	DC.B	"      IN FACT,      "
	DC.B	"IT'S QUITE PATHETIC "
	DC.B	"SOB . SOB . BOOHOO.."
	DC.B	"   SO IT WOULD BE   "
	DC.B	"     REPEATED,      "
	DC.B	"     AMPLIFIED,     "
	DC.B	"     DISTORTED.     "
	DC.B	"  AS WOULD SAY ANY  "
	DC.B	"  MEMBER OF M.C.S.  " 
	DC.B	"        ...         "
	DC.B	"NOW COME BACK TO THE"
	DC.B	"TRADITIONAL CRAPTALK"
	DC.B	"        ...         "
	DC.B	" I KNOW THIS SCREEN "
	DC.B	"   IS A LAME ONE    "
	DC.B	" BUT IT'S OUR FIRST "
	DC.B	"      DEMO ...      "
	DC.B	"                    "
	DC.B	" THE MORE I LOOK AT "
	DC.B	"THIS SCREEN,THE MORE"
	DC.B	" I FIND IT LAME ... "
	DC.B	"                    "
	DC.B	"SINCE YOU ARE STILL "
	DC.B	"  THERE, I'LL GIVE  "
	DC.B	"   THE CREDITS...   "
	DC.B	"                    "
	DC.B	"-    CODING BY     -"
	DC.B	"......ANDRAAX......."
	DC.B	"- GFX CREATION AND -"
	DC.B	"-  LITTLE CHANGES  -"
	DC.B	"-       BY         -"
	DC.B	"......SHREIDER......"
	DC.B	"-    MUSIKK  BY    -"
	DC.B	"......MY SISTER....."
	DC.B	"...OOPS,IT WAS BY..."
	DC.B	"......MAD  MAX......"
	DC.B	"                    "
	DC.B	" I PASS THE KEYBOARD"
	DC.B	"    TO ANDRAAX ..   "
	DC.B	"                    "
	DC.B	"     HELLO GUYS     "
	DC.B	"                    "
	DC.B	"  I KNOW THAT MANY  "
	DC.B	"   SCREENS MADE BY  "
	DC.B	"   NEW CREWS ARE    "
	DC.B	"MUCH MORE IMPRESSIVE"
	DC.B	" ( FULLTRACK, ETC ) " 
	DC.B	"BUT WHAT'S THE POINT"
	DC.B	"   IN RIPPING ...   "
	DC.B	"                    "
	DC.B	"I WANT TO PASS SOME "
	DC.B	"   FUCKINGS....     "
	DC.B	"      TO TRIO       "
	DC.B	" THE MOST LAME GUYS "
	DC.B	"   I'VE EVER SEEN   "
	DC.B	"AND TO PEOPLE LIVING"
	DC.B	"     IN NANTES      "
	DC.B	"(   EXCEPT M.C.S   )"
	DC.B	"( AND JUJU MALLONE )"
	DC.B	"                    "
	DC.B	"  NOW LET'S GO FOR  "
	DC.B	"  THE  TRADITIONAL  "
	DC.B	"      GREETINGS     "
	DC.B	"    LET'S GREET :   "
	DC.B	"- THE WHOLE M.C.S. -"
	DC.B	"-    ESPECIALLY    -"
	DC.B	"-   THE BOOLIGAN   -"
	DC.B	"        AND         "
	DC.B	"-   JUJU MALLONE   -"
	DC.B	"PLIZ KEEP ON CODING."
	DC.B	"                    "
	DC.B	"   THAT'S ALL FOR   "
	DC.B	"      GREETINGS     "
	DC.B	"  COZ I DON'T KNOW  "
	DC.B	" MANY CODING PEOPLE "
	DC.B	"                    "
	DC.B	"  I SEND A HI TO :  "
	DC.B	"-      N.G.C.      -"
	DC.B	"-    DEFCON  4     -"
	DC.B	"-   THE MISFITS    -"
	DC.B	" - CHAOS  MOTION   -"
	DC.B	"(OR WAS IT CHAOTIC.)"
	DC.B	"-      NEXT        -"
	DC.B	"- AND YOU WHO READ -"
	DC.B	"THIS CRAPPY TEXT NOW"
	DC.B	"                    "
	DC.B	" BY THE WAY, I'M ON "
	DC.B	"    THE 25/11/91    "
	DC.B	" I DON'T KNOW WHEN  "
	DC.B	"    YOU ARE,BUT     "
	DC.B	"......BYE  BYE......"
	DC.B	"                    "
	DC.B	"BE CAREFUL, HERE ARE"
	DC.B	"  THE HOOLINEPTIES  "
	DC.B	" ( THE HOOLIGAN IS  "
	DC.B	"GOOD, HE TYPES QUITE"
	DC.B	"WELL WIZ A KAMISOLE)"
	DC.B	"                    "
	DC.B	"STOP THE PRESS . WHO"
	DC.B	"IS THAT. OF COURSE, "
	DC.B	"IT COULD ONLY BE THE"
	DC.B	"FOOLIGAN OF MCS....."
	DC.B	" MY FRIEND ANDRAAX  "
	DC.B	"  JUST GAVE ME THE  "
	DC.B	"SOURCE OF HIS SCREEN"
	DC.B	"SO I COULD TELL HIM "
	DC.B	"WHAT I THINK OF IT. "
	DC.B	"WELL, I ADMIT, IT'S "
	DC.B	"  LAME, BUT THAT'S  "
	DC.B	"NORMAL: I TAUGHT HIM"
	DC.B	"EVERYTHING HE KNOWS."
	DC.B	"                    "
	DC.B	"WELL, IN FACT,ANDRAX"
	DC.B	" DOESN'T WANT ME TO "
	DC.B	"OPTIMIZE HIS CODE,SO"
	DC.B	"THERE REALLY AINT A "
	DC.B	" THING I CAN DO FOR "
	DC.B	"   HIM...(SOB,SOB)  "
	DC.B	"BY THE WAY, SHREIDER"
	DC.B	"STOLE DIS EXPRESSION"
	DC.B	"FROM ME.(I'M TALKING"
	DC.B	"ABOUT THE 'SOB,SOB')"
	DC.B	"                    "
	DC.B	" AND ANYWAYS, HE'S  "
	DC.B	"JUST A LIAR: HE DOES"
	DC.B	" NOT HAVE A SISTER. "
	DC.B	"                    "
	DC.B	"DID YOU SEE MY GAME."
	DC.B	"  THE 'WIGGLE IT'.  "
	DC.B	"I SEND FUCKS TO ALL "
	DC.B	"OF THOSE WHO SAY ITS"
	DC.B	" HARD: I DON'T MAKE "
	DC.B	"      GAMES FOR     "
	DC.B	"   LAME-PLAYERS...  "
	DC.B	"  HOHO, IT'S ABOUT  "
	DC.B	"TIME I GOT READY TO "
	DC.B	" GO TO SCHOOL...SEE "
	DC.B	"YOU IN ONE OF MY OWN"
	DC.B	"     SCREENS..      "
	DC.B	"                    "
	DC.B	" THIS IS MUCH,MUCH  "
	DC.B	"LATER, AND ITS STILL"
	DC.B	" THE MOOLIGAN ON ZE "
	DC.B	"  KEYBOARD. I JUST  "
	DC.B	" FINISHED TO DEBUG  "
	DC.B	"ANDRIIX'S SCREEN(WOA"
	DC.B	"LES FONTS, EH.) AND "
	DC.B	"HE IS SITTING AT MY "
	DC.B	"FEETS, BEGIN FOR ME "
	DC.B	"TO WRITE A FEW KS,SO"
	DC.B	"HERE I AM...FIRST, I"
	DC.B	"  WOULD LIKE TO SAY "
	DC.B	" THAT MY TEXT'S BEEN"
	DC.B	" CHANGED: I NORMALY "
	DC.B	"HAD MANY QUESTION 'N"
	DC.B	" EXCALMATION MARKS, "
	DC.B	" BUT CHIE(UN)RAIDER "
	DC.B	"TOOK THEM AWAY, COZ "
	DC.B	"ANDRAOX IS TOO LAZY "
	DC.B	"TO TEST THEM. (BOOO)"
	DC.B	"     SO THERE.      "
	DC.B	"                    "
	DC.B	"EPISDABORDALAFINPASK"
	DC.B	"BONNONMAISHOCESTVRAI"
	DC.B	"KOA. (NO, IT WASN'T "
	DC.B	"       A BUG)       "
	DC.B	"                    "
	DC.B	"HONESTLY, I THINK WE"
	DC.B	"SHOULD EXCUSE A. FOR"
	DC.B	"DIS LAME SCREEN: HE "
	DC.B	" DIDN'T CODE A LOT, "
	DC.B	" COZ HE SPENDS HIS  "
	DC.B	" TIME SPYING A GIRL "
	DC.B	"HE IS DESEPERATLY IN"
	DC.B	"LOVE WITH...HE EVEN "
	DC.B	"PAINTED HIS BLOCK IN"
	DC.B	"PINK FOR HER.BUT SHE"
	DC.B	" KEEPS ON REJECTING "
	DC.B	"HIM (SHE'S GOING OUT"
	DC.B	"WITH AN OTHER GUY HE"
	DC.B	"      HATES...)     "
	DC.B	"                    "
	DC.B	" YA KLE GFA KIM FAIT"
	DC.B	"         CA!        "
	DC.B	" EVERYBODY CAN'T BE "
	DC.B	"LIKE AXEL (ISN'T IT "
	DC.B	"      SPIROU)       "
	DC.B	"                    "
	DC.B	"CODER UN P'TIT COUP "
	DC.B	"C'EST AGREEEAABBBLLE"
	DC.B	"MAIS FAUT PAS FAIRE "
	DC.B	"TROP DE TAAABBBLLLES"
	DC.B	"    PASKE SINON     "
	DC.B	"C'EST COME L'ETAABLE"
	DC.B	"(ENFIN,LES ECURIES, "
	DC.B	" MAIS CA RIME PAS!) "
	DC.B	"                    "
	DC.B	"I'M CRYYYIIIIINNNGGG"
	DC.B	"                    "
	DC.B	"HHEEELLLPPP ME-EEEEE"
	DC.B	"                    "
	DC.B	"TRALALA TSOIN TSOIN "
	DC.B	"                    "
	DC.B	"TTTTTTTTTTTTTTTTTTTT"
	DC.B	"RRRRRRRRRRRRRRRRRRRR"
	DC.B	"YYYYYYYYYYYYYYYYYYYY"
	DC.B	"                    "
	DC.B	"TTTTTTTTTTTTTTTTTTTT"
	DC.B	"OOOOOOOOOOOOOOOOOOOO"
	DC.B	"                    "
	DC.B	"RRRRRRRRRRRRRRRRRRRR"
	DC.B	"EEEEEEEEEEEEEEEEEEEE"
	DC.B	"AAAAAAAAAAAAAAAAAAAA"
	DC.B	"DDDDDDDDDDDDDDDDDDDD"
	DC.B	"                    "
	DC.B	"DDDDDDDDDDDDDDDDDDDD"
	DC.B	"IIIIIIIIIIIIIIIIIIII"
	DC.B	"SSSSSSSSSSSSSSSSSSSS"
	DC.B	"...................."
	DC.B	"                    "
	DC.B	" SI TU NE VIENS PAS "
	DC.B	"   AU HOOLIGAN, LE  "
	DC.B	" HOOLIGAN VIENDRA A "
	DC.B	"TOI.SI.PASKEBONKOA. "
	DC.B	"{}}{~äT''∂(!&êê'(*/*"
	DC.B	"TOUT BUG!LES FEMMES "
	DC.B	"ET LES ENFANTS APRES"
	DC.B	"        MOA.        "
	DC.B	"                    "
	DC.B	"C'EST VRAIMENT TROP "
	DC.B	"   INJUSTE ALORS.   "
	DC.B	"BIZAR, VOUS AVEZ DIT"
	DC.B	"BIZAR. BON SANG MAIS"
	DC.B	"C'EST BIEN SUR.NON, "
	DC.B	"NON, IL NE SERA PAS "
	DC.B	"DIT,KOIKIL ARRIVE KE"
	DC.B	"J'PUISSE ME REPENTIR"
	DC.B	"IL Y A KELK CHOSE DE"
	DC.B	"   POURRI DANS LE   "
	DC.B	"  ROYAUME DU ST...  "
	DC.B	"LE FOND DE L'AIR EST"
	DC.B	"FRAIS.TO CODE OR NOT"
	DC.B	"TO CODE,ON S'EN FOUT"
	DC.B	"PAS MAL.JE CODE DONC"
	DC.B	"     JE SUIS.       "
	DC.B	"                    "
	DC.B	"  IF YOU ARE STILL  "
	DC.B	"ALIVE, YOU CAN CARRY"
	DC.B	"ON READING MY TEXT.."
	DC.B	"                    "
	DC.B	"' ET BIEN,HOOLIGAN '"
	DC.B	"' IL VAS FALOIR KE '"
	DC.B	"'   TU CODE POUR   '"
	DC.B	"'ACCOMPLIR TA QUETE'"
	DC.B	"                    "
	DC.B	"'KE CODE. KE QUETE.'"
	DC.B	" THAT'S ALL, FOLKS  "
	DC.B	"   SO LET'S ARWP    "
	DC.B	" AND NEVER FORGET : "
	DC.B	" GARDENING WAS GOOD "
	DC.B	"    FOR YOU ...     "
	DC.B	"                    "
	
	DC.B	-1

TEXTPOS:	DC.W	0		* POSITION IN TEXT
SCREENPOS:DC.W	0		* POSITION IN THE SCREEN OF THE
				  * ACTUEL LETTER

VBLCOUNT:	DC.W	0		* COUNT VBLs FOR TEXT APP/DISAP

*****************************
****                     ****
****   COOR TABLE FOR    ****
****   THROWN OBJECTS    ****
****                     ****
*****************************

THROWPOS:	DC.W	84-5,84-10,84-14
	DC.W	84-18,83-18,82-18,81-18,80-18,79-18,78-18,77-18
	DC.W	77-18,76-18,76-18,76-18,77-18,77-18,78-18,79-18
	DC.W	80-18,81-18,82-18,83-18,84-18,85-18,86-18,87-18
	DC.W	88-18,89-18,90-18,92-18,94-18,96-18,98-18,100-18
	DC.W	102-18,105-18,108-18,111-18,114-18,117-18,120-18
	DC.W	123-18,126-18,129-18,132-18,135-18,138-18,141-18
	DC.W	144-18,147-18,150-18,-1,153-18,132,129,-1,126,124,122
	DC.W	121,120,121,122,124,126,129,132,135,132,129,126
	DC.W	124,122,121,120,121,122,124,126,129,126,124,122
	DC.W	121,120,121,122,124,126,129,132,135,132,129,126
	DC.W	-1
	
BOUNCE:	DC.W	0		* TABLE POSITION

TAB:	INCLUDE	"DEMO.PTS"

*****************************
****                     ****
****     INCBIN PART     ****
****                     ****
*****************************

CAR:	INCBIN	"CAR.PI1"	

ZIK:	INCBIN	"MADMAX3.MUS"

PARALX1:	INCBIN	"PARAL1.PLX"
PARALX2:	INCBIN	"PARAL2.PLX"
PARALX3:	INCBIN	"PARAL3.PLX"
PARALX4:	INCBIN	"PARAL4.PLX"
PARALX5:	INCBIN	"PARAL5.PLX"
PARALX6:	INCBIN	"PARAL6.PLX"
PARALX7:	INCBIN	"PARAL7.PLX"
	
CLOUD1:	INCBIN	"CLOUD1.PLX"
CLOUD2:	INCBIN	"CLOUD2.PLX"
CLOUD3:	INCBIN	"CLOUD3.PLX"
CLOUD4:	INCBIN	"CLOUD4.PLX"
CLOUD5:	INCBIN	"CLOUD5.PLX"
CLOUD6:	INCBIN	"CLOUD6.PLX"

SPRITE:	INCBIN	"BOUTEILL.SPR"
SPRMSK:	INCBIN	"BOUTEILL.MSK"

PAN_SPR:	INCBIN	"PANNEAU.SPR"
PAN_MSK:	INCBIN	"PANNEAU.MSK"

SCREEN:	DC.L	0		* SCREEN ADR : HERE !!
	END