' BLOBTEST
' Steven Tattersall
'
' This is a short program to draw a 2 dimensional "blob"
'
' The amount of attraction at (x,y) is given by:
'
' strength * ( 1 - radius^2) ^ 4  in 2 dimensions
'
' where radius = sqr ( x^2 + Y^2 )
'
'
'
'
max_r=180                        ! radius of the blobs
'
blob1x%=50
blob1y%=50
blob2x%=140
blob2y%=160
'
FOR y%=0 TO 199
  FOR x%=0 TO 319
    den=(FN blob(x%-blob1x%,y%-blob1y%)+FN blob(x%-blob2x%,y%-blob2y%))
    den%=MIN(16*den,15)
    PSET x%,y%,den%
  NEXT x%
NEXT y%
END
'
FUNCTION blob(x%,y%)
  ' ------------------------------------------------------------
  ' x and y are the co-ordinate values given as input
  '
  LET distance=SQR(x%*x%+y%*y%) !Calc distance from centre
  IF distance>max_r THEN
    LET density=0               !Outside max_r, density=0
  ELSE
    LET fraction=(distance/max_r)
    ' Convert distance to the
    ' range 0 to 1
    LET density=(1-fraction^2)^4
    ' calculate density function
  ENDIF
  ' ------------------------------------------------------------
  RETURN density
ENDFUNC
