*****************************************************************
*	 	  FRONT/BACK-ZOOMER fr 4-PLANE			*
*      	     03/10/93  	  Zaxx/Animal Mine/Independent		*
*****************************************************************
*								*
*   CORRECT:    0= LETZTE STUFE SOURCE 1:1 BENUTZEN		*
*   X_WORDS:	X-BREIT	= WORD(16 PIX)*flexi			*					
*   Y_LINES:	Y-BREIT	= flexi					*
*   STEPS:	ZOOMSTEPS = flexi				*
*   PLANES	= 4						*
*								*
*	A0	= ZEIGER AUF BUFFER FšR TABELLE			*
*	 TABELLE:						*
* 		GFX_ADR		(LONG,ABSOLUT)			*
*		Y		(WORD)				*
*		FšR JEDEN STEP					*
*		(DS.W	ZOOM_STEPS*3)				*	
*	A1	= BUFFER FšR GEZOOMTE GFX    			*
*		(STEPS*X_WORDS*4*Y_LINES+X_WORDS*16*Y_LINES   ) *
*		( BUFFER IN BYTES !! )				*
*	A2	= POINTER AUF GFX				*
*								*	
*****************************************************************


* BEISPIELAUFRUF

	MOVE.W	#20,X_WORDS		; X-320
	MOVE.W	#108,Y_LINES		; Y-108
	MOVE.W	#10,STEPS		; 30 STUFEN
	MOVE.W	#0,CORRECT		; 100% SAUBER
	LEA	ZOOM_GFX+50,A2		; POINTER AUF GFX	
	LEA	ALL_SPACE,A1		; BUF FšR DATA
	LEA	BLA_BLA,A0		; BUF FšR TAB
	BSR	PRECALC_ZOOM		; zoom precalc

	MOVE.W	#7,-(SP)
	TRAP	#1
	ADDQ.L	#2,SP
	CLR.W	-(SP)
	TRAP	#1
	






*********************************************************				    *
*	ZOOM INIT+PRECALC				*
*********************************************************

PRECALC_ZOOM:
*
* GFX FšR DAS BERECHEN UMSHIFTEN (PLANES -> "HIGH BITS")
*
	BSR	PRESHIFT_ZOOM
*
* OFFSETS FšR DATA-FETCH-ADR IN ZOOMROUT BERECHNEN 	
*
	BSR	CALC_OFFSET_TAB
*
* ZOOMANIMATION IN BUFFER VORBERECHNEN 	
*
	BSR	PRECALC_TO_BUF
	RTS
	


***************************************************************
*	            MAINPRECALC-ZOOM			      *
***************************************************************
PRECALC_TO_BUF:
	MOVEQ	#0,D0
	MOVE.W	X_WORDS(PC),D0
	LSL.L	#3,D0
	MOVE.L	D0,HELP_DX
	LSL.L	#1,D0
	SWAP	D0
	MOVE.L	D0,HELP_CHECK
	SWAP	D0	
	MOVEQ	#0,D1
	MOVE.W	Y_LINES(PC),D1	
	MOVE.W	STEPS(PC),D2
	DIVU.W	D2,D0
	DIVU.W	D2,D1
	MOVE.W	D0,ZOOM_X		; SAVE ZOOMSTEP VORKOMMA	
	MOVE.W	D1,ZOOM_Y	
	MOVE.W	#0,D0
	MOVE.W	#0,D1
	DIVU.W	D2,D0
	DIVU.W	D2,D1
	MOVE.W	D0,ZOOM_X+2		; SAVE ZOOMSTEP NACHKOMMA	
	MOVE.W	D1,ZOOM_Y+2	
*
* REINZOOMEN DER GFX
*
	MOVE.L	#65536,D0		; START BEI X=1,Y=1
	MOVE.L	#65536,D1
PRE_IN_ZOOM:
	ADD.L	ZOOM_X(PC),D0
	ADD.L	ZOOM_Y(PC),D1
	MOVE.L	D0,D2
	MOVE.L	D1,D3
	SWAP	D2
	SWAP	D3
	MOVE.L	A1,(A0)+		; SAVE START-ADR
	MOVE.W	D3,(A0)+		; SAVE Y-DIF
	BSR	MAIN_ZOMMER
	CMP.L	HELP_CHECK(PC),D0
	BLT.S	PRE_IN_ZOOM
	TST.W	CORRECT
	BNE.S	SHIT_CALC_ON
	MOVE.W	Y_LINES(PC),D0
	MULU.W	X_WORDS(PC),D0
	SUBQ.W	#1,D0
	MOVE.L	HELP_ADR(PC),A3
	MOVE.L	-6(A0),A4
COPY_FINAL:
	MOVE.L	(A3)+,(A4)+
	MOVE.L	(A3)+,(A4)+
	DBF	D0,COPY_FINAL				
SHIT_CALC_ON:	
	RTS


MAIN_ZOMMER:
* D2 X-ZOOM
* D3 Y-ZOOM
	MOVEQ	#0,D4
	MOVE.W	X_WORDS(PC),D4
	LSL.L	#4,D4
* X-STEP AUSRECHNEN
	DIVU.W	D2,D4			; DIV VORKOMMA
	MOVE.W	D4,D5
	MOVE.W	#0,D4 
	SWAP	D5
	DIVU.W	D2,D4			; DIV NACHKOMMA
	MOVE.W	D4,D5			; D5 = X-ZOOMSTEP * 65536
	MOVE.W	X_WORDS(PC),D4
	LSL.W	#4,D4			; FULL-X
	SUB.W	D2,D4
	LSR.W	#1,D4			; D4 = START-X
	MOVE.W	D2,D7
	ADD.W	D4,D2			; D2 = END-X
	LEA	ROUT_OFFSET(PC),A2
	LEA	ALL_ZOOM_ROUT+2(PC),A3
	ADD.W	D4,D4
	ADDA.W	D4,A2
	MOVE.W	(A2),D4			; OFFSET FšR CODE...
	LEA	-2(A3,D4.W),A4		; A4 = JUMP IN CODE

* Y-STEP AUSRECHNEN
	MOVEQ	#0,D4
	MOVE.W	Y_LINES(PC),D4
	DIVU.W	D3,D4			; Y-FULL / Y
	MOVE.W	D4,D6
	SWAP	D6
	MOVE.W	#0,D4
	DIVU.W	D3,D4			; Y-FULL / Y
	MOVE.W	D4,D6			; D6 = ZOOM-Y		
	MOVE.L	D6,HELP_YZOOM
	MOVE.L	BLACK_BIRD(PC),A5	; A5 = GFX-DATA
	SUBQ.W	#1,D7			; D7 = X-LOOP COUNTER
	MOVEQ	#0,D4
CALC_ZOOM_X2:
	SWAP	D4
	MOVE.W	(A2)+,D6		, OFFSET FšR CODE
	MOVE.W	D4,(A3,D6.W)
	SWAP	D4
	ADD.L	D5,D4			; ADD X-STEP TO X
	DBF	D7,CALC_ZOOM_X2
	MOVE.W	D3,D7
	SUBQ.W	#1,D7			; D7 = Y-LOOP COUNTER
	LEA	-2(A3,D6.W),A3		; A3 = END OF CODE
	MOVE.W	(A3),OLD_CODE
	MOVE.W	#$4E75,(A3)		; PLACE "RTS"
	MOVE.W	D2,D3
	AND.W	#%1111110000,D2
	LSR.W	#1,D2
	LEA	(A1,D2.W),A6		; A6 = ZOOM-GFX DESTINATION 
	AND.W	#%1111,D3
	NEG.W	D3
	ADD.W	#17,D3
	MOVE.W	D3,CHECK_SHIFT
	MOVE.L	#0,A2
DO_ZOOM_REAL:
	MOVEQ	#0,D3
	MOVEQ	#0,D4
	MOVEQ	#0,D5
	MOVEQ	#0,D6
	JSR	(A4)			; CALC GFX
	CMPI.W	#17,CHECK_SHIFT
	BNE.S	NO_FUCKBUG
	MOVEM.W	D3-D6,-8(A6)		; FALS REST FEHLT...
	SWAP	D3
	SWAP	D4
	SWAP	D5
	SWAP	D6
	MOVEM.W	D3-D6,(A6)
	BRA.S	NEXT_Y_Z
NO_FUCKBUG:
	MOVE.W	CHECK_SHIFT(PC),D2		; ZOOMDATA SHIFTEN 
	LSL.W	D2,D3
	LSL.W	D2,D4
	LSL.W	D2,D5
	LSL.W	D2,D6
	MOVEM.W	D3-D6,(A6)		; UND AUF SCREEN....
NEXT_Y_Z:
	ADD.L	HELP_DX(PC),A1
	ADD.L	HELP_DX(PC),A6
	ADD.L	HELP_YZOOM(PC),A2
	MOVE.L	A2,D3
	SWAP	D3
	ADD.W	D3,D3
	ADD.W	D3,D3
	MOVE.L	MULU_Y_GFX(PC,D3.W),D3
	MOVE.L	BLACK_BIRD(PC),A5
	LEA	(A5,D3.L),A5	
	DBF	D7,DO_ZOOM_REAL
	MOVE.W	OLD_CODE(PC),(A3)
	RTS
MULU_Y_GFX:
	REPT	101
	DC.B	'ZAXXZOOM'
	ENDR


***************************************************************
*	            MAIN-ZOOM SHIFTROUT			      *
***************************************************************
ALL_ZOOM_ROUT:
* 20  = 1
* 326 = 16
* FšR ALLE 320-PIX IN X
OFFX	SET	0
	REPT	20
* P1
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P2
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P3
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P4
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P5
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P6
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P7
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P8
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P9
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P10
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P11
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P12
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P13
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P14
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P15
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* P16
	MOVE.B	32000(A5),D2
	ADD.B	D2,D2
	ADDX.W	D3,D3	
	ADD.B	D2,D2
	ADDX.W	D4,D4	
	ADD.B	D2,D2
	ADDX.W	D5,D5	
	ADD.B	D2,D2
	ADDX.W	D6,D6	
* SAVE GFX
	MOVEM.W	D3-D6,OFFX(A1)
OFFX	SET	OFFX+8
	ENDR
	RTS

***************************************************************
*             OFFSET IN ZOOMROUT PRECALC		      *
***************************************************************
CALC_OFFSET_TAB:
	LEA	ROUT_OFFSET(PC),A4
	MOVEQ	#0,D0
	MOVE.W	#20-1,D1
MAIN_OFF_CALC: 
	MOVE.W	#15-1,D2
MAIN_OFF_CALC_2:
	MOVE.W	D0,(A4)+
	ADD.W	#20,D0
	DBF	D2,MAIN_OFF_CALC_2
	MOVE.W	D0,(A4)+
	ADD.W	#26,D0
	DBF	D1,MAIN_OFF_CALC
	REPT	2
	MOVE.W	D0,(A4)+
	ENDR
	LEA	MULU_Y_GFX(PC),A4
	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.W	X_WORDS(PC),D0
	LSL.L	#4,D0			; *16
	MOVE.W	Y_LINES(PC),D2
CALC_STEPTAB:
	MOVE.L	D1,(A4)+
	ADD.L	D0,D1
	DBF	D2,CALC_STEPTAB	
	RTS

***************************************************************
*             GFX-PRESHIFTEN FšR ZOOMCALC		      *
***************************************************************
PRESHIFT_ZOOM:
	MOVE.L	A2,HELP_ADR		; BRAUCH MAN NOCHMAL...
	MOVE.L	A1,BLACK_BIRD		; POINTER AUF BUF SAVEN
	MOVE.W	X_WORDS(PC),D6
	MULU.W	Y_LINES(PC),D6
	SUBQ.W	#1,D6
SHIFT_16:
	MOVEM.W	(A2)+,D0-D3		; 16 PIX HOLEN
	MOVEQ	#16-1,D4
SHIFT_1:
	MOVEQ	#0,D5
	ADDX.W	D0,D0
	ROXL.B	D5
	ADDX.W	D1,D1			; PLANES IN "BYTES"
	ROXL.B	D5			; UMRECHNEN
	ADDX.W	D2,D2
	ROXL.B	D5
	ADDX.W	D3,D3
	ROXL.B	D5
	LSL.W	#4,D5			; IN "HIGH BITS" SHIFTEN
	MOVE.B	D5,(A1)+
	DBF	D4,SHIFT_1
	DBF	D6,SHIFT_16
	RTS

******************** PC-RELATIVE DATA ! *************************
HELP_ADR:	DC.L	0
HELP_DX:	DC.L	0
HELP_CHECK:	DC.L	0
BLACK_BIRD:	DC.L	0
ZOOM_X:		DC.L	0
ZOOM_Y:		DC.L	0
HELP_YZOOM:	DC.L	0
OLD_CODE:	DC.W	0
CHECK_SHIFT:	DC.W	0
X_WORDS:	DC.W	0
Y_LINES:	DC.W	0
CORRECT:	DC.W	0
STEPS:		DC.W	0
ROUT_OFFSET:	DS.W	322
*****************************************************************
*                     END OF ZOOMER				*
*****************************************************************





         



	SECTION	DATA

; "INFINITY" LOGO 320*108 mit 16 COLORS
ZOOM_GFX:
	INCBIN	A:\INFINITY.BIN
	EVEN

	

	SECTION	BSS
BLA_BLA:	DS.W	200
ALL_SPACE:	DS.B	200000
	
			