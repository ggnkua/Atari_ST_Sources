	JSR	SETSUPER
	JSR	SAVEPAL
	JSR	FLASHDOWN
	JSR	SETSCREEN
	JSR	CLEARSCRS
	JSR	SETPAL
	JSR	SETPIC
	JSR	SETVAR
	JSR	STARTMUSIC
	JSR	INIT
DEMOLOOP:
	CMP.W	#1,WFNC
	BNE	DEMOLOOP
	MOVE.W	#0,WFNC

*	MOVE.W	#$700,$FF8240
	JSR	TEXTSCROLL
	JSR	TEXTSCROLL
	JSR	SHOWSCROLL
	
	CMP.W	#5,COUNT
	BNE	NONEWCHAR
	JSR	SETFONT
	MOVE.W	#0,COUNT
NONEWCHAR:
	ADD.W	#1,COUNT

*	MOVE.W	#$000,$FF8240

	CMP.B	#185,$FFFC02
	BNE	DEMOLOOP

	JSR	ENDMUSIC
	JSR	RETINIT
	JSR	OLDPAL
	CLR.W	-(SP)
	TRAP	#1
*******************************
SETSUPER:
	CLR.L	-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,SAVESUPER
	RTS
ENDSUPER:
	MOVE.L	SAVESUPER,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	RTS
*******************************
SETSCREEN:
	MOVE.W	#0,-(SP)
	MOVE.L	#$78000,-(SP)
	MOVE.L	#$78000,-(SP)
	MOVE.W	#$5,-(SP)
	TRAP	#14
	ADD.L	#12,SP
	RTS
*******************************
SETVAR:
	MOVE.L	#SCROLLTEXT,TEXTPTR
	RTS 
*******************************
CLEARSCRS:
	LEA	$78000,A0
	MOVE.W	#8000,D0
CLRLOOP:
	CLR.L	(A0)+
	DBF.W	D0,CLRLOOP
	RTS
*******************************
STARTMUSIC:
	move.l #(music+$86e),(music+$1aa)	relocate
	move.l #(music+$86f),(music+$828)	music
	move.l #(music+$86e),(music+$83c)	to
	move.l #(music+$c4e),(music+$c3e)	play
	move.l #(music+$2cfa),(music+$c42)	at
	move.l #(music+$356a),(music+$c46)	this
	move.l #(music+$416c),(music+$c4a)	address
	clr.l d0				init
	jsr (music+8)				mfp etc
	moveq #3,d0				music #1
	jsr (music)				call music
	rts					do old vbl code
ENDMUSIC:
	move.l #$8080000,$ff8820		clear
	move.l #$9090000,$ff8820		sound
	move.l #$a0a0000,$ff8820		chip
	move.l #$707ffff,$ff8820		registers
	jsr (music+12)				restore mfp etc.
	move.b #$f0,$fffa23			new timer c
	RTS
*******************************
INIT:
	MOVE.L	$70.W,OLDVBL
	MOVE.L	#NEWVBL,$70.W
	RTS
RETINIT:	
	MOVE.L	OLDVBL,$70.W
	RTS
*******************************
FLASHDOWN:
	MOVE.W	#$777,D1
	REPT	7
	JSR	VSYNC
	MOVE.W	D1,$FF8240
	SUB.W	#$111,D1
	JSR	WAIT
	ENDR
	RTS
*******************************
VSYNC:
	MOVE.W	#37,-(SP)
	TRAP	#14
	ADDQ.L	#2,SP
	RTS
WAIT:
	MOVE.W	#$7FFF,D0
WAITLOOP:
	DBF.W	D0,WAITLOOP
	RTS	
*******************************
SAVEPAL:
	MOVE.L	#$FF8240,A0
	MOVE.L	#PALSAVE,A1
	REPT	8
	MOVE.L	(A0)+,(A1)+
	ENDR
	RTS
*******************************
OLDPAL:
	MOVE.L	#PALSAVE,A0
	MOVE.L	#$FF8240,A1
	REPT	8
	MOVE.L	(A0)+,(A1)+
	ENDR
	RTS
*******************************
SETPAL:
	MOVE.L	#PIC+2,A0
	MOVE.L	#$FF8240,A1
	REPT	8
	MOVE.L	(A0)+,(A1)+
	ENDR
	RTS
*******************************
SETPIC:
	MOVE.L	#PIC+34,A0
	MOVE.L	#$78000,A1
	MOVE.W	#40*190,D0
SETPICLOOP:
	MOVE.L	(A0)+,(A1)+
	DBF.W	D0,SETPICLOOP	
	RTS
*******************************
*          GNUUUU!!!          *
*******************************
TEXTSCROLL:
	MOVE.L	#SCRAREA,A0
	MOVE.W	#7,D0
SCRLOOP:
	ROXL.W	160(A0)
	ROXL.W	152(A0)
	ROXL.W	144(A0)
	ROXL.W	136(A0)
	ROXL.W	128(A0)
	ROXL.W	120(A0)
	ROXL.W	112(A0)
	ROXL.W	104(A0)
	ROXL.W	96(A0)
	ROXL.W	88(A0)
	ROXL.W	80(A0)
	ROXL.W	72(A0)
	ROXL.W	64(A0)
	ROXL.W	56(A0)
	ROXL.W	48(A0)
	ROXL.W	40(A0)
	ROXL.W	32(A0)
	ROXL.W	24(A0)
	ROXL.W	16(A0)
	ROXL.W	8(A0)
	ROXL.W	(A0)
	ADD.W	#168,A0
	DBF.W	D0,SCRLOOP
	RTS
*******************************
SHOWSCROLL:
	MOVE.L	#SCRAREA,A0
	MOVE.L	#$78000+160*192+6,A1
	MOVE.W	#7,D0
SHOWLOOP:
	MOVE.W	(A0),(A1)
	MOVE.W	8(A0),8(A1)
	MOVE.W	16(A0),16(A1)
	MOVE.W	24(A0),24(A1)
	MOVE.W	32(A0),32(A1)
	MOVE.W	40(A0),40(A1)
	MOVE.W	48(A0),48(A1)
	MOVE.W	56(A0),56(A1)
	MOVE.W	64(A0),64(A1)
	MOVE.W	72(A0),72(A1)
	MOVE.W	80(A0),80(A1)
	MOVE.W	88(A0),88(A1)
	MOVE.W	96(A0),96(A1)
	MOVE.W	104(A0),104(A1)
	MOVE.W	112(A0),112(A1)
	MOVE.W	120(A0),120(A1)
	MOVE.W	128(A0),128(A1)
	MOVE.W	136(A0),136(A1)
	MOVE.W	144(A0),144(A1)
	MOVE.W	152(A0),152(A1)
	ADD.W	#168,A0
	ADD.W	#160,A1
	DBF.W	D0,SHOWLOOP
	RTS
*******************************
SETFONT:
	MOVE.L	TEXTPTR,A0
	CLR.W	D0
	MOVE.B	(A0)+,D0
	CMP.W	#$FF,D0
	BEQ	TEXTWRAP
	MOVE.L	A0,TEXTPTR

	SUB.W	#32,D0
	MULS	#2,D0

	MOVE.L	#FONTTAB,A1
	ADD.W	D0,A1
	MOVE.W	(A1)+,D0
	MOVE.L	#FONT+34,A1
	ADD.W	D0,A1
	MOVE.L	#SCRAREA+160,A0
	REPT	8
	MOVE.W	(A1),(A0)
	ADD.W	#160,A1
	ADD.W	#168,A0
	ENDR
	RTS	
TEXTWRAP:
	MOVE.L	#SCROLLTEXT,TEXTPTR
	JMP	SETFONT
*******************************
NEWVBL:
	movem.l d0-a6,-(sp)			save registers
	jsr (music+4)				call music player
	movem.l (sp)+,d0-a6			restore registers
	MOVE.W	#1,WFNC
	RTE
*******************************
	SECTION	DATA
OLDVBL:		DC.L	0
SAVESUPER:	DC.L	0
WFNC:		DC.W	0
COUNT:		DC.W	0
TEXTPTR:	DC.L	0
RASTERPTR:	DC.L	0
*******************************
FONT:	INCBIN	A:\FONTS2.PI1
		EVEN
PIC:	INCBIN	A:\THEBILD.PI1
		EVEN
music	incbin 'B:\chamber.mus'
		EVEN
FONTTAB:
	DC.W	0,8,16,24,32,40,48,56,64,72,80,88,96,104,112,120,128
	DC.W	136,144,152,2560,2568,2576,2584,2592,2600,2608,2616
	DC.W	2624,2632,2640,2648,2656,2664,2672,2680,2688,2696
	DC.W	2704,2712,5120,5128,5136,5144,5152,5160,5168,5176
	DC.W	5184,5192,5200,5208,5216,5224,5232,5240,5248,5256
	DC.W	5264,5272
		EVEN
SCROLLTEXT:	
	DC.B	'            HELLO AND WELCOME TO   "THE MUSIC COLLECTION"....A LITTLE COLLECTION OF MUSIC WE HAVE AND OTHERS WANT,SO COPY THIS DISC ALL YOU GUYS WHO ASKED FOR IT.... ANYWAY,THIS DISC WAS FASTLY PUT TOGETHER BY LORD.D WITH HELP '       
        DC.B    'FROM FEAR WHO CODED THIS LAME INTRO,WITH DIGI MUSIC BY MAD MAX OF TEX.'
        DC.B    'THIS DISC CONTAINS MOSTLY OF MAD MAX MUSIX BUT HAS ALSO MUSIX BY COUNT ZERO OF ELECTRONIC IMAGES,LAP OF NEXT AND EVEN AN COOL OF THE CAREBEARS (ONLY TWO OR THREE OF THOSE,I THINK!)...AND '
        DC.B    'IS ALSO SOME DIGI DRUM MUSIC AND MUSIC FROM THE VERY COOL GAME "NINJA REMIX",RIPPED BY JACKY FROM ACF OF THE ALLIANCE.SOME OF THE MUSIX ARE RIPPED BY ME AND OTHERS I JUST HAVE GAINED FROM DIFFERENT SOURCE DISCS I HAVE...'
        DC.B    'NOW FOR THE GREETINGS: ME-SO-CORNY (HA,HA,HA..YOU HAVE NOT CODED ANYTHING IN THIS INTRO!,BUT KEEP CODING DUDE!)...DR.ZAGO (WELL,IS THIS ENOUGH MUSIC FOR YOU OR WHAT?)..EMLING (THANX FOR ALL THE MUSIC YOU HAVE SENT TO US!,'
        DC.B    'SAY HELLO TO YOURE FRIENDS IN FRANCE FROM US!)...OB (YOURE "ROSLAGEN" DEMO WAS FUNNY,BUT THE "LILLE SVEN" DEMO HAD TO BAD SOUND QUALITY!)..TOXIC (HAS CORNY SENT YOU THE GUESTSCREEN YET?)...DARK SHADOW OF THE INVIZIBLES'
        DC.B    ' (SEND US YOURE MEGADEMO FAST MAN!)...PAULSSON (GIVE ME THE QUESTIONS OF THE DRIVING LICENSE SOON!).   THATS ALL I THINK.BUT REMEMBER TO CREDIT THE MAKER OF THE MUSIC YOU USE IN YOURE DEMOS (THATS THE LEAST YOU CAN DO'
	DC.B	' FOR THE GREAT MUSICIANS!).LORD SAYS SAYONARA SUCKERS (FEAR DOES NOT SAY ANYTHING COZ HE HAS HIS FACE FULL OF COFFE!)....OH!..THANX TO EMLING FOR THIS GREAT PACKER WE USED IN THIS DEMO,WE WILL USE IT IN OUR MEGADEMO'
        DC.B	' TOO!.....THIS DISC WAS RELEASED: FRIDAY THE 13:TH IN THE YEAR OF 1992 IN THE MIDDLE OF SWEDEN!....                                                                       PRESS SPACE!                                ',$FF

		EVEN
	SECTION	BSS
PALSAVE:
	DS.W	16
	EVEN
SCRAREA:
	DS.B	160*20   
       	END     