* - OUR ADRESS :
* - OVERLANDERS. 27,CHEMIN DES CYCLAMENS. 73000 CHAMBERY. FRANCE.
  
RES	EQU	2
PAS	EQU	5

*	REGISTRES DU MFP

GPIP	EQU	0
AER	EQU	2
DDR	EQU	4
IERA	EQU	6
IERB	EQU	8
IPRA	EQU	10
IPRB	EQU	12
ISRA	EQU	14
ISRB	EQU	16
IMRA	EQU	18
IMRB	EQU	20
VR	EQU	22
TACR	EQU	24
TBCR	EQU	26
TCDCR	EQU	28
TADR	EQU	30
TBDR	EQU	32
TCDR	EQU	34
TDDR	EQU	36
SCR	EQU	38
UCR	EQU	40
RSR	EQU	42
TSR	EQU	44
UDR	EQU	46
START:
	CLR.L	-(SP)		SUPERVISEUR
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	MOVE.L	D0,OLDPILE	
	DC.W	$A00A
	BSR	SAVE_REFS
	MOVEQ	#0,D0
	LEA	$FFFF8240.W,A0
	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A0)+
	MOVE.L	D0,(A0)
ATTEND:
	MOVEQ	#100,D7
DRIVE:	MOVE.W	#37,-(SP)
	TRAP	#14
	ADDQ.L	#2,SP
	DBRA	D7,DRIVE
	CLR.B	$FFFFFA07.W
	CLR.B	$FFFFFA09.W
	MOVE.L	#RETURN,$70.W
	MOVE.L	#RETURN,$68.W
	MOVE.L	#RETURN,$78.W
	MOVE.L	#SCREEN1,D0
	AND.L	#$FFFFFF00,D0
	ADD.L	#256,D0
	MOVE.L	D0,ECRAN1	
	MOVE.L	#SCREEN2,D0
	AND.L	#$FFFFFF00,D0
	ADD.L	#256,D0
	MOVE.L	D0,ECRAN2	
	CLR.B	$FF8260
	MOVE.L	ECRAN1,A1		
	MOVE.L	ECRAN2,A2
	MOVE.L	DECOR,D1
	MOVE.L	DECOR+4,D2
	MOVE.L	#276*20-1,D0
DCOPY:	MOVE.L	D1,(A1)+
	MOVE.L	D2,(A1)+
	MOVE.L	D1,(A2)+
	MOVE.L	D2,(A2)+
	DBRA	D0,DCOPY
	MOVE.L	#MEGA_TXT,MEGA_PT
	MOVEQ	#0,D0
	MOVE.L	MEGA_PT,A0
	MOVE.B	(A0),D0
	MULU	#800,D0
	ADD.L	#BIGALPHA,D0
	MOVE.L	D0,MEGA_LET
	MOVE.B	#32,NB_DEC
	MOVE.L	#FD_RBND,RBND_PT
	MOVE.L	#FD_ANIM,FOND_PT
	MOVE.B	#8,ANIM_CMP
	MOVE.L	#FOND_OND,OND_PT
	MOVE.B	#189,OND_CMP
	MOVEQ	#1,D0
	JSR	MUSIC
INTS:	BSR	PRG
	MOVEM.L	PALETTE,D0-D7	
	MOVEM.L	D0-D7,$FF8240	
VSYNC:	
	CMPI.B	#$39,$FFFFFC02.W
	BEQ	FIN
	MOVE.L	ECRAN1,D0
	MOVE.L	ECRAN2,ECRAN1
	MOVE.L	D0,ECRAN2
	MOVE.B	ECRAN1+1,$FFFF8201.W
	MOVE.B	ECRAN1+2,$FFFF8203.W
TSTVSYNC:
	TST.B	VS
	BNE.S	TSTVSYNC
	MOVE.B	#1,VS
	MOVE.L	ECRAN2,A0
	MOVE.L	A0,A2
	LEA	6(A0),A0
	LEA	8(A0),A1
	MOVEQ	#4,D0
SCROLL:	MOVE.W	(A1),(A0)
	MOVE.W	8(A1),8(A0)
	MOVE.W	16(A1),16(A0)
	MOVE.W	24(A1),24(A0)
	MOVE.W	32(A1),32(A0)
	MOVE.W	40(A1),40(A0)
	MOVE.W	48(A1),48(A0)
	MOVE.W	56(A1),56(A0)
	MOVE.W	64(A1),64(A0)
	MOVE.W	72(A1),72(A0)
	MOVE.W	80(A1),80(A0)
	MOVE.W	88(A1),88(A0)
	MOVE.W	96(A1),96(A0)
	MOVE.W	104(A1),104(A0)
	MOVE.W	112(A1),112(A0)
	MOVE.W	120(A1),120(A0)
	MOVE.W	128(A1),128(A0)
	MOVE.W	136(A1),136(A0)
	MOVE.W	144(A1),144(A0)
	MOVE.W	11*160(A1),11*160(A0)
	MOVE.W	11*160+8(A1),11*160+8(A0)
	MOVE.W	11*160+16(A1),11*160+16(A0)
	MOVE.W	11*160+24(A1),11*160+24(A0)
	MOVE.W	11*160+32(A1),11*160+32(A0)
	MOVE.W	11*160+40(A1),11*160+40(A0)
	MOVE.W	11*160+48(A1),11*160+48(A0)
	MOVE.W	11*160+56(A1),11*160+56(A0)
	MOVE.W	11*160+64(A1),11*160+64(A0)
	MOVE.W	11*160+72(A1),11*160+72(A0)
	MOVE.W	11*160+80(A1),11*160+80(A0)
	MOVE.W	11*160+88(A1),11*160+88(A0)
	MOVE.W	11*160+96(A1),11*160+96(A0)
	MOVE.W	11*160+104(A1),11*160+104(A0)
	MOVE.W	11*160+112(A1),11*160+112(A0)
	MOVE.W	11*160+120(A1),11*160+120(A0)
	MOVE.W	11*160+128(A1),11*160+128(A0)
	MOVE.W	11*160+136(A1),11*160+136(A0)
	MOVE.W	11*160+144(A1),11*160+144(A0)
	MOVE.W	22*160(A1),22*160(A0)
	MOVE.W	22*160+8(A1),22*160+8(A0)
	MOVE.W	22*160+16(A1),22*160+16(A0)
	MOVE.W	22*160+24(A1),22*160+24(A0)
	MOVE.W	22*160+32(A1),22*160+32(A0)
	MOVE.W	22*160+40(A1),22*160+40(A0)
	MOVE.W	22*160+48(A1),22*160+48(A0)
	MOVE.W	22*160+56(A1),22*160+56(A0)
	MOVE.W	22*160+64(A1),22*160+64(A0)
	MOVE.W	22*160+72(A1),22*160+72(A0)
	MOVE.W	22*160+80(A1),22*160+80(A0)
	MOVE.W	22*160+88(A1),22*160+88(A0)
	MOVE.W	22*160+96(A1),22*160+96(A0)
	MOVE.W	22*160+104(A1),22*160+104(A0)
	MOVE.W	22*160+112(A1),22*160+112(A0)
	MOVE.W	22*160+120(A1),22*160+120(A0)
	MOVE.W	22*160+128(A1),22*160+128(A0)
	MOVE.W	22*160+136(A1),22*160+136(A0)
	MOVE.W	22*160+144(A1),22*160+144(A0)
	MOVE.W	33*160(A1),33*160(A0)
	MOVE.W	33*160+8(A1),33*160+8(A0)
	MOVE.W	33*160+16(A1),33*160+16(A0)
	MOVE.W	33*160+24(A1),33*160+24(A0)
	MOVE.W	33*160+32(A1),33*160+32(A0)
	MOVE.W	33*160+40(A1),33*160+40(A0)
	MOVE.W	33*160+48(A1),33*160+48(A0)
	MOVE.W	33*160+56(A1),33*160+56(A0)
	MOVE.W	33*160+64(A1),33*160+64(A0)
	MOVE.W	33*160+72(A1),33*160+72(A0)
	MOVE.W	33*160+80(A1),33*160+80(A0)
	MOVE.W	33*160+88(A1),33*160+88(A0)
	MOVE.W	33*160+96(A1),33*160+96(A0)
	MOVE.W	33*160+104(A1),33*160+104(A0)
	MOVE.W	33*160+112(A1),33*160+112(A0)
	MOVE.W	33*160+120(A1),33*160+120(A0)
	MOVE.W	33*160+128(A1),33*160+128(A0)
	MOVE.W	33*160+136(A1),33*160+136(A0)
	MOVE.W	33*160+144(A1),33*160+144(A0)
	MOVE.W	44*160(A1),44*160(A0)
	MOVE.W	44*160+8(A1),44*160+8(A0)
	MOVE.W	44*160+16(A1),44*160+16(A0)
	MOVE.W	44*160+24(A1),44*160+24(A0)
	MOVE.W	44*160+32(A1),44*160+32(A0)
	MOVE.W	44*160+40(A1),44*160+40(A0)
	MOVE.W	44*160+48(A1),44*160+48(A0)
	MOVE.W	44*160+56(A1),44*160+56(A0)
	MOVE.W	44*160+64(A1),44*160+64(A0)
	MOVE.W	44*160+72(A1),44*160+72(A0)
	MOVE.W	44*160+80(A1),44*160+80(A0)
	MOVE.W	44*160+88(A1),44*160+88(A0)
	MOVE.W	44*160+96(A1),44*160+96(A0)
	MOVE.W	44*160+104(A1),44*160+104(A0)
	MOVE.W	44*160+112(A1),44*160+112(A0)
	MOVE.W	44*160+120(A1),44*160+120(A0)
	MOVE.W	44*160+128(A1),44*160+128(A0)
	MOVE.W	44*160+136(A1),44*160+136(A0)
	MOVE.W	44*160+144(A1),44*160+144(A0)
	LEA	55*160(A0),A0
	LEA	55*160(A1),A1
	DBRA	D0,SCROLL
	LEA	158(A2),A0
	MOVE.L	ECRAN1,A1
	LEA	159(A1),A1
	MOVE.B	(A1),(A0)
	MOVE.B	11*160(A1),11*160(A0)
	MOVE.B	22*160(A1),22*160(A0)
	MOVE.B	33*160(A1),33*160(A0)
	MOVE.B	44*160(A1),44*160(A0)
	MOVE.B	55*160(A1),55*160(A0)
	MOVE.B	66*160(A1),66*160(A0)
	MOVE.B	77*160(A1),77*160(A0)
	MOVE.B	88*160(A1),88*160(A0)
	MOVE.B	99*160(A1),99*160(A0)
	MOVE.B	110*160(A1),110*160(A0)
	MOVE.B	121*160(A1),121*160(A0)
	MOVE.B	132*160(A1),132*160(A0)
	MOVE.B	143*160(A1),143*160(A0)
	MOVE.B	154*160(A1),154*160(A0)
	MOVE.B	165*160(A1),165*160(A0)
	MOVE.B	176*160(A1),176*160(A0)
	MOVE.B	187*160(A1),187*160(A0)
	MOVE.B	198*160(A1),198*160(A0)
	ADD.L	#209*160,A1
	ADD.L	#209*160,A0
	MOVE.B	(A1),(A0)
	MOVE.B	11*160(A1),11*160(A0)
	MOVE.B	22*160(A1),22*160(A0)
	MOVE.B	33*160(A1),33*160(A0)
	MOVE.B	44*160(A1),44*160(A0)
	MOVE.B	55*160(A1),55*160(A0)
	SUBQ.B	#1,NB_DEC
	BGT.S	B2
	MOVE.B	#32,NB_DEC
	ADDQ.L	#1,MEGA_PT
	MOVE.L	MEGA_PT,D0
	CMP.L	#FINTXT,D0
	BLT.S	OKTXT
	MOVE.L	#MEGA_TXT,MEGA_PT
OKTXT:	MOVE.L	MEGA_PT,A0
	MOVEQ	#0,D0
	MOVE.B	(A0),D0
	MULU	#800,D0
	ADD.L	#BIGALPHA,D0
	MOVE.L	D0,MEGA_LET

B2:	MOVE.L	MEGA_LET,A0
	LEA 	159(A2),A1
	MOVEQ	#0,D2
	MOVE.W	#$FFFF,D1
	MOVEQ	#24,D0

B3:	TST.B	(A0)+
	BEQ.S	FOND
	MOVE.B	D1,(A1)
	MOVE.B	D1,160(A1)
	MOVE.B	D1,320(A1)
	MOVE.B	D1,480(A1)
	MOVE.B	D1,640(A1)
	MOVE.B	D1,800(A1)
	MOVE.B	D1,960(A1)
	MOVE.B	D1,1120(A1)
	MOVE.B	D1,1280(A1)
	MOVE.B	D1,1440(A1)
	MOVE.B	D1,1600(A1)
	LEA	11*160(A1),A1	
	DBRA	D0,B3
	BRA.S	PLANES

FOND:	MOVE.B	D2,(A1)
	MOVE.B	D2,160(A1)
	MOVE.B	D2,320(A1)
	MOVE.B	D2,480(A1)
	MOVE.B	D2,640(A1)
	MOVE.B	D2,800(A1)
	MOVE.B	D2,960(A1)
	MOVE.B	D2,1120(A1)
	MOVE.B	D2,1280(A1)
	MOVE.B	D2,1440(A1)
	MOVE.B	D2,1600(A1)

	LEA	11*160(A1),A1
	DBRA	D0,B3

PLANES:	ADD.L	#25,MEGA_LET
	MOVEQ	#24,D0
	LEA	6(A2),A2
B4:	MOVE.W	(A2),D1
	MOVE.W	D1,160(A2)
	MOVE.W	D1,320(A2)
	MOVE.W	D1,480(A2)
	MOVE.W	D1,640(A2)
	MOVE.W	D1,800(A2)
	MOVE.W	D1,960(A2)
	MOVE.W	D1,1120(A2)
	MOVE.W	D1,1280(A2)
	MOVE.W	D1,1440(A2)
	MOVE.W	D1,1600(A2)
	MOVE.W	1*8(A2),D1
	MOVE.W	D1,1*160+1*8(A2)
	MOVE.W	D1,2*160+1*8(A2)
	MOVE.W	D1,3*160+1*8(A2)
	MOVE.W	D1,4*160+1*8(A2)
	MOVE.W	D1,5*160+1*8(A2)
	MOVE.W	D1,6*160+1*8(A2)
	MOVE.W	D1,7*160+1*8(A2)
	MOVE.W	D1,8*160+1*8(A2)
	MOVE.W	D1,9*160+1*8(A2)
	MOVE.W	D1,10*160+1*8(A2)
	MOVE.W	2*8(A2),D1
	MOVE.W	D1,1*160+2*8(A2)
	MOVE.W	D1,2*160+2*8(A2)
	MOVE.W	D1,3*160+2*8(A2)
	MOVE.W	D1,4*160+2*8(A2)
	MOVE.W	D1,5*160+2*8(A2)
	MOVE.W	D1,6*160+2*8(A2)
	MOVE.W	D1,7*160+2*8(A2)
	MOVE.W	D1,8*160+2*8(A2)
	MOVE.W	D1,9*160+2*8(A2)
	MOVE.W	D1,10*160+2*8(A2)
	MOVE.W	3*8(A2),D1
	MOVE.W	D1,1*160+3*8(A2)
	MOVE.W	D1,2*160+3*8(A2)
	MOVE.W	D1,3*160+3*8(A2)
	MOVE.W	D1,4*160+3*8(A2)
	MOVE.W	D1,5*160+3*8(A2)
	MOVE.W	D1,6*160+3*8(A2)
	MOVE.W	D1,7*160+3*8(A2)
	MOVE.W	D1,8*160+3*8(A2)
	MOVE.W	D1,9*160+3*8(A2)
	MOVE.W	D1,10*160+3*8(A2)
	MOVE.W	4*8(A2),D1
	MOVE.W	D1,1*160+4*8(A2)
	MOVE.W	D1,2*160+4*8(A2)
	MOVE.W	D1,3*160+4*8(A2)
	MOVE.W	D1,4*160+4*8(A2)
	MOVE.W	D1,5*160+4*8(A2)
	MOVE.W	D1,6*160+4*8(A2)
	MOVE.W	D1,7*160+4*8(A2)
	MOVE.W	D1,8*160+4*8(A2)
	MOVE.W	D1,9*160+4*8(A2)
	MOVE.W	D1,10*160+4*8(A2)
	MOVE.W	5*8(A2),D1
	MOVE.W	D1,1*160+5*8(A2)
	MOVE.W	D1,2*160+5*8(A2)
	MOVE.W	D1,3*160+5*8(A2)
	MOVE.W	D1,4*160+5*8(A2)
	MOVE.W	D1,5*160+5*8(A2)
	MOVE.W	D1,6*160+5*8(A2)
	MOVE.W	D1,7*160+5*8(A2)
	MOVE.W	D1,8*160+5*8(A2)
	MOVE.W	D1,9*160+5*8(A2)
	MOVE.W	D1,10*160+5*8(A2)
	MOVE.W	6*8(A2),D1
	MOVE.W	D1,1*160+6*8(A2)
	MOVE.W	D1,2*160+6*8(A2)
	MOVE.W	D1,3*160+6*8(A2)
	MOVE.W	D1,4*160+6*8(A2)
	MOVE.W	D1,5*160+6*8(A2)
	MOVE.W	D1,6*160+6*8(A2)
	MOVE.W	D1,7*160+6*8(A2)
	MOVE.W	D1,8*160+6*8(A2)
	MOVE.W	D1,9*160+6*8(A2)
	MOVE.W	D1,10*160+6*8(A2)
	MOVE.W	7*8(A2),D1
	MOVE.W	D1,1*160+7*8(A2)
	MOVE.W	D1,2*160+7*8(A2)
	MOVE.W	D1,3*160+7*8(A2)
	MOVE.W	D1,4*160+7*8(A2)
	MOVE.W	D1,5*160+7*8(A2)
	MOVE.W	D1,6*160+7*8(A2)
	MOVE.W	D1,7*160+7*8(A2)
	MOVE.W	D1,8*160+7*8(A2)
	MOVE.W	D1,9*160+7*8(A2)
	MOVE.W	D1,10*160+7*8(A2)
	MOVE.W	8*8(A2),D1
	MOVE.W	D1,1*160+8*8(A2)
	MOVE.W	D1,2*160+8*8(A2)
	MOVE.W	D1,3*160+8*8(A2)
	MOVE.W	D1,4*160+8*8(A2)
	MOVE.W	D1,5*160+8*8(A2)
	MOVE.W	D1,6*160+8*8(A2)
	MOVE.W	D1,7*160+8*8(A2)
	MOVE.W	D1,8*160+8*8(A2)
	MOVE.W	D1,9*160+8*8(A2)
	MOVE.W	D1,10*160+8*8(A2)
	MOVE.W	9*8(A2),D1
	MOVE.W	D1,1*160+9*8(A2)
	MOVE.W	D1,2*160+9*8(A2)
	MOVE.W	D1,3*160+9*8(A2)
	MOVE.W	D1,4*160+9*8(A2)
	MOVE.W	D1,5*160+9*8(A2)
	MOVE.W	D1,6*160+9*8(A2)
	MOVE.W	D1,7*160+9*8(A2)
	MOVE.W	D1,8*160+9*8(A2)
	MOVE.W	D1,9*160+9*8(A2)
	MOVE.W	D1,10*160+9*8(A2)
	MOVE.W	10*8(A2),D1
	MOVE.W	D1,1*160+10*8(A2)
	MOVE.W	D1,2*160+10*8(A2)
	MOVE.W	D1,3*160+10*8(A2)
	MOVE.W	D1,4*160+10*8(A2)
	MOVE.W	D1,5*160+10*8(A2)
	MOVE.W	D1,6*160+10*8(A2)
	MOVE.W	D1,7*160+10*8(A2)
	MOVE.W	D1,8*160+10*8(A2)
	MOVE.W	D1,9*160+10*8(A2)
	MOVE.W	D1,10*160+10*8(A2)
	MOVE.W	11*8(A2),D1
	MOVE.W	D1,1*160+11*8(A2)
	MOVE.W	D1,2*160+11*8(A2)
	MOVE.W	D1,3*160+11*8(A2)
	MOVE.W	D1,4*160+11*8(A2)
	MOVE.W	D1,5*160+11*8(A2)
	MOVE.W	D1,6*160+11*8(A2)
	MOVE.W	D1,7*160+11*8(A2)
	MOVE.W	D1,8*160+11*8(A2)
	MOVE.W	D1,9*160+11*8(A2)
	MOVE.W	D1,10*160+11*8(A2)
	MOVE.W	12*8(A2),D1
	MOVE.W	D1,1*160+12*8(A2)
	MOVE.W	D1,2*160+12*8(A2)
	MOVE.W	D1,3*160+12*8(A2)
	MOVE.W	D1,4*160+12*8(A2)
	MOVE.W	D1,5*160+12*8(A2)
	MOVE.W	D1,6*160+12*8(A2)
	MOVE.W	D1,7*160+12*8(A2)
	MOVE.W	D1,8*160+12*8(A2)
	MOVE.W	D1,9*160+12*8(A2)
	MOVE.W	D1,10*160+12*8(A2)
	MOVE.W	13*8(A2),D1
	MOVE.W	D1,1*160+13*8(A2)
	MOVE.W	D1,2*160+13*8(A2)
	MOVE.W	D1,3*160+13*8(A2)
	MOVE.W	D1,4*160+13*8(A2)
	MOVE.W	D1,5*160+13*8(A2)
	MOVE.W	D1,6*160+13*8(A2)
	MOVE.W	D1,7*160+13*8(A2)
	MOVE.W	D1,8*160+13*8(A2)
	MOVE.W	D1,9*160+13*8(A2)
	MOVE.W	D1,10*160+13*8(A2)
	MOVE.W	14*8(A2),D1
	MOVE.W	D1,1*160+14*8(A2)
	MOVE.W	D1,2*160+14*8(A2)
	MOVE.W	D1,3*160+14*8(A2)
	MOVE.W	D1,4*160+14*8(A2)
	MOVE.W	D1,5*160+14*8(A2)
	MOVE.W	D1,6*160+14*8(A2)
	MOVE.W	D1,7*160+14*8(A2)
	MOVE.W	D1,8*160+14*8(A2)
	MOVE.W	D1,9*160+14*8(A2)
	MOVE.W	D1,10*160+14*8(A2)
	MOVE.W	15*8(A2),D1
	MOVE.W	D1,1*160+15*8(A2)
	MOVE.W	D1,2*160+15*8(A2)
	MOVE.W	D1,3*160+15*8(A2)
	MOVE.W	D1,4*160+15*8(A2)
	MOVE.W	D1,5*160+15*8(A2)
	MOVE.W	D1,6*160+15*8(A2)
	MOVE.W	D1,7*160+15*8(A2)
	MOVE.W	D1,8*160+15*8(A2)
	MOVE.W	D1,9*160+15*8(A2)
	MOVE.W	D1,10*160+15*8(A2)
	MOVE.W	16*8(A2),D1
	MOVE.W	D1,1*160+16*8(A2)
	MOVE.W	D1,2*160+16*8(A2)
	MOVE.W	D1,3*160+16*8(A2)
	MOVE.W	D1,4*160+16*8(A2)
	MOVE.W	D1,5*160+16*8(A2)
	MOVE.W	D1,6*160+16*8(A2)
	MOVE.W	D1,7*160+16*8(A2)
	MOVE.W	D1,8*160+16*8(A2)
	MOVE.W	D1,9*160+16*8(A2)
	MOVE.W	D1,10*160+16*8(A2)
	MOVE.W	17*8(A2),D1
	MOVE.W	D1,1*160+17*8(A2)
	MOVE.W	D1,2*160+17*8(A2)
	MOVE.W	D1,3*160+17*8(A2)
	MOVE.W	D1,4*160+17*8(A2)
	MOVE.W	D1,5*160+17*8(A2)
	MOVE.W	D1,6*160+17*8(A2)
	MOVE.W	D1,7*160+17*8(A2)
	MOVE.W	D1,8*160+17*8(A2)
	MOVE.W	D1,9*160+17*8(A2)
	MOVE.W	D1,10*160+17*8(A2)
	MOVE.W	18*8(A2),D1
	MOVE.W	D1,1*160+18*8(A2)
	MOVE.W	D1,2*160+18*8(A2)
	MOVE.W	D1,3*160+18*8(A2)
	MOVE.W	D1,4*160+18*8(A2)
	MOVE.W	D1,5*160+18*8(A2)
	MOVE.W	D1,6*160+18*8(A2)
	MOVE.W	D1,7*160+18*8(A2)
	MOVE.W	D1,8*160+18*8(A2)
	MOVE.W	D1,9*160+18*8(A2)
	MOVE.W	D1,10*160+18*8(A2)
	MOVE.B	19*8(A2),D1
	MOVE.B	D1,1*160+19*8(A2)
	MOVE.B	D1,2*160+19*8(A2)
	MOVE.B	D1,3*160+19*8(A2)
	MOVE.B	D1,4*160+19*8(A2)
	MOVE.B	D1,5*160+19*8(A2)
	MOVE.B	D1,6*160+19*8(A2)
	MOVE.B	D1,7*160+19*8(A2)
	MOVE.B	D1,8*160+19*8(A2)
	MOVE.B	D1,9*160+19*8(A2)
	MOVE.B	D1,10*160+19*8(A2)
	LEA	11*160(A2),A2
	DBRA	D0,B4
	BRA	VSYNC

*******************	INTERRUPTIONS

SET_TIMB

	MOVE.W	SR,-(A7)
	MOVE.W	#$2700,SR
	
*	DETOURNE VECTEUR 

	LEA	$FFFA01,A0
	
	BCLR.B	#0,IMRA(A0)
	BCLR.B	#0,ISRA(A0)
	BCLR.B	#0,IPRA(A0)
	BCLR.B	#0,IERA(A0)
	MOVE.L	#FD_HBL,$120.W
	
	CLR.B	TBCR(A0)

*	MOVE.B	#1,TBDR(A0)
*	MOVE.B	#8,TBCR(A0)

	BSET	#0,IERA(A0)
	BSET	#0,IMRA(A0)

*	ANNULE TIMER C & D

	BCLR	#5,IERB(A0)
	BCLR	#4,IERB(A0)

	RTE

*
*	STOP TIMER B
*

BRK_TIMB
	MOVE.W	SR,-(A7)
	MOVE.W	#$2700,SR

*	ANNULE TIMER B

	LEA	$FFFA01,A0
	BCLR	#0,IMRA(A0)
	BCLR	#0,ISRA(A0)
	BCLR	#0,IPRA(A0)
	BCLR	#0,IERA(A0)

*	AUTORISE TIMER C ET D

	BSET	#5,IERB(A0)
	BSET	#4,IERA(A0)
	RTE

***********	

TIMERA:	MOVE	#$2100,SR
	STOP	#$2100
	
	MOVE	#$2700,SR		INTERDIT PERTURBATIONS IT
	REPT	65
	NOP
	ENDR
	MOVE.B	#0,$FFFF820A.W
	REPT	35
	NOP
	ENDR
	MOVE.B	#2,$FFFF820A.W
	
	lea	$fffffa01.w,a4
	clr.b	24(a4)		controle de A a 0=fin
	bclr.b	#5,14(a4)		fin routine A
	bclr.b	#5,6(a4)		fin timer A	

******	CALCULE L'ADRESSE BASE DU FOND....

	MOVE.L	FOND_PT,A6	POINTEUR BASE D'ECRAN-DECALAGE
	MOVE.L	RBND_PT,A5
	SUBQ.B	#1,ANIM_CMP
	BEQ.S	OK_ANIM
	LEA	-2(A6),A6
	BRA.S	SUITE_ANIM
OK_ANIM:	LEA	FD_ANIM+16,A6
	MOVE.B	#8,ANIM_CMP
SUITE_ANIM:
	MOVE.L	A6,FOND_PT	HOP SAUVEGARDE ADRESSE

	CMP.L	#FIN_RBND-2,A5	FIN MOUVEMENT REBOND?
	BLT.S	OK_RBND		NON
	MOVE.L	#FD_RBND,A5	SINON REPOINTE DEBUT
	BRA.S	SUITE_RBND
OK_RBND:	ADD.L	#2,A5
SUITE_RBND:
	MOVE.L	A5,RBND_PT	HOP STOCKE LE DECALAGE
	ADD.W	(A5),A6		HOP ADRESSE FINALE

	MOVE.L	OND_PT,A5		POINTEUR BUFFER ONDULATION
	SUBQ.B	#1,OND_CMP	FIN DE L'ONDUL?
	BEQ.S	OK_OND		OUI
	LEA	2(A5),A5		SINON ON AVANCE DANS L'ONDUL
	BRA.S	SUITE_OND
OK_OND:	LEA	FOND_OND,A5
	MOVE.B	#189,OND_CMP
SUITE_OND:
	MOVE.L	A5,OND_PT		STOCKE POINTEUR BUFFER ONDUL
		
*	ON VA GARDER A5 INTACT POUR LES HBL...

	MOVE.W	(A5)+,D3		D3=DECALAGE ONDULATION
	MOVEM.L	(A6,D3.W),D4-D7	ON CHARGE CES REGISTRE A L'AVANCE 

*	...DANS LE REGISTRE A6 QUI VA RESTER INTACT JUSQU'A LA HBL!

	MOVE	#$2300,SR		REAUTORISE ITS SAUF HBL

	RTE			FIN ROUTINE A

*
*	ROUTINE HBL DE CHAGEMENT PALETTE POUR FOND....
*



FD_HBL:	MOVEM.L	D4-D7,$FFFF8250.W	HOP TEMPS DE CHARGEMENT PALETTE ECONOMISE!
	LEA	48(A6),A6
	MOVE.W	(A5)+,D3		DECALAGE ONDULATION
	MOVEM.L	(A6,D3.W),D4-D7	HOP COULEURS + DEC ONDUL
	SUBQ.B	#1,BORDER_CMP
	BEQ.S	BORDER_HBL
	BCLR	#0,$FFFFFA0F.W
	RTE

FD_HBL2:	MOVEM.L	D4-D7,$FFFF8250.W
	LEA	48(A6),A6
	MOVE.W	(A5)+,D3		DECALAGE ONDULATION
	MOVEM.L	(A6,D3.W),D4-D7	HOP COULEURS + DEC ONDUL
	BCLR	#0,$FFFFFA0F.W
	RTE

OK_PAL2:	MOVE.L	A6,FD_HBL2+2	
	BCLR	#0,$FFFFFA0F.W
	RTE

INTER_HBL:MOVE.L	#FD_HBL2,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE

*
*	ROUTINE HBL BORDER BAS
*

BORDER_HBL:	
	MOVE.L	#BORDER,$120.W
	BCLR	#0,$FFFFFA0F.W
	RTE

BORDER:	
FD_HBL3:	MOVEM.L	D4-D7,$FFFF8250.W
	LEA	48(A6),A6		A6=POINTEUR PALETTE DECOR
	MOVE.W	(A5)+,D3		DECALAGE ONDULATION
	MOVEM.L	(A6,D3.W),D4-D7	HOP COULEURS + DEC ONDUL

	LEA	$FFFF8209.W,A3
	LEA	SWITCH,A4
	MOVEQ	#0,D3

WAIT229:	MOVE.B	(A3),D3		SYNCHRO MILIEU LIGNE
	CMP.B	#8,D3
	BLT.S	WAIT229
	CMP	#32,D3
	BGT	RATE		RATê!!
	JMP	-8(A4,D3)		SYNCHRO...

SWITCH: REPT	19 		BORDER BAS
	NOP
        ENDR
	MOVE.B	#$FC,$FFFF820A.W	60HZ
        REPT 	9
	NOP
        ENDR
	MOVE.B	#$FE,$FFFF820A.W	50 HZ

	MOVEM.L	D4-D7,$47(A3)	HOP COULEURS LIGNE SUIVANTE
	LEA	48(A6),A6		A6 POUR LES HBL SUIVANTES
	MOVE.W	(A5)+,D3		DECALAGE ONDULATION
	MOVEM.L	(A6,D3.W),D4-D7	HOP COULEURS + DEC ONDUL
          BCLR	#0,$FFFFFA0F.W
	MOVE.L	#INTER_HBL,$120.W	HBL D'ATTENTE RESYNCHRO ECRAN
	
	RTE

RATE:	CLR.B	$FFFFFA1B.W		ARRETE TIMER B
	BCLR	#0,$FFFFFA0F.W

	RTE


*
* VBL PROGRAM
*
*	RESTORE POINTEUR PALETTE

VBL:	MOVE.L	A4,-(SP)

	LEA	$FFFFFA01.W,A4	TIMER A...
	MOVE.B	#50,30(A4)	REG DONNEE:  50
	MOVE.B	#6,24(A4)		DELAY MODE A, FREQUENCE/100
	BSET.B	#5,6(A4)		TIM A EN ROUTE
	BSET.B	#5,18(A4)		OK
	MOVE.L	#TIMERA,$134.W	ADRESSE ROUTINE...

	MOVE.B	#227,BORDER_CMP	

	MOVE.B	#1,TBDR(A4)
	MOVE.L	#FD_HBL,$120.W
	MOVE.B	#8,TBCR(A4)	LANCE LE TIM B

	CLR.B	VS

VBL_ZIK:	JSR	MUSIC+8		ZIK!
	
	LEA	$FFFF8250.W,A4
	MOVEQ	#0,D7
	MOVE.L	D7,(A4)+
	MOVE.L	D7,(A4)+
	MOVE.L	D7,(A4)+
	MOVE.L	D7,(A4)

	MOVE.L	(SP)+,A4
	
	RTE


*	DETOURNE VBL & TIMER B

PRG:	MOVE.L	#VBL,$70.W
	BSR	SET_TIMB
	RTS

*****

RETURN:	RTE

******************* PROCEDURE D'ERREUR RESERVATION MEMOIRE

ERREUR:	CLR.W	-(SP)
	TRAP	#1

*********	ROUTINE DE SAUVEGARDE DE TOUTES LES DONNEES SYSTEME....

SAVE_REFS:
	MOVEM.L	$FFFF8240.W,D0-D7
	MOVEM.L	D0-D7,OLD_PAL

	MOVE.B	$FFFF8260.W,OLD_RES

	MOVE.B	$FFFF820A.W,OLD_HZ

	MOVE.B	$FFFF8201.W,OLD_SCREEN+1
	MOVE.B	$FFFF8203.W,OLD_SCREEN+2

	MOVE.W	#$25,-(SP)	SYNCHRO VBL...
	TRAP	#14
	ADDQ.L	#2,SP

	LEA	$FFFFFA01.W,A0
	MOVE.B	IERA(A0),OLD_IERA
	MOVE.B	IERB(A0),OLD_IERB
	MOVE.B	IMRA(A0),OLD_IMRA
	MOVE.B	IMRB(A0),OLD_IMRB
	MOVE.B	TACR(A0),OLD_TACR
	MOVE.B	TBCR(A0),OLD_TBCR
	MOVE.B	TCDCR(A0),OLD_TCDCR
	MOVE.B	TADR(A0),OLD_TADR
	MOVE.B	TBDR(A0),OLD_TBDR
	MOVE.B	TCDR(A0),OLD_TCDR
	MOVE.B	TDDR(A0),OLD_TDDR
	
	MOVE.L	$70.W,OLD_VBL
	MOVE.L	$68.W,OLD_HBL
	MOVE.L	$78.W,OLD_VMFP
	MOVE.L	$134.W,OLD_TIMA
	MOVE.L	$120.W,OLD_TIMB
	MOVE.L	$114.W,OLD_TIMC
	MOVE.L	$110.W,OLD_TIMD
	MOVE.L	$118.W,OLD_KEYB

	RTS

*********	ROUTINE DE RESTAURATION DE TOUTES LES DONNEES SYSTEME....

REST_REFS:

	CLR.B	$FFFFFA07.W	ARRETE TOUT!
	CLR.B	$FFFFFA09.W

	MOVE.W	#$25,-(SP)	ATTEND VBL...
	TRAP	#1
	ADDQ.L	#2,SP

	MOVE.L	OLD_VBL,$70.W
	MOVE.L	OLD_HBL,$68.W
	MOVE.L	OLD_VMFP,$78.W
	MOVE.L	OLD_TIMA,$134.W
	MOVE.L	OLD_TIMB,$120.W
	MOVE.L	OLD_TIMC,$114.W
	MOVE.L	OLD_TIMD,$110.W
	MOVE.L	OLD_KEYB,$118.W	

	LEA	$FFFFFA01.W,A0
	MOVE.B	OLD_IMRA,IMRA(A0)
	MOVE.B	OLD_IMRB,IMRB(A0)
	MOVE.B	OLD_TACR,TACR(A0)
	MOVE.B	OLD_TBCR,TBCR(A0)
	MOVE.B	OLD_TCDCR,TCDCR(A0)
	MOVE.B	OLD_TADR,TADR(A0)
	MOVE.B	OLD_TBDR,TBDR(A0)
*	MOVE.B	OLD_TCDR,TCDR(A0)		DEREGLE CLAVIER SINON!
*	MOVE.B	OLD_TDDR,TDDR(A0)
	MOVE.B	OLD_IERA,IERA(A0)
	MOVE.B	OLD_IERB,IERB(A0)

	MOVEM.L	OLD_PAL,D0-D7
	MOVEM.L	D0-D7,$FFFF8240.W

	MOVE.B	OLD_RES,$FFFF8260.W

	MOVE.B	OLD_HZ,$FFFF820A.W

	MOVE.B	OLD_SCREEN+1,$FFFF8201.W
	MOVE.B	OLD_SCREEN+2,$FFFF8203.W

	RTS
	
*********

FIN:	BSR	REST_REFS 	RESTAURE DONNEES

	MOVE.L	#$08000000,$FFFF8800.W	ARRET SON
	MOVE.L	#$09000000,$FFFF8800.W
	MOVE.L	#$0A000000,$FFFF8800.W

	MOVE.L	OLDPILE,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP

	MOVE.W	#24,-(SP)		ANCIENNE CONFIGURATION CLAVIER
	TRAP	#14
	ADDQ.L	#2,SP

	CLR.W	-(SP)
	TRAP	#1

*******************	DATAS
	
	EVEN

PALETTE:	DC.W	0,$222,$444,$666,$777,$666,$444,$222,2,4,6,$117,$117,6,4,2
SAUVE_PAL:DS.W	16
	EVEN

MEGA_TXT:	INCBIN	\megascrl.ovr\DIM_TXT.BIN
FINTXT:	DC.B	53,53
	EVEN

DECOR:	INCBIN	\megascrl.ovr\DECOR.BIN		! MOTIF FOND MEGASCROLLER
	EVEN

BIGALPHA:	INCBIN	\megascrl.ovr\BIGALPHA.BIN	! DATAS LETTRES MEGASCROLLER
	EVEN

FD_RBND:	INCBIN	\megascrl.ovr\FONDRBND.BIN
FIN_RBND:
	EVEN

FD_ANIM:	INCBIN	\megascrl.ovr\FD_BUFF.BIN
FIN_ANIM:
	EVEN

FOND_OND:	INCBIN	\megascrl.ovr\FOND_OND.BIN
FIN_OND:
	EVEN

MUSIC:	INCBIN	\megascrl.ovr\OLDSHIT.IMG
	EVEN

OND_CMP:	DS.B	1
NB_DEC:	DS.B	1
VS:	DS.B	1
ANIM_CMP:	DS.B	1
BORDER_CMP: DS.B	1

	EVEN

OLDPILE:	DS.L	1
ECRAN1:	DS.L	1
ECRAN2:	DS.L	1
MEGA_PT:	DS.L	1
MEGA_LET:	DS.L	1
BUFFER:	DS.L	1
RBND_PT:	DS.L	1
FOND_PT:	DS.L	1
OND_PT:	DS.L	1
	
*********	DONNEES DE SAUVEGARDE DES DONNEES SYSTEME...

	EVEN

OLD_PAL:	DS.L	9

OLD_SCREEN: DS.L	1

OLD_VBL:	DS.L	1
OLD_HBL:	DS.L	1
OLD_VMFP:	DS.L	1
OLD_TIMA:	DS.L	1
OLD_TIMB:	DS.L	1
OLD_TIMC:	DS.L	1
OLD_TIMD:	DS.L	1
OLD_KEYB:	DS.L	1
OLD_USP:	DS.L	1

OLD_IERA:	DS.B	1
OLD_IERB:	DS.B	1
OLD_IMRA:	DS.B	1
OLD_IMRB:	DS.B	1
OLD_TACR:	DS.B	1
OLD_TBCR:	DS.B	1
OLD_TCDCR:DS.B	1
OLD_TADR:	DS.B	1
OLD_TBDR:	DS.B	1
OLD_TCDR:	DS.B	1
OLD_TDDR:	DS.B	1

OLD_RES:	DS.B	1
OLD_HZ:	DS.B	1

	EVEN

	SECTION	BSS

	EVEN

SCREEN1:	DS.B	277*160+512
SCREEN2:	DS.B	277*160+512

	EVEN

	END