;HI THERE! THIS IS THE WILD BOYS
;FIRST EVER DEMO! IF YOU WANT TO
;SEND ME GRAFIX OR MUZIX, OR WANT
;TO WRITE TO ME, HERE'S MY ADDRESS;
;
;   THE WILD BOYS
;   131 GUNNERSBURY AVENUE
;   ACTON W3 8LE
;   LONDON
;NOW, THE SOURCE CODE!  


    	JSR INIT		;INITIALISE
	JSR CONVERT		;CONVERT TEXT 
	JSR SETUP2		;SET UP DATA
	MOVE.W #$777,$FFFF8244.W
	JSR TWB			;DRAW SOUND CHANNEL FLASHER GRAFIX
	JSR DO_LOGO		;DRAW LOGO
	JSR SETMUS		;START MUZIX
	MOVE.L #VBL,$70.W
	JSR WAIT	
	JMP END

DO_LOGO	LEA LOGO,A0
	LEA $78002+48,A1	;POSITION OF LOGO ON SCREEN
	MOVEQ.W #80-1,D0	;HEIGHT OF LOGO -1
.L2	MOVEQ.W #8-1,D1		;WIDTH IN 16 PIXEL STEPS -1
.L1	MOVE.L (A0)+,(A1)+	
	MOVE.W (A0)+,(A1)+
	ADDQ.W #2,A1
	DBF D1,.L1
	LEA 160-64(A1),A1	;NEXT LINE
	DBF D0,.L2
	RTS

VOICE	LEA $FFFF8800.W,A0
	MOVEQ.W #0,D0
	MOVEQ.W #0,D1
	MOVEQ.W #0,D2
	MOVE.B #8,(A0)		;GET CHANNEL A VOLUME
	MOVE.B (A0),D0
	MOVE.B #9,(A0)		;GET CHANNEL B VOLUME
	MOVE.B (A0),D1
	MOVE.B #10,(A0)		;GET CHANNEL C VOLUME
	MOVE.B (A0),D2
	BTST #4,D0		;IS IT AN ENVELOPE?
	BEQ .L1
	MOVE.B #10,D0		;IF IT IS,VOLUME=10
.L1	ADD.B D0,D0		;MULTIPLY BY 2 FOR WORD ALIGNMENT
	BTST #4,D1
	BEQ .L2
	MOVE.B #10,D1
.L2	ADD.B D1,D1
	BTST #4,D2
	BEQ .L3
	MOVE.B #10,D2
.L3	ADD.B D2,D2
	LEA COLS1,A0
	LEA COLS2,A1
	LEA COLS3,A2
	LEA (A0,D0.W),A0
	LEA (A1,D1.W),A1
	LEA (A2,D2.W),A2
	MOVE.L A0,VOICE1	;SET UP NEW COLOURS FOR EACH CHANNEL	
	MOVE.L A1,VOICE2
	MOVE.L A2,VOICE3
	RTS

TWB	LEA $78006+(160*162)+(6*8),A0	;DESTINATION ADDRESSES
	LEA $78002+(160*162)+(6*8)+24,A1
	LEA $78004+(160*162)+(6*8)+48,A2
	LEA FONT+(144*('T'-65)),A3	;SOURCE ADDRESSES
	LEA FONT+(144*('W'-65)),A4
	LEA FONT+(144*('B'-65)),A5
	MOVEQ.W #4-1,D0
	MOVEQ.L #0,D7
.L1	MOVEQ.W #36-1,D1
	MOVEM.L A0-A2,-(A7)
.L2	MOVE.B (A3)+,(A0)
	MOVE.B (A4)+,(A1)
	MOVE.B (A5)+,(A2)
	LEA 160(A0),A0
	LEA 160(A1),A1
	LEA 160(A2),A2
	DBF D1,.L2
	MOVEM.L (A7)+,A0-A2
	CMPI.L #1,D7		;IF D7=1,THEN ADD 1 TO DESTINATION ADDRESS
				 ;TO INCREASE BY 8 PIXELS(1 BYTE)
	BEQ .L3
	ADDQ.L #1,A0
	ADDQ.L #1,A1
	ADDQ.L #1,A2
	MOVE.L #1,D7
	DBF D0,.L1
	RTS	
.L3	ADDQ.L #7,A0		;IF D7 IS NOT 1,ADD 7 TO DESTINATION ADDRESS
				;TO BYPASS BITPLANES 2,3 AND 4
	ADDQ.L #7,A1
	ADDQ.L #7,A2
	MOVE.L #0,D7
	DBF D0,.L1
	RTS	
	
TB1	CLR.B $FFFFFA1B.W	;STOP TIMER B
	MOVE.L #TB,$120.W	;SET UP NEW INTERRUPT VECTOR
	MOVE.B #4,$FFFFFA21.W	;EVERY 4 SCANLINES, DO TIMER B INTERRUPT
	MOVE.B #8,$FFFFFA1B.W	;START TIMER B
	BCLR #0,$FFFFFA0F.W	;O.K CPU I'VE DONE THE TIMER B INTERRUPT
	RTE

PAL1	DC.W 0,0,0,0,0,0,$707,$706,$705,$704,$703,$702,$701,$700
	DC.W $710,$720,$730,$740,$750,$760,$770
	DC.W $670,$570,$470,$370,$270,$170,$070

PALST	DC.L PAL1+2

LOGOCOL	DC.L LCOL1
	DC.L LCOL2
	DC.L LCOL3
TIMER	DC.B 3,0
	DC.B 4,0
	DC.B 5,0
LCOL1	DC.W $505,$303,$404	;COLOURS FOR LOGO
	DC.W $606,$404,$505
	DC.W $707,$505,$606
	DC.W $717,$606,$707
	DC.W $727,$707,$717
	DC.W -1
LCOL2	DC.W $066,$033
	DC.W $077,$044
	DC.W $177,$055
	DC.W $277,$066
	DC.W $377,$077
	DC.W $477,$177
	DC.W $577,$277
	DC.W -1
LCOL3	DC.W $003,$001
	DC.W $004,$002
	DC.W $005,$003
	DC.W $006,$004
	DC.W $007,$005
	DC.W $117,$006
	DC.W $117,$007
	DC.W $227,$117
	DC.W -1

SHADE	SUBQ.B #1,TIMER		;ROUT. TO FLASH LOGO 
	BNE .L2
	MOVE.B #2,TIMER
	ADDQ.L #6,LOGOCOL
	MOVEA.L LOGOCOL,A0
	CMPI.W #-1,(A0)
	BNE .L2
	MOVE.L #LCOL1,LOGOCOL
.L2	SUBQ.B #1,TIMER+2
	BNE .L3
	ADDQ.L #4,LOGOCOL+4
	MOVE.B #3,TIMER+2
	MOVEA.L LOGOCOL+4,A0
	CMPI.W #-1,(A0)
	BNE .L3
	MOVE.L #LCOL2,LOGOCOL+4
.L3	SUBQ.B #1,TIMER+4
	BNE .L4
	ADDQ.L #4,LOGOCOL+8
	MOVE.B #5,TIMER+4
	MOVEA.L LOGOCOL+8,A0
	CMPI.W #-1,(A0)
	BNE .L4
	MOVE.L #LCOL3,LOGOCOL+8
.L4	RTS

VBL	CLR.B $FFFFFA1B.W
	MOVE.L #TB1,$120.W	;FIRST TIMER B VECTOR
	MOVE.B #30,$FFFFFA21.W
	MOVE.B #8,$FFFFFFA1B.W
	LEA $FFFF8240.W,A0
	LEA LOGOCOL,A2
	MOVEA.L (A2)+,A1
	CLR.W (A0)+
	MOVE.W PAL1,(A0)+
	MOVE.L #PAL1+2,PALST
	MOVE.W (A1)+,(A0)+
	MOVE.W (A1)+,2(A0)
	MOVE.W (A1)+,6(A0)
	MOVEA.L (A2)+,A1
	MOVE.W (A1)+,10(A0)
	MOVE.W (A1)+,14(A0)
	MOVEA.L (A2)+,A1
	MOVE.W (A1)+,18(A0)
	MOVE.W (A1)+,22(A0)	
	MOVE.B #33,COUNT
	MOVE.L #BARS,BARST
	JSR SCROLL
	MOVEA.L $4DA.W,A0
	JSR (A0)
	JSR VOICE
	JSR SHADE
	RTE

BARS	DC.W $400,$500,$600,$700,$600,$500,$400
	DC.W 0,0,0,0
	DC.W $040,$050,$060,$070,$060,$050,$040
	DC.W 0,0,0,0
	DC.W $004,$005,$006,$007,$006,$005,$004
	DC.W 0,0,0,0,0,0,0,0,0,0,0,0,0
BARST	DC.L BARS

SCROLL	MOVEA.L TPOS,A0	    ;THE GOOD BIT,THE BYTE BENDER ROUT!
	LEA YPOS,A1
	MOVEA.L A1,A6
	MOVE.W #40-1,D1
.L2	MOVEA.L (A1)+,A2    ;A2=DESTINATION
	MOVEA.L (A0)+,A3    ;A3=SOURCE
	REPT 36
	MOVE.B (A3)+,(A2)
	LEA 160(A2),A2
	ENDR
	DBF D1,.L2	
	ADDQ.L #4,TPOS	    ;INCREASE POSITION IN TEXT.
			    ;1 LONGWORD=8 PIXELS OF TEXT
	SUBQ.W #1,LENGTH       ;END OF TEXT?    
	BNE .L3
	MOVE.W #8000-40,LENGTH ;8000=2000 CHARACTERS DIVIDED BY 4
	MOVE.L #TEXTAB,TPOS    ;SET POINTER TO START OF TEXT
.L3	RTS		       ;PISS OFF

SETUP2	MOVE.L #2000-1,D0      ;2000 CHARACTER OF TEXT(TOOK ME AGES!)
	LEA TEXTAB,A0
	LEA FONT,A1
	LEA TEXT1,A2
.L1	MOVEQ.W #0,D1
	MOVE.B (A2)+,D1
	SUBI.B #'A',D1
	MULU #(4*36),D1 	;4*36=LENGTH OF 1 CHARACTER IN FONT(BYTES)
	LEA (A1,D1.W),A3
	MOVE.L A3,(A0)+		;FIRST STRIP OF 8 PIXELS
	LEA 36(A3),A3		;1 STRIP = 36 BYTES(EACH CHARACTER IN FONT IS
			 	;36 PIXELS IN HEIGHT, AND 1 BIT PLANE) 
	MOVE.L A3,(A0)+		;SECOND STRIP
	LEA 36(A3),A3
	MOVE.L A3,(A0)+		;THIRD STRIP
	LEA 36(A3),A3
	MOVE.L A3,(A0)+		;FOURTH STRIP 
	DBF D0,.L1	

	MOVEQ.B #0,D7		;SET UP SINE WAVE SCREEN ADDRESSES	
	MOVEQ.L #0,D1
	MOVEQ.W #40-1,D0
	LEA SINETAB,A0		;SINE TABLE,DATA STORED AS Y COORDINATES(0-199)
	LEA YPOS,A1
	LEA $78000+(12*160),A6  ;OFFSET FOR ADDRESSES
.L4	MOVEQ.W #0,D6
	MOVE.B (A0)+,D6
	MULU #160,D6		;160=1 SCANLINE
	LEA (A6,D6),A3		;ADD OFFSET TO ADDRESS
	LEA (A3,D1),A3		;ADD 1 OR 7 TO ADDRESS.1=SECOND BYTE OF THE
				;WORD,7=1ST BYTE OF NEXT WORD.
	MOVE.L A3,(A1)+
	CMPI.B #0,D7
	BNE .L5			
	ADDQ.L #1,D1
	MOVE.B #1,D7
	DBF D0,.L4
	RTS
.L5	ADDQ.L #7,D1
	MOVE.B #0,D7
	DBF D0,.L4
	RTS

INIT	
;INTO SUPERVISOR
	BSR SUPER
;MOUSE OFF
	DC.W $A000
	DC.W $A00A
	MOVE.B #$12,$FFFFFC02.W
;TIMER C OFF
	MOVE.W #5,-(A7)
	MOVE.W #26,-(A7)
	TRAP #14
	ADDQ.L #4,A7
;NEW STACK
	MOVEA.L (A7),A0
	MOVEA.L #MY,A7
	MOVE.L A0,(A7)
;CLEAR COLOURS
	LEA $FFFF8240,A0
	MOVEQ.W #15,D0
.L1	CLR.W (A0)+
	DBF D0,.L1	
;SET SCREENS
	CLR.W -(A7)
	PEA $78000	;PHYSICAL
	PEA $78000	;LOGICAL
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
;CLEAR SCREEN
	LEA $78000,A0
	MOVE.W #32000/4-1,D0
.L2	CLR.L (A0)+
	DBF D0,.L2
;SAVE SYSTEM VARIABLES
	LEA VARS,A0
	MOVE.B $FFFFFA07.W,(A0)+
	MOVE.B $FFFFFA13.W,(A0)+
	MOVE.B $FFFFFA21.W,(A0)+
	MOVE.B $FFFFFA1B.W,(A0)+
	MOVE.L $70.W,(A0)+	
	MOVE.L $120.W,(A0)+
	MOVE.L $118.W,(A0)+
;ENABLE TIMER B
	ORI.B #1,$FFFFFA07.W
	ORI.B #1,$FFFFFA13.W
;NEW VECTORS
	MOVE.L #KEY,$118.W
;IPL=3 AND EXIT
	MOVE.W #$2300,SR
	RTS	

VARS	DS.B 16
	DS.L 40
MY	DS.L 1	

SUPER	CLR.L -(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	MOVE.L D0,STACK
	RTS

USER	MOVE.L STACK,-(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	CLR.W -(A7)
	MOVE.W #$4C,-(A7)
	TRAP #1
STACK	DS.L 1

WAIT	CMPI.B #57,$FFFFFC02.W
	BNE WAIT
.L1	CMPI.B #57,$FFFFFC02.W
	BEQ .L1
	RTS

KEY	BCLR #6,$FFFFFA11.W
	RTE

TB	MOVE.L A0,-(A7)
	MOVEA.L PALST,A0
	MOVE.W (A0),$FFFF8242.W
	MOVEA.L BARST,A0
	MOVE.W (A0),$FFFF8240.W
	ADDQ.L #2,BARST
	ADDQ.L #2,PALST
	SUBQ.B #1,COUNT
	BEQ .L1
	MOVE.L (A7)+,A0
	BCLR #0,$FFFFFA0F.W
	RTE
.L1	MOVEA.L VOICE1,A0
	MOVE.W (A0),$FFFF8250.W
	MOVEA.L VOICE2,A0
	MOVE.W (A0),$FFFF8244.W
	MOVEA.L VOICE3,A0
	MOVE.W (A0),$FFFF8248.W
	MOVE.B #-1,COUNT
	MOVEA.L (A7)+,A0
	BCLR #0,$FFFFFA0F.W
	RTE

END	MOVE.W #$2700,SR
;STOP MUZIX
	JSR ENDMUS
;RESET SYSTEM VARIABLES
	LEA VARS,A0
	MOVE.B (A0)+,$FFFFFA07.W
	MOVE.B (A0)+,$FFFFFA13.W
	MOVE.B (A0)+,$FFFFFA21.W
	MOVE.B (A0)+,$FFFFFA1B.W
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$120.W
	MOVE.L (A0)+,$118.W
;RESET COLOURS
	MOVE.W #$777,$FFFF8240.W
	MOVE.W #0,$FFFF8242.W
	MOVE.W #0,$FFFF825E.W
	MOVE.W #$2300,SR
;MOUSE ON
	DC.W $A000	
	DC.W $A009
	MOVE.B #$08,$FFFFFC02.W
;ENABLE TIMER C
	MOVE.W #5,-(A7)
	MOVE.W #27,-(A7)
	TRAP #14
	ADDQ.L #4,A7
;SET SCREENS
	CLR.W -(A7)
	PEA $78000	;PHYSICAL
	PEA $78000	;LOGICAL
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
;EXIT
	BRA USER	;YOU MUST CHANGE THIS BIT TO
			;MOVEA.L 4,A0
			;JMP (A0)
		        ;IF YOU ARE CREATING AN EXECUTABLE FILE, SO THAT
			;WHEN YOU HIT SPACE, THE MACHINE IS RESET. 	
			;YOU CAN KEEP THIS BIT IF YOU ARE RUNNING THE
		        ;CODE FROM AN ASSEMBLER, SO THAT IT RETURNS OK.

CONVERT	
;ROUT TO CONVERT CHARACTERS
;SO THAT THE PROGRAM CAN LOCATE THE
;CHARACTERS IN CTAB
	LEA TEXT1(PC),A0
	LEA CTAB(PC),A1
	MOVE.W #0,LENGTH
.A	MOVE.B (A0)+,D0
	MOVEA.L A1,A2
	MOVEQ.W #19-1,D1
.B	CMP.B (A2)+,D0
	BEQ .C
	ADDQ.L #1,A2
	DBF D1,.B	
	BRA .D
.C	MOVE.B (A2),-1(A0)
.D	ADDQ.W #4,LENGTH
	CMPI.B #-1,(A0)
	BNE .A
	SUBI.W #40,LENGTH
	RTS	
LENGTH	DC.W 0
CTAB	DC.B '?','['
	DC.B '!','\'
	DC.B '.',']'
	DC.B ',','^'
	DC.B '-','_'
	DC.B ' ',96
	DC.B '(',97
	DC.B ')',98
	DC.B 39,99
	DC.B '0',100,'1',101,'2',102,'3',103,'4',104
	DC.B '5',105,'6',106,'7',107,'8',108,'9',109
;2000 CHARS
TEXT1	DC.B ".........HI THERE! IT'S POWER MAN ON THE"
	DC.B " KEYBOARD. I AM ONE OF THE TWO MEMBERS O"
	DC.B "F A NEW DEMO CREW CALLED THE WILD BOYS. "
	DC.B "THE OTHER MEMBER IS JJ, WHO DOESN'T DO A"
	DC.B "NYTHING EXCEPT GIVE ME IDEAS AND CONSTRU"
	DC.B "CTIVE CRITICISM LIKE - THAT DEMO IS CRAP"
	DC.B "! ETC....HOWEVER, HE IS ONLY 13 AND MAY "
	DC.B "SOMEDAY BECOME A CODER WITH MY HELP....."
	DC.B "..I CODED THIS DEMO ON 23-10-90, THE SAM"
	DC.B "E DAY I GOT THE LOST BOYS CEBIT '90 DISK"
	DC.B ", WHICH ASKED PEOPLE TO SEND THEM INTROS"
	DC.B ". NOW, LET ME TALK ABOUT THIS DEMO. IT I"
	DC.B "S NOW EIGHT O'CLOCK AT NIGHT AND I HAVE "
 	DC.B "JUST FINISHED THE BYTE-BENDER ROUT. I AM"
	DC.B " TRYING TO DECIDE WHAT ELSE TO INCLUDE.."
	DC.B "I HAVE TO GO NOW BUT I'LL BE BACK SOON.."
	DC.B "..IT IS 1 O'CLOCK IN THE MOURNING AND I "
	DC.B "HAVE ALMOST FINISHED THE DEMO. I STILL H"
	DC.B "AVE TO DESIGN A LOGO AND INCLUDE SOME MU"
	DC.B "ZIX AND ADD A SOUND CHANNEL FLASHER ROUT"
        DC.B "INE.....WELL, I'M TIRED SO I'M GOING TO "
	DC.B "BED NOW, BUT I'LL CARRY ON WITH THIS TEX"
	DC.B "T IN THE MOURNING.....YAAAAWN....IT'S MO"
	DC.B "URNING TIME! I HAVE BEEN UP FOR A FEW HO"
	DC.B "URS NOW, AND ALMOST COMPLETED THE DEMO! "
	DC.B "ALL THATS LEFT FOR ME TO DO IS THE LOGO."
	DC.B " SO, I'LL DO THAT NOW, SEE YOU IN A BIT!"
 	DC.B "..........RIGHT, AT LONG LAST, I'VE FINI"
	DC.B "SHED THIS DEMO! NOW ALL I HAVE TO DO IS "
	DC.B "TO WRITE SOME MORE SODDING TEXT, WHICH I"
	DC.B "S GOING TO TAKE ME AGES! WELL, I HAVE TO"
	DC.B " START SOME TIME. I'LL START BY TALKING "
	DC.B "ABOUT ME. I AM POWER MAN, I'M 18 AND I A"
	DC.B "M THE CODER, GRAFIX MAN AND MUZIX RIPPER"
	DC.B ". IN FACT, I DO EVERYTHING! HOWEVER, JJ "
	DC.B "DOES GIVE ME DEMOS AND GAMES TO RIP THE "
	DC.B "MUZIX, AND ALSO GIVES ME PACKERS. IF THE"
	DC.B " LOST BOYS INCLUDE MY SOURCE CODE LISTIN"
	DC.B "G ON THIS DISK, THEN YOU WILL FIND MY AD"
	DC.B "DRESS ON IT. PLEASE SEND ME GRAFIX AND M"
	DC.B "UZIX TO USE ON MY DEMOS AND INTROS, BECA"
	DC.B "USE I NEED PEOPLE TO JOIN THE WILD BOYS."
	DC.B " NOW, THE GREETINGS! HI TO THE CAREBEARS"
	DC.B "(LIKED YOUR CUDDLY DEMO, CAN'T WAIT FOR "
 	DC.B "YOUR NEW ONE! GOOD DIGI-SYNTH ROUT TOO!)"
	DC.B ", THE LOST BOYS(I'VE GOT YOUR MINDBOMB D"
	DC.B "EMO, BUT ONLY THE MAIN MENU AND THE BBC "
	DC.B "SCREEN WORK! GOOD MAIN MENU THOUGH...), "
	DC.B "THE REPLICANTS, SYNC, OMEGA,2 LIFE CREW "
	DC.B "ETC.I'M GOING NOW,SO CHILL OUT.........."
	DC.B -1
	EVEN

FONT	INCBIN BYTEFONT.DAT			;FONT DATA
SINETAB	DC.B $2A,$2D,$30,$33,$37,$3A,$3D,$40    ;SINE TABLE DATA
	DC.B $43,$45,$48,$4A,$4C,$4E,$4F,$51
	DC.B $52,$53,$53,$53,$53,$53,$53,$52
	DC.B $51,$50,$4E,$4C,$4A,$48,$46,$43
	DC.B $41,$3E,$3B,$38,$34,$31,$2E,$2A
LOGO	INCBIN LOGO1.DAT			;LOGO DATA 
TEXTAB	DS.L 8000				;ABSOLUTE ADDRESSES FOR TEXT STRIPS
YPOS	DS.L 40					;SCREEN ADDRESSES TO PUT TEXT
TPOS	DC.L TEXTAB				;POSITION IN TEXT
COUNT	DC.B 0,0
SETMUS	MOVE.L #MUSIC+58,A0
	MOVE.L #(MUSIC+($3FE8E-$3FD40))+28,(MUSIC+68)
	MOVE.L #(MUSIC+($3FD7C-$3FD40))+28,(MUSIC+(14+68))
	JSR (A0)
	RTS
ENDMUS	CLR.L $4DA.W
	MOVE.L #$700FF00,$FFFF8800.W
	MOVE.L #$8000000,$FFFF8800.W
	MOVE.L #$9000000,$FFFF8800.W
	MOVE.L #$A000000,$FFFF8800.W
	RTS
MUSIC	INCBIN MUZAK.PRG
VOICE1	DC.L COLS1
VOICE2	DC.L COLS2
VOICE3	DC.L COLS3
COLS1	DC.W $100,$100,$100,$200,$200,$200,$300
	DC.W $300,$400,$500,$600,$700,$710,$730
COLS2	DC.W $010,$010,$010,$020,$020,$020,$030
	DC.W $030,$040,$050,$060,$070,$071,$073
COLS3	DC.W $001,$001,$001,$002,$002,$002,$003
	DC.W $003,$004,$005,$006,$007,$017,$037
