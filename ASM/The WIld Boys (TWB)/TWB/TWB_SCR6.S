;THIS IS THE SOURCE CODE FOR THE
;BIG SCROLL DEMO. SORRY, NOT MUCH
;DOCUMENTATION BUT I COULDN'T BE
;BOTHERED......
;
;DATA FILES USED:
;16X16.DAT	- FONT DATA
;MUZIX13.DAT	- MUZIX DATA
 	

        JSR INIT
	MOVE.W #$777,$FFFF8242.W
	JSR CONVERT
	JSR SETUP1
	JSR SETUP2
	JSR SETMUS

	MOVE.L #VBL,$70.W
	CLR.B $FFFF8203.W
START	CMPI.B #57,$FFFFFC02.W
	BEQ EXIT
SWAP	JSR SWAP70
Z	BRA Z
EXIT	JMP END

TB1	MOVE.W PAL1,$FFFF8242.W	
	MOVE.W PAL2,$FFFF8240.W	
	CLR.B $FFFFFA1B.W
	MOVE.B #3,$FFFFFA21.W
	MOVE.L #TB2,$120.W
	MOVE.L #PAL1+2,PAL
	MOVE.L #PAL2+2,PALB
	MOVE.B #8,$FFFFFA1B.W 
	BCLR #0,$FFFFFA0F.W
	RTE

TB2	MOVEM.L A0/A1,-(A7)
	MOVEA.L PAL,A0
	MOVEA.L PALB,A1
	MOVE.W (A0)+,$FFFF8242.W
	MOVE.W (A1)+,$FFFF8240.W
	CMPA.L #PALEND,A0
	BGE .L1
	MOVE.L A0,PAL
	MOVE.L A1,PALB
	MOVEM.L (A7)+,A0/A1
	BCLR #0,$FFFFFA0F.W
	RTE
.L1	CLR.B $FFFFFA1B.W	
	MOVEM.L (A7)+,A0/A1
	BCLR #0,$FFFFFA0F.W
	RTE

PAL	DC.L PAL1
PALB	DC.L PAL2

	;64
PAL1	DC.W $700,$710,$720,$730,$740,$750,$760,$770
	DC.W $670,$570,$470,$370,$270,$170,$070,$071
	DC.W $072,$073,$074,$075,$076,$077,$067,$057
	DC.W $047,$037,$027,$017,$007,$107,$207,$307
	DC.W $407,$507,$607,$707,$706,$705,$704,$703
	DC.W $702,$701,$700,$711,$722,$733,$744,$755
	DC.W $766,$777,$776,$775,$774,$773,$772,$771
	DC.W $770,$670,$570,$470,$370,$270,$170,$070
PALEND

PAL2	DC.W 0,0,0,0
	DC.W 0,0,0,0,0,0,0,0
V1	DC.W 0,0,0,0,0,0,0,0
	DC.W 0,0,0,0,0,0,0,0
V2	DC.W 0,0,0,0,0,0,0,0
	DC.W 0,0,0,0,0,0,0,0
V3	DC.W 0,0,0,0,0,0,0,0
	DC.W 0,0,0,0,0,0,0,0
	DC.W 0,0,0,0

VOLP	DC.W 0,0,0,0,0,0,0,0
	DC.W 0,0,0,0,0,0,0,0
	DC.W 0,0,0,$111,$111,0,0,0
	DC.W 0,0,0,$111,$111,0,0,0
	DC.W 0,0,$111,$222,$222,$111,0,0
	DC.W 0,0,$111,$222,$222,$111,0,0
	DC.W 0,$111,$222,$333,$333,$222,$111,0
	DC.W 0,$111,$222,$333,$333,$222,$111,0
	DC.W $111,$222,$333,$444,$444,$333,$222,$111
	DC.W $111,$222,$333,$444,$444,$333,$222,$111
	DC.W $222,$333,$444,$555,$555,$444,$333,$222
	DC.W $222,$333,$444,$555,$555,$444,$333,$222
	DC.W $333,$444,$555,$666,$666,$555,$444,$333
	DC.W $333,$444,$555,$666,$666,$555,$444,$333
	DC.W $444,$555,$666,$777,$777,$666,$555,$444
	DC.W $444,$555,$666,$777,$777,$666,$555,$444

DO_VOL	MOVEQ.W #0,D0
	MOVEQ.W #0,D1
	MOVEQ.W #0,D2
	LEA $FFFF8800.W,A0
	MOVE.B #8,(A0)
	MOVE.B (A0),D0
	MOVE.B #9,(A0)
	MOVE.B (A0),D1
	MOVE.B #10,(A0)
	MOVE.B (A0),D2	
	BTST #4,D0
	BEQ .L1
	MOVE.B #10,D0
.L1	BTST #4,D1
	BEQ .L2
	MOVE.B #10,D1
.L2	BTST #4,D2
	BEQ .L3
	MOVE.B #10,D2
.L3	ASL.W #4,D0
	ASL.W #4,D1
	ASL.W #4,D2
	LEA VOLP,A0
	LEA (A0,D0.W),A1
	LEA (A0,D1.W),A2
	LEA (A0,D2.W),A3
	LEA V1,A4
	REPT 8
	MOVE.W (A1)+,(A4)+
	ENDR
	LEA V2,A4
	REPT 8
	MOVE.W (A2)+,(A4)+
	ENDR
	LEA V3,A4
	REPT 8
	MOVE.W (A3)+,(A4)+
	ENDR
	RTS
		
TEMP	DS.L 1

BIG	MOVEA.L TPOS1,A1
	MOVEA.L (A1)+,A2
	MOVEA.L (A1)+,A3
	MOVE.L (A1)+,TEMP
	MOVE.W L,D4
	MOVEQ.W #15,D6
	SUB.W D4,D6    ;D6=16-L
	MOVEQ.W #18,D7
	SUB.W D6,D7    ;D7=20-(16-L)				
	CMPI.W #16,D7
	BLT .L1
	MOVEQ.W #15,D7                     
.L1	LEA (A2,D4.W),A2
	REPT 16
	BSR DRAW
	LEA 16(A2),A2
	LEA 16(A3),A3
	ADDI.L #16,TEMP 	
	ENDR
	RTS

DRAW	MOVEA.L A0,A6
	MOVEA.L A2,A4
	MOVEA.L A3,A5
	MOVE.W D6,-(A7)
.L1	MOVE.B #$80,D5
	OR.B (A4)+,D5
	MOVE.B D5,.L2+1	
	NOP 
.L2	MOVE.W D0,(A0)
	ADDQ.W #8,A0	
	DBF D6,.L1		
	MOVE.W (A7)+,D6                 
	           
   	MOVE.W D7,-(A7)
.L3	MOVE.B #$80,D5
	OR.B (A5)+,D5
	MOVE.B D5,.L4+1
	NOP
.L4	MOVE.W D0,(A0)
	ADDQ.W #8,A0	
	DBF D7,.L3
	MOVE.W (A7)+,D7

	CMPI.W #13,D4
	BLT .L6
	MOVE.W D4,-(A7)
	SUBI.W #13,D4
	MOVEA.L TEMP,A4
.L33 	MOVE.B #$80,D5
	OR.B (A4)+,D5
	MOVE.B D5,.L44+1
	NOP
.L44	MOVE.W D0,(A0)
	ADDQ.W #8,A0	
	DBF D4,.L33
	MOVE.W (A7)+,D4
	
.L6	MOVEQ.W #11-1,D5
.L5	REPT 20
	MOVE.W (A6),(A0)
	ADDQ.W #8,A6
	ADDQ.W #8,A0
	ENDR	
	DBF D5,.L5

	RTS	

SH0	DC.W %0000000000000000
	DC.W %0000000000000000
	DC.W %1111111111111111
	DC.W %1111111111111111

SH8	DC.W %0000000000000000
	DC.W %0000000011111111
	DC.W %1111111111111111
	DC.W %1111111100000000

SETUP1	LEA LDATA,A0
.L11	MOVEQ.W #16-1,D0
.L12	MOVEQ.W #16-1,D1
.L1	CMPI.B #0,(A0)
	BNE .L3
	CMPI.B #1,1(A0)
	BNE .L3
	MOVE.B #1,(A0)+
	DBF D1,.L1
	DBF D0,.L12
	BRA .END	
.L3	CMPI.B #1,(A0)
	BNE .L33
	CMPI.B #1,1(A0)
	BNE .L33
	MOVE.B #2,(A0)+
	DBF D1,.L1
	DBF D0,.L12
	BRA .END	
.L33	CMPI.B #1,(A0)
	BNE .L333
	CMPI.B #0,1(A0)
	BNE .L333
	MOVE.B #3,(A0)+
	DBF D1,.L1
	DBF D0,.L12
	BRA .END	
.L333	MOVE.B #0,(A0)+
	DBF D1,.L1
	DBF D0,.L12
.END	CMPA.L #L_END,A0
	BLT .L11
	RTS
		 
SETUP2	MOVE.W CHARS,D0
	LEA TEXTAB1,A0
	LEA TEXT,A1
	LEA LDATA,A2
.L1	MOVEQ.W #0,D1
	MOVE.B (A1)+,D1
	SUBI.B #'A',D1
	MULU #256,D1
	LEA (A2,D1.W),A3
	MOVE.L A3,(A0)+
	DBF D0,.L1
	MOVE.W #0,L
	MOVE.L #TEXTAB1,TPOS1
	RTS 

TEXT	DC.B "....HELLO EVERYONE, UNCLE POWER MAN IS H"
	DC.B "ERE! RIGHT THEN, FIRSTLY I WILL SAY THAT"
	DC.B " THIS SCROLLTEXT WILL BE CRAP BECUASE I "
	DC.B "HAVE GOT A BLOODY COLD AND I CAN'T BREAT"
	DC.B "H, SO I AM QUITE PISSED OFF. BUT, I WILL"
	DC.B " TRY AND WRITE SOMETHING GOOD...HMMMM..."
	DC.B "I KNOW, I WILL TALK ABOUT MY COLLEGE. I "
	DC.B "HAD A MULTIPLE CHOICE TEST A FEW DAYS AG"
	DC.B "O AND GOT A C PLUS. THIS WAS BECUASE THE"
	DC.B "RE WAS A TIME LIMIT ON THE TEST, AND JUS"
	DC.B "T BEFORE I RAN OUT OF TIME, I GUESSED AL"
	DC.B "L THE ONES I HADN'T DONE. WHAT A MISTAKE"
	DC.B " THAT WAS! I LATER FOUND OUT THAT THE ON"
	DC.B "ES I GOT WRONG WHERE SUBTRACTED FROM THE"
	DC.B " ONES I GOT RIGHT! ANYWAY, MY TEACHER AL"
	DC.B "SO HAD THE NERVE TO SAY THAT IF YOU GET "
	DC.B " A C GRADE, YOU CAN ONLY BE AN AVERAGE P"
	DC.B "ROGRAMMER! I FELT LIKE ASKING HIM IF HE "
	DC.B "COULD MAKE A FULL SCREEN BIG SCROLLING S"
	DC.B "CROLLER LIKE THIS WITH MUZIX AND RASTERS"
	DC.B " BUT I DECIDED THAT I WOULDN'T EMBARASS "
	DC.B "HIM! ANYWAY, THE MORAL OF THIS STORY IS "
	DC.B "THAT IF YOU DO BAD IN A TEST, GET PISSED"
	DC.B " AND FORGET ABOUT IT, AS THEY ARE USELES"
	DC.B "S FOR REALLY TESTING YOUR ABILITY. I MEA"
	DC.B "N, I GOT A GRADE C, AND DO YOU THINK I A"
	DC.B "M AN AVERAGE PROGRAMMER? IF A LAMER GOT "
	DC.B "AN A OR B GRADE, DOES THAT MAKE THEM GOO"
	DC.B "D CODERS? NO! THEY ARE SHIT AT PROGRAMMI"
	DC.B "NG! RIGHT THEN, I HOPE YOU ARE NOT ASLEE"
	DC.B "P BECAUSE I WILL NOW TALK ABOUT NEIGHBOU"
	DC.B "RS! WELL, ITS SATURDAY SO NO NEIGHBOURS "
	DC.B "TODAY BUT YESTERDAY(FRIDAY,DICKHEAD!), J"
	DC.B "IM AND PAUL ACCUSED ROB OF STEALING PART"
	DC.B "S FOR THE GARAGE. GAIL IS PISSED OFF BEC"
	DC.B "AUSE PAUL FORGET HER BIRTHDAY(WHAT A BIT"
	DC.B "CH!), AND HILARY IS PISSING PAUL OFF BEC"
	DC.B "AUSE SHE WANTS TO WORK WITH HIM. ALL IN "
	DC.B "ALL,EVERYONE IS PISSING EVERYONE OFF, EX"
	DC.B "CEPT HENRY AND BRONY(CANT SPELL HER FULL"
	DC.B " NAME) WHO ARE ENGAGED. WELL THATS NEIGH"
	DC.B "BOURS, NOW HOME AND AWAY! NO, I DONT THI"
	DC.B "NK I WILL BORE YOU WITH THAT, BUT I WILL"
	DC.B " DO THE GREETINGS! THESE GREETINGS ARE T"
	DC.B "O HACKING CREWS AND NOT DEMO CREWS FOR A"
	DC.B " CHANGE(SO PLEASE GREET ME IN YOUR MENUS"
	DC.B "!). GREETINGS GO TO - THE MEDWAY BOYS, P"
	DC.B "OMPEY PIRATES(GREAT PACKER ON MENU 84!),"
	DC.B "JUSTICE DEPARTMENT,ERRR, I DON'T KNOW AN"
	DC.B "Y ONE ELSE, SO FAREWELL AND CHILL OUT..."
	DC.B -1
	EVEN

CHARS	DC.W (50*40)-1

TEXTAB1	DS.L (50*40)

TPOS1	DC.L TEXTAB1

LDATA	INCBIN 16X16.DAT
	EVEN
L_END

L	DC.W 0

VBL	MOVE.W #0,$FFFF8240.W
	MOVE.W #$FA1B,A0
	CLR.B (A0)
  	MOVE.B #4,6(A0)
	MOVE.L #TB1,$120.W
	MOVE.B #8,(A0)	
	MOVE.L #START,2(A7)
	MOVEA.L $4CE+4.W,A0
	JSR (A0)
	RTE

SWAP70	MOVE.L #SWAP78,SWAP+2
	LEA $70000+(4*160),A0
	MOVEM.W SH0,D0-D3
	JSR BIG
	JSR DO_VOL
	MOVE.B #0,$FFFF8203.W
	RTS

SWAP78	MOVE.L #SWAP70,SWAP+2
	LEA $78000+(4*160),A0
	MOVEM.W SH8,D0-D3
	JSR BIG
	JSR DO_VOL
	CMPI.W #15,L
	BEQ .L1
	ADDQ.W #1,L
	MOVE.B #$80,$FFFF8203.W
	RTS
.L1	MOVE.W #0,L
	ADDQ.L #4,TPOS1
	SUBQ.W #1,LENGTH1
	BEQ .L2
	MOVE.B #$80,$FFFF8203.W
	RTS
.L2	MOVE.L #TEXTAB1,TPOS1
	MOVE.W #2000-2,LENGTH1
	MOVE.B #$80,$FFFF8203.W
	RTS

IKBD	LEA $FFFFFC00.W,A0
.L1	MOVE.B (A0),D1
	BTST #1,D1
	BEQ .L1
	MOVE.B D0,2(A0)
	RTS

INIT	
;INTO SUPERVISOR
	BSR SUPER
;MOUSE OFF
	DC.W $A000
	DC.W $A00A
	MOVE.B #$12,D0
	BSR IKBD
;TIMER C OFF
	MOVE.W #5,-(A7)
	MOVE.W #26,-(A7)
	TRAP #14
	ADDQ.L #4,A7
;NEW STACK
	MOVEA.L (A7),A0
	MOVEA.L #MY,A7
	MOVE.L A0,(A7)
;CLEAR COLOURS
	LEA $FFFF8240,A0
	MOVEQ.W #15,D0
.L1	CLR.W (A0)+
	DBF D0,.L1	
;SET SCREENS
	CLR.W -(A7)
	PEA $78000	;PHYSICAL
	PEA $70000	;LOGICAL
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
;CLEAR SCREENS
	LEA $78000,A0
	LEA $70000,A1
	MOVE.W #32000/4-1,D0
.L2	CLR.L (A0)+
	CLR.L (A1)+
	DBF D0,.L2
;SAVE SYSTEM VARIABLES
	LEA VARS,A0
	MOVE.B $FFFFFA07.W,(A0)+
	MOVE.B $FFFFFA13.W,(A0)+
	MOVE.B $FFFFFA21.W,(A0)+
	MOVE.B $FFFFFA1B.W,(A0)+
	MOVE.L $70.W,(A0)+	
	MOVE.L $120.W,(A0)+
	MOVE.L $118.W,(A0)+
;ENABLE TIMER B
	CLR.B $FFFFFA07.W
	CLR.B $FFFFFA09.W
	ORI.B #1,$FFFFFA07.W
	ORI.B #1,$FFFFFA13.W
;NEW VECTORS
	BCLR #6,$FFFFFA09.W
	MOVE.L #KEY,$118.W
	BSET #6,$FFFFFA09.W
;IPL=3 AND EXIT
	MOVE.W #$2300,SR
	RTS	

VARS	DS.B 16
	DS.L 40
MY	DS.L 1	

SUPER	CLR.L -(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	MOVE.L D0,STACK
	RTS

USER	MOVE.L STACK,-(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	CLR.W -(A7)
	TRAP #1
STACK	DS.L 1

WAIT	CMPI.B #57,$FFFFFC02.W
	BNE WAIT
.L1	CMPI.B #57,$FFFFFC02.W
	BEQ .L1
	RTS

KEY	BCLR #6,$FFFFFA11.W
	RTE

END	MOVE.W #$2700,SR
	JSR ENDMUS
;RESET SYSTEM VARIABLES
	LEA VARS,A0
	MOVE.B (A0)+,$FFFFFA07.W
	MOVE.B (A0)+,$FFFFFA13.W
	MOVE.B (A0)+,$FFFFFA21.W
	MOVE.B (A0)+,$FFFFFA1B.W
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$120.W
	MOVE.L (A0)+,$118.W
;RESET COLOURS
	MOVE.W #$777,$FFFF8240.W
	MOVE.W #0,$FFFF8242.W
	MOVE.W #0,$FFFF8246.W
;MOUSE ON
	DC.W $A000	
	DC.W $A009
	MOVE.B #$08,D0
	BSR IKBD
;ENABLE TIMER C
	MOVE.W #5,-(A7)
	MOVE.W #27,-(A7)
	TRAP #14
	ADDQ.L #4,A7
;SET SCREENS
	CLR.W -(A7)
	PEA $78000	;PHYSICAL
	PEA $78000	;LOGICAL
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
;EXIT
	MOVE.W #$2300,SR
	BRA USER	

CONVERT	
;ROUT TO CONVERT CHARACTERS
;SO THAT THE PROGRAM CAN LOCATE THE
;CHARACTERS GIVEN IN CTAB

	LEA TEXT(PC),A0
	LEA CTAB(PC),A1
.A	MOVE.B (A0)+,D0
	MOVEA.L A1,A2
	MOVEQ.W #21-1,D1
.B	CMP.B (A2)+,D0 		;COMPARE CHARACTER WITH THOSE IN CTAB
	BEQ .C
	ADDQ.L #1,A2
	DBF D1,.B	
	BRA .D
.C	MOVE.B (A2),-1(A0)	;CHANGE IT TO CORRESPONDING CHARACTER IN CTAB
.D	CMPI.B #-1,(A0)
	BNE .A
	MOVE.W #2000-2,LENGTH1	;SET UP LENGTHS FOR ALL SCROLLINES
	RTS	
LENGTH1	DC.W 0
CTAB	DC.B '0','['
	DC.B '1','\'
	DC.B '2',']'
	DC.B '3','^'
	DC.B '4','_'
	DC.B '5',96
	DC.B '6',97
	DC.B '7',98
	DC.B '8',99
	DC.B '9',100,'(',101,'!',102,')',103,':',104
	DC.B '-',105,'.',106,',',107,"'",108,'"',109
	DC.B "?",110," ",111
	DC.B -1

SETMUS	MOVEQ.W #0,D0
	JSR MUZIX_K
	MOVE.L #MUZIX_K+($55BCA-$55B34),$4CE+4.W
	RTS

ENDMUS	CLR.L $4CE+4.W
	MOVE.L #$700FF00,$FFFF8800.W
	MOVE.L #$8000000,$FFFF8800.W
	MOVE.L #$9000000,$FFFF8800.W
	MOVE.L #$A000000,$FFFF8800.W
	RTS

MUZIX_K	INCBIN MUZIX13.DAT
