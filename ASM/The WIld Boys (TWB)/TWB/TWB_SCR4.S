;CHILL OUT,THE POWER MAN PRESENTS
;THE SOURCE CODE FOR THE TRACKING
;SPRITES DEMO. THE DATA FILES USED
;ARE:
;
;VOL.DAT	-GRAFIX DATA FOR THE VOLUME INDICATORS
;LOGO.DAT	-LOGO DATA	
;BALLS.DAT	-BALL DATA
;MASK2.DAT	-BALL MASK DATA(TO MAKE THEM MOVE INFRONT OF EACH OTHER)
;MAGAFONT.DAT	-FONT DATA	
;MUZIX9.DAT	-MUZIX DATA	
;Y.S		-TRACKING SPRITES Y MOVEMENT FILE(YOU CAN CHANGE THIS)
;SINEWV_4.S     -BYTE BENDER CONTROL DATA

  	JSR INIT
	JSR CONVERT
	JSR SETUP1
	JSR SET1
	JSR COLS
	JSR DO_LOGO
	MOVE.W #$2700,SR
	MOVE.L #TIMERB,$120.W
	MOVE.L #VBL1,$70.W
	JSR SETMUS
	MOVE.W #$2300,SR
	JSR WAIT
	JMP END

VOL	INCBIN VOL.DAT

V_ADS	DC.L VOL,VOL+320,VOL+640,VOL+960
	DC.L VOL+1280,VOL+1600,VOL+1920,VOL+2240
	DC.L VOL+2560,VOL+2880,VOL+3200,VOL+3520
	DC.L VOL+3840,VOL+4160,VOL+4480,VOL+4480+320
	
VOL1	DC.L $78000+(90*160)+16
VOL2	DC.L $78000+(90*160)+64
VOL3	DC.L $78000+(90*160)+112

VOLUME	MOVEQ.W #0,D0
	MOVEQ.W #0,D1
	MOVEQ.W #0,D2
	LEA V_ADS,A1
	LEA $FFFF8800.W,A0
	MOVE.B #8,(A0)
	MOVE.B (A0),D0
	MOVE.B #9,(A0)
	MOVE.B (A0),D1
	MOVE.B #10,(A0)
	MOVE.B (A0),D2
	BTST #4,D0
	BEQ .L1
	MOVE.B #10,D0
.L1	BTST #4,D1
	BEQ .L2
	MOVE.B #10,D1
.L2	BTST #4,D2
	BEQ .L3
	MOVE.B #10,D2
.L3	ASL.W #2,D0
	ASL.W #2,D1
	ASL.W #2,D2
	LEA (A1,D0.W),A2
	LEA (A1,D1.W),A3
	LEA (A1,D2.W),A4

	MOVEA.L (A2),A1
	LEA $78000+(90*160)+16,A2
	BSR SHOW
	MOVEA.L (A3),A1
	LEA $78000+(90*160)+64,A2
	BSR SHOW
	LEA $78000+(90*160)+112,A2
	MOVEA.L (A4),A1

SHOW	MOVE.L (A1)+,(A2)
	MOVE.L (A1)+,8(A2)
	MOVE.L (A1)+,16(A2)
	MOVE.L (A1)+,24(A2)

	MOVE.L (A1)+,160(A2)
	MOVE.L (A1)+,168(A2)
	MOVE.L (A1)+,176(A2)
	MOVE.L (A1)+,184(A2)

	MOVE.L (A1)+,320(A2)
	MOVE.L (A1)+,328(A2)
	MOVE.L (A1)+,336(A2)
	MOVE.L (A1)+,344(A2)

	MOVE.L (A1)+,480(A2)
	MOVE.L (A1)+,488(A2)
	MOVE.L (A1)+,496(A2)
	MOVE.L (A1)+,504(A2)
					
	MOVE.L (A1)+,640(A2)
	MOVE.L (A1)+,648(A2)
	MOVE.L (A1)+,656(A2)
	MOVE.L (A1)+,664(A2)

	MOVE.L (A1)+,800(A2)
	MOVE.L (A1)+,808(A2)
	MOVE.L (A1)+,816(A2)
	MOVE.L (A1)+,824(A2)

	MOVE.L (A1)+,960(A2)
	MOVE.L (A1)+,968(A2)
	MOVE.L (A1)+,976(A2)
	MOVE.L (A1)+,984(A2)

	MOVE.L (A1)+,1120(A2)
	MOVE.L (A1)+,1128(A2)
	MOVE.L (A1)+,1136(A2)
	MOVE.L (A1)+,1144(A2)

	MOVE.L (A1)+,1280(A2)
	MOVE.L (A1)+,1288(A2)
	MOVE.L (A1)+,1296(A2)
	MOVE.L (A1)+,1304(A2)

	MOVE.L (A1)+,1440(A2)
	MOVE.L (A1)+,1448(A2)
	MOVE.L (A1)+,1456(A2)
	MOVE.L (A1)+,1464(A2)

	MOVE.L (A1)+,1600(A2)
	MOVE.L (A1)+,1608(A2)
	MOVE.L (A1)+,1616(A2)
	MOVE.L (A1)+,1624(A2)

	MOVE.L (A1)+,1760(A2)
	MOVE.L (A1)+,1768(A2)
	MOVE.L (A1)+,1776(A2)
	MOVE.L (A1)+,1784(A2)

	MOVE.L (A1)+,1920(A2)
	MOVE.L (A1)+,1928(A2)
	MOVE.L (A1)+,1936(A2)
	MOVE.L (A1)+,1944(A2)

	MOVE.L (A1)+,2080(A2)
	MOVE.L (A1)+,2088(A2)
	MOVE.L (A1)+,2096(A2)
	MOVE.L (A1)+,2104(A2)

	MOVE.L (A1)+,2240(A2)
	MOVE.L (A1)+,2248(A2)
	MOVE.L (A1)+,2256(A2)
	MOVE.L (A1)+,2264(A2)

	MOVE.L (A1)+,2400(A2)
	MOVE.L (A1)+,2408(A2)
	MOVE.L (A1)+,2416(A2)
	MOVE.L (A1)+,2424(A2)

	MOVE.L (A1)+,2560(A2)
	MOVE.L (A1)+,2568(A2)
	MOVE.L (A1)+,2576(A2)
	MOVE.L (A1)+,2584(A2)

	MOVE.L (A1)+,2720(A2)
	MOVE.L (A1)+,2728(A2)
	MOVE.L (A1)+,2736(A2)
	MOVE.L (A1)+,2744(A2)

	MOVE.L (A1)+,2880(A2)
	MOVE.L (A1)+,2888(A2)
	MOVE.L (A1)+,2896(A2)
	MOVE.L (A1)+,2904(A2)

	MOVE.L (A1)+,3040(A2)
	MOVE.L (A1)+,3048(A2)
	MOVE.L (A1)+,3056(A2)
	MOVE.L (A1)+,3064(A2)

	RTS
	
LOGO	INCBIN LOGO.DAT

DO_LOGO	LEA LOGO,A0
	MOVEQ.W #1,D0
	MOVE.L #$78004+(22*160)+40,A1
	BSR PUT
	LEA LOGO+576,A0
	MOVEQ.W #7,D0
	MOVE.L #$78004+(22*160)+65,A1
	BSR PUT
	LEA LOGO+1152,A0
	MOVEQ.W #1,D0
	MOVE.L #$78004+(22*160)+96,A1
PUT	MOVEQ.W #6-1,D2
.L2	MOVEQ.W #48-1,D1
	MOVEA.L A1,A2
.L1	MOVE.B (A0)+,(A2)
	MOVE.B (A0)+,2(A2)
	LEA 160(A2),A2	
	DBF D1,.L1
	LEA (A1,D0.W),A1
	CMPI.W #1,D0
	BEQ .L3
	MOVEQ.W #1,D0
	DBF D2,.L2
	RTS
.L3	MOVEQ.W #7,D0
	DBF D2,.L2
	RTS
	
PAL1	DC.W $330,$440,$550,$660,$770,$770,$660,$550,$440,$330,0,0,0,0
	DC.W $030,$040,$050,$060,$070,$070,$060,$050,$040,$030,0,0,0,0
	DC.W $300,$400,$500,$600,$700,$700,$600,$500,$400,$300,0,0,0,0

	DC.W $101,$202,$303,$404,$505,$606,$707,$707,$606,$505,$404,$303,$202,$101
	DC.W $001,$002,$003,$004,$005,$006,$007,$007,$006,$005,$004,$003,$002,$001
	DC.W $100,$200,$300,$400,$500,$600,$700,$700,$600,$500,$400,$300,$200,0
PAL1END	

PALST2	DC.L PAL1END

PALST	DC.L PAL1
	
TIMERB2	MOVE.L A0,-(A7)
	MOVEA.L PALST,A0
	MOVE.W (A0)+,$FFFF8240.W
	MOVE.L A0,PALST	
	MOVEA.L PALST2,A0
	MOVE.W -(A0),$FFFF8242.W
	MOVE.L A0,PALST2	
	MOVE.L (A7)+,A0
	BCLR #0,$FFFFFA0F.W
	RTE

TIMERB3	MOVE.L A0,-(A7)
	MOVE.W #$FA1B,A0
	CLR.B (A0)
	MOVE.L #TIMERB2,$120.W
	MOVE.L #PAL1,PALST
	MOVE.L #PAL1END,PALST2
	MOVE.B #2,6(A0)
	MOVE.B #8,(A0)		
	LEA $FFFF8242.W,A0
	MOVE.W #$007,(A0)+
	MOVE.W #$077,(A0)+
	MOVE.W #$066,(A0)+
	MOVE.W #$055,(A0)+
	MOVE.W #$044,(A0)+
	MOVE.W #$033,(A0)+
	MOVE.W #$022,(A0)
	MOVEA.L (A7)+,A0	
	BCLR #0,$FFFFFA0F.W
	RTE

TIMERB	CLR.B $FFFFFA1B.W
	MOVE.L #TIMERB3,$120.W
	MOVE.B #32,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.W #$070,$FFFF8242.W
	MOVE.W #$050,$FFFF8244.W
	MOVE.W #$030,$FFFF8246.W
	BCLR #0,$FFFFFA0F.W
	RTE
		
VBL1	CLR.B $FFFFFA1B.W
	MOVE.B #84,$FFFFFA21.W
	MOVE.L #TIMERB,$120.W
	MOVE.B #8,$FFFFFA1B.W
	LEA $FFFF8242.W,A0
	MOVE.W #$707,(A0)+
	MOVE.W #$505,(A0)+
 	MOVE.W #$303,(A0)+
	MOVE.W #$777,(A0)+
	MOVE.W #$707,(A0)+
	MOVE.W #$505,(A0)+
	MOVE.W #$303,(A0)+
	MOVE.W #$666,(A0)+
	MOVE.W #$707,(A0)+
	MOVE.W #$505,(A0)+
	MOVE.W #$303,(A0)+
	MOVE.W #$444,(A0)+
	MOVE.W #$707,(A0)+
	MOVE.W #$505,(A0)+
	MOVE.W #$303,(A0)	
	JSR SCROLL
.L1	CMPI.B #57,$FFFFFC02.W
	BEQ .L2
	CMPI.B #180,$FFFF8207.W
	BLT .L1
	JSR DOBALL
	JSR VOLUME
	RTE
.L2	MOVE.L #RET,$70.W
	RTE

RET	RTE

BALLS	INCBIN BALLS.DAT
MASK	INCBIN MASK2.DAT

BALLDAT	DS.B (93*10) ;OFFSET WORD,SOURCE AD LONG
BALLEND
	
	INCLUDE Y.S
WW1	INCLUDE Y.S
WW2	INCLUDE Y.S

WSTART	DC.L WW2

SINE	DC.W 0,0,1,2,4,8,16,24,32,40,48,56,64,72,80,88,96
	DC.W 104,112,120,128,136,144,152,160,168,176,184
	DC.W 192,200,208,216,224,232,240,248,256,264,272
	DC.W 280,288,296,300,302,303,304,304
	DC.W -1

NUMBER	DS.W 1
TEMP	DS.W 1

SET1	MOVEQ.W #0,D5
	LEA SINE,A0
	LEA BALLS,A2
	LEA BALLDAT,A3
	LEA MASK,A6
.L1	MOVEQ.L #0,D1	 ;OFFSET
	MOVE.W (A0)+,D0
	BMI SET2
	ADDQ.W #1,D5
.L12	CMPI.W #16,D0
	BGT .L4
.L10	MOVE.W D0,D7
	MULU #(8*16),D0
	MULU #(4*16),D7
	LEA (A2,D0.W),A4 ;SOURCE 	
	LEA (A6,D7.W),A5
	BRA .L5		
.L4	CMPI.W #32,D0
	BGT .L9
	ADDQ.W #8,D1	
	SUBI.W #16,D0
	BRA .L10
.L9	ADDI.W #16,D1
	SUBI.W #32,D0
	BRA .L12
.L5	MOVE.W D1,(A3)+
	MOVE.L A4,(A3)+  
	MOVE.L A5,(A3)+
	BRA .L1		

SET2	SUBQ.W #1,D5
	SUBQ.W #2,A0
.L1	MOVEQ.L #0,D1	 ;OFFSET
	MOVE.W -(A0),D0
.L12	CMPI.W #15,D0
	BGT .L4
.L10	MOVE.W D0,D7
	MULU #(8*16),D0
	MULU #(4*16),D7 
	LEA (A2,D0.W),A4 ;SOURCE 	
	LEA (A6,D7.W),A5
	BRA .L5		
.L4	CMPI.W #31,D0
	BGT .L9
	ADDQ.W #8,D1	
	SUBI.W #16,D0
	BRA .L10
.L9	ADDI.W #16,D1
	SUBI.W #32,D0
	BRA .L12
.L5	MOVE.W D1,(A3)+
	MOVE.L A4,(A3)+ 
	MOVE.L A5,(A3)+ 
	DBF D5,.L1		

SET3	LEA TRACK,A0
	LEA BALLDAT,A1
	MOVEQ.W #25-1,D0
.L6	MOVE.L A1,(A0)+
	ADDA.L #20,A1	
	DBF D0,.L6
	MOVE.W #$007,$FFFF8242.W
	MOVE.W #$006,$FFFF8244.W
 	MOVE.W #$004,$FFFF8246.W
	RTS	
 
TRACK	DS.L 25

DOBALL	MOVEA.L WSTART,A6
	MOVEA.L A6,A2
	SUBQ.L #4,WSTART
	CMPI.L #WW1,WSTART
	BNE .L121
	MOVE.L #WW2,WSTART
.L121 	MOVEQ.W #21-1,D0

	LEA TRACK,A0
.L20	MOVEA.L (A6)+,A3
	MOVEA.L (A0)+,A1
	MOVE.W (A1),D1
	LEA (A3,D1.W),A4	
	MOVE.L #0,(A4)
	MOVE.L #0,8(A4)
	MOVE.L #0,160(A4)
	MOVE.L #0,168(A4)
	MOVE.L #0,320(A4)
	MOVE.L #0,328(A4)
	MOVE.L #0,480(A4)
	MOVE.L #0,488(A4)
	MOVE.L #0,640(A4)
	MOVE.L #0,648(A4)
	MOVE.L #0,800(A4)
	MOVE.L #0,808(A4)
	MOVE.L #0,960(A4)
	MOVE.L #0,968(A4)
	MOVE.L #0,1120(A4)
	MOVE.L #0,1128(A4)
	MOVE.L #0,1280(A4)
	MOVE.L #0,1288(A4)
	MOVE.L #0,1440(A4)
	MOVE.L #0,1448(A4)
	MOVE.L #0,1600(A4)
	MOVE.L #0,1608(A4)
	MOVE.L #0,1760(A4)
	MOVE.L #0,1768(A4)
	MOVE.L #0,1920(A4)
	MOVE.L #0,1928(A4)
	MOVE.L #0,2080(A4)
	MOVE.L #0,2088(A4)
	MOVE.L #0,2240(A4)
	MOVE.L #0,2248(A4)
	MOVE.L #0,2400(A4)
	MOVE.L #0,2408(A4)
	MOVE.L #0,2560(A4)
	MOVE.L #0,2568(A4)
	MOVE.L #0,2720(A4)
	MOVE.L #0,2728(A4)
	MOVE.L #0,2880(A4)
	MOVE.L #0,2888(A4)
	MOVE.L #0,3040(A4)
	MOVE.L #0,3048(A4)
	MOVE.L #0,3200(A4)
	MOVE.L #0,3208(A4)
	MOVE.L #0,3360(A4)
	MOVE.L #0,3368(A4)
	MOVE.L #0,3520(A4)
	MOVE.L #0,3528(A4)
	MOVE.L #0,3680(A4)
	MOVE.L #0,3688(A4)

	DBF D0,.L20

	LEA TRACK,A0
	MOVEQ.W #21-1,D0
	MOVEA.L A2,A6
.L1	MOVEA.L (A6)+,A3
	MOVEA.L (A0)+,A1
	ADDI.L #10,-4(A0)
	CMPA.L #BALLEND,A1
	BNE .L2
	MOVE.L #BALLDAT,-4(A0)
.L2	MOVE.W (A1)+,D1
	MOVEA.L (A1)+,A2 ;S
	LEA (A3,D1.W),A4 ;D
	MOVE.L (A1)+,A5	
 
	MOVE.W (A5)+,D6
	AND.W D6,640(A4)
	AND.W D6,642(A4)
	MOVE.W (A5)+,D6
	AND.W D6,648(A4)
	AND.W D6,650(A4)
	MOVE.L (A2)+,D6
	OR.L D6,640(A4)
	MOVE.L (A2)+,D6
	OR.L D6,648(A4)

	MOVE.W (A5)+,D6
	AND.W D6,800(A4)
	AND.W D6,802(A4)
	MOVE.W (A5)+,D6
	AND.W D6,808(A4)
	AND.W D6,810(A4)
	MOVE.L (A2)+,D6
	OR.L D6,800(A4)
	MOVE.L (A2)+,D6
	OR.L D6,808(A4)

	MOVE.W (A5)+,D6
	AND.W D6,960(A4)
	AND.W D6,962(A4)
	MOVE.W (A5)+,D6
	AND.W D6,968(A4)
	AND.W D6,970(A4)
	MOVE.L (A2)+,D6
	OR.L D6,960(A4)
	MOVE.L (A2)+,D6
	OR.L D6,968(A4)

	MOVE.W (A5)+,D6
	AND.W D6,1120(A4)
	AND.W D6,1122(A4)
	MOVE.W (A5)+,D6
	AND.W D6,1128(A4)
	AND.W D6,1130(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1120(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1128(A4)

	MOVE.W (A5)+,D6
	AND.W D6,1280(A4)
	AND.W D6,1282(A4)
	MOVE.W (A5)+,D6
	AND.W D6,1288(A4)
	AND.W D6,1290(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1280(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1288(A4)

	MOVE.W (A5)+,D6
	AND.W D6,1440(A4)
	AND.W D6,1442(A4)
	MOVE.W (A5)+,D6
	AND.W D6,1448(A4)
	AND.W D6,1450(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1440(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1448(A4)

	MOVE.W (A5)+,D6
	AND.W D6,1600(A4)
	AND.W D6,1602(A4)
	MOVE.W (A5)+,D6
	AND.W D6,1608(A4)
	AND.W D6,1610(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1600(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1608(A4)

	MOVE.W (A5)+,D6
	AND.W D6,1760(A4)
	AND.W D6,1762(A4)
	MOVE.W (A5)+,D6
	AND.W D6,1768(A4)
	AND.W D6,1770(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1760(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1768(A4)

	MOVE.W (A5)+,D6
	AND.W D6,1920(A4)
	AND.W D6,1922(A4)
	MOVE.W (A5)+,D6
	AND.W D6,1928(A4)
	AND.W D6,1930(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1920(A4)
	MOVE.L (A2)+,D6
	OR.L D6,1928(A4)

	MOVE.W (A5)+,D6
	AND.W D6,2080(A4)
	AND.W D6,2082(A4)
	MOVE.W (A5)+,D6
	AND.W D6,2088(A4)
	AND.W D6,2090(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2080(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2088(A4)

	MOVE.W (A5)+,D6
	AND.W D6,2240(A4)
	AND.W D6,2242(A4)
	MOVE.W (A5)+,D6
	AND.W D6,2248(A4)
	AND.W D6,2250(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2240(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2248(A4)

	MOVE.W (A5)+,D6
	AND.W D6,2400(A4)
	AND.W D6,2402(A4)
	MOVE.W (A5)+,D6
	AND.W D6,2408(A4)
	AND.W D6,2410(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2400(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2408(A4)

	MOVE.W (A5)+,D6
	AND.W D6,2560(A4)
	AND.W D6,2562(A4)
	MOVE.W (A5)+,D6
	AND.W D6,2568(A4)
	AND.W D6,2570(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2560(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2568(A4)

	MOVE.W (A5)+,D6
	AND.W D6,2720(A4)
	AND.W D6,2722(A4)
	MOVE.W (A5)+,D6
	AND.W D6,2728(A4)
	AND.W D6,2730(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2720(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2728(A4)

	MOVE.W (A5)+,D6
	AND.W D6,2880(A4)
	AND.W D6,2882(A4)
	MOVE.W (A5)+,D6
	AND.W D6,2888(A4)
	AND.W D6,2890(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2880(A4)
	MOVE.L (A2)+,D6
	OR.L D6,2888(A4)

	MOVE.W (A5)+,D6
	AND.W D6,3040(A4)
	AND.W D6,3042(A4)
	MOVE.W (A5)+,D6
	AND.W D6,3048(A4)
	AND.W D6,3050(A4)
	MOVE.L (A2)+,D6
	OR.L D6,3040(A4)
	MOVE.L (A2)+,D6
	OR.L D6,3048(A4)

	DBF D0,.L1
	RTS	

VBL	JSR SCROLL
	RTE

COLS	LEA $FFFF8240.W,A0
	LEA FONT,A1
	MOVEQ.W #8-1,D0
.L1	MOVE.W (A1)+,(A0)+
	DBF D0,.L1
	MOVEQ.W #7-1,D0
	ADDQ.L #2,A0
	LEA $FFFF8242.W,A1
.L2	MOVE.W (A1)+,(A0)+			
	DBF D0,.L2
	MOVE.W #$777,$FFFF8240+(8*2).W
	RTS

W1	INCLUDE SINEWV_4.S
W2	INCLUDE SINEWV_4.S
W3
WAVEST	DC.L W3-(40*4)			;POSITION IN ABOVE DATA

SCROLL	;ROUTINE TO SCROLL BYTE BENDER SCROLLINE
	
	MOVEA.L TPOS1,A0	;A0 POINTS TO ABSOLUTE ADDRESSES FOR TEXT
	MOVEA.L WAVEST,A1	;A1 POINTS TO DESTINATION ADDRESSES
	MOVEQ.W #0,D0		;OFFSET FROM LEFT HAND SIDE OF SCREEN
	BSR A			;DRAW ONE STRIP(BYTE IN WIDTH) OF LETTER
				;WHY THE HELL I CALLED IT 'A' I DON'T KNOW!
	MOVEQ.W #1,D0		;NEXT OFFSET(2ND BYTE ALONG)
	BSR A			;DRAW 2ND STRIP
	MOVEQ.W #8,D0		;AND SO ON.....
	BSR A
	MOVEQ.W #9,D0
	BSR A
	MOVEQ.W #16,D0
	BSR A
	MOVEQ.W #17,D0
	BSR A
	MOVEQ.W #24,D0
	BSR A
	MOVEQ.W #25,D0
	BSR A
	MOVEQ.W #32,D0
	BSR A
	MOVEQ.W #33,D0
	BSR A
	MOVEQ.W #40,D0
	BSR A
	MOVEQ.W #41,D0
	BSR A
	MOVEQ.W #48,D0
	BSR A
	MOVEQ.W #49,D0
	BSR A
	MOVEQ.W #56,D0
	BSR A
	MOVEQ.W #57,D0
	BSR A
	MOVEQ.W #64,D0
	BSR A
	MOVEQ.W #65,D0
	BSR A
	MOVEQ.W #72,D0
	BSR A
	MOVEQ.W #73,D0
	BSR A
	MOVEQ.W #80,D0
	BSR A
	MOVEQ.W #81,D0
	BSR A
	MOVEQ.W #88,D0
	BSR A
	MOVEQ.W #89,D0
	BSR A
	MOVEQ.W #96,D0
	BSR A
	MOVEQ.W #97,D0
	BSR A
	MOVEQ.W #104,D0
	BSR A
	MOVEQ.W #105,D0
	BSR A
	MOVEQ.W #112,D0
	BSR A
	MOVEQ.W #113,D0
	BSR A
	MOVEQ.W #120,D0
	BSR A
	MOVEQ.W #121,D0
	BSR A
	MOVE.W #128,D0
	BSR A
	MOVE.W #129,D0
	BSR A
	MOVE.W #136,D0
	BSR A
	MOVE.W #137,D0
	BSR A
	MOVE.W #144,D0
	BSR A
	MOVE.W #145,D0
	BSR A
	MOVE.W #152,D0
	BSR A
	MOVE.W #153,D0
	BSR A
	SUBQ.L #4,WAVEST	 ;NEW POSITION IN BYTE BENDER CONTROL DATA
	CMPI.L #W2-(40*4),WAVEST ;IS IT AT THE BEGINNING?
	BNE .L1			 ;PISS OFF IF IT ISN'T
	MOVE.L #W3-(40*4),WAVEST ;RESET POSITION TO END-(40*4) 
.L1	SUBQ.W #1,LENGTH1	 ;END OF TEXT?
	BNE .L2			 ;IF NOT, BUGGER OFF (THIS IS GETTING BORING!)
	MOVE.W #8000-40,LENGTH1  ;RESET LENGTH
	MOVE.L #TEXTAB1,TPOS1	 ;RESET POSITION IN TEXT TO START
 	RTS			 ;PISS OFF (NOT AGAIN!)
.L2	ADDQ.L #4,TPOS1		 ;INCREASE POSITION IN TEXT
	RTS			 ;YOU KNOW WHAT THIS MEANS!

A	;ROUTINE TO DRAW ONE STRIP OF A CHARACTER
	;FOR A 32 PIXELS DOWN,2 BP FONT

	MOVEA.L (A0)+,A2
	MOVEA.L (A1)+,A3
	LEA (A3,D0.W),A3
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)

	CLR.B 160(A3)
	CLR.B 162(A3)
	CLR.B 164(A3)

	MOVE.B (A2)+,320(A3)
	MOVE.B (A2)+,322(A3)
	MOVE.B (A2)+,324(A3)

	MOVE.B (A2)+,480(A3)
	MOVE.B (A2)+,482(A3)
	MOVE.B (A2)+,484(A3)

	MOVE.B (A2)+,640(A3)
	MOVE.B (A2)+,642(A3)
	MOVE.B (A2)+,644(A3)

	MOVE.B (A2)+,800(A3)
	MOVE.B (A2)+,802(A3)
	MOVE.B (A2)+,804(A3)

	MOVE.B (A2)+,960(A3)
	MOVE.B (A2)+,962(A3)
	MOVE.B (A2)+,964(A3)

	MOVE.B (A2)+,1120(A3)
	MOVE.B (A2)+,1122(A3)
	MOVE.B (A2)+,1124(A3)

	MOVE.B (A2)+,1280(A3)
	MOVE.B (A2)+,1282(A3)
	MOVE.B (A2)+,1284(A3)

	MOVE.B (A2)+,1440(A3)
	MOVE.B (A2)+,1442(A3)
	MOVE.B (A2)+,1444(A3)

	MOVE.B (A2)+,1600(A3)
	MOVE.B (A2)+,1602(A3)
	MOVE.B (A2)+,1604(A3)

	MOVE.B (A2)+,1760(A3)
	MOVE.B (A2)+,1762(A3)
	MOVE.B (A2)+,1764(A3)

	MOVE.B (A2)+,1920(A3)
	MOVE.B (A2)+,1922(A3)
	MOVE.B (A2)+,1924(A3)

	MOVE.B (A2)+,2080(A3)
	MOVE.B (A2)+,2082(A3)
	MOVE.B (A2)+,2084(A3)

	MOVE.B (A2)+,2240(A3)
	MOVE.B (A2)+,2242(A3)
	MOVE.B (A2)+,2244(A3)

	MOVE.B (A2)+,2400(A3)
	MOVE.B (A2)+,2402(A3)
	MOVE.B (A2)+,2404(A3)

	MOVE.B (A2)+,2560(A3)
	MOVE.B (A2)+,2562(A3)
	MOVE.B (A2)+,2564(A3)

	MOVE.B (A2)+,2720(A3)
	MOVE.B (A2)+,2722(A3)
	MOVE.B (A2)+,2724(A3)

	MOVE.B (A2)+,2880(A3)
	MOVE.B (A2)+,2882(A3)
	MOVE.B (A2)+,2884(A3)

	MOVE.B (A2)+,3040(A3)
	MOVE.B (A2)+,3042(A3)
	MOVE.B (A2)+,3044(A3)

	MOVE.B (A2)+,3200(A3)
	MOVE.B (A2)+,3202(A3)
	MOVE.B (A2)+,3204(A3)

	MOVE.B (A2)+,3360(A3)
	MOVE.B (A2)+,3362(A3)
	MOVE.B (A2)+,3364(A3)

	MOVE.B (A2)+,3520(A3)
	MOVE.B (A2)+,3522(A3)
	MOVE.B (A2)+,3524(A3)

	MOVE.B (A2)+,3680(A3)
	MOVE.B (A2)+,3682(A3)
	MOVE.B (A2)+,3684(A3)

	MOVE.B (A2)+,3840(A3)
	MOVE.B (A2)+,3842(A3)
	MOVE.B (A2)+,3844(A3)

	MOVE.B (A2)+,4000(A3)
	MOVE.B (A2)+,4002(A3)
	MOVE.B (A2)+,4004(A3)

	MOVE.B (A2)+,4160(A3)
	MOVE.B (A2)+,4162(A3)
	MOVE.B (A2)+,4164(A3)

	CLR.B 4320(A3)
	CLR.B 4322(A3)
	CLR.B 4324(A3)

	CLR.B 4480(A3)
	CLR.B 4482(A3)
	CLR.B 4484(A3)
	RTS	

SETUP1	LEA TEXTAB1,A0		;ADDRESS TO PUT ABSOLUTE ADDRESSES
	LEA FONT+16,A1		;ADDRESS OF FONT DATA
	LEA TEXT1,A2		;ADDRESS OF TEXT
	MOVEQ.L #(25*3),D7	;LENGTH OF 1 STRIP IN BYTES	
	MOVE.L #2000-1,D0	;2080 CHARACTERS
	BSR SETUP

SETUP2	RTS
	;YOU CAN CALL SETUP AGAIN FROM HERE
	;TO SET UP ABSOLUTE ADDRESSES FOR ANOTHER
	;SCROLLINE USING THE SAME FORMAT AS ABOVE 

SETUP	MOVEQ.W #0,D1
	MOVE.B (A2)+,D1
	SUBI.B #'A',D1
	MOVE.L D7,D6
	MULU #4,D6
	MULU D6,D1 	
	LEA (A1,D1.W),A3	;1ST STRIP
	MOVE.L A3,(A0)+		
	LEA (A3,D7),A3		;2ND STRIP	
	MOVE.L A3,(A0)+		
	LEA (A3,D7),A3		;3RD STRIP
	MOVE.L A3,(A0)+		
	LEA (A3,D7),A3		;4TH STRIP
	MOVE.L A3,(A0)+		
	DBF D0,SETUP		;END OF TEXT?
	RTS

CONVERT	
;ROUT TO CONVERT CHARACTERS
;SO THAT THE PROGRAM CAN LOCATE THE
;CHARACTERS GIVEN IN CTAB

	LEA TEXT1(PC),A0
	LEA CTAB(PC),A1
.A	MOVE.B (A0)+,D0
	MOVEA.L A1,A2
	MOVEQ.W #21-1,D1
.B	CMP.B (A2)+,D0 		;COMPARE CHARACTER WITH THOSE IN CTAB
	BEQ .C
	ADDQ.L #1,A2
	DBF D1,.B	
	BRA .D
.C	MOVE.B (A2),-1(A0)	;CHANGE IT TO CORRESPONDING CHARACTER IN CTAB
.D	CMPI.B #-1,(A0)
	BNE .A
	MOVE.W #8000-40,LENGTH1	;SET UP LENGTHS FOR ALL SCROLLINES
	RTS	
LENGTH1	DC.W 0
CTAB	DC.B '0','['
	DC.B '1','\'
	DC.B '2',']'
	DC.B '3','^'
	DC.B '4','_'
	DC.B '5',96
	DC.B '6',97
	DC.B '7',98
	DC.B '8',99
	DC.B '9',100,'(',101,'!',102,')',103,':',104
	DC.B '-',105,'.',106,',',107,"'",108,'"',109
	DC.B "?",110," ",111
	DC.B -1

TEXT1	DC.B "............DUDES! IT'S POWER MAN HERE. "
	DC.B "IT IS SUNDAY EVENING AND I AM JUST FINIS"
	DC.B "HING OFF THIS DEMO. IT WAS JJ'S BIRTHDAY"
	DC.B " YESTERDAY(HE'S 14).                    "
	DC.B "WANTS ME TO MAKE HIM A BIRTHD           "
	DC.B "AY DEMO! YOU WISH! WELL, MAYBE I MIGHT M"
	DC.B "AKE HIM ONE, BUT AT THE TIME OF WRITING "
	DC.B "I AM NOT SURE IF I'LL HAVE ENOUGH DISK S"
	DC.B "PACE. LAST NIGHT I WAS UP TILL 1 IN THE "
	DC.B "MORING MAKING MY TRACKING SPRITE ROUT. T"
	DC.B "HIS DEMO IS THE FIRST TO USE IT. YESTERD"
	DC.B "AY I ALSO MADE MY SAMPLE REPLAY ROUT., W"
	DC.B "HICH I WILL USE TO MAKE SOME WICKED SAMP"
	DC.B "LE DEMOS, THAT IS IF I GET MY SAMPLER FI"
	DC.B "XED! BY THE WAY, MOST OF MY ROUTINES ARE"
	DC.B " AVAILABLE FOR A PRICE, SO WRITE TO ME! "
	DC.B "NOW, WE WILL TALK ABOUT DEMOS. THESE DAY"
	DC.B "S DEMOS ARE ALL ALIKE, AREN'T THEY? DIST"
	DC.B "ORTED LOGO,SCROLLINE,RASTERS, WHY CAN'T "
	DC.B "PEOPLE PUT SOMETHING ORIGINAL IN THEIR D"
	DC.B "EMOS LIKE ME. I MEAN, MY REFLECTION DEMO"
	DC.B " IS QUITE ORIGINAL, I HAVEN'T SEEN ANY E"
	DC.B "XCEPT THE ONE IN THE SUPERIOR DEMO. ONE "
	DC.B "THING I DON'T LIKE IS A DEMO WITH HARD T"
	DC.B "O READ SCROLLINES. MY SCROLLERS ARE STIL"
	DC.B "L IMPRESSIVE BUT ARE ALSO EASY TO READ.B"
	DC.B "Y THE WAY, DO LIKE THE MUZIX? I ONLY USE"
	DC.B " REALLY GOOD MUZIX IN MY DEMOS, BUT I AM"
	DC.B " RUNNING OUT! JJ WAS SUPPOSED TO SEND ME"
	DC.B " SOME STUFF TO HACK THE MUZIX FROM BUT I"
	DC.B " AM STILL WAITING(SO GET OF YOUR LAZY AR"
	DC.B "SE JJ AND WORK!). IF YOU HAVEN'T FALLEN "
	DC.B "ASLEEP READING THIS CRAPPY TEXT, THEN I "
	DC.B "WILL TALK ABOUT FAST. IN CASE YOU ARE TH"
	DC.B "ICK, THIS STANDS FOR FEDERATION AGAINST "
	DC.B "SOFTWARE THEFT. PERSONALLY, I THINK THEY"
	DC.B " ARE A BUNCH OF SODDING WANKERS BUT THAT"
	DC.B "'S ANOTHER STORY. BACK TO FAST. I DON'T "
	DC.B "KNOW WHY THEY ARE EVEN BOTHERING. I MEAN"
	DC.B " HOW CAN THEY POSSIBLY PROVE THAT SOMEON"
	DC.B "E HAS COPIED SOFTWARE ILLEGALY UNLESS TH"
	DC.B "E PIRATE IS SO STUPID THAT HE MARKS HIS "
	DC.B "DISKS - PIRATE GAMES - AND SHOWS THEM OF"
	DC.B "F TO THE COPS ETC. ALL I CAN SAY TO FAST"
	DC.B " IS - YOU WON'T CATCH ME!!! NOW, SOME GR"
	DC.B "EETINGS TO THE CAREBEARS, THE LOST BOYS,"
	DC.B " DYNAMIC DUO, INNER CIRCLE, DIGITAL INSA"
	DC.B "NITY, OVERLAMERS(WHAT THE HELL ARE YOU D"
	DC.B "OING CODING DEMOS IN GFA BASIC?),SYNC,OM"
	DC.B "EGA,AND THE REST.....CHILL OUT.........."       
	DC.B -1	;END OF TEXT MARKER
	EVEN
TEXTAB1	DS.L 8000				;ABSOLUTE ADDRESSES FOR TEXT 1		
TPOS1	DC.L TEXTAB1				;POSITION IN TEXT 1
FONT	INCBIN MAGAFONT.DAT
	DCB.B 300,0

IKBD	LEA $FFFFFC00.W,A0
.L1	MOVE.B (A0),D1
	BTST #1,D1
	BEQ .L1
	MOVE.B D0,2(A0)
	RTS		

INIT	
;INTO SUPERVISOR
	BSR SUPER
;MOUSE OFF
	DC.W $A000
	DC.W $A00A
	MOVE.B #$12,D0
	BSR IKBD
;TIMER C OFF
	MOVE.W #5,-(A7)
	MOVE.W #26,-(A7)
	TRAP #14
	ADDQ.L #4,A7
;NEW STACK
	MOVEA.L (A7),A0
	MOVEA.L #MY,A7
	MOVE.L A0,(A7)
;CLEAR COLOURS
	LEA $FFFF8240,A0
	MOVEQ.W #15,D0
.L1	CLR.W (A0)+
	DBF D0,.L1	
;SET SCREENS
	CLR.W -(A7)
	PEA $78000	;PHYSICAL
	PEA $70000	;LOGICAL
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
;CLEAR SCREENS
	LEA $78000,A0
	LEA $70000,A1
	MOVE.W #32000/4-1,D0
.L2	CLR.L (A0)+
	CLR.L (A1)+
	DBF D0,.L2
;SAVE SYSTEM VARIABLES
	LEA VARS,A0
	MOVE.B $FFFFFA07.W,(A0)+
	MOVE.B $FFFFFA13.W,(A0)+
	MOVE.B $FFFFFA21.W,(A0)+
	MOVE.B $FFFFFA1B.W,(A0)+
	MOVE.L $70.W,(A0)+	
	MOVE.L $120.W,(A0)+
	MOVE.L $118.W,(A0)+
;ENABLE TIMER B
	CLR.B $FFFFFA07.W
	CLR.B $FFFFFA09.W
	ORI.B #1,$FFFFFA07.W
	ORI.B #1,$FFFFFA13.W
;NEW VECTORS
	BCLR #6,$FFFFFC09.W
	MOVE.L #KEY,$118.W
	BSET #6,$FFFFFA09.W
;IPL=3 AND EXIT
	MOVE.W #$2300,SR
	RTS	

VARS	DS.B 16
	DS.L 40
MY	DS.L 1	

SUPER	CLR.L -(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	MOVE.L D0,STACK
	RTS

USER	MOVE.L STACK,-(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	CLR.W -(A7)
	TRAP #1
STACK	DS.L 1

WAIT	CMPI.B #57+128,$FFFFFC02.W
	BNE WAIT
	RTS

KEY	BCLR #6,$FFFFFA11.W
	RTE

END	MOVE.W #$2700,SR
	JSR ENDMUS
;RESET SYSTEM VARIABLES
	LEA VARS,A0
	MOVE.B (A0)+,$FFFFFA07.W
	MOVE.B (A0)+,$FFFFFA13.W
	MOVE.B (A0)+,$FFFFFA21.W
	MOVE.B (A0)+,$FFFFFA1B.W
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$120.W
	MOVE.L (A0)+,$118.W
;RESET COLOURS
	MOVE.W #$777,$FFFF8240.W
	MOVE.W #0,$FFFF8242.W
	MOVE.W #0,$FFFF8246.W
;MOUSE ON
	DC.W $A000	
	DC.W $A009
	MOVE.B #$08,D0
	JSR IKBD
;ENABLE TIMER C
	MOVE.W #5,-(A7)
	MOVE.W #27,-(A7)
	TRAP #14
	ADDQ.L #4,A7
;SET SCREENS
	CLR.W -(A7)
	PEA $78000	;PHYSICAL
	PEA $78000	;LOGICAL
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
;EXIT
	MOVE.W #$2300,SR
	BRA USER	

SETMUS	MOVE.W #$4E75,MUZIX_G+106
	MOVE.W #$4E75,MUZIX_G+258
	MOVE.L #1,D6
	JSR MUZIX_G+46
	RTS

ENDMUS	JSR MUZIX_G+32+174
	MOVE.L #$700FF00,$FFFF8800.W
	MOVE.L #$8000000,$FFFF8800.W
	MOVE.L #$9000000,$FFFF8800.W
	MOVE.L #$A000000,$FFFF8800.W
	RTS

MUZIX_G	INCBIN MUZIX9.DAT


