;SOURCE CODE FOR THE HORIZONTAL TWIST
;SCROLL DEMO!
;
;DATA FILES USED:
;
;SCREEN3.DAT	- DATA FOR SCROLLING PICTURE
;H_TWIST3.S	- DATA TO CONTROL TWIST MOVEMENT
;VOL4.DAT	- VOLUME INDICATOR GRAFIX
;MUZIX1.DAT	- MUZIX DATA (WHAT ELSE?)
;MUZIX6.DAT	- MORE MUZIX!
;MEGAFONT.DAT	- FONT (AMIGA FONT,BUT I DONT LIKE THAT WORD!)
;BLAST2.DAT	- FONT FOR VERTICAL SCROLLERS
 
   	JSR INIT
	JSR CONVERT
	JSR SET1
	JSR SET2
	JSR SETADS2
	JSR COLS
	MOVE.W #132,COUNT
	JSR SETADS1
	JSR SETSCR
	MOVE.L #ENDMUS,STOPMUS
	MOVE.W #$2700,SR
	JSR SETMUS
	MOVE.L #VBL,$70.W
	MOVE.W #$2300,SR
	JSR WAIT
	JMP END

MCOUNT	DC.W 0
MFLAG	DC.W 0

SCROLL2	MOVEA.L TPOS2,A1
	LEA TABLE,A2
	MOVE.W OFF,D7	
	MOVEA.L (A1)+,A3
	LEA (A3,D7.W),A3	
	MOVE.W OFF2,D1
	BRA .L2
.L4	MOVEA.L (A1)+,A3
	MOVEQ.W #32,D1
.L2	CMPA.L #AD70,A2
	BGE .L5
	MOVE.W (A2)+,D0
.L1	MOVEA.L (A0)+,A4
	MOVE.W (A3),(A4)
	MOVE.W 2(A3),8(A4)
	MOVE.W (A3),144(A4)
	MOVE.W 2(A3),152(A4)
	DBF D0,.L1
	SUBQ.W #1,D1
	BEQ .L3
	ADDQ.L #4,A3
	BRA .L2
.L5	ADDQ.W #4,OFF
	CMPI.W #4*32,OFF
	BLT .L6
	MOVE.W #0,OFF
	ADDQ.L #4,TPOS2
	MOVE.W #32,OFF2
	SUBQ.W #1,LENGTH2
	BEQ .L7
	RTS
.L7	MOVE.W #1680-2,LENGTH2
	MOVE.L #TEXTAB2,TPOS2
	RTS
.L6	SUBQ.W #1,OFF2
	RTS
.L3	MOVEQ.W #32,D1
	BRA .L4

TABLE	DC.W 0,0,0,0,0,0,0,0,0,0,0,0
	DC.W 1,1,1,1,1,1,1
	DCB.W 36,2 
	DC.W 1,1,1,1,1,1,1
	DC.W 0,0,0,0,0,0,0,0,0,0,0,0

AD70	DS.L 160
AD78	DS.L 160
OFF	DC.W 0
OFF2	DC.W 32

SETADS2	LEA AD70,A0
	LEA AD78,A1
	LEA $70004+(5*160),A2
	LEA $78004+(5*160),A3
	MOVE.W #160-1,D0
.L1	MOVE.L A2,(A0)+
	MOVE.L A3,(A1)+
	LEA 160(A2),A2
	LEA 160(A3),A3
	DBF D0,.L1
	RTS

T1	CLR.B $FFFFFA1B.W
	MOVE.B #1,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.L #T2,$120.W
	MOVE.W #$110,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

T2	MOVE.L #T3,$120.W
	MOVE.W #$220,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

T3	MOVE.L #T4,$120.W
	MOVE.W #$330,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

T4	MOVE.L #T5,$120.W
	MOVE.W #$440,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

T5	MOVE.L #T6,$120.W
	MOVE.W #$550,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

T6	MOVE.L #T7,$120.W
	MOVE.W #$660,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

T7	CLR.B $FFFFFA1B.W
	MOVE.L #TB1,$120.W
	MOVE.B #23,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.W #$770,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB1	CLR.B $FFFFFA1B.W
	MOVE.B #1,$FFFFFA21.W
	MOVE.L #TB2,$120.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.W #$011,$FFFF8240+(2*8).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB2	MOVE.L #TB3,$120.W
	MOVE.W #$022,$FFFF8240+(2*8).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB3	MOVE.L #TB4,$120.W
	MOVE.W #$033,$FFFF8240+(2*8).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB4	MOVE.L #TB5,$120.W
	MOVE.W #$044,$FFFF8240+(2*8).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB5	MOVE.L #TB6,$120.W
	MOVE.W #$055,$FFFF8240+(2*8).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB6	MOVE.L #TB7,$120.W
	MOVE.W #$066,$FFFF8240+(2*8).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB7	CLR.B $FFFFFA1B.W
	MOVE.L #TB8,$120.W
	MOVE.B #118,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.W #$077,$FFFF8240+(2*8).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB8	CLR.B $FFFFFA1B.W
	MOVE.B #1,$FFFFFA21.W
	MOVE.L #TB9,$120.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.W #$066,$FFFF8240+(2*8).W
	MOVE.W #$660,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB9	MOVE.L #TB10,$120.W
	MOVE.W #$055,$FFFF8240+(2*8).W
	MOVE.W #$550,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB10	MOVE.L #TB11,$120.W
	MOVE.W #$044,$FFFF8240+(2*8).W
	MOVE.W #$440,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB11	MOVE.L #TB12,$120.W
	MOVE.W #$033,$FFFF8240+(2*8).W
	MOVE.W #$330,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB12	MOVE.L #TB13A,$120.W
	MOVE.W #$022,$FFFF8240+(2*8).W
	MOVE.W #$220,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB13A	MOVE.L #TB13B,$120.W
	MOVE.W #$011,$FFFF8240+(2*8).W
	MOVE.W #$110,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB13B	MOVE.L #TB13,$120.W
	MOVE.W #$0,$FFFF8240+(2*8).W
	MOVE.W #$0,$FFFF8240+(2*4).W
	BCLR #0,$FFFFFA0F.W
	RTE

TB13	CLR.B $FFFFFA1B.W
	MOVEM.L A0/A1,-(A7)
	LEA FONT+2,A0
	LEA $FFFF8242.W,A1
	MOVE.W (A0)+,(A1)+
	MOVE.W (A0)+,(A1)+
	MOVE.W (A0)+,(A1)+
	MOVE.W (A0)+,(A1)+
	MOVE.W (A0)+,(A1)+
	MOVE.W (A0)+,(A1)+
	MOVE.W (A0)+,(A1)+
	MOVEM.L (A7)+,A0/A1
	BCLR #0,$FFFFFA0F.W
	RTE

SETSCR	LEA SCR,A2
	MOVEA.L ADS70,A0
	MOVEA.L ADS78,A1
	MOVE.W #132-1,D0
.L1	REPT 16
	MOVE.W (A2),(A0)
	MOVE.W (A2)+,(A1)
	ADDQ.W #8,A0
	ADDQ.W #8,A1
	ENDR
	LEA (4*8)(A0),A0
	LEA (4*8)(A1),A1
	DBF D0,.L1
	RTS

UP	;A0=S,A1=D
	MOVEA.L (A0)+,A2
	MOVE.W #131-1,D0
.L1	MOVEA.L (A0)+,A3
	MOVEA.L (A1)+,A4
	MOVE.W (A3),(A4)
	MOVE.W 8(A3),8(A4)
	MOVE.W 16(A3),16(A4)
	MOVE.W 24(A3),24(A4)
	MOVE.W 32(A3),32(A4)
	MOVE.W 40(A3),40(A4)
	MOVE.W 48(A3),48(A4)
	MOVE.W 56(A3),56(A4)
	MOVE.W 64(A3),64(A4)
	MOVE.W 72(A3),72(A4)
	MOVE.W 80(A3),80(A4)
	MOVE.W 88(A3),88(A4)
	MOVE.W 96(A3),96(A4)
	MOVE.W 104(A3),104(A4)
	MOVE.W 112(A3),112(A4)
	MOVE.W 120(A3),120(A4)
	DBF D0,.L1	
	MOVEA.L (A1)+,A4
	MOVE.W (A2),(A4)
	MOVE.W 8(A2),8(A4)
	MOVE.W 16(A2),16(A4)
	MOVE.W 24(A2),24(A4)
	MOVE.W 32(A2),32(A4)
	MOVE.W 40(A2),40(A4)
	MOVE.W 48(A2),48(A4)
	MOVE.W 56(A2),56(A4)
	MOVE.W 64(A2),64(A4)
	MOVE.W 72(A2),72(A4)
	MOVE.W 80(A2),80(A4)
	MOVE.W 88(A2),88(A4)
	MOVE.W 96(A2),96(A4)
	MOVE.W 104(A2),104(A4)
	MOVE.W 112(A2),112(A4)
	MOVE.W 120(A2),120(A4)
	SUBQ.W #1,COUNT
	BGT .L2
	MOVE.L #LEFT,ZZZ+2
	MOVE.L #LEFT,ZZZ2+2
	MOVE.W #16,COUNT	
.L2	RTS 

DOWN	;A0=S,A1=D
	MOVEA.L (A1)+,A2
	MOVE.W #131-1,D0
.L1	MOVEA.L (A0)+,A3
	MOVEA.L (A1)+,A4
	MOVE.W (A3),(A4)
	MOVE.W 8(A3),8(A4)
	MOVE.W 16(A3),16(A4)
	MOVE.W 24(A3),24(A4)
	MOVE.W 32(A3),32(A4)
	MOVE.W 40(A3),40(A4)
	MOVE.W 48(A3),48(A4)
	MOVE.W 56(A3),56(A4)
	MOVE.W 64(A3),64(A4)
	MOVE.W 72(A3),72(A4)
	MOVE.W 80(A3),80(A4)
	MOVE.W 88(A3),88(A4)
	MOVE.W 96(A3),96(A4)
	MOVE.W 104(A3),104(A4)
	MOVE.W 112(A3),112(A4)
	MOVE.W 120(A3),120(A4)
	DBF D0,.L1	
	MOVEA.L (A0)+,A4
	
	MOVE.W (A4),(A2)
	MOVE.W 8(A4),8(A2)
	MOVE.W 16(A4),16(A2)
	MOVE.W 24(A4),24(A2)
	MOVE.W 32(A4),32(A2)
	MOVE.W 40(A4),40(A2)
	MOVE.W 48(A4),48(A2)
	MOVE.W 56(A4),56(A2)
	MOVE.W 64(A4),64(A2)
	MOVE.W 72(A4),72(A2)
	MOVE.W 80(A4),80(A2)
	MOVE.W 88(A4),88(A2)
	MOVE.W 96(A4),96(A2)
	MOVE.W 104(A4),104(A2)
	MOVE.W 112(A4),112(A2)
	MOVE.W 120(A4),120(A2)
	SUBQ.W #1,COUNT
	BGT .L2
	MOVE.L #RIGHT,ZZZ+2
	MOVE.L #RIGHT,ZZZ2+2
	MOVE.W #16,COUNT	
.L2	RTS 
	
LEFT	;A0=S,A1=D
	MOVE.W #132-1,D0
.L1	MOVEA.L (A0)+,A2
	MOVEA.L (A1)+,A3
	MOVE.W (A2),D1

	MOVE.W 8(A2),(A3)
	MOVE.W 16(A2),8(A3)
	MOVE.W 24(A2),16(A3)
	MOVE.W 32(A2),24(A3)
	MOVE.W 40(A2),32(A3)
	MOVE.W 48(A2),40(A3)
	MOVE.W 56(A2),48(A3)
	MOVE.W 64(A2),56(A3)
	MOVE.W 72(A2),64(A3)
	MOVE.W 80(A2),72(A3)
	MOVE.W 88(A2),80(A3)
	MOVE.W 96(A2),88(A3)
	MOVE.W 104(A2),96(A3)
	MOVE.W 112(A2),104(A3)
	MOVE.W 120(A2),112(A3)
	MOVE.W D1,120(A3)
	
	DBF D0,.L1
	SUBQ.W #1,COUNT
	BGT .L2
	MOVE.L #DOWN,ZZZ+2
	MOVE.L #DOWN,ZZZ2+2
	MOVE.W #132,COUNT
.L2	RTS	

RIGHT	;A0=S,A1=D
	MOVE.W #132-1,D0
.L1	MOVEA.L (A0)+,A2
	MOVEA.L (A1)+,A3
	MOVEA.L A3,A4
	
	MOVE.W (A2),8(A3)
	MOVE.W 8(A2),16(A3)
	MOVE.W 16(A2),24(A3)
	MOVE.W 24(A2),32(A3)
	MOVE.W 32(A2),40(A3)
	MOVE.W 40(A2),48(A3)
	MOVE.W 48(A2),56(A3)
	MOVE.W 56(A2),64(A3)
	MOVE.W 64(A2),72(A3)
	MOVE.W 72(A2),80(A3)
	MOVE.W 80(A2),88(A3)
	MOVE.W 88(A2),96(A3)
	MOVE.W 96(A2),104(A3)
	MOVE.W 104(A2),112(A3)
	MOVE.W 112(A2),120(A3)

	MOVE.W 120(A2),(A4)
	DBF D0,.L1
	SUBQ.W #1,COUNT
	BGT .L2
	MOVE.L #UP,ZZZ+2
	MOVE.L #UP,ZZZ2+2
	MOVE.W #132,COUNT
.L2	RTS	


SCR	INCBIN SCREEN3.DAT
ADS70	DS.L 132
ADS78	DS.L 132
COUNT	DC.W 132

SETADS1	LEA ADS70,A0
	LEA ADS78,A1
	LEA $70006+(34*160)+16,A2
	LEA $78006+(34*160)+16,A3
	MOVE.W #132-1,D1
.L2	MOVE.L A2,(A0)+
	MOVE.L A3,(A1)+
	LEA 160(A2),A2
	LEA 160(A3),A3 
	DBF D1,.L2
	RTS	

SCROLL	MOVEA.L TPOS1,A0
	ADDQ.L #4,TPOS1
	SUBQ.W #1,LENGTH1
	BGT .START
	MOVE.L #TEXTAB1,TPOS1
	MOVE.W #(560*4)-40,LENGTH1
.START	MOVEQ.W #0,D5
	MOVEQ.W #20-1,D6
	MOVEA.L WAVE,A1
	SUBI.L #16,WAVE
	CMPI.L #W1S,WAVE
	BGT .L1
	MOVE.L #W1,WAVE
.L1	MOVE.W (A1),D0
	MOVE.W 2(A1),D1
	MOVE.L 4(A1),D2
	MOVE.L 8(A1),D3
	MOVE.L 12(A1),D4
	MOVEA.L (A0)+,A4
	LEA (A2,D4.W),A3
	SUBQ.W #1,D0
	LEA (A3,D5.W),A3
	MOVE.W D0,D7
	LEA (A4,D2.W),A5
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)
	LEA 160(A3),A3
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)
	LEA 160(A3),A3
.A	MOVE.B (A5)+,(A3)
	MOVE.B (A5)+,2(A3)
	MOVE.B (A5)+,4(A3)
	LEA (A5,D1.W),A5
	LEA 160(A3),A3
	DBF D0,.A
	LEA (A4,D3.W),A5
.B	MOVE.B (A5)+,(A3)
	MOVE.B (A5)+,2(A3)
	MOVE.B (A5)+,4(A3)
	LEA (A5,D1.W),A5
	LEA 160(A3),A3
	DBF D7,.B
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)
	LEA 160(A3),A3
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)
	ADDQ.W #1,D5
	MOVE.W (A1)+,D0
	MOVE.W (A1)+,D1
	MOVE.L (A1)+,D2
	MOVE.L (A1)+,D3
	MOVE.L (A1)+,D4
	MOVEA.L (A0)+,A4
	LEA (A2,D4.W),A3
	SUBQ.W #1,D0
	LEA (A3,D5.W),A3
	MOVE.W D0,D7
	LEA (A4,D2.W),A5
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)
	LEA 160(A3),A3
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)
	LEA 160(A3),A3
.A1	MOVE.B (A5)+,(A3)
	MOVE.B (A5)+,2(A3)
	MOVE.B (A5)+,4(A3)
	LEA (A5,D1.W),A5
	LEA 160(A3),A3
	DBF D0,.A1
	LEA (A4,D3.W),A5
.B1	MOVE.B (A5)+,(A3)
	MOVE.B (A5)+,2(A3)
	MOVE.B (A5)+,4(A3)
	LEA (A5,D1.W),A5
	LEA 160(A3),A3
	DBF D7,.B1
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)
	LEA 160(A3),A3
	CLR.B (A3)
	CLR.B 2(A3)
	CLR.B 4(A3)
	ADDQ.W #7,D5
	DBF D6,.L1
	RTS

W1S	INCLUDE H_TWIST3.S
W1	INCLUDE H_TWIST3.S
	
WAVE	DC.L W1
AD1	DS.L 1

COLS	LEA $FFFF8240.W,A0
	LEA FONT,A1
	MOVEQ.W #8-1,D0
.L1	MOVE.W (A1)+,(A0)+
	DBF D0,.L1
	MOVE.W #$777,$FFFF8240+(8*2).W
	RTS

SINE	DC.W 0,0,4,4,8,8
	DCB.W 20,16
	DCB.W 20,-16
	DC.W -8,-8,-4,-4,0,0
SINEND

LAD70	DS.L 1
LAD78	DS.L 1
SINEAD	DC.L SINE
	
CONVERT	
	LEA TEXT1(PC),A0
	LEA CTAB(PC),A1
.A	MOVE.B (A0)+,D0
	MOVEA.L A1,A2
	MOVEQ.W #21-1,D1
.B	CMP.B (A2)+,D0 		;COMPARE CHARACTER WITH THOSE IN CTAB
	BEQ .C
	ADDQ.L #1,A2
	DBF D1,.B	
	BRA .D
.C	MOVE.B (A2),-1(A0)	;CHANGE IT TO CORRESPONDING CHARACTER IN CTAB
.D	CMPA.L #T_END,A0
	BLT .A
	MOVE.W #(560*4)-40,LENGTH1	;SET UP LENGTHS FOR ALL SCROLLINES
	MOVE.W #1680-2,LENGTH2	;SET UP LENGTHS FOR ALL SCROLLINES
	RTS	
LENGTH1	DC.W 0
LENGTH2	DC.W 0
CTAB	DC.B '0','['
	DC.B '1','\'
	DC.B '2',']'
	DC.B '3','^'
	DC.B '4','_'
	DC.B '5',96
	DC.B '6',97
	DC.B '7',98
	DC.B '8',99
	DC.B '9',100,'(',101,'!',102,')',103,':',104
	DC.B '-',105,'.',106,',',107,"'",108,'"',109
	DC.B "?",110," ",111
	DC.B -1

TEXT1	DC.B ".................YO DUDES! FLASHBAK JACK"
	DC.B " HERE. NOT MUCH TIME TO TALK, I'M ON MY "
	DC.B "OUT OF POWERMANS HOUSE. I AM GOING TO MY"
        DC.B " HOME SWEET HOME, SO I'LL SEE YOU THERE."
        DC.B "................BACK HOME NOW AND THE TI"
        DC.B "ME IS 5.0 PM. I'M GOING TO TALK ABOUT MU"
        DC.B "XIC RIPPING ON THE ST. RIPPING MUZIX IS "
        DC.B "ONE THING, BUT WHERE YOU GET IT FROM IS "
        DC.B "ANOTHER. POWER MAN RIPPED ALL THE MUZIX "
        DC.B "FOR THIS DEMO BUT ALL THE MUZIX ON OUR N"
        DC.B "EXT DEMO WILL BE RIPPED BY ME FROM THE A"
        DC.B "MIGA..........CREDITS ON THIS DEMO GO TO"
        DC.B " POWER MAN FOR CODING,GRAFIX AND MUZIX R"
        DC.B "IPPING. CHILL OUT DUDES................."
	DC.B -1
TEXT2	DC.B "              WOW!  YES IS THE BARREL SC"
	DC.B "ROLLER. THIS DEMO WAS CODED BY POWER MAN"
	DC.B " AND I ALSO RIPPED THE MUZIX AND DID THE"
	DC.B " GRAFIX. DESIGN IS BY JACK. WELL, WHAT S"
	DC.B "HALL WE TALK ABOUT? I KNOW, I WILL TALK "
	DC.B "ABOUT 3D VECTORS! WELL, I AM WRITING SOM"
	DC.B "E 3D VECTOR ROUTINES FOR MY NEXT DEMO, S"
	DC.B "O I CAN MAKE A 3D VECTOR DEMO AND A VECT"
	DC.B "ORBALL DEMO. NOT JUST LINE VECTORS THOUG"
	DC.B "H, I WILL ALSO HAVE FILLED VECTORS. BY T"
	DC.B "HE WAY, IT IS NOW 2.18 PM ON JANUARY 12,"
	DC.B " 1991, AND IT IS A SUNNY FRIDAY AFTERNOO"
	DC.B "N. I SHOULD BE STUDYING AT THE COLLEGE L"
	DC.B "IBRARY, SO I HOPE MY TEACHER DIDN'T SEE "
	DC.B "ME HEADING IN THE DIRECTION OF THE CAR P"
	DC.B "ARK! SPEAKING OF CARS, SOME STUPID COW W"
	DC.B "ROTE OFF MY OLD CAR ABOUT A MONTH AGO. I"
	DC.B " MEAN, SHE SMASHED INTO MY REAR AT 40 MP"
	DC.B "H WHILE I WAS WAITING TO TURN RIGHT. ONL"
	DC.B "Y A WOMAN COULD DO THAT..........ANYWAY,"
	DC.B " HER INSURANCE PAID UP AND I GOT MYSELF "
	DC.B "A NEW CAR. NOT BRAND NEW, BUT VERY NICE."
	DC.B " ITS A GOLF GL, ONLY THING I DONT LIKE I"
	DC.B "S THE AUTOMATIC GEARBOX (MAKES YOU LAZY!"
	DC.B "), BUT ITS GOT A NICE STEREO WITH GRAPHI"
	DC.B "C EQUALIZER, JUST THE THING FOR CRUISING"
	DC.B " DOWN THE GREAT WEST ROAD WITH VANILLA I"
	DC.B "CE PUMPING OUT THE SPEAKERS! BY THE WAY,"
	DC.B " IF YOU ARE THAT BITCH WHO WROTE OFF MY "
	DC.B "CAR (I AM LUCKY TO BE ALIVE!) THEN SUCK!"
	DC.B " RIGHT THEN, LETS COVER ONE MORE SUBJECT"
	DC.B " BEFORE I CHIP: DEMOS! YES, I MUST MAKE "
	DC.B "A REQUEST (SORRY IF YOU HAVE READ THIS I"
	DC.B "N ANOTHER SCROLLER). I NEED SOME GRAFIX "
	DC.B "AND MUZIX FOR MY NEXT DEMO. PLEASE SEND "
	DC.B "ME ANY GRAFIX (FONTS, LOGOS ETC...) AND "
	DC.B "MUZIX (SOUNDCHIP, DIGI, .MOD FILES ETC) "
	DC.B "WITH THE CODE TO PLAY YOUR MUZIX. YOU WI"
	DC.B "LL FIND MY ADDRESS ON RESET. YOU MIGHT S"
	DC.B "EE A PREVIEW SCREEN OF MY NEXT DEMO SOON"
	DC.B "..................CHILL OUT............."
	DC.B "LETS GO................................."
T_END	DC.B -1
	;1680 IN TEXT2
	;LINE 635
	EVEN

VBL	CLR.B $FFFFFA1B.W
	MOVE.B #5,$FFFFFA21.W
	MOVE.L #T1,$120.W
	MOVE.B #8,$FFFFFA1B.W	
	MOVE.W #$070,$FFFF8242.W
	MOVE.W #$700,$FFFF8246.W
	MOVE.W #0,$FFFF8240+(2*4).W
	LEA $FFFFFC02.W,A0
	CMPI.W #1,MFLAG
	BEQ .L2
	CMPI.B #59,(A0)
	BEQ MUZIX_1
	CMPI.B #60,(A0)
	BEQ MUZIX_2
.L1	MOVEA.L $4CE+4.W,A0
	CMPA.L #0,A0
	BEQ VB
	JSR (A0) 
.L2	CMPI.B #60,MCOUNT
	BEQ VB
	MOVE.W #0,MFLAG
VB	JMP SWAP70
MUZIX_1	MOVEA.L STOPMUS,A0
	JSR (A0)
	MOVE.L #ENDMUS,STOPMUS
	MOVE.L #VBL,$70.W
	JSR SETMUS		
	BRA VB
MUZIX_2	MOVE.W #1,MFLAG
	MOVEA.L STOPMUS,A0
	JSR (A0)
	MOVE.L #ENDMUS2,STOPMUS
	MOVE.L #VBL,$70.W
	CLR.L $4CE+4.W
	JSR SETMUS2		
	BRA VB

SWAP70	MOVE.B #0,$FFFF8203.W
	MOVE.L #SWAP78,VB+2
	;DRAW ON 70
	LEA $70000,A2
	BSR SCROLL
	LEA ADS78,A0
	LEA ADS70,A1
ZZZ	JSR UP
	LEA D_ADS70,A2
	JSR VOLUME
	LEA AD70,A0
	JSR SCROLL2	
	RTE

SWAP78	MOVE.B #$80,$FFFF8203.W
	MOVE.L #SWAP70,VB+2
	;DRAW ON 78
	LEA $78000,A2
	BSR SCROLL
	LEA ADS70,A0
	LEA ADS78,A1
ZZZ2	JSR UP
	LEA D_ADS78,A2
	JSR VOLUME
	LEA AD78,A0
	JSR SCROLL2
	RTE

IKBD	LEA $FFFFFC00.W,A0
.L1	MOVE.B (A0),D1
	BTST #1,D1
	BEQ .L1
	MOVE.B D0,2(A0)
	RTS

INIT	
;INTO SUPERVISOR
	BSR SUPER
;MOUSE OFF
	DC.W $A000
	DC.W $A00A
	MOVE.B #$12,D0
	BSR IKBD
;TIMER C OFF
	MOVE.W #5,-(A7)
	MOVE.W #26,-(A7)
	TRAP #14
	ADDQ.L #4,A7
;NEW STACK
	MOVEA.L (A7),A0
	MOVEA.L #MY,A7
	MOVE.L A0,(A7)
;CLEAR COLOURS
	LEA $FFFF8240,A0
	MOVEQ.W #15,D0
.L1	CLR.W (A0)+
	DBF D0,.L1	
;SET SCREENS
	CLR.W -(A7)
	PEA $78000
	PEA $70000
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
;CLEAR SCREENS
	LEA $78000,A0
	LEA $70000,A1
	MOVE.W #32000/4-1,D0
.L2	CLR.L (A0)+
	CLR.L (A1)+
	DBF D0,.L2
;SAVE SYSTEM VARIABLES
	LEA VARS,A0
	MOVE.B $FFFFFA07.W,(A0)+
	MOVE.B $FFFFFA13.W,(A0)+
	MOVE.B $FFFFFA21.W,(A0)+
	MOVE.B $FFFFFA1B.W,(A0)+
	MOVE.B $FFFFFA17.W,(A0)+
	MOVE.B $FFFFFA09.W,(A0)+
	MOVE.B $FFFFFA1F.W,(A0)+
	MOVE.B $FFFFFA19.W,(A0)+
	MOVE.L $70.W,(A0)+	
	MOVE.L $120.W,(A0)+
	MOVE.L $118.W,(A0)+
	CLR.B $FFFFFA07.W
	CLR.B $FFFFFA09.W
	ORI.B #1,$FFFFFA07.W
	ORI.B #1,$FFFFFA13.W
;NEW VECTORS
	BCLR #6,$FFFFFA09.W
	MOVE.L #KEY,$118.W
	BSET #6,$FFFFFA09.W
;IPL=3 AND EXIT
	MOVE.W #$2300,SR
	RTS	

PHYSIC	DS.L 1
LOGIC	DS.L 1
SWAP	DC.B 0,0
SWAP2	DC.B 0,0
VARS	DS.L 10
	DS.L 40
MY	DS.L 1	

SUPER	CLR.L -(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	MOVE.L D0,STACK
	RTS

USER	MOVE.L STACK,-(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	CLR.W -(A7)
	TRAP #1
STACK	DS.L 1

WAIT	CMPI.B #57,$FFFFFC02.W
	BNE WAIT
.L1	CMPI.B #57,$FFFFFC02.W
	BEQ .L1
	RTS

KEY	MOVE.B $FFFFFC02.W,MCOUNT
	BCLR #6,$FFFFFA11.W
	RTE

STOPMUS	DS.L 1

END	MOVE.W #$2700,SR
	MOVEA.L STOPMUS,A0
	JSR (A0)
;RESET SYSTEM VARIABLES
	LEA VARS,A0
	MOVE.B (A0)+,$FFFFFA07.W
	MOVE.B (A0)+,$FFFFFA13.W
	MOVE.B (A0)+,$FFFFFA21.W
	MOVE.B (A0)+,$FFFFFA1B.W
	MOVE.B (A0)+,$FFFFFA17.W
	MOVE.B (A0)+,$FFFFFA09.W
	MOVE.B (A0)+,$FFFFFA1F.W
	MOVE.B (A0)+,$FFFFFA19.W
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$120.W
	MOVE.L (A0)+,$118.W
;RESET COLOURS
	MOVE.W #$777,$FFFF8240.W
	MOVE.W #0,$FFFF8242.W
	MOVE.W #0,$FFFF8246.W
;MOUSE ON
	DC.W $A000	
	DC.W $A009
	MOVE.B #$08,D0
	BSR IKBD
;ENABLE TIMER C
	MOVE.W #5,-(A7)
	MOVE.W #27,-(A7)
	TRAP #14
	ADDQ.L #4,A7
;SET SCREENS
	CLR.W -(A7)
	PEA $78000	;PHYSICAL
	PEA $78000	;LOGICAL
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7
;EXIT
	MOVE.W #$2300,SR
	BRA USER	

VOL	INCBIN VOL4.DAT

V_ADS	DC.L VOL,VOL+(1*(8*30))
	DC.L VOL+(2*(8*30)),VOL+(3*(8*30))
	DC.L VOL+(4*(8*30)),VOL+(5*(8*30))
	DC.L VOL+(6*(8*30)),VOL+(7*(8*30))
	DC.L VOL+(8*(8*30)),VOL+(9*(8*30))
	DC.L VOL+(10*(8*30)),VOL+(11*(8*30))
	DC.L VOL+(12*(8*30)),VOL+(13*(8*30))
	DC.L VOL+(14*(8*30)),VOL+(15*(8*30))

D_ADS70	DC.L $70000+48
	DC.L $70000+48+24
	DC.L $70000+48+24+24
	
D_ADS78	DC.L $78000+48
	DC.L $78000+48+24
	DC.L $78000+48+24+24

VOLUME	;A2 = D_ADS70 OR 78
	MOVEQ.W #0,D1
	MOVEQ.W #0,D2
	MOVEQ.W #0,D3
	LEA $FFFF8800.W,A0
	MOVE.B #8,(A0)
	MOVE.B (A0),D1
	MOVE.B #9,(A0)
	MOVE.B (A0),D2
	MOVE.B #10,(A0)
	MOVE.B (A0),D3
	BTST #4,D1
	BEQ .L1
	MOVE.B #10,D1
.L1	BTST #4,D2
	BEQ .L2
	MOVE.B #10,D2
.L2	BTST #4,D3
	BEQ .L3
	MOVE.B #10,D3
.L3	LEA V_ADS,A1
	ASL.W #2,D1
	ASL.W #2,D2
	ASL.W #2,D3
	LEA (A1,D1.W),A3
	BSR DRAW_V
	LEA (A1,D2.W),A3
	BSR DRAW_V
	LEA (A1,D3.W),A3
DRAW_V	MOVEA.L (A3),A3
	MOVE.L (A2)+,A4

	MOVE.L (A3)+,(A4)
	MOVE.L (A3)+,8(A4)
	MOVE.L (A3)+,160(A4)
	MOVE.L (A3)+,168(A4)
	MOVE.L (A3)+,320(A4)
	MOVE.L (A3)+,328(A4)
	MOVE.L (A3)+,480(A4)
	MOVE.L (A3)+,488(A4)
	MOVE.L (A3)+,640(A4)
	MOVE.L (A3)+,648(A4)
	MOVE.L (A3)+,800(A4)
	MOVE.L (A3)+,808(A4)
	MOVE.L (A3)+,960(A4)
	MOVE.L (A3)+,968(A4)
	MOVE.L (A3)+,1120(A4)
	MOVE.L (A3)+,1128(A4)
	MOVE.L (A3)+,1280(A4)
	MOVE.L (A3)+,1288(A4)
	MOVE.L (A3)+,1440(A4)
	MOVE.L (A3)+,1448(A4)
	MOVE.L (A3)+,1600(A4)
	MOVE.L (A3)+,1608(A4)
	MOVE.L (A3)+,1760(A4)
	MOVE.L (A3)+,1768(A4)
	MOVE.L (A3)+,1920(A4)
	MOVE.L (A3)+,1928(A4)
	MOVE.L (A3)+,2080(A4)
	MOVE.L (A3)+,2088(A4)
	MOVE.L (A3)+,2240(A4)
	MOVE.L (A3)+,2248(A4)
	MOVE.L (A3)+,2400(A4)
	MOVE.L (A3)+,2408(A4)
	MOVE.L (A3)+,2560(A4)
	MOVE.L (A3)+,2568(A4)
	MOVE.L (A3)+,2720(A4)
	MOVE.L (A3)+,2728(A4)
	MOVE.L (A3)+,2880(A4)
	MOVE.L (A3)+,2888(A4)
	MOVE.L (A3)+,3040(A4)
	MOVE.L (A3)+,3048(A4)
	MOVE.L (A3)+,3200(A4)
	MOVE.L (A3)+,3208(A4)
	MOVE.L (A3)+,3360(A4)
	MOVE.L (A3)+,3368(A4)
	MOVE.L (A3)+,3520(A4)
	MOVE.L (A3)+,3528(A4)
	MOVE.L (A3)+,3680(A4)
	MOVE.L (A3)+,3688(A4)
	MOVE.L (A3)+,3840(A4)
	MOVE.L (A3)+,3848(A4)
	MOVE.L (A3)+,4000(A4)
	MOVE.L (A3)+,4008(A4)
	MOVE.L (A3)+,4160(A4)
	MOVE.L (A3)+,4168(A4)
	MOVE.L (A3)+,4320(A4)
	MOVE.L (A3)+,4328(A4)
	MOVE.L (A3)+,4480(A4)
	MOVE.L (A3)+,4488(A4)
	MOVE.L (A3)+,4640(A4)
	MOVE.L (A3)+,4648(A4)
	RTS

SETMUS	MOVEQ.W #1,D0
	JSR MUZIX1
	MOVE.L #MUZIX1+($52D2C-$52C6C),$4CE+4.W
	RTS
ENDMUS	CLR.L $4CE+4.W
	MOVE.L #$700FF00,$FFFF8800.W
	MOVE.L #$8000000,$FFFF8800.W
	MOVE.L #$9000000,$FFFF8800.W
	MOVE.L #$A000000,$FFFF8800.W
	RTS
MUZIX1	INCBIN MUZIX1.DAT

SETMUS2	MOVE.W #$4E75,MUZIX_E+106
	MOVE.W #$4E75,MUZIX_E+258
	MOVE.L #1,D6
	JSR MUZIX_E+46
	RTS

ENDMUS2	MOVE.L #$700FF00,$FFFF8800.W
	MOVE.L #$8000000,$FFFF8800.W
	MOVE.L #$9000000,$FFFF8800.W
	MOVE.L #$A000000,$FFFF8800.W
	RTS
MUZIX_E	INCBIN MUZIX6.DAT

SETLOGO	MOVE.L #SINE,SINEAD
	MOVE.L #SPR70,LAD70
	MOVE.L #SPR78,LAD78
	RTS

SPR70	DS.L 192
SPR78	DS.L 192

SETADS	LEA $78000+16,A0
	LEA $70000+16,A1
	LEA SPR70,A2
	LEA SPR78,A3

SETAD	MOVE.W #192-1,D0
.L1	MOVE.L A0,(A3)+
	MOVE.L A1,(A2)+
	LEA 160(A0),A0
	LEA 160(A1),A1
	DBF D0,.L1
	RTS

SET1	LEA FONT+16,A0
	LEA TEXTAB1,A1
	LEA TEXT1,A2
.L1	CMPI.B #-1,(A2)
	BEQ .L2
	MOVEQ.W #0,D0
	MOVE.B (A2)+,D0
	SUBI.B #'A',D0
	MULU #(12*26),D0
	LEA (A0,D0.W),A3
	MOVE.L A3,(A1)+
	LEA (3*26)(A3),A3
	MOVE.L A3,(A1)+
	LEA (3*26)(A3),A3
	MOVE.L A3,(A1)+
	LEA (3*26)(A3),A3
	MOVE.L A3,(A1)+
	BRA .L1
.L2	RTS

SET2	LEA FONT2,A0
	LEA TEXTAB2,A1
	LEA TEXT2,A2
.L1	CMPI.B #-1,(A2)
	BEQ .L2
	MOVEQ.W #0,D0
	MOVE.B (A2)+,D0
	SUBI.B #'A',D0
	MULU #(4*32),D0
	LEA (A0,D0.W),A3
	MOVE.L A3,(A1)+
	BRA .L1
.L2	RTS

TPOS1	DC.L TEXTAB1
TEXTAB1	DS.L (560*4)
TPOS2	DC.L TEXTAB2
TEXTAB2	DS.L 1680

FONT	INCBIN MEGAFONT.DAT
	DCB.B 300,0
	EVEN
FONT2	INCBIN BLAST2.DAT
