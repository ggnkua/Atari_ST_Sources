;RASTER COLOURS BY THE POWER MAN
;OF THE WILD BOYS. USE THE SOURCE
;CODE WISELY, OH ASPIRING ONE, AND
;LET THE FORCE OF POWER MAN MAKE
;YOU A GREAT CODER (don't worry, he's
;just drunk or something...).



	CLR.L -(A7)		;SUPERVISOR MODE
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	MOVE.L D0,STACK
	CLR.W -(A7)		;SET SCREEN ADDRESSES AND LOW RES
	PEA $78000
	PEA $78000
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7			
	MOVE.L $118.W,OLD118
	MOVE.L $120.W,OLD120
	MOVE.L $70.W,OLD70
	MOVE.B $FFFFFA07.W,OLDA07
	MOVE.B $FFFFFA13.W,OLDA13
	MOVE.B $FFFFFA1B.W,OLDA1B
	MOVE.B $FFFFFA21.W,OLDA21
	DC.W $A000
	DC.W $A00A			;HIDE MOUSE
	MOVE.B #$12,$FFFFFC02.W 	;DISABLE MOUSE
	MOVE.L #KEY,$118.W		;NEW KEYBOARD ROUTINE
	MOVE.W #5,-(A7)			;5=TIMER C INTERRUPT
	MOVE.W #26,-(A7)		;26=DISBALE INTERRUPT
	TRAP #14
	ADDQ.L #4,A7
	BSR CLEAR			;CLEAR SCREEN
 	ORI.B #1,$FFFFFA07.W		;ENABLE TIMER B
	ORI.B #1,$FFFFFA13.W
	MOVE.L #TIMERB,$120.W
	MOVE.L #VBL,$70.W
WAIT	CMPI.B #57,$FFFFFC02.W		;SPACE BAR?
	BNE WAIT
	MOVE.W #$2700,SR		;DISBALE INTERRUPTS
	MOVE.L OLD70,$70.W
	MOVE.L OLD120,$120.W
	MOVE.L OLD118,$118.W
	MOVE.B OLDA07,$FFFFFA07.W
	MOVE.B OLDA13,$FFFFFA13.W
	MOVE.B OLDA1B,$FFFFFA1B.W
	MOVE.B OLDA21,$FFFFFA21.W
	MOVE.W #5,-(A7)			;5=TIMER C INTERRUPT
	MOVE.W #27,-(A7)		;27=ENABLE INTERRUPT
	TRAP #14
	ADDQ.L #4,A7
	DC.W $A000
	DC.W $A009			;SHOW MOUSE
	MOVE.B #$08,$FFFFFC02.W 	;ENABLE MOUSE
	MOVE.W #$777,$FFFF8240.W	;SET COLOUR 0 TO WHITE
	MOVE.W #$2300,SR
	MOVE.L STACK,-(A7)
	MOVE.W #32,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	CLR.L -(A7)
	TRAP #1

VBL	CLR.B $FFFFFA1B.W	    ;STOP TIMER B FOR SYNCRONISATION
	MOVE.B #10,$FFFFFA21.W      ;COUNTER=10 SCANLINES
	MOVE.L #COLOURS+2,PAL       ;START OF COLOUR TABLE
        MOVE.W COLOURS,$FFFF8240.W  ;SET COLOUR 0 TO FIRST COLOUR
	MOVE.B #8,$FFFFFA1B.W	    ;START TIMER B
	RTE			    ;PISS OFF

TIMERB	CLR.B $FFFFFA1B.W	    ;STOP TIMER B
	MOVE.L A0,-(A7)		    ;SAVE A0
	MOVEA.L PAL,A0              ;A0=ADDRESS OF COLOUR
	MOVE.W (A0)+,$FFFF8240.W    ;COLOUR=0 NEW COLOUR
        MOVE.L A0,PAL		    ;PAL=NEXT COLOUR IN TABLE
	MOVEA.L (A7)+,A0	    ;RESTORE A0
	MOVE.B #10,$FFFFFA21.W      ;COUNTER=10 SCANLINES
	MOVE.B #8,$FFFFFA1B.W	    ;START TIMER B
	BCLR #0,$FFFFFA0F.W	    ;I'VE DONE THE ROUTINE
	RTE			    ;GO AWAY

KEY	MOVE.W #$2500,SR	;SET IPL TO LEVEL 5 SO THAT ONLY
				;LEVEL 6 AND 7 INTERRUPTS CAN OCCUR.
	BCLR #6,$FFFFFA11.W     ;I'VE DONE THE ROUTINE
	RTE			;SOD OFF

CLEAR	LEA $78000,A0
	MOVE.W #32000/4-1,D0
.L1	CLR.L (A0)+
	DBF D0,.L1
	RTS

PAL	DC.L COLOURS+2    

COLOURS	DC.W $700,$710,$720,$730,$740,$750,$760,$770,$670,$570
	DC.W $470,$370,$270,$170,$070,$071,$072,$073,$074,$075

OLD70	DS.L 1
OLD118	DS.L 1
OLD120	DS.L 1
OLDA07	DS.B 1
OLDA13	DS.B 1
OLDA21	DS.B 1
OLDA1B	DS.B 1
STACK	DS.L 1