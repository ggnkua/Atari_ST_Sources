
; Kicking out the left and right borders
; Coding by Powerman of the Wild Boys
; No Comments in the code - read the article in the Ledgers Disk Magazine 3



SCREEN		EQU $70000

		CLR.L -(A7)
		MOVE.W #32,-(A7)
		TRAP #1
		ADDQ.L #6,A7
		MOVE.L D0,STACK
		MOVE.L #MY,A7
		JSR INIT
		JSR MAIN
		JMP END

MAIN		LEA SCREEN+(160*81),A6
		MOVE.W #50,D7
.L1		MOVE.W #28-1,D6
.L2		MOVE.W #17,-(A7)
		TRAP #14
		ADDQ.L #2,A7
		MOVE.W D0,(A6)+
		MOVE.W D0,(A6)+
		MOVE.W D0,(A6)+
		MOVE.W D0,(A6)+
		DBF D6,.L2
		ADDQ.L #6,A6
		DBF D7,.L1
		MOVE.W #0,$FFFF8240+(2*15).W

		MOVE.W #$2700,SR
		MOVE.L #VBL,$70.W
		MOVE.W #$2300,SR

START		CMPI.B #57,$FFFFFC02.W
		BNE START
		MOVE.W #$2700,SR			
		RTS

PAL		DC.W 0,$700,$700,$700,$700,$700,$700,$700,$700,$700,$700,$700,$700,$700,$700,$700

VBL		CLR.B $FFFFFA1B.W
		MOVE.B #(8*10)-1,$FFFFFA21.W		MUST BE MULTIPLE OF 8 MINUS 1
		MOVE.B #8,$FFFFFA1B.W
		MOVE.L #TIMER_B,$120.W
		RTE

TIMER_B		CLR.B $FFFFFA1B.W
		MOVE.W #$2700,SR
		LEA $FFFF8209.W,A0
		LEA $FFFF8260.W,A1
		LEA $FFFFFA21.W,A3
		MOVEQ.W #0,D0
		MOVEQ.W #0,D1
		MOVEQ.W #16,D2
		MOVEQ.W #2,D3
		MOVEQ.W #0,D4
		MOVE.B #200,(A3)
		MOVE.B #8,$FFFFFA1B.W
		MOVE.B (A3),D5
.L1		CMP.B (A3),D5
		BEQ .L1
.L2		MOVE.B (A0),D0
		BEQ.S .L2
		SUB.W D0,D2
		LSL.W D2,D1
		CLR.B $FFFFFA1B.W
		DCB.W 87,$4E71
		LEA $FFFF820A.W,A0
		MOVE.W #50,D0
.L3		NOP
		MOVE.B D3,(A1)
		MOVE.B D4,(A1)
		DCB.W 89,$4E71
		MOVE.B D4,(A0)
		MOVE.B D3,(A0)
		DCB.W 13,$4E71
		MOVE.B D3,(A1)
		NOP
		MOVE.B D4,(A1)
		DCB.W 9,$4E71
		DBF D0,.L3
		BCLR #0,$FFFFFA0F.W
		RTE
	
INIT		MOVE.B $FFFFFA07.W,OLD
		MOVE.B $FFFFFA09.W,OLD+1
		MOVE.B $FFFFFA1B.W,OLD+2
		MOVE.B $FFFFFA21.W,OLD+3
		MOVE.B $FFFFFA13.W,OLD+4
		MOVE.B $FFFFFA15.W,OLD15
		MOVE.L $70.W,OLD70
		MOVE.L $120.W,OLD120
		MOVE.L $118.W,OLD118
		CLR.B $FFFFFA07.W
		CLR.B $FFFFFA09.W
		CLR.B $FFFFFA13.W
		CLR.B $FFFFFA15.W
		ORI.B #1,$FFFFFA07.W
		ORI.B #1,$FFFFFA13.W
		CLR.B $FFFFFA1B.W
		CLR.W -(A7)
		PEA SCREEN
		PEA SCREEN
		MOVE.W #5,-(A7)
		TRAP #14
		LEA 12(A7),A7
		MOVE.B #$12,D0
		BSR IKBD
		RTS

END		MOVE.W #$2700,SR
		MOVE.L OLD70,$70.W
		MOVE.L OLD120,$120.W	
		BCLR #6,$FFFFFA09.W
		MOVE.L OLD118,$118.W
		MOVE.B OLD,$FFFFFA07.W
		MOVE.B OLD+1,$FFFFFA09.W
		MOVE.B OLD+2,$FFFFFA1B.W
		MOVE.B OLD+3,$FFFFFA21.W
		MOVE.B OLD+4,$FFFFFA13.W
		MOVE.B OLD15,$FFFFFA15.W
		BSET #6,$FFFFFA15.W
		BSET #5,$FFFFFA15.W
		CLR.B $FFFFFA1B.W
		LEA $FFFF8800.W,A0
		MOVE.L #$700FF00,(A0)
		MOVE.L #$8080000,(A0)
		MOVE.L #$9090000,(A0)
		MOVE.L #$A0A0000,(A0)
		MOVE.B #2,$FFFF820A.W
		MOVE.B #0,$FFFF8260.W
		MOVE.B #7,$FFFF8203.W
		MOVE.B #$80,$FFFF8201.W
		MOVE.B #8,D0
		BSR IKBD
		MOVE.W #$777,$FFFF8240.W
		MOVE.W #0,$FFFF8246.W
		MOVE.W #$2300,SR
		MOVE.L STACK,-(A7)
		MOVE.W #32,-(A7)
		TRAP #1
		ADDQ.L #6,A7
		CLR.L -(A7)
		TRAP #1

IKBD		LEA $FFFFFC00.W,A0
.L1		MOVE.B (A0),D1
		BTST #1,D1
		BEQ .L1
		MOVE.B D0,2(A0)
		RTS
STACK		DS.L 1
		DS.L 128
MY		DS.L 1

OLD70		DS.L 1
OLD120		DS.L 1
OLD118		DS.L 1
OLD		DS.B 6
OLD15		DS.W 1
