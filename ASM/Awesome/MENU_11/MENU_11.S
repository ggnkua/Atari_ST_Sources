;  Awesome kinky chains intro coded (hmmm bolted - but you can't tell) by 
;  Ruthless 1991.

	OPT	O-,S-,D-
	JMP	BEGIN
BEGIN	lea	FILE,a0
.clear	clr.l	(a0)+
	clr.l	(a0)+
	clr.l	(a0)+
	clr.l	(a0)+
	cmp.l	#endbss,a0
	blt.s	.clear
	MOVE.L	4(A7),A5
	LEA	STACK,A7
	MOVE.L	$C(A5),D0
	ADDI.L	#$100,D0
	MOVE.L	D0,-(SP)
	MOVE.L	A5,-(SP)
	MOVE.L	#$4A0000,-(SP)
	TRAP	#1
	LEA	12(SP),SP
	PEA	STACK
	MOVE.W	#$20,-(SP)
	TRAP	#1
	MOVE.L	D0,OLDSP
	JSR	INIT

MLOOP	MOVE.B	$469,D0
VSYNC1	CMP.B	$469,D0
	BEQ	VSYNC1
	JSR	SWITCH
	JSR	DOPRLAX
	jsr	scroller
	jsr	SCROLL8X8
KEYS	MOVE.B	$FFFFFC02.W,D0
	TST.B	D0
	BEQ.S	MLOOP

	CMP.B	OLDKEYP,D0
	BEQ.S	MLOOP
	MOVE.B	D0,OLDKEYP

	CMP.B	#11,D0
	BNE.S	.NHZ

	EORI.W	#$700,WATERCOL
	EORI.B	#2,HZ

.NHZ	SUBQ.B	#1,D0
	CMPI.B	#1,D0
	BGE.S	MAX
	BRA	MLOOP
MAX	CMPI.B	#NUMFILES,D0
	BLE.S	OKNUM
	BRA	MLOOP
OKNUM	MOVE.B	D0,FILE+1
	JSR	TURNOFF
LOAD	MOVE.W	FILE,D0
	MOVEQ	#0,D1
	MOVE.W	D0,D1
	SUBQ.W	#1,D1
	LSL	#2,D1
	LEA	NAMES,A0
	ADDA.L	D1,A0
	MOVE.L	(A0),A6
	PEA	ENV
	PEA	ENV
	MOVE.L	A6,-(SP)
	MOVE.L	#$4B0000,-(SP)
	TRAP	#1
	LEA	16(SP),SP
EXIT	CLR.W	-(SP)
	TRAP	#1
TURNOFF	JSR	HBLOFF
	MOVE.L	SCREEN1,A0
	MOVE.W	#64000/4/4-1,D7
.CLS	CLR.L	(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	DBRA	D7,.CLS
	MOVE.W	OLDRES,-(SP)
	MOVE.L	OLDSCR,-(SP)
	MOVE.L	OLDSCR,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	MOVEM.L	OLDPAL,D0-D7
	MOVEM.L	D0-D7,$FF8240
	MOVE.B	HZ,$FFFF820A.W
	MOVE.L	OLDSP,-(SP)
	MOVE.W	#$20,-(SP)
	TRAP	#1
	ADDQ.L	#6,SP
	RTS
INIT	MOVEM.L	$FF8240,D0-D7
	MOVEM.L	D0-D7,OLDPAL
	MOVE.B	HZ,$FFFF820A.W
	clr.l	$ff8240
	clr.l	$ff8244
	clr.l	$ff8248
	clr.l	$ff824c
	clr.l	$ff8250
	clr.l	$ff8254
	clr.l	$ff8258
	clr.l	$ff825c
	MOVE.L	#$222,$FF8240
	MOVE.L	#$4440666,$FF8244
	MOVE.W	WATERCOL,$FF8248
	MOVE.L	#SCREENS+256,D0
	ANDI.L	#-256,D0
	MOVE.L	D0,SCREEN1
	ADD.L	#32000,D0
	MOVE.L	D0,SCREEN2
	MOVE.L	$44E,OLDSCR
	MOVE.W	#2,-(SP)
	TRAP	#14
	ADDQ.L	#2,SP
	MOVE.W	D0,OLDRES
	CLR.W	-(SP)
	MOVE.L	SCREEN1,-(SP)
	MOVE.L	#-1,-(SP)
	MOVE.W	#5,-(SP)
	TRAP	#14
	LEA	12(SP),SP
	MOVEQ	#MUSICNO,D0
	JSR	MUSIC
	JSR	CUTBACK
	JSR	HBLON
	RTS

SWITCH	MOVE.L	SCREEN1,D0
	MOVE.L	SCREEN2,SCREEN1
	MOVE.L	D0,SCREEN2
	LSR.W	#8,D0
	MOVE.L	D0,$FF8200
	RTS

HBLON	MOVE.W	#1,$4EE.W
	MOVE.B	#$12,$FFFC02
	LEA	OLDMFP,A0
	MOVE.B	$FFFFFA07.W,(A0)+
	MOVE.B	$FFFFFA09.W,(A0)+
	MOVE.B	$FFFFFA13.W,(A0)+
	MOVE.B	$FFFFFA15.W,(A0)+
	MOVE.B	$FFFFFA1F.W,(A0)+
	MOVE.B	$FFFFFA19.W,(A0)+
	MOVE.B	$FFFFFA17.W,(A0)+
	CLR.B	$484.W
	MOVE.W	#$2700,SR
	MOVE.L	$118.W,OLDKEY
	MOVE.L	#KEYVEC,$118.W
	MOVE.L	$70.W,OLDVBL
	MOVE.L	#NEWVBL,$70.W
	MOVE.B	#%01000000,$FFFFFA15.W
	MOVE.B	#%00000001,$FFFFFA07.W
	BSET.B	#3,$FFFFFA17.W
	MOVE.W	#$2300,SR
	RTS

HBLOFF	MOVE.W	#$2700,SR
	MOVE.L	OLDKEY,$118.W
	MOVE.L	OLDVBL,$70.W
	MOVE.L	#IPLNUL,$120.W
	LEA	OLDMFP,A0
	MOVE.B	(A0)+,$FFFFFA07.W
	MOVE.B	(A0)+,$FFFFFA09.W
	MOVE.B	(A0)+,$FFFFFA13.W
	MOVE.B	(A0)+,$FFFFFA15.W
	MOVE.B	(A0)+,$FFFFFA1F.W
	MOVE.B	(A0)+,$FFFFFA19.W
	MOVE.B	(A0)+,$FFFFFA17.W
	MOVE.L	#$08080000,$FF8800
	MOVE.L	#$09090000,$FF8800
	MOVE.L	#$0A0A0000,$FF8800
	MOVE.W	#$2300,SR
	MOVE.B	#7,$484.W
FLUSH	BTST.B	#0,$FFFC00
	BEQ.S	.DONE
	MOVE.B	$FFFC02,D0
	BRA.S	FLUSH
.DONE	MOVE.B	#$8,$FFFC02
	RTS

NEWVBL	MOVEM.L	D0-D7/A0-A6,-(SP)
	MOVE.W	#$2700,SR
	ORI.B	#1,$FFFFFA07.W
	ORI.B	#1,$FFFFFA13.W
	CLR.B	$FFFFFA1B.W
	MOVE.L	#HBL1,$120.W
	MOVE.B	#5,$FFFFFA21.W
	MOVE.B	#8,$FFFFFA1B.W
	MOVE.W	WATERCOL,$FF8248
	JSR	MUSIC+MUSICJUMP
	move.l	#BIGPAL,CPOS
	MOVE.W	#$2300,SR
	MOVEM.L	(SP)+,D0-D7/A0-A6
	MOVE.L	OLDVBL,-(SP)
	RTS

KEYVEC	MOVE.W	#$2500,SR
	MOVE.W	D0,-(SP)
	MOVE.B	$FFFC00,D0
	BTST	#7,D0
	BEQ.S	.NOKEY
	MOVE.B	$FFFC02,KEYPRESS
.NOKEY	BCLR	#6,$FFFA11
	MOVE.W	(SP)+,D0
	RTE

IPLNUL	BCLR.B	#5,$FFFFFA0F.W
	RTE

HBL1	MOVE.L	A0,-(SP)
	MOVE.L	CPOS,A0
	MOVE.L	(A0)+,$FF8250
	MOVE.L	4(A0),$FF8254
	MOVE.L	A0,CPOS
	MOVE.L	(SP)+,A0
	BCLR	#0,$FFFFFA0F.W
	RTE

scroller	lea	buffer,a1
	move.l	a1,a2
	addq.l	#2,a2
	rept	110
	move.L	(a2)+,(a1)+
	endr
	lea	buffer+400,a1
	subq.w	#1,charlen
	tst.w	charlen
	bgt.s	.notnew
	move.l	mpos,a0
	moveq	#0,d0
	move.w	#16,charlen
	move.b	(a0)+,d0
	cmp.b	#0,(a0)
	bne.s	.nei
	lea	text,a0
	clr.w	flat
	bra.s	.nemess
.nei	cmp.b	#$ff,(a0)
	bne.s	.nemess
	lea	text,a0
.nemess	move.l	a0,mpos
	sub.b	#' ',d0
	lsl	#5,d0
	lea	font,a0
	add.l	d0,a0
	rept	16
	move.w	(a0)+,(a1)+
	endr
.notnew	jsr	display
	rts

display	move.l	SCREEN2,a0
	add.l	#18*160+6,a0
	lea	buffer,a1
	move.l	wpos,a2
	add.l	#8,a2
	cmp.l	#wave+720,a2
	blt.s	.new
	lea	wave,a2
.new	move.l	a2,wpos
	tst.w	flat
	beq.s	.notflat
	lea	flatwave,a2
.notflat	moveq	#19,d5
.chunk3	move.w	(a2),d3
	sub	#8*160,d3
	add	d3,a0
add	set	0
	rept	50
	clr.w	add(a0)
add	set	add+160
	endr
	sub	d3,a0
	moveq	#15,d6
	move.w	#%1111000000000000,d4
.chunk2	move.w	(a2)+,d3
	add.w	d3,a0
	move.w	(A1)+,d1
	btst	#15,d1
	beq.s	.n15
	or.w	d4,160(a0)
	or.w	d4,320(a0)
.n15	btst	#14,d1
	beq.s	.n14
	or.w	d4,480(a0)
	or.w	d4,640(a0)
.n14	btst	#13,d1
	beq.s	.n13
	or.w	d4,800(a0)
	or.w	d4,960(a0)
.n13	btst	#12,d1
	beq.s	.n12
	or.w	d4,1120(a0)
	or.w	d4,1280(a0)
.n12	btst	#11,d1
	beq.s	.n11
	or.w	d4,1440(a0)
	or.w	d4,1600(a0)
.n11	btst	#10,d1
	beq.s	.n10
	or.w	d4,1760(a0)
	or.w	d4,1920(a0)
.n10	btst	#9,d1
	beq.s	.n9
	or.w	d4,2080(a0)
	or.w	d4,2240(a0)
.n9	btst	#8,d1
	beq.s	.n8
	or.w	d4,2400(a0)
	or.w	d4,2560(a0)
.n8	btst	#7,d1
	beq.s	.n7
	or.w	d4,2720(a0)
	or.w	d4,2880(a0)
.n7	btst	#6,d1
	beq.s	.n6
	or.w	d4,3040(a0)
	or.w	d4,3200(a0)
.n6	btst	#5,d1
	beq.s	.n5
	or.w	d4,3360(a0)
	or.w	d4,3520(a0)
.n5	btst	#4,d1
	beq.s	.n4
	or.w	d4,3680(a0)
	or.w	d4,3840(a0)
.n4	btst	#3,d1
	beq.s	.n3
	or.w	d4,4000(a0)
	or.w	d4,4160(a0)
.n3	btst	#2,d1
	beq.s	.n2
	or.w	d4,4320(a0)
	or.w	d4,4480(a0)
.n2	btst	#1,d1
	beq.s	.n1
	or.w	d4,4640(a0)
	or.w	d4,4800(a0)
.n1	btst	#0,d1
	beq.s	.n0
	or.w	d4,4960(a0)
	or.w	d4,5120(a0)
.n0	lsr	#4,d4
	subq	#3,d6
	sub.w	d3,a0
	dbra	d6,.chunk2
	addq.l	#8,a0
	dbra	d5,.chunk3
	rts

DOPRLAX	ADD.W	#1,X1
	MOVE.W	X1,XPOS
	MOVE.W	#160*28,YPOS
	MOVE.W	#12*160,YSTEP
	JSR	DRAWBACK
	MOVE.W	D5,X1
	ADD.W	#2,X2
	MOVE.W	X2,XPOS
	JSR	DRAWBACK
	MOVE.W	D5,X2
	ADD.W	#3,X3
	MOVE.W	X3,XPOS
	JSR	DRAWBACK
	MOVE.W	D5,X3
	ADD.W	#4,X4
	MOVE.W	X4,XPOS
	JSR	DRAWBACK
	MOVE.W	D5,X4
	ADD.W	#5,X5
	MOVE.W	X5,XPOS
	JSR	DRAWBACK
	MOVE.W	D5,X5
	RTS

DRAWBACK	CMP.W	#16,XPOS
	BLT.S	NOT32
	SUB.W	#16,XPOS
	BRA	DRAWBACK
NOT32	CMP.W	#0,XPOS
	BGE.S	NOT02
	ADD.W	#16,XPOS
	BRA	NOT32
NOT02	MOVE.W	XPOS,D5
	MOVEQ	#0,D4
	MOVE.L	SCREEN2,A1
	ADDA.L	#160*94,A1
	MOVE.L	A1,A2
	MOVE.W	YPOS,D1
	MOVE.W	YSTEP,D2
	ADD.W	D2,YPOS
	ADD.W	#160,YSTEP
	SUB.L	D1,A2
	ADD.L	D1,A1
	MOVE.W	D5,D4
	LEA	BACKBUF,A0
	LSL	#6,D4
	ADD.L	D4,A0
	MOVE.W	#9-1,D6
GOG
ADD	SET	0
	REPT	20
	MOVE.L	(A0),ADD(A1)
	MOVE.L	(A0),ADD(A2)
ADD	SET	ADD+8
	ENDR
	ADDQ.L	#4,A0
	LEA	160(A1),A1
	LEA	160(A2),A2
	DBRA	D6,GOG
	RTS

CUTBACK	LEA	BACKBUF(PC),A1
	MOVE.W	#16-1,D6
CLP2	MOVE.W	#16-1,D7
	LEA	BACKPIC,A0
CLP1	MOVE.W	(A0)+,D2
	ROR.W	D6,D2
	MOVE.W	D2,(A1)+
	MOVE.W	(A0)+,D2
	ROR.W	D6,D2
	MOVE.W	D2,(A1)+
	DBRA	D7,CLP1
	DBRA	D6,CLP2
	RTS

SCROLL8X8	CMP.W	#8,CCNT
	BLT.S	NOTNEW
	JSR	NEWCHAR
NOTNEW	LEA	CHARBUF,A3
	MOVE.L	A3,A0
	ADDQ.W	#1,CCNT
	LEA	BUFF8,A1
	MOVE.W	#8-1,D7
SCROLLOOP	LSL.W	(A0)+
	ADDQ.L	#2,A0
ADD	SET	152
	REPT	20
	ROXL	ADD(A1)
ADD	SET	ADD-8
	ENDR
	LEA	160(A1),A1
	DBRA	D7,SCROLLOOP
	JSR	COPY
	RTS

NEWCHAR	MOVEQ	#0,D0
	MOVE.L	MPOS8,A0
	MOVE.B	(A0)+,D0
	CMP.B	#0,(A0)
	BNE.S	NOTEND
	MOVE.L	#MESSAGE,A0
NOTEND	MOVE.L	A0,MPOS8
	CLR.W	CCNT
	LEA	FONTBIN,A0
	LEA	CHARBUF,A1
	SUB.W	#32,D0
	LSL	#3,D0
	ADD.W	D0,A0
	REPT	8
	MOVE.B	(A0)+,(A1)+
	MOVE.B	#0,(A1)+
	ADDQ.L	#2,A1
	ENDR
	RTS
COPY	LEA	BUFF8,A0
	MOVE.L	SCREEN2,A1
	ADDQ.L	#4,A1
	MOVE.L	A1,A2
	ADD.L	#160*191,A2
	MOVEQ	#6,D7
.LINLP
ADD	SET	0
	REPT	20
	MOVE.W	ADD(A0),ADD(A1)
	MOVE.W	ADD(A0),ADD(A2)
ADD	SET	ADD+8
	ENDR
	LEA	160(A0),A0
	LEA	160(A1),A1
	LEA	160(A2),A2
	DBRA	D7,.LINLP
	RTS

	SECTION	DATA
wave	incbin	wave.dat
	even
font	incbin	sideways.dat
	even
BACKPIC	INCBIN	CHAIN.DAT
	EVEN
FONTBIN	INCBIN	8X8_FONT.DAT
	EVEN
FPOS	DC.L	FONTBIN
CCNT	DC.W	25
CHAR	DC.B	0
CPOS	DC.L	BIGPAL
BIGPAL	DC.W	$700,$700,$710,$700,$710,$710,$720,$710,$720,$720,$730,$720,$730,$730,$740
	DC.W	$730,$740,$740,$750,$740,$750,$750,$760,$750,$760,$760,$770,$760,$770,$770
	DC.W	$670,$770,$670,$670,$570,$670,$570,$570,$470,$570,$470,$470,$370,$470,$370
	DC.W	$370,$270,$370,$270,$270,$170,$270,$170,$170,$070,$170,$070,$070,$071,$070
	DC.W	$071,$071,$072,$071,$072,$072,$073,$072,$073,$073,$074,$073,$074,$074,$075
	DC.W	$074,$075,$075,$076,$075,$076,$076,$077,$076,$077,$077,$067,$077,$067,$067
	DC.W	$057,$067,$057,$057,$047,$057,$047,$047,$037,$047,$037,$037,$027,$037,$027
	DC.W	$027,$017,$027,$017,$017,$007,$017,$007,$007,$107,$007,$107,$107,$207,$107
	DC.W	$207,$207,$307,$207,$307,$307,$407,$307,$407,$407,$507,$407,$507,$507,$607
	DC.W	$507,$607,$607,$707,$607,$707,$707,$706,$707,$706,$706,$705,$706,$705,$705
	DC.W	$704,$705,$704,$704,$703,$704,$703,$703,$702,$703,$702,$702,$701,$702,$701
	DC.W	$701,$700,$701,$700

HZ	DC.B	2
OLDKEYP	DC.B	0
WATERCOL	DC.W	$777
NAMES	DC.L	FILE1,FILE2,FILE3,FILE4,FILE5,FILE6,FILE7,FILE8,FILE9



charlen	dc.w	16
wpos	dc.l	wave
flatwave	dcb.w	80,160*64
flat	dc.w	1
mpos	dc.l	introtext
pause	dc.w	0


introtext	dc.b	"   RUTHLESS IS PROUD TO PRESENT AWESOME MENU 11.   "

	dcb.b	14,$20	; end of intro message
	dc.b	0

text	DC.B	"   WELL AS A LOT OF YOU NOTICED, THE TWO GUYS WHO WROTE THE TEXT ON MENU 10 SPELT AWESOME WRONGLY!   WHAT, I DID?    NO YOU MUST HAVE CONFUSED ME WITH SOMEONE ELSE! "
	DC.B	"  O.K BEFORE I GO ON WITH THE REST OF THE BULLSHIT I AM GOING TO TELL YOU THAT WE HAVE LOST 2 MEMBERS OF THE GROUP WHO ARE SCORPION AND DEMON-X....   HOW SAD!  WELL NEVER MIND - ALL THE BEST FOR THE FUTURE"
	DC.B	" GUYS - NO HARD FEELINGS!   TO MAKE UP FOR LOSING 2 MEMBERS I HAVE ALLOWED 4 NEW MEMBERS TO JOIN!  YES 4!   DOUBLE THE 2 WE LOST.  THESE GUYS GO UNDER THE NAME OF:- FROSTY, JOLLEY ROGER, K-KLASS, TECHNO AND ONIXUS...."
	DC.B	"  SO THE BEST JUST GET BETTER!   SO I AM HERE AGAIN TO BORE YOU ALL WITH MY REPETITIVE BULLSHIT!    WE NOW HAVE 9 MEMBERS, HMMM, I THINK THAT IS A LOT OF MEMBERS - I WONDER IF ANY OF THEM ARE GONNA LEAVE?  WHO KNOWS I MIGHT LEAVE"
	DC.B	" AND BUY AN I.B.M WHICH MOST PEOPLE SEEM TO BE DOING AT THE MOMENT.  AT LEAST IT IS GOOD TO SEE THAT SOFTWARE IS PICKING UP AND THE MAGAZINES ARE FULL OF BULLSHIT, SORRY I MEAN NEW SOFTWARE.  OH BEFORE I FORGET A SPECIAL MESSAGE TO"
	DC.B	" A NEW CONTACT... HIYA TERRORIST IS THIS ENOUGH PUBLICITY FOR YOU?  OH YEAH, I JUST THOUGHT I WOULD TELL YOU THAT I KNOW WHO YOU ARE - GOT THAT BISH!  BUT PLEASE KEEP WRITING BECAUSE IT IS GOOD FUN!!!   MESSAGE ENDS.   THAT MESSAGE WILL"
	DC.B	" NOT MEAN ANYTHING TO ANYONE BUT BISH SO JUST IGNORE IT!   WELL I AM BORED TO MISERY NOW SO I AM GOING TO CALL IT A WRAP.    SO UNTIL MENU 12, LETS WRAP DUDES........                "

	dc.b	$ff	; end of message
	even

MPOS8	DC.L	MESSAGE
MESSAGE	DC.B	'   THE RELEASE DATE OF THIS MENU IS SUNDAY THE 15TH OF SEPTEMBER 1991.   YOU CAN PRESS THE FOLLOWING KEYS.'
	DC.B	'  1. ZONE WARRIOR+   2. FRUIT MACHINE   3. BOING   4. PUZZMANIA+   0. HERTZ      WELL HOW DO YOU LIKE THIS NEW INTRO IT IS NOT DESIGNED TO BE'
	DC.B	' TECHNICALLY IMPRESSIVE BUT TO ASSEMBLE TO A SMALL SIZE!   IF YOU WOULD LIKE TO GET IN CONTACT WITH ANY MEMBERS OF AWESOME FOR ANY REASON AT ALL'
	DC.B	' THEN JUST LOOK AROUND ON SOME OF OUR MENUS FOR POSSIBLE SIGNS OF AN ADDRESS!   OR IF YOU DONT MIND WAITING A WHILE FOR A REPLY THEN JUST SIMPLY WRITE TO US AT...'
	DC.B	'   AWESOME, P.O BOX 9296, 109 RVK, ICELAND...  I REPEAT...   AWESOME, P.O BOX 9296, 109 RVK, ICELAND...  WE DO NOT LIVE IN ICELAND BUT WE HAVE A COUPLE OF GOOD FRIENDS'
	DC.B	' WHO WILL PASS ALL CORESSPONDENCE ONTO ME!!!   O.K, THE GREETINGS WILL COME ON THIS SCROLLER SHORTLY, BUT FIRST A FEW MESSAGES...  IF YOU DONT AGREE WITH HOW I DO THIS,'
	DC.B	' THEN GET STUFT AND MEDITATE LIKE A BUDIST - WE MAKE MENUS AGAIN AND AGAIN AND AGAIN WITH YOU READING THIS AT THE OTHER END - O.K NOW SUCKERS LET US CUT THE CRAP CAUSE YOU KNOW IT'
	DC.B	' TAKES A NATION OF MILLIONS TO HOLD US BACK!!!!   O.K ENOUGH PERSONAL EGO-TRIPPING, I THINK IT IS NOW TIME TO SEND OUT SOME GREETINGS TO THE DUDES WHO DESERVE IT!'
	DC.B	'    DREAM TRIPPING GREETINGS GOTO THE FOLLOWING READERS OF SCROLLTEXT:-  THE REST OF AWESOME (WHO ARE - ZELDA - THE EDITMAN - FROSTY - K KLASS - PHOENIX - JOLLEY ROGER - TECHNO - ONIXUS),'
	DC.B	' THE REPLICANTS (ESP. CAMEO AND COBRA - NICE TALKING TO YOU STEPH!), SYNDICATE (ESP. FLOSSY), FUZION (ESP. DOCNO AND KELLY-X), T.S.B (ESP. ZINE), POMPEY PIRATES (ESP. GENIE, YUM-YUM AND LOVELY LULU!),'
	DC.B	' THE MEDWAY BOYS (ESP. ZIPPY AND GINO), THE INNER CIRCLE (ESP. THE VANDAL (A DAMN FINE GUY!), GRIFF THE DEALER, MASTER, COUNT ZERO AND PERVY PHANTOM!), MUG-UK, HAL (IT IS REALLY NICE BIENG A HAL-ITE!),'
	DC.B	' E.G.B (NICE CHAT), DEREK M.D AND FAMILY, NEW POWER GENERATION (ESP. T.G.A), THE UNTOUCHABLES (ESP. MATT AND STORMLORD), FUTURE MINDS (ESP. SKYLINE), BAD BOYS (ESP PETER), EVIL FORCE (ESP. JASON ELITE),'
	DC.B	' MAD VISION (ESP. HAL, HIGHLANDER AND SPY 3), THE AVENGER, ALAN B, ELECTRONIC, IMPACT (ESP. TOXIC), THE QUARTET (ESP. NICK AND BLOODANGEL), ROYAL INSTANT PUDDING (ESP. TITANIC TARZAN AND HIS FRIEND),'
	DC.B	' IENX (ESP. IVO), SUPERIOR (ESP. WANDERER AND AXE), DEATH (ESP. SPIFF), JAM, PEDRO, THE RUDE DUDE, ARAKIS (ESP. FRODON), T.M.C (ESP. TEDDY), NOW 5 (ESP. FALCON), THE SOURCE (ESP. KALAMAZOO), EXODUS,'
	DC.B	' THE EGOTRIPPERS (ESP. KEGS AND SHADE), RAEGUN, QUATTRO (HOWS THE SPELLING?), ALL MEMBERS OF AUTOMATION EXCEPT THEIR WEIRDO MODEM MAN, ANGELS OF MERCY (ESP. KIM), BENNY, LOTUS, B.B.C (ESP. ANDY AND LAW),'
	DC.B	' MONSTER BEETLE, JOKER, CARL, GENESIS INC. (LAWZ ONLY), NEXT, STUART, THE FOX, GEORGE FROM GREECE AND THE OVERLANDERS....   IF THERE ARE ANY SPELLING MISTAKES IN ALL THAT BULLSHIT THEN SPEAK TO QUATTRO '
	DC.B	'THE GUY WHO HAS NEVER SPELT NOTHING WRONG IN HIS LIFE BEFORE - AINT THAT RIGHT EH?    ANYHOW LETS WRAP THIS SHIT!!!                 '  

	DC.B	0	; end of little scroller

MUSIC	INCBIN	scorch2.MUS	; The MADMAX music FILE
	EVEN

MUSICNO	EQU	1	; The music number

MUSICJUMP	EQU	8	; 8=MADMAX	

NUMFILES	EQU	4	; The number of FILEs

FILE1	DC.B	'click',0		; The FILE names
	EVEN
FILE2	DC.B	'sexy',0
	EVEN
FILE3	DC.B	'fatty',0
	EVEN
FILE4	DC.B	'click',0
	EVEN
FILE5	DC.B	'',0
	EVEN
FILE6	DC.B	'',0
	EVEN
FILE7	DC.B	'',0
	EVEN
FILE8	DC.B	'',0
	EVEN
FILE9	DC.B	'',0
	EVEN


	SECTION	BSS
FILE	DS.W	1
ENV	DS.L	1
CHARBUF	DS.L	10
BUFF8	DS.B	160*10
BACKBUF	DS.W	16*8*20
XPOS	DS.W	1
YPOS	DS.W	1
YSTEP	DS.W	1
X1	DS.W	1
X2	DS.W	1
X3	DS.W	1
X4	DS.W	1
X5	DS.W	1
X6	DS.W	1
X7	DS.W	1
X8	DS.W	1
buffer	ds.w	220
KEYPRESS	DS.L	1
OLDPAL	DS.L	8
OLDMFP	DS.L	2
OLDKEY	DS.L	1
OLDVBL	DS.L	1
OLDSP	DS.L	1
OLDSCR	DS.L	1
OLDRES	DS.L	1
	DS.L	200
STACK	DS.L	0
SCREEN1	DS.L	1
SCREEN2	DS.L	1
	DS.B	160*8
SCREENS	DS.B	256
	DS.B	160*200*2
endbss	ds.l	16
	END
