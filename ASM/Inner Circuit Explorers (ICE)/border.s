;  TERMINATE THE LOWER BORDER 
;   BY TECHWAVE 0300-29623
;AND SOME RASTERS TO HELP CODFISH

INIT:	MOVE.B $FFFA07,DATA1
	MOVE.B $FFFA09,DATA2
	MOVE.B $FFFA13,DATA3
	MOVE.L $120,VEKTOR1
	MOVE.L $70,VEKTOR2

	CLR.B  $FFFA1B
	CLR.B  $FFFA09     
	BSET   #0,$FFFA07 ;IERA
	BSET   #0,$FFFA13 ;MASK	  
	MOVE.B #1,$FFFA21
	MOVE.L #HBL,$120
	MOVE.B #8,$FFFA1B ;TIM.B
	MOVE.L #VBL,$70


KEY:	MOVE.B #0,VBLFLAG
WAIT:	CMP.B #1,VBLFLAG
	BNE WAIT
	CMP.B #185,$FFFC02
	BNE KEY
	BRA BACK
*******************************
VBL:	MOVE.B #1,VBLFLAG
	MOVE.W #$F00,$FF8240
	CLR.B $FFFA1B	    ;OFF
	MOVE.B #100,$FFFA21 ;DAT
	MOVE.B #8,$FFFA1B   ;ON
	RTE
*******************************
HBL:	CLR.B $FFFA1B       ;OFF
	MOVE.B #255,$FFFA21   ;DAT
	MOVE.B #8,$FFFA1B   ;ON
	MOVE.B #254,D0

SYNC:	CMP.B $FFFA21,D0
	BNE SYNC
	ADDQ.W #1,$FF8240
	SUBQ #1,D0
	CMP.B #254-99,D0
	BNE SYNC

SYNC2:	CMP.B $FFFA21,D0
	BNE SYNC2
	ADDQ #1,$FF8240

	MOVE.B #$FC,$FF820A
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	MOVE.B #$FE,$FF820A

	SUBQ #1,D0
SYNC3:	CMP.B $FFFA21,D0
	BNE SYNC3
	ADDQ.W #1,$FF8240
	SUBQ #1,D0
	CMP.B #254-140,D0
	BNE SYNC3
	BCLR #0,$FFFA0F     
	RTE
*******************************
BACK:	MOVE.B DATA1,$FFFA07
	MOVE.B DATA2,$FFFA09
	MOVE.B DATA3,$FFFA13
	MOVE.L VEKTOR1,$120
	MOVE.L VEKTOR2,$70
	MOVE.B #$FE,$FF820A
	RTS

DATA1:	DC.B 0
DATA2:	DC.B 0
DATA3:	DC.B 0
VBLFLAG:DC.B 0

VEKTOR1:DC.L 0
VEKTOR2:DC.L 0
