' programme de scan des r‚pertoires et liste des images GIF.
' program‚ par MAXILYS.       modifi‚ par BLISS le 20/5/1994.
'                  pas de copyright
'
' OPEN "o",#99,"f:\gif_lst.txt"
DO
  FILESELECT #"DOSSIER ¶ SCANNER","\*.GIF","",chem$
  EXIT IF chem$=""
  IF RIGHT$(chem$)<>"\"
    a%=RINSTR(chem$,"\")
    chem$=LEFT$(chem$,a%)
  ENDIF
  PRINT ''chem$
  ' PRINT #99,''chem$
  scan_gif
LOOP
' CLOSE #99
'
> PROCEDURE scan_gif
  ~FSETDTA(BASEPAGE+128)
  a%=FSFIRST(chem$+"*.GIF",0)
  DO UNTIL a%
    nom$=CHAR{BASEPAGE+158}
    PRINT nom$;TAB(12);"|";
    ' PRINT #99,nom$;TAB(12);"|";
    taille_gif
    a%=FSNEXT()
  LOOP
RETURN
> PROCEDURE taille_gif
  larg&=0
  haut&=0
  flag%=0
  adr%=V:flag%
  OPEN "i",#1,chem$+nom$
  SEEK #1,6
  BGET #1,adr%,4
  POKE V:larg&+1,PEEK(adr%)
  POKE V:larg&,PEEK(adr%+1)
  POKE V:haut&+1,PEEK(adr%+2)
  POKE V:haut&,PEEK(adr%+3)
  PRINT STR$(larg&,4);STR$(haut&,4);"|";STR$(LOF(#1),7);"|"
  ' PRINT #99,STR$(larg&,4);STR$(haut&,4);"|";STR$(LOF(#1),7);"|"
  CLOSE #1
RETURN
