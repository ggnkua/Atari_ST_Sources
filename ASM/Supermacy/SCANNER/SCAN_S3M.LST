' Programme de scan des r‚pertoires et liste
' des modules soundtrack ScreamTracker3 .S3M
'
'      fait par BLISS le 30/7/1994
'             pas de copyright
'
' merci … Maxilys pour le squelette du programme.
' merci … Simplet/Abstract pour la documentation.
'
' OPEN "o",#99,"F:\s3m_lst.txt"
DO
  FILESELECT #"DOSSIER ¶ SCANNER","\*.S3M","",chem$
  EXIT IF chem$=""
  IF RIGHT$(chem$)<>"\"
    a%=RINSTR(chem$,"\")
    chem$=LEFT$(chem$,a%)
  ENDIF
  PRINT ''chem$
  ' PRINT #99,''chem$
  scan_s3m
LOOP
' CLOSE #99
'
> PROCEDURE scan_s3m
  ~FSETDTA(BASEPAGE+128)
  a%=FSFIRST(chem$+"*.S3M",0)
  DO UNTIL a%
    nom$=CHAR{BASEPAGE+158}
    PRINT nom$;TAB(12);"|";
    ' PRINT #99,nom$;TAB(12);"|";
    titre_s3m
    a%=FSNEXT()
  LOOP
RETURN
> PROCEDURE titre_s3m
  LOCAL tit$,nbvoies|,nbvadlib|,i%
  OPEN "i",#1,chem$+nom$
  '
  tit$=STRING$(29,0)    ! song name: les 28 premiers octets du fichier
  BGET #1,V:tit$,28
  tit$=CHAR{V:tit$}
  '
  SEEK #1,64         ! on se place sur le d‚but des infos de voies
  CLR nbvoies|,nbvadlib|
  FOR i%=1 TO 32
    SELECT INP(#1)
    CASE 255                 ! voie inutilis‚e
    CASE 0 TO 15,128 TO 143  ! voie "sample" y compris d‚sactiv‚e (bit 7 on)
      INC nbvoies|
    CASE 16 TO 35            ! voie "adlib"
      INC nbvadlib|
    DEFAULT
      PRINT "erreur dans les infos voies!"
      STOP
    ENDSELECT
  NEXT i%
  '
  PRINT tit$;TAB(41);"|";STR$(LOF(#1),7);"|";STR$(nbvoies|,4);" |";STR$(nbvadlib|,4);" |"
  ' PRINT #99,tit$;TAB(41);"|";STR$(LOF(#1),7);"|";STR$(nbvoies|,4);" |";STR$(nbvadlib|,4);" |"
  CLOSE #1
RETURN
