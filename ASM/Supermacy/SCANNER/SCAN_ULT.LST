' Programme de scan des r‚pertoires et liste
' des modules soundtrack UltraTracker .ULT
'
'      fait par BLISS le 14/10/1994
'             pas de copyright
'
' merci … Maxilys pour le squelette du programme.
'    fait d'aprŠs les sp‚cification d'UT 1.6
'
' OPEN "o",#99,"F:\ult_lst.txt"
DO
  FILESELECT #"DOSSIER ¶ SCANNER","\*.ULT","",chem$
  EXIT IF chem$=""
  IF RIGHT$(chem$)<>"\"
    a%=RINSTR(chem$,"\")
    chem$=LEFT$(chem$,a%)
  ENDIF
  PRINT ''chem$
  ' PRINT #99,''chem$
  scan_ult
LOOP
' CLOSE #99
'
> PROCEDURE scan_ult
  ~FSETDTA(BASEPAGE+128)
  a%=FSFIRST(chem$+"*.ULT",0)
  DO UNTIL a%
    nom$=CHAR{BASEPAGE+158}
    PRINT nom$;TAB(12);"|";
    ' PRINT #99,nom$;TAB(12);"|";
    titre_ult
    a%=FSNEXT()
  LOOP
RETURN
> PROCEDURE titre_ult
  LOCAL tit$,nbvoies|,i%,sizess|
  OPEN "i",#1,chem$+nom$
  '
  SEEK #1,15            ! on se place sur le song name
  tit$=STRING$(33,0)
  BGET #1,V:tit$,32     ! qui fait 32 char asciiz
  tit$=CHAR{V:tit$}
  '
  SEEK #1,12            ! on se place sur l'indication de version du fichier
  IF VAL(INPUT$(3,#1))<=3       ! si 001 … 003 (UT 1.0 … UT 1.5)
    sizess|=64                  ! la structure de sample fait 64 octets.
  ELSE                          ! sinon si 004 (UT 1.6)
    sizess|=66                  ! elle a deux octets de plus.
  ENDIF
  SEEK #1,47            ! on se place sur la taille (octet*32) du commentaire
  i%=INP(#1)
  RELSEEK #1,i%*32      ! on est sur l'octet de nombre de samples
  i%=INP(#1)
  RELSEEK #1,i%*sizess|+256  ! on passe les samples et la sequence
  nbvoies|=INP(#1)+1
  '
  PRINT tit$;TAB(45);"|";STR$(LOF(#1),7);"|";STR$(nbvoies|,4);" |"
  ' PRINT #99,tit$;TAB(45);"|";STR$(LOF(#1),7);"|";STR$(nbvoies|,4);" |"
  CLOSE #1
RETURN
