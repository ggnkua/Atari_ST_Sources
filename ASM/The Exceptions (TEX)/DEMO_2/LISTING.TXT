

1. Initialisierungsteil:
     charset = $70000    ;Startadresse des Zeichensatzes
     hoehe   = 32        ;Grî·e der Zeichen

create:
     moveq   #5,d0       ;Wir haben im Zeichensatz 6 Zeilen
     lea     tabelle,a0
     move.l  #charset,d2
spalte:
     move.w   #9,d1      ;und 10 Spalten
zeile:
     move.l  d2,(a0)+    ;wir erzeugen eine Tabelle mit      add.l   #$10,d2     ;Zeigern auf die einzelnen Zeichen
     dbf     d1,zeile    ;(diese sind 32 Pixel breit = $10)
     add.l   #hoehe*160-160,d2  ;(und 32 Pixel hoch)
     dbf     d0,spalte

     move.w  #hoehe,zanz ;dies ist die Anzahl der Zeilen

Nach dieser Initalisierung mu· die Interruptroutine nur noch  ins System eingebunden werden.
     move.l  #irq,$4d2   ;Zugriff  auf  $4d2  nur  im  Supervisor                          ;Modus

Nun   kann   das   Hauptprogramm  machen   was   es   will,   die Interruptroutine scrollt vor sich hin.

2.Die Interruptroutine:

scrollbase=$f8000        ;Zeiger auf den Bildschirm

block0=$30000            ;Zeiger auf die Puffer
block1=$32000            ;benîtigte Grî·e: hoehe*160
block2=$34000
block3=$36000
block4=$38000
block5=$3a000
block6=$3c000
block7=$3e000

puff0=$40000             ;Zeiger auf Nachschubpuffer
puff1=$40100             ;benîtigte Grî·e: hoehe*8
puff2=$40200
puff3=$40300
puff4=$40400
puff5=$40500
puff6=$40600
puff7=$40700

irq:
     move.w  nr,d2            ;nr ist ein ZÑhler
     addq.w  #1,nr
     and.w   #$f,nr           ;der als Pufferzeiger dient
                              ;15 wegen 32 Pixel Breite
     asl.w   #2,d2
     move.l  #irqtab,a4
     move.l  0(a4,d2.w),a2    ;fÅr jeden Puffer eine eigene
     jmp     (a2)             ;Routine

no109:                   ;Routine fÅr Puffer 1 und 9
     move.l #block0,a6   ;Puffer 0 anzeigen
     bsr    show
     move.l #block1,a6   ;Puffer 1 verschieben
     move.l #puff1,a5    ;Zeiger auf Nachschubpuffer
     bra    linksw

no210:                   ;analog zu no109
     move.l #block1,a6
     bsr    show
     move.l #block2,a6
     move.l #puff2,a5
     bra    linksw

no311:
     move.l #block2,a6
     bsr    show
     move.l #block3,a6
     move.l #puff3,a5
     bra    linksw

no412:
     move.l #block3,a6
     bsr    show
     move.l #block4,a6
     move.l #puff4,a5
     bra    linksw

no513:
     move.l #block4,a6
     bsr    show
     move.l #block5,a6
     move.l #puff5,a5
     bra    linksw

no614:
     move.l #block5,a6
     bsr    show
     move.l #block6,a6
     move.l #puff6,a5
     bra    linksw

no715:
     move.l #block6,a6
     bsr    show
     move.l #block7,a6
     move.l #puff7,a5
     bra    linksw

no8:
     move.w #8,teil      ;2.HÑlfte der 32 Pixel
     move.l #block7,a6
     bsr    show
     bsr    addpuff      ;Nachschubpuffer vorbereiten
     move.l #block0,a6
     move.l #puff0,a5
     bra    linksw

no0:
     move.l #block7,a6
     bsr    show
     move.l #text,a0          ;Zeiger auf den Textanfang
     move.l #tabelle,a1       ;Zeiger auf die Umwandlungstabelle
     clr.w  teil              ;linke HÑlfte der 32 Pixel
nochmal:
     move.w zeiger,d0         ;ZÑhler im Text      move.b 0(a0,d0.w),d1     ;nÑchstes Zeichen
noeffect:
     cmp.b  #$3f,d1           ;Endekennung
     bne    noch2
     clr.w  zeiger            ;dann von vorne anfangen
     bra    nochmal
noch2:
     ext.w  d1                ;sonst Zeiger auf Zeichen holen
     asl.w  #2,d1
     move.l 0(a1,d1.w),zeichen1
     addq.w #1,zeiger
     move.w zeiger,d0         ;Zeiger auf nÑchstes Zeichen
notex:
     move.b 0(a0,d0.w),d1
     cmp.b  #$3f,d1           ;Endekennung
     bne    noch3
     move.b (a0),d1           ;dann erstes Zeichen nachscrollen
noch3:
     ext.w  d1
     asl.w  #2,d1             ;sonst Zeiger auf 2.Zeichen
     move.l 0(a1,d1.w),zeichen2

     bsr    addpuff           ;Nachschubpuffer vorbereiten
     move.l #block0,a6        ;und block0 scrollen
     move.l #puff0,a5

linksw:
     move.w zanz,d0           ;Zeilenanzahl
     subq.w #1,d0             ;als dbf ZÑhler
zeilen:
     movem.l 8(a6),d1-d7/a0-a4     ;um 16 Pixel nach links
     movem.l d1-d7/a0-a4,(a6)      ;scrollen
     movem.l 56(a6),d1-d7/a0-a4
     movem.l d1-d7/a0-a4,48(a6)
     movem.l 104(a6),d1-d7/a0-a4
     movem.l d1-d7/a0-a4,96(a6)
     add.l  #144,a6
     move.l 8(a6),(a6)+
     move.l 8(a6),(a6)+
     move.l (a5)+,(a6)+            ;Nachschubpuffer anhÑngen
     move.l (a5)+,(a6)+
     dbf   d0,zeilen
     rts

;Anzeigen des fertigen Puffers auf dem Bildschirm:
show:
     move.w zanz,d0
     subq.w #1,d0
     move.l #scrollbase,a5
szeilen:
     movem.l (a6),d1-d7/a0-a4
     movem.l d1-d7/a0-a4,(a5)
     movem.l 48(a6),d1-d7/a0-a4
     movem.l d1-d7/a0-a4,48(a5)
     movem.l 96(a6),d1-d7/a0-a4
     movem.l d1-d7/a0-a4,96(a5)
     movem.l 144(a6),d4-d7
     movem.l d4-d7,144(a5)
     add.l   #160,a6
     add.l   #160,a5
     dbf     d0,szeilen
     rts

;vorbereiten des Nachschubpuffers:
addpuff:
     movem.l d0-d7/a0/a1,-(sp)
     move.w  zanz,d7
     subq.w  #1,d7             ;Zeilenzahl als dbf
     move.w  teil,d6
     moveq   #8,d5
     move.l  zeichen1,d4
     cmp.w   #8,d6
     bne     puffer0
     move.l  zeichen2,d5
     sub.l   d4,d5
puffer0:                      ;puffer 0 wird nie geschiftet
     move.w d7,d0
     move.l #puff0,a0
     move.l d4,a1
puff0b:                       ;nur linke oder rechte HÑlfte der
     move.l 0(a1,d6.w),(a0)+  ;32 Pixel kopiert
     move.l 4(a1,d6.w),(a0)+
     add.l  #160,a1
     dbf    d0,puff0b

     move.w #$e,d2            ;7 Puffer jeweils 2 Pixel
puff1a:
     move.w d2,d0
     move.w d2,d3
     asl.w #1,d0
     move.l #puffer,a0
     move.l 0(a0,d0.w),a0     ;Pufferadresse nach a0
     move.l d4,a1             ;Zeichenadresse nach a1
     move.w d7,d0             ;Zeilenanzahl nach d0

puff1b:
     move.w 0(a1,d5.l),d1     ;plane0 shiften
     swap   d1
     move.w 0(a1,d6.w),d1
     rol.l  d3,d1
     move.w d1,(a0)+

     move.w 2(a1,d5.l),d1     ;plane 1 shiften
     swap   d1
     move.w 2(a1,d6.w),d1
     rol.l  d3,d1
     move.w d1,(a0)+

     move.w 4(a1,d5.l),d1     ;plane2 shiften
     swap   d1
     move.w 4(a1,d6.w),d1
     rol.l  d3,d1
     move.w d1,(a0)+

     move.w 6(a1,d5.l),d1     ;plane3 shiften
     swap   d1
     move.w 6(a1,d6.w),d1
     rol.l  d3,d1
     move.w d1,(a0)+

     add.l  #160,a1
     dbf    d0,puff1b         ;alle Zeilen

     subq.w #2,d2
     bne    puff1a            ;alle 7 Puffer

     movem.l (sp)+,d0-d7/a0/a1
     rts

Folgende Variablen werden benîtigt:
nr: dc.w 0
zanz: dc.l 0
teil: dc.l 0
zeiger:dc.w 0
zeichen1:dc.l 0
zeichen2:dc.l 0
tabelle: blk.l 64,0      ;platz fÅr 64 Zeiger auf die Zeichen

puffer: dc.l puff0,puff1,puff2,puff3,puff4,puff5,puff6,puff7
     irqtab: dc.l no0,no109,no210,no311,no412,no513,no614,no715
        dc.l no8,no109,no210,no311,no412,no513,no614,no715

text: dc.b 0,1,2,3,4,5,6,7,8,9,10,$3f
even

Nun   benîtigen Sie   nur  noch  eine   Graphikseite   mit   den entsprechenden Zeichen, und schon kann es losscrollen.   -Udo-
