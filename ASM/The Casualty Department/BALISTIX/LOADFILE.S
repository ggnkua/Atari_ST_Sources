*	ON ENTRY:
*	D0=TRACK 
*	D1=SECTOR
*	D2=NUMBER OF SECTORS TO LOAD
*	A5=LOAD ADDRESS

RELOCATE_CODE

	PEA	0
	MOVE.W	#32,-(A7)
	TRAP	#1
	ADDQ.L	#6,A7

	LEA	MAIN(PC),A0
	LEA	$CA00,A1
	MOVE.L	#END_OF_CODE-MAIN,D0
RELOC	MOVE.B	(A0)+,(A1)+
	DBF	D0,RELOC

FIRST	CLR.L	D0
	MOVE.W	#0,D0		;TRACK
	MOVE.W	#2,D1		;SECTOR
	MOVE.W	#8,D2		;NUMBER OF SECTORS TO READ
	LEA	$30000,A5
	MOVE.L	A5,A4
	BSR	MAIN

	MOVE.L	#$67934691,$2F500	;BOOT SECTOR CODE DID
	MOVE.L	#$15800,$2F600		;THIS ON THE ORIGINAL
	MOVE.W	#$6589,D0		;UNFILED VERSION
	MOVE.W	#$7801,D2		;
	MOVE.W	#$1206,D3		;

GO	JMP	$30000



MAIN	MOVEM.L	D0-A6,-(A7)
	MOVE.L	A5,A4
	MOVE.L	A5,A2
	ADD.W	D0,D1
	MULU	D1,D2
	MOVE.L	D2,D0
	BSR	NEWFILE
	BSR	BINDEC1
	LEA	STRIN2(PC),A6
	BSR	LOAD_FILE	
	MOVEM.L	(A7)+,D0-A6
	CLR.L	D2
	RTS


LOAD_FILE
	MOVEM.L	D0-A6,-(A7)
	BSR	OPEN
	BSR	READ
	BSR	CLOSE
	MOVEM.L	(A7)+,D0-A6
	RTS
	

CALC	SUB.L	#1,D1
	MULU	#5120,D0
	MULU	#512,D1
	ADD.L	D1,D0
	MULU	#512,D2
	MOVE.L	D2,D3		;VALUE FOR READ
	MOVE.L	D0,D6		;VALUE FOR LSEEK
	RTS


OPEN	
;	BSR	PRINT_NAME
	CLR.W	-(A7)		;FILE TYPE
	MOVE.L	A6,-(A7)	;FILE NAME
	MOVE.W	#$3D,-(A7)
	TRAP	#1
	ADD.L	#8,A7
	TST.L	D0
	BMI	EXIT
	MOVE.W	D0,D7
	RTS

	
READ	MOVE.L	A2,-(A7)	;BUFFER FOR FILE
	MOVE.L	#1000000,-(A7)	;READ ALL OF FILE
	MOVE.W	D7,-(A7)	;FILE HANDLE
	MOVE.W	#$3F,-(A7)
	TRAP	#1
	ADD.L	#12,A7
	TST.L	D0
	BMI 	EXIT
	MOVE.L	D0,LENGTH
	RTS

CLOSE	MOVE.W	D7,-(A7)
	MOVE.W	#$3E,-(A7)
	TRAP	#1
	ADDQ.L	#4,A7
	TST.L	D0
	BMI 	EXIT
	RTS


PRINT_NAME
	LEA	TOPLEFT(PC),A3
	BSR	PRINT
	MOVE.L	A6,A3
	BSR	PRINT
	RTS

PRINT	MOVE.L	A3,-(A7)
	MOVE.W	#9,-(A7)
	TRAP	#1
	ADDQ.L	#6,A7
	RTS


EXIT	CLR.W	-(A7)
	TRAP	#1

*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*
NEWFILE	BSR	CALC
	MOVE.L	D6,D0		;PUT IN D0 FOR CONVERSION TO ASCII
	ADD.W	D3,D0
	RTS


BINDEC1	LEA	STRIN2(PC),A0
	CLR.L	(A0)
	LEA	STRIN2(PC),A0

bindec 	movem.l	d0-d1/a0,-(A7) 
	MOVE.B	#' ',D1
	TST.W	D0
	BPL.S	notneg
	MOVE.B	#"-",D1
	NEG.W	D0
notneg	MOVE.B	D1,(A0)+
	ADDA.L	#4,A0		this figure previously 5 but some
	MOVE.W	#4,D1	failure on display caused a change to 4...
binloop EXT.L	D0
	DIVS	#10,D0
	SWAP	D0
	MOVE.B	D0,-(A0)
	ADD.B	#"0",(A0)
	SWAP	D0
	DBRA	D1,binloop
OVER	movem.l	(A7)+,d0-d1/a0
	RTS

LENGTH	DS.L	1
STRING	DS.L	1
STRIN2	DS.L	1
	DC.B	0,0,0,0,0,0,0
	EVEN

*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*

TOPLEFT	DC.B	27,"Y",32,32,"LOADING: ",0
	EVEN

END_OF_CODE	DS.B	1