DIM blocs%(3,1)
a$=SPACE$(6000)
adr%=VARPTR(a$)                 ! At the beginning of the datas, a table.
offset%=27*4                    ! adr%+offset% points just after this table.
HIDEM
SETCOLOR 0,&H445
SETCOLOR 15,&H0
~XBIOS(5;L:-1;L:-1;W:0)
BLOAD "a:\scr_zoom\chars.xxx",XBIOS(2)
FOR comp%=0 TO 26               ! 27 characters.
  LPOKE adr%,offset%            ! Save the offset of this char.
  adr%=adr%+4
  FOR comp2%=comp%*6 TO (comp%*6)+5     ! 6 v_line per char.
    nb_blocs%=0                 ! Count the nb of blocs in one v_line.
    col%=0
    y%=0
    bloc%=0
    REPEAT
      col2%=PTST(comp2%,y%)
      IF col2%<>col% THEN       ! It' s the beginning or the end of a bloc.
        IF bloc%=0 THEN         ! If bloc=0, it' s the beginning.
          INC nb_blocs%
          blocs%(nb_blocs%,0)=y%
          bloc%=1               ! The beginning of a bloc.
        ELSE                    ! If bloc=1, it' s the end.
          blocs%(nb_blocs%,1)=y%
          bloc%=0               ! The end of a bloc.
        ENDIF
        col%=col2%
      ENDIF
      INC y%
    UNTIL y%=6
    DPOKE VARPTR(a$)+offset%,nb_blocs% ! Save nb_blocs for this v_line.
    offset%=offset%+2
    '    PRINT AT(3,3);"nb blocs:";nb_blocs%
    IF nb_blocs%<>0
      FOR comp3%=1 TO nb_blocs%
        DPOKE VARPTR(a$)+offset%,blocs%(comp3%,0)
        offset%=offset%+2
        DPOKE VARPTR(a$)+offset%,blocs%(comp3%,1)
        offset%=offset%+2
        '        PRINT AT(3,3+comp3%*2-1);blocs%(comp3%,0)
        '        PRINT AT(3,3+comp3%*2);blocs%(comp3%,1)
      NEXT comp3%
    ENDIF
    '    REPEAT
    '  UNTIL INKEY$<>""
  NEXT comp2%
NEXT comp%
length%=offset%
' *
' * Redraw the chars to see if it' s really OK.
' *
adr%=VARPTR(a$)
FOR comp%=0 TO 26
  adr2%=VARPTR(a$)+LPEEK(adr%)
  adr%=adr%+4
  FOR comp2%=comp%*6 TO (comp%*6)+5
    nb_blocs%=DPEEK(adr2%)
    adr2%=adr2%+2
    IF nb_blocs%<>0
      FOR comp3%=1 TO nb_blocs%
        debut%=DPEEK(adr2%)
        adr2%=adr2%+2
        fin%=DPEEK(adr2%)
        adr2%=adr2%+2
        LINE comp2%,100+debut%,comp2%,100+fin%-1
      NEXT comp3%
    ENDIF
  NEXT comp2%
NEXT comp%
' BSAVE "d:\scr_zoom\fonts.xxx",varptr(a$),length%
