*EDDIES TIMER B STOS EXTENSION!!




	CLR.W -(A7)		;SET SCREEN ADDRESSES AND LOW RES
	PEA $78000
	PEA $78000
	MOVE.W #5,-(A7)
	TRAP #14
	LEA 12(A7),A7			

	MOVEQ.L	#0,D1
	MOVE.B	CHECK,D1
	CMP.B	#1,D1		*CHECK IF ON OR OFF
	BEQ	STOP_TIMER
	MOVE.B	#1,CHECK
	MOVE.L $118.W,OLD118
	MOVE.L $120.W,OLD120
	MOVE.L $70.W,OLD70
	MOVE.B $FFFFFA07.W,OLDA07
	MOVE.B $FFFFFA13.W,OLDA13
	MOVE.B $FFFFFA1B.W,OLDA1B
	MOVE.B $FFFFFA21.W,OLDA21
	DC.W $A000
	DC.W $A00A			;HIDE MOUSE
	MOVE.B #$12,$FFFFFC02.W 	;DISABLE MOUSE

	MOVE.W #5,-(A7)			;5=TIMER C INTERRUPT
	MOVE.W #26,-(A7)		;26=DISBALE INTERRUPT
	TRAP #14
	ADDQ.L #4,A7
 	ORI.B #1,$FFFFFA07.W		;ENABLE TIMER B
	ORI.B #1,$FFFFFA13.W
	MOVE.L #TIMERB,$120.W
	MOVE.L #VBL,$70.W

	RTS			

STOP_TIMER
	MOVE.B	#0,CHECK		*SET CHECK FLAG
*	MOVE.W #$2700,SR		;DISBALE INTERRUPTS
	MOVE.L OLD70,$70.W
	MOVE.L OLD120,$120.W
	MOVE.L OLD118,$118.W
	MOVE.B OLDA07,$FFFFFA07.W
	MOVE.B OLDA13,$FFFFFA13.W
	MOVE.B OLDA1B,$FFFFFA1B.W
	MOVE.B OLDA21,$FFFFFA21.W
	MOVE.W #5,-(A7)			;5=TIMER C INTERRUPT
	MOVE.W #27,-(A7)		;27=ENABLE INTERRUPT
	TRAP #14
	ADDQ.L #4,A7
	DC.W $A000
	DC.W $A009			;SHOW MOUSE
	MOVE.B #$08,$FFFFFC02.W 	;ENABLE MOUSE
	MOVE.W #$777,$FFFF8240.W	;SET COLOUR 0 TO WHITE
	MOVE.W #$2300,SR


	RTS

VBL	CLR.B $FFFFFA1B.W	    ;STOP TIMER B FOR SYNCRONISATION
	ADD.L	#1,$462
	ADD.L	#1,$466			*INC FRAME COUNTERS
	MOVE.B #10,$FFFFFA21.W      ;COUNTER=10 SCANLINES
	MOVE.L #COLOURS+2,PAL       ;START OF COLOUR TABLE
	MOVE.L	#COLOURS1+2,PAL1
        MOVE.W COLOURS,$FFFF8244.W  ;SET COLOUR 0 TO FIRST COLOUR
	MOVE.W	COLOURS,$FFFF8246.W
	MOVE.W	COLOURS1,$FFFF824C.W
	MOVE.W	COLOURS1,$FFFF824E.W
	MOVE.W	COLOURS1,$FFFF8248.W
	MOVE.W	COLOURS1,$FFFF824A.W
	MOVE.B #8,$FFFFFA1B.W	    ;START TIMER B
	RTE			    ;PISS OFF

TIMERB	CLR.B $FFFFFA1B.W	    ;STOP TIMER B

	MOVEM.L A0-A1,-(A7)		    ;SAVE A0
	MOVEA.L PAL,A0              ;A0=ADDRESS OF COLOUR
	MOVEA.L	PAL1,A1
	MOVE.W	(A0),$FFFF8246.W
	MOVE.W (A0)+,$FFFF8244.W    ;COLOUR=0 NEW COLOUR
	MOVE.W	(A1),$FFFF824C.W
	MOVE.W	(A1),$FFFF8248.W
	MOVE.W	(A1),$FFFF824A.W
	MOVE.W	(A1)+,$FFFF824E.W

	MOVE.W	#$200,$FFFF8242.W

        MOVE.L A0,PAL		    ;PAL=NEXT COLOUR IN TABLE
	MOVE.L	A1,PAL1
	MOVEM.L	(A7)+,A0-A1		*RESTORE OLDIES

	MOVE.B #10,$FFFFFA21.W      ;COUNTER=10 SCANLINES
	MOVE.B #8,$FFFFFA1B.W	    ;START TIMER B
	BCLR #0,$FFFFFA0F.W	    ;I'VE DONE THE ROUTINE
	RTE			    ;GO AWAY

KEY	MOVE.W #$2500,SR	;SET IPL TO LEVEL 5 SO THAT ONLY
				;LEVEL 6 AND 7 INTERRUPTS CAN OCCUR.
	BCLR #6,$FFFFFA11.W     ;I'VE DONE THE ROUTINE
	RTE			;SOD OFF

CLEAR	LEA $78000,A0
	MOVE.W #32000/4-1,D0
.L1	CLR.L (A0)+
	DBF D0,.L1
	RTS

PAL	DC.L COLOURS+2    
PAL1	DC.L	COLOURS1+2

COLOURS	DC.W $000,$000,$000,$000,$000,$000,$300,$500,$700,$722
	DC.W $700,$500,$300,$000,$000,$000,$000,$000,$000,$000

COLOURS1	DC.W $000,$000,$000,$002,$004,$006,$117,$227,$337,$447
		DC.W $337,$227,$117,$006,$004,$002,$000,$000,$000,$000

OLD70	DS.L 1
OLD118	DS.L 1
OLD120	DS.L 1
OLDA07	DS.B 1
OLDA13	DS.B 1
OLDA21	DS.B 1
OLDA1B	DS.B 1
STACK	DS.L 1
CHECK	DC.B	0