5 key off : mode 1
10 menu$ (1)=" SYSTEM "
15 menu$ (1,1)="QUIT"
20 menu$ (1,2)="RESTART"
25 menu$ (1,3)="CLEAR SCREEN"
30 menu$ (1,4)="SHOW FRONTPAGES"
35 menu$ (1,5)="SHOW EDITORIALS"
40 menu$ (1,6)="SHOW TEXTFILES"
42 menu$ (1,7)="DELETE FILE"
45 menu$ (2)=" FRONTPAGE"
50 menu$ (2,1)="MAKE"
60 menu$ (2,2)="PROVERB 1"
70 menu$ (2,3)="PROVERB 2"
80 menu$ (2,4)="PROVERB 3"
90 menu$ (2,5)="LOAD"
100 menu$ (2,6)="SAVE"
110 menu$ (2,7)="PRINT"
120 menu$ (3)=" EDITORIAL "
130 menu$ (3,1)="EDITORS"
140 menu$ (3,2)="ADRESSE"
150 menu$ (3,3)="TEXT"
160 menu$ (3,4)="CLUB CASH"
170 menu$ (3,5)="LOAD"
180 menu$ (3,6)="SAVE"
190 menu$ (3,7)="PRINT"
200 menu$ (4)=" TEXTPAGES "
210 menu$ (4,1)="MAKE"
220 menu$ (4,2)="LOAD"
230 menu$ (4,3)="SAVE"
240 menu$ (4,4)="PRINT"
250 dim TXT$(49) : clw 
260 centre "NEWSMAKER (English translation)" : cdown
270 centre "made by" : cdown
280 centre "KINATAWAN KAPSANAN" : cdown
290 centre "using" : cdown
300 centre "STOS" : cdown : cdown
310 centre "PRESS ANY KEY"
320 wait key : clw 
330 menu on 
340 on menu goto 1000,2000,3000,4000
350 on menu on 
360 goto 360
370 X=errn : locate 10,20
380 if X=48 then print "NOT FOUND" : goto 420
390 if X=49 then print "NOT READY" : goto 420
400 if X=52 then print "DISK ERROR" : goto 420
410 if X=83 then print "ADJUST CABLE" else print "Send ";errn,errl;" to aetesaki@sunomi.no"
420 boom : wait 250 : return 
430 X=errn : locate 10,20
440 if X=49 then print "NOT READY" : goto 490
450 if X=50 then print "WRITE PROTECTED" : goto 490
460 if X=51 then print "FULL DISK" : goto 490
470 if X=52 then print "DISK ERROR" : goto 490
480 if X=83 then print "ADJUST CABLE" else print "Send ";errn,errl;" to aetesaki@sunomi.no"
490 boom : wait 250 : return 
500 locate 10,20 : if errn=10 then print "PRINTER NOT READY" else print "Send ";errn,errl;" to aetesaki@sunomi.no"
510 boom : wait 250 : return 
1000 on mnselect goto 1020,1030,1050,1060,1070,1080,1090
1010 goto 340
1020 default : end 
1030 menu freeze 
1040 clear : goto 5
1050 clw : goto 340
1060 dir/w "*.FOR" : goto 340
1070 dir/w "*.RED" : goto 340
1080 dir/w "*.TXT" : goto 340
1090 input " No. ";SF$
1100 input " Surename ";X$
1110 kill SF$+"."+X$
1120 goto 340
2000 on mnselect gosub 2020,2060,2100,2140,2180,2270,2370
2010 goto 340
2020 input " No. : ";FXR
2030 input " Year: ";FAR
2040 input " Number of members";FAM
2050 clw : return 
2060 input FAA$
2070 input FAB$
2080 input " Name : ";FAC$
2090 clw : return 
2100 input FBA$
2110 input FBB$
2120 input " Name : ";FBC$
2130 clw : return 
2140 input FCA$
2150 input FCB$
2160 input " Name : ";FCC$
2170 clw : return 
2180 input " No. ";SF$ : on error goto 2260
2190 SF$=SF$+".FOR"
2200 open in #1,SF$
2210 input #1,FXR : input #1,FAR : input #1,FAM
2220 input #1,FAA$,FAB$,FAC$
2230 input #1,FBA$,FBB$,FBC$
2240 input #1,FCA$,FCB$,FCC$
2250 close #1
2255 clw : return 
2260 gosub 370 : resume 2255
2270 input " No. : ";SF$
2280 on error goto 2360
2290 SF$=SF$+".FOR"
2300 open out #1,SF$
2310 print #1,FXR : print #1,FAR : print #1,FAM
2320 print #1,FAA$ : print #1,FAB$ : print #1,FAC$
2330 print #1,FBA$ : print #1,FBB$ : print #1,FBC$
2340 print #1,FCA$ : print #1,FCB$ : print #1,FCC$
2350 close #1
2355 clw : return 
2360 gosub 430 : resume 2355
2370 input "Number of copies:";SA
2375 on error goto 2600
2380 while SA<>0
2390 print "Press any key to start" : wait key 
2400 lprint chr$(27)+"x1";chr$(27)+"k1";chr$(27)+chr$(14),"  KINATAWAN KAPSANAN"
2410 lprint "     No. ";FXR," 19";FAR," Members : ";FAM
2420 lprint "_____________________________________________________________________________"
2430 lprint : lprint 
2440 lprint "",FAA$
2450 lprint "",FAB$
2460 lprint chr$(27)+"4";"",FAC$
2470 lprint chr$(27)+"5" : lprint 
2480 lprint "",FBA$
2490 lprint "",FBB$
2500 lprint chr$(27)+"4";"",FBC$
2510 lprint chr$(27)+"5" : lprint 
2520 lprint "",FCA$
2530 lprint "",FCB$
2540 lprint chr$(27)+"4";"",FCC$
2550 lprint chr$(27)+"5"
2560 print "Done. New sheet, please"
2570 lprint chr$(12)
2580 print "Press any key" : wait key 
2590 dec SA : wend : return 
2600 gosub 500 : resume 
3000 on mnselect gosub 3020,3060,3100,3120,3160,3250,3340
3010 goto 340
3020 input " Editor contact ";RNR$
3030 input " Print  ";ROP$
3040 SF$=ROP$ : ROP$="Ca.  "+SF$
3050 clw : return 
3060 input " Name  ";RNA$
3070 input " Adresse ";RXD$
3080 input " Post adresse ";RPO$
3090 clw : return 
3100 line input RTA$,RTB$,RTC$,RTD$,RTE$
3110 clw : return list 3100-3200
3120 input " Deposit    ";RKI
3130 input " Withdrawal ";RKU
3140 input " Balance    ";RKS
3150 clw : return 
3160 input " No. ";SF$
3170 on error goto 3240
3180 SF$=SF$+".red"
3190 open in #1,SF$
3200 input #1,RNR$,ROP$,RNA$,RXD$,RPO$
3210 input #1,RTA$,RTB$,RTC$,RTD$,RTE$
3220 input #1,RKI,RKU,RKS
3230 close #1
3235 clw : return 
3240 gosub 370 : resume 3235
3250 input " No. ";SF$
3260 on error goto 3330
3270 SF$=SF$+".red"
3280 open out #1,SF$
3290 print #1,RNR$ : print #1,ROP$ : print #1,RNA$ : print #1,RXD$ : print #1,RPO$
3300 print #1,RTA$ : print #1,RTB$ : print #1,RTC$ : print #1,RTD$ : print #1,RTE$
3310 print #1,RKI : print #1,RKU : print #1,RKS
3320 close #1
3325 clw : return 
3330 gosub 430 : resume 3325
3340 on error goto 2600
3350 input "No. of copies : ";SA
3360 while SA<>0
3370 print "Press a key to start" : wait key 
3380 lprint chr$(27)+"x1";chr$(27)+"k1";chr$(27)+chr$(14),"  Editor in charge" : lprint 
3390 lprint "     Members : ",RNR$," Print ";ROP$ : lprint 
3400 lprint "     Address" : lprint "",RNA$ : lprint "",RXD$ : lprint "",RPO$ : lprint : lprint 
3410 lprint chr$(14);chr$(27)+"4";RTA$ : lprint chr$(14);RTB$ : lprint chr$(14);RTC$ : lprint chr$(14);RTD$ : lprint chr$(14);RTE$ : lprint : lprint 
3420 lprint chr$(14);" Deposit : ";RKI : lprint chr$(14);" Withdrawal:";RKU : lprint : lprint chr$(14);" Balanse  : ";RKS : lprint "==============================" : lprint : lprint chr$(27)+"5"
3430 lprint "   Innmelding av nye medlemmer. Fyll ut kortet under"
3440 lprint "   og send inn. Kortet legges i en lukket konvolutt"
3450 lprint "   sammen med Kr. 5,-- i frimerker (RETURPORTO)." : lprint 
3460 lprint "----------------------------------------------------------------------------" : lprint 
3470 lprint "   Medlemsnummeret til eier av dette blad : _______________" : lprint : lprint 
3480 lprint "   Fullt navn : ___________________________________________" : lprint 
3490 lprint "   Adresse    : ___________________________________________" : lprint 
3500 lprint "   Post nr.   : _________ Poststed : ______________________" : lprint 
3510 lprint "   F|dt  Dato/M}ned/]r : ____/____-19____ F|dselsnr. : ____" : lprint 
3520 lprint "   F|deby     : ___________________________________________"
3530 lprint "   F|dselsnr. er ikke n|dvendig } fylle ut." : lprint 
3540 lprint "   MIKKE MUS KLUBBS MEDLEMSNUMMER : _______________________"
3550 lprint "_____________________________________________________________________________";chr$(12)
3560 dec SA : wend : return 
4000 on mnselect gosub 4020,4070,4170,4270
4010 goto 340
4020 clw 
4030 for X=0 to 49
4040 input X+1;"/50:";TXT$(X)
4050 next 
4060 return 
4070 clw 
4080 input " Filename: ";SF$
4090 SF$=SF$+".txt" : on error goto 4160
4100 open in #1,SF$
4110 for X=0 to 49
4120 input #1,TXT$(X)
4130 next X
4140 close 
4150 clw : return 
4160 gosub 370 : resume 4150
4170 clw : on error goto 4260
4180 input " Filename: ";SF$
4190 SF$=SF$+".txt"
4200 open out #1,SF$
4210 for X=0 to 49
4220 print #1,TXT$(X)
4230 next X
4240 close 
4250 clw : return 
4260 gosub 430 : resume 4250
4270 input "No. of copies : ";SA
4280 on error goto 4380
4290 while SA<>0
4300 print "Press a key to start" : wait key 
4310 lprint chr$(27)+"x1";chr$(27)+"k1"
4320 for X=0 to 49
4330 lprint TXT$(X)
4340 next X
4350 lprint chr$(12)
4360 dec SA
4370 wend : clw : return 
4380 gosub 500 : resume 
