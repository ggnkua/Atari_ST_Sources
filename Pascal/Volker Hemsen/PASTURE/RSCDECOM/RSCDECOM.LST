$%3,*%,E$,N-,I+,U-,C-,S&,S>,P<,F<,RC&,B+
'
' rsc_pfad$="TEST.RSC"
' s_pfad$="TESTR.S"
' CHAR{BASEPAGE+129}="G:\pasture.rsc"
'
'
rsc_pfad$=CHAR{BASEPAGE+129}
s_pfad$=@gfn$(rsc_pfad$)+"S"
'
PRINT CHR$(27)+"p   RSC-Decompiler V1.1  (fr ST-Pascal plus)   "+CHR$(27)+"q"
PRINT "geschrieben in GFA-Basic 3.0 von Volker Hemsen - 10.07.93,19.07.93"
PRINT "Es wird ein Assemblermodul erzeugt, welches mit TurboAss"
PRINT "bersetzt werden kann, und dann als O-File eingebunden wird."
PRINT "bersetzte Datei:"'rsc_pfad$
PRINT "erzeugte Datei:"'s_pfad$
PRINT " - Bitte warten - "
'
IF EXIST(rsc_pfad$)
  OPEN "i",#1,rsc_pfad$
  rsc_len%=LOF(#1)
  DIM rsc&(rsc_len%/2),reloc|(rsc_len%)
  ARRAYFILL reloc|(),&HFE
  BGET #1,V:rsc&(0),rsc_len%
  CLOSE #1
  DIM z$(rsc_len%/6),z%(rsc_len%/6)
  '
  @conv_dfn_to_pas(@gfn$(rsc_pfad$)+"DFN")
  '
  CLR z%
  '
  @set_string("OPT D+",0)
  @set_string("GLOBAL RSRC_ADR,RSRC_ANZ",0)
  @set_string("RSRC_ANZ:",0)
  @set_string("move.w    #"+STR$(PRED(rsc&(&H14/2)))+",d0",0)
  @set_string("          rts",0)
  @set_string("RSRC_ADR:",0)
  @set_string("move.l    (sp)+,a0",0)
  @set_string("move.w    (sp)+,d0",0)
  @set_string("asl.w     #2,d0",0)
  @set_string("move.l    TREES(PC,d0.w),d0",0)
  @set_string("jmp       (a0)",0)
  @set_string("TREES:",0)
  '
  @decom_objektbaum
  @decom_objektfeld
  '
  @decom_strings
  @decom_bitblk
  @decom_iconblk
  @decom_tedinfo
  @decom_images
  @set_string("END",0)
  '
  @set_label
  '
  '  PRINT "- erzeugtes Listing - Ende mit Space -"
  '
  '  FOR z%=0 TO zend%
  '  PRINT z$(z%)
  '  EXIT IF INP(2)=32
  ' NEXT z%
  OPEN "o",#1,s_pfad$
  STORE #1,z$(),0 TO SUCC(zend%)
  CLOSE #1
  '
ENDIF
'
> PROCEDURE decom_objektbaum
  anz&=PRED(rsc&(&H16/2))
  o%=rsc&(&H12/2)
  CLR z$
  FOR i&=0 TO anz&
    a%=LONG{V:rsc&(0)+o%}
    reloc|(a%)=&HFF
    @set_string("dc.l"+CHR$(9)+"L"+HEX$(a%),o%)
    ADD o%,4
    '   IF LEN(z$)
    '     z$=z$+","
    '  ENDIF
    '    z$=z$+tree_label$(i&)
    '    IF (LEN(z$)>50) OR (i&=anz&)
    '    @set_string("GLOBAL"+CHR$(9)+z$,0)
    '    CLR z$
    '  ENDIF
  NEXT i&
RETURN
> PROCEDURE decom_objektfeld
  anz&=rsc&(&H14/2)
  o%=(rsc&(1)/2)
  @set_string(";*******************  Objekte  *******************",0)
  FOR i&=0 TO PRED(anz&)
    '                       next             head                 tail
    z$="dc.w"+CHR$(9)+STR$(rsc&(o%))+","+STR$(rsc&(o%+1))+","+STR$(rsc&(o%+2))
    '              type              flags                 state
    z$=z$+","+STR$(rsc&(o%+3))+",$"+HEX$(rsc&(o%+4))+",$"+HEX$(rsc&(o%+5))
    @set_string(z$,o%*2)
    a%={V:rsc&(o%+6)}
    SELECT rsc&(o%+3)
    CASE 20,25,27
      @set_string("dc.l"+CHR$(9)+"$"+HEX$(a%),(o%+6)*2)
    DEFAULT
      reloc|(a%)=&HFF
      @set_string("dc.l"+CHR$(9)+"L"+HEX$(a%),(o%+6)*2)
    ENDSELECT
    @set_string("dc.w"+CHR$(9)+STR$(rsc&(o%+8))+","+STR$(rsc&(o%+9))+","+STR$(rsc&(o%+10))+","+STR$(rsc&(o%+11)),(o%+8)*2)
    ADD o%,12
  NEXT i&
RETURN
> PROCEDURE decom_tedinfo
  anz&=rsc&(&H18/2)
  IF anz&>0
    o%=(rsc&(2)/2)
    @set_string(";*******************  Tedinfos  *******************",0)
    FOR i&=0 TO PRED(anz&)
      z$="dc.l"+CHR$(9)+"L"+HEX$({V:rsc&(o%)})+","+"L"+HEX$({V:rsc&(o%+2)})+","+"L"+HEX$({V:rsc&(o%+4)})
      reloc|({V:rsc&(o%)})=&HFF
      reloc|({V:rsc&(o%+2)})=&HFF
      reloc|({V:rsc&(o%+4)})=&HFF
      @set_string(z$,o%*2)
      z$="dc.w"+CHR$(9)+STR$(rsc&(o%+6))
      FOR j&=7 TO 13
        z$=z$+","+STR$(rsc&(o%+j&))
      NEXT j&
      @set_string(z$,(o%+6)*2)
      ADD o%,14
    NEXT i&
  ENDIF
RETURN
> PROCEDURE decom_iconblk
  anz&=rsc&(&H1A/2)
  IF anz&>0
    o%=rsc&(3)/2
    @set_string(";*******************  Iconblks  *******************",0)
    FOR i&=0 TO PRED(anz&)
      z$="dc.l"+CHR$(9)
      FOR j&=0 TO 2
        IF j&>0
          z$=z$+","
        ENDIF
        a%={V:rsc&(o%+2*j&)}
        reloc|(a%)=&HFF
        z$=z$+"L"+HEX$(a%)
      NEXT j&
      @set_string(z$,o%*2)
      z$="dc.w"+CHR$(9)+"$"+HEX$(rsc&(o%+6))
      FOR j&=7 TO 16
        z$=z$+","+STR$(rsc&(o%+j&))
      NEXT j&
      @set_string(z$+",0",(o%+6)*2)
      ADD o%,17         !in RSC-Datei nur 17 Worte
    NEXT i&
  ENDIF
RETURN
> PROCEDURE decom_bitblk
  anz&=rsc&(&H1C/2)
  IF anz&>0
    o%=rsc&(4)/2
    @set_string(";*******************  Bitblks  *******************",0)
    FOR i&=0 TO PRED(anz&)
      a%={V:rsc&(o%)}
      z$="dc.l"+CHR$(9)
      reloc|(a%)=&HFF
      z$=z$+"L"+HEX$(a%)
      @set_string(z$,o%*2)
      z$="dc.w"+CHR$(9)+STR$(rsc&(o%+2))
      FOR j&=3 TO 6
        z$=z$+","+STR$(rsc&(o%+j&))
      NEXT j&
      @set_string(z$,(o%+2)*2)
      ADD o%,7
    NEXT i&
  ENDIF
RETURN
> PROCEDURE decom_strings
  a%=rsc&(&HC/2)
  e%=rsc&(&HE/2)
  CLR z$
  @set_string(";*******************  Strings  *******************",0)
  FOR i%=a% TO PRED(e%)
    a$=CHAR{V:rsc&(0)+i%}
    IF LEN(a$)
      z$=CHR$(34)+a$+CHR$(34)+","
    ELSE
      z$=a$
    ENDIF
    @set_string("dc.b"+CHR$(9)+z$+"0",i%)
    ADD i%,LEN(a$)
  NEXT i%
RETURN
> PROCEDURE decom_images
  a%=rsc&(&HE/2)
  IF a%>0
    IF rsc&(5)=0
      e%=rsc&(4)
    ELSE IF rsc&(4)=0
      e%=rsc&(5)
    ELSE
      e%=MIN(rsc&(4),rsc&(5))
    ENDIF
    '    PRINT HEX$(e%)
    s%=a%
    CLR z$
    @set_string(";*******************  Images  *******************",0)
    FOR i%=a% TO PRED(e%)
      IF LEN(z$)
        z$=z$+","
      ENDIF
      z$=z$+"$"+HEX$(BYTE{V:rsc&(0)+i%})
      IF ((reloc|(i%)=&HFF) AND (i%<>a%)) OR (LEN(z$)>50) OR (i%=PRED(e%))
        @set_string("dc.b"+CHR$(9)+z$,s%)
        '        PRINT HEX$(s%),z$
        '       ~INP(2)
        CLR z$
        s%=i%
      ENDIF
    NEXT i%
  ENDIF
RETURN
'
PROCEDURE set_string(z$,adr%)
  z$(z%)=z$
  z%(z%)=adr%
  INC z%
RETURN
> PROCEDURE set_label
  FOR z%=0 TO PRED(DIM?(z$()))
    EXIT IF LEN(z$(z%))=0
    a|=reloc|(z%(z%))
    IF (a|=&HFF)
      z$(z%)="L"+HEX$(z%(z%))+":"+CHR$(9)+z$(z%)
    ELSE IF (a|=&HFE)
      z$(z%)=CHR$(9)+CHR$(9)+z$(z%)
    ELSE
      z$(z%)=tree_label$(a|)+":"+CHR$(9)+z$(z%)
    ENDIF
  NEXT z%
  zend%=z%
RETURN
'
> PROCEDURE read_dfn(pfad$)
  IF EXIST(pfad$)
    OPEN "i",#1,pfad$
    anz&=INP(#1)+SHL(INP(#1),8)
    FOR i&=0 TO PRED(anz&)
      oindex&=INP(#1)
      bindex&=INP(#1)
      typ&=INP(#1)
      btyp&=INP(#1)
      a$=STRING$(12,0)
      BGET #1,V:a$,10
      IF btyp&=0
        tree_label$(oindex&)=TRIM$(CHAR{V:a$})
        '        PRINT oindex&,tree_label$(oindex&)
      ENDIF
      '        PRINT TRIM$(a$)
      '     EXIT IF INP(2)=32
    NEXT i&
    CLOSE #1
  ENDIF
RETURN
> PROCEDURE conv_dfn_to_pas(pfad$)
  IF EXIST(pfad$)
    OPEN "i",#1,pfad$
    OPEN "o",#2,@gfn$(pfad$)+"I"
    PRINT #2,"const"
    anz&=INP(#1)+SHL(INP(#1),8)
    FOR i&=0 TO PRED(anz&)
      oindex&=INP(#1)
      bindex&=INP(#1)
      typ&=INP(#1)
      btyp&=INP(#1)
      a$=STRING$(12,0)
      BGET #1,V:a$,10
      IF baumtyp&=0
        a&=oindex&
      ELSE
        a&=bindex&
      ENDIF
      PRINT #2,CHR$(9)+CHAR{V:a$}+" = "+STR$(a&)+";"
      '      PRINT CHR$(9)+TRIM$(a$)+" = "+STR$(a&)+";"
      '     TEXT 100,100,CHAR{V:a$}
      '    ~INP(2)
      '      PRINT TRIM$(a$)
      '     EXIT IF INP(2)=32
    NEXT i&
    CLOSE #1
    CLOSE #2
  ENDIF
RETURN
'
FUNCTION gfn$(p$)
  RETURN LEFT$(rsc_pfad$,RINSTR(rsc_pfad$,"."))
ENDFUNC
